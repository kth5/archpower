# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.14.3
pkgrel=1
pkgdesc="GObject-based wrapper around the Exiv2 library"
url="https://gitlab.gnome.org/GNOME/gexiv2"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later)
depends=(
  exiv2
  glib2
  glibc
  gcc-libs
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
  python-gobject
  vala
)
source=("git+$url.git?signed#tag=gexiv2-$pkgver")
b2sums=('1390c2902b16c703430f8b4db0877f1528d9c036bee46105221fa60393d8a5903f0831edbce11df82061f78a863d11e5cba282fbaf749f882a5416252ddc657f')
validpgpkeys=(
  AC9CD4E32D7C7F6357BA8ADD10F6E970175D29E1 # Jens Georg <mail@jensge.org>
)

build() {
  local meson_options=(
    -D gtk_doc=true
    -D tests=true
  )

  arch-meson gexiv2 build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libg{lib,object,io}-2.0.so
    libexiv2.so
  )
  provides+=(libgexiv2.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
