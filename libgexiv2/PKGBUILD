# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.14.6
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://gitlab.gnome.org/GNOME/gexiv2'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later)
depends=(exiv2 gcc-libs glib2 glibc)
makedepends=(git glib2-devel gobject-introspection meson python-gobject vala)
source=("git+$url.git?signed#tag=gexiv2-$pkgver")
b2sums=('458c6beace8137de8ac4ee5ec9c128b0db423da557f153c865de354664c1c05bc78c13f118915319c7cae6df0a2460ab3945a61ddcf9c7a91ff348f5cf8b6414')
validpgpkeys=(AC9CD4E32D7C7F6357BA8ADD10F6E970175D29E1) # Jens Georg <mail@jensge.org>

build() {
  arch-meson gexiv2 build -D gtk_doc=true -D tests=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libg{lib,object,io}-2.0.so
    libexiv2.so
  )
  provides+=(libgexiv2.so)

  meson install -C build --destdir "$pkgdir"
}
