# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.19.0
pkgrel=1
pkgdesc="A general purpose formula parser & interpreter"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python' 'gcc-libs' 'glibc')
makedepends=('boost' 'mdds' 'spdlog')
source=("https://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz")
# https://gitlab.com/ixion/ixion/-/releases
sha256sums=('b4864d7a55351a09adbe9be44e5c65b1d417e80e946c947951d0e8428b9dcd15')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
#  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # doesn't fix it
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # LICENSE file is missing in tarball
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
