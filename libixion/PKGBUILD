# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.20.0
pkgrel=1
pkgdesc="A general purpose formula parser & interpreter"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/ixion/ixion"
license=('MPL-2.0')
depends=('boost-libs' 'python' 'gcc-libs' 'glibc')
makedepends=('boost' 'mdds' 'spdlog')
source=(https://gitlab.com/api/v4/projects/ixion%2Fixion/packages/generic/source/${pkgver}/libixion-${pkgver}.tar.xz)
# https://gitlab.com/ixion/ixion/-/releases
sha256sums=('8466a05f19f60b7568641a449704666308848f7bcaf8335f92ee9474a451e4cd')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
  # improves from 5 down to 3 times Unused shared library
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
