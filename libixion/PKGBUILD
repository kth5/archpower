# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.19.0
pkgrel=7.1
pkgdesc="A general purpose formula parser & interpreter"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python' 'gcc-libs' 'glibc')
makedepends=('boost' 'mdds' 'spdlog')
source=("https://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz"
         https://gitlab.com/ixion/ixion/-/commit/7b1df6fc.patch)
# https://gitlab.com/ixion/ixion/-/releases
sha256sums=('b4864d7a55351a09adbe9be44e5c65b1d417e80e946c947951d0e8428b9dcd15'
            '2252cee824721c9b9ad46989f98c6d7d192d01635f74b0d9dac9072d86e599f2')

prepare() {
  # Fix build with -Werror=undef
  patch -d $pkgname-$pkgver -p1 < 7b1df6fc.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
#  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # doesn't fix it
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # LICENSE file is missing in tarball
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
