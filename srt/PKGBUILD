# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=srt
pkgver=1.5.0
pkgrel=1
pkgdesc="Secure Reliable Transport library"
url="https://www.srtalliance.org/"
arch=(x86_64 powerpc64le powerpc riscv64)
license=(MPL2)
depends=(openssl)
makedepends=(git cmake)
options=(debug)
_commit=060c0d1a2af8082c053018fa6a6fe2dba67d71a8  # tags/v1.5.0
source=("git+https://github.com/Haivision/srt#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd srt
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd srt
}

build() {
  cmake -S srt -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_BINDIR=bin \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_INCLUDEDIR=include \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_STATIC=ON \
    -DENABLE_TESTING=ON
  cmake --build build
}

check() {
  cd build
  ./uriparser-test
  ./utility-test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set ts=2 sw=2:
