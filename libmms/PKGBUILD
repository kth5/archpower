# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libmms
pkgver=0.6.4
pkgrel=6
pkgdesc='MMS stream protocol library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://sourceforge.net/projects/libmms/'
license=(LGPL-2.1-or-later)
depends=(glibc)
source=(
  "$pkgname-$pkgver.tar.gz::https://download.sourceforge.net/libmms/libmms-$pkgver.tar.gz"
  libmms-Remove-requires-glib2.0.patch
)
sha512sums=('9771c697515f5232eaeaff79e68fe15e34d8aa38aa5d3d68525216357223f314c544d71b5fe18d79a77682f41b1b5d9fb5e7c3c99d0cba5723d2e3de31faec96'
            'ff081bcbece948f9b18c9e8f5ec27b528aeaf5cc9b49bf040e59e34b571aeb37eff8d27ecb5e4d3588d5883a83d56485afc46de689fb14e3c6a716dd48fd12b7')
b2sums=('2b0029cde3e985b1a0310e7d00acafb54a53606c656fee1b87a086a31ecc1e488aec31e86880959f23a077a20594a6c939efea3e08545a3d47f804ed762395a4'
        '1db96bf72e1e297ac9ff22303e922be01694868441005ee9948734c6a7c8424c05e131cc919544c77408957e43fed74d5c5ac30035fc6a39f3e00f35e978e6f5')

prepare() {
  cd "$pkgname-$pkgver"

  # Remove "Requires: glib-2.0" since libmms no longer depends on GLib.
  patch -p1 -i "$srcdir/libmms-Remove-requires-glib2.0.patch"

  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --disable-static

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
