# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=curl
pkgname=(curl libcurl-compat libcurl-gnutls)
pkgver=8.18.0
pkgrel=3
pkgdesc='command line tool and library for transferring data with URLs'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://curl.se/'
license=('MIT')
makedepends=(
  # for building
  'git'
  'patchelf'
  # actual package dependencies
  'ca-certificates'
  'brotli'
  'gnutls'
  'krb5'
  'libidn2'
  'libnghttp2'
  'libnghttp3'
  'libngtcp2'
  'libpsl'
  'libssh2'
  'nettle'
  'zlib'
  'zstd')
checkdepends=('valgrind')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg
source=("git+https://github.com/curl/curl.git#tag=curl-${pkgver//./_}?signed")
sha512sums=('304ddac52ee589f6e6e168747d720ea1ecc6f66adb469b70e0a1ee7c9f0bf5ed12e2538647bb746b8a6aaa1e3860e9b00ed29cd2079ca2e4c25a2afc972f0e6a')

_backports=(
  # vquic: handle SOCKEMSGSIZE correctly
  'cf4164fa8dc3e09b2fb729ba231ecbf7b5d015de'
)

_reverts=(
)

prepare() {
  cd "$pkgbase"

  local _c
  for _c in "${_backports[@]}"; do
    if [[ $_c == *..* ]]; then
      git log --oneline --reverse "${_c}"
    else
      git log --oneline -1 "${_c}"
    fi
    git cherry-pick -n -m1 "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done

  # no '-DEV' in version, release date from tagged commit...
  sed -i \
    -e "/\WLIBCURL_VERSION\W/c #define LIBCURL_VERSION \"${pkgver}\"" \
    -e "/\WLIBCURL_TIMESTAMP\W/c #define LIBCURL_TIMESTAMP \"$(git log -1 --format=%cs "curl-${pkgver//./_}")\"" \
    include/curl/curlver.h

  autoreconf -fi
}

build() {
  local _configure_options=(
    --prefix='/usr'
    --mandir='/usr/share/man'
    --disable-ldap
    --disable-ldaps
    --disable-manual
    --enable-ipv6
    --enable-threaded-resolver
    --enable-websockets
    --with-gssapi
    --with-libssh2
    --with-nghttp3
    --with-ngtcp2
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'
  )

  mkdir build-curl{,-compat,-gnutls}

  # build curl
  cd "${srcdir}"/build-curl

  "${srcdir}/${pkgbase}"/configure \
    "${_configure_options[@]}" \
    --enable-versioned-symbols \
    --without-gnutls \
    --with-openssl \
    --with-fish-functions-dir=/usr/share/fish/vendor_completions.d/ \
    --with-zsh-functions-dir=/usr/share/zsh/site-functions/
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  # build libcurl-compat
  cd "${srcdir}"/build-curl-compat

  "${srcdir}/${pkgbase}"/configure \
    "${_configure_options[@]}" \
    --disable-versioned-symbols \
    --without-gnutls \
    --with-openssl
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
  patchelf --set-soname 'libcurl-compat.so.4' ./lib/.libs/libcurl.so

  # build libcurl-gnutls
  cd "${srcdir}"/build-curl-gnutls

  "${srcdir}/${pkgbase}"/configure \
    "${_configure_options[@]}" \
    --disable-versioned-symbols \
    --with-gnutls \
    --without-openssl
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
  patchelf --set-soname 'libcurl-gnutls.so.4' ./lib/.libs/libcurl.so
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd build-curl
  # -v: verbose
  # -a: keep going on failure (so we see everything which breaks, not just the first failing test)
  # -k: keep test files after completion
  # -am: automake style TAP output
  # -p: print logs if test fails
  # -j: parallelization
  # disable test 433, since it requires the glibc debug info
  make TFLAGS="-v -a -k -p -j$(nproc) !433" test-nonflaky
}

package_curl() {
  depends=(
    'ca-certificates'
    'brotli' 'libbrotlidec.so'
    'krb5' 'libgssapi_krb5.so'
    'libidn2' 'libidn2.so'
    'libnghttp2' 'libnghttp2.so'
    'libnghttp3' 'libnghttp3.so'
    'libngtcp2' 'libngtcp2.so'
    'libpsl' 'libpsl.so'
    'libssh2' 'libssh2.so'
    'openssl' 'libcrypto.so' 'libssl.so'
    'zlib' 'libz.so'
    'zstd' 'libzstd.so')
  provides=('libcurl.so')
  replaces=('wcurl')
  conflicts=('wcurl')

  cd build-curl

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install -C scripts

  cd "${srcdir}/${pkgname}"

  # license
  install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m0644 COPYING
}

package_libcurl-compat() {
  pkgdesc='command line tool and library for transferring data with URLs (no versioned symbols)'
  depends=(
    'ca-certificates'
    'brotli' 'libbrotlidec.so'
    'krb5' 'libgssapi_krb5.so'
    'libidn2' 'libidn2.so'
    'libnghttp2' 'libnghttp2.so'
    'libnghttp3' 'libnghttp3.so'
    'libngtcp2' 'libngtcp2.so'
    'libpsl' 'libpsl.so'
    'libssh2' 'libssh2.so'
    'openssl' 'libcrypto.so' 'libssl.so'
    'zlib' 'libz.so'
    'zstd' 'libzstd.so')
  provides=('libcurl-compat.so')

  cd "${srcdir}"/build-curl-compat

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib/libcurl{,-compat}.so.4.8.0
  rm "${pkgdir}"/usr/lib/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0 4.7.0; do
    ln -s libcurl-compat.so.4.8.0 "${pkgdir}"/usr/lib/libcurl.so.${version}
    ln -s libcurl-compat.so.4.8.0 "${pkgdir}"/usr/lib/libcurl-compat.so.${version}
  done

  install -dm 0755 "${pkgdir}"/usr/share/licenses
  ln -s curl "${pkgdir}"/usr/share/licenses/libcurl-compat
}

package_libcurl-gnutls() {
  pkgdesc='command line tool and library for transferring data with URLs (no versioned symbols, linked against gnutls)'
  depends=(
    'ca-certificates'
    'brotli' 'libbrotlidec.so'
    'gnutls' # 'libgnutls.so'
    'krb5' 'libgssapi_krb5.so'
    'libidn2' 'libidn2.so'
    'libnghttp2' 'libnghttp2.so'
    'libnghttp3' 'libnghttp3.so'
    'libngtcp2' 'libngtcp2.so' 'libngtcp2_crypto_gnutls.so'
    'libpsl' 'libpsl.so'
    'libssh2' 'libssh2.so'
    'nettle' 'libnettle.so'
    'zlib' 'libz.so'
    'zstd' 'libzstd.so')
  provides=('libcurl-gnutls.so')

  cd "${srcdir}"/build-curl-gnutls

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib/libcurl{,-gnutls}.so.4.8.0
  rm "${pkgdir}"/usr/lib/libcurl.{a,so}*
  ln -s libcurl-gnutls.so.4 "${pkgdir}"/usr/lib/libcurl-gnutls.so
  for version in 3 4 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0 4.7.0; do
    ln -s libcurl-gnutls.so.4.8.0 "${pkgdir}"/usr/lib/libcurl-gnutls.so.${version}
  done

  install -dm 0755 "${pkgdir}"/usr/share/licenses
  ln -s curl "${pkgdir}"/usr/share/licenses/libcurl-gnutls
}
