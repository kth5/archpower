# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libqrtr-glib
pkgname=(
  libqrtr-glib
  libqrtr-glib-docs
)
pkgver=1.4.0
pkgrel=1
pkgdesc="Qualcomm IPC Router protocol helper library"
url="https://gitlab.freedesktop.org/mobile-broadband/libqrtr-glib"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
)
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqrtr-glib.git?signed#tag=${pkgver/[a-z]/-&}")
b2sums=('b2b8fe0f01fcb3dd6fcd55883c837c461c57791f7e472d448bb096c0e32b0ceea191b72731479343024b39e119af5f6d6ae358fccfbd9bee48077776145626e8')
validpgpkeys=(
  D88D6B90198F924A78705D8C3CAD53398973FFFA # Aleksander Morgado <aleksander@aleksander.es>
  3A948E16E8860C68CD68441D734614319E26A433 # Dan Williams <dan@ioncontrol.co>
)

prepare() {
  cd libqrtr-glib
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson libqrtr-glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libqrtr-glib() {
  provides=(libqrtr-glib.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libqrtr-glib-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
