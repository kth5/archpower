# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libqrtr-glib
pkgname=(
  libqrtr-glib
  libqrtr-glib-docs
)
pkgver=1.2.2
pkgrel=4
pkgdesc="Qualcomm IPC Router protocol helper library"
url="https://gitlab.freedesktop.org/mobile-broadband/libqrtr-glib"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
)
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqrtr-glib.git?signed#tag=${pkgver/[a-z]/-&}")
b2sums=('2887ad9ebc45a329610b435f9461d244f1a783481adacdd092781a97488751e4e58e256b7b55f7ce5b228b12d5afeccdb81640e3f681d8da42396d3dc9a2d243')
validpgpkeys=(
  D88D6B90198F924A78705D8C3CAD53398973FFFA # Aleksander Morgado <aleksander@aleksander.es>
)

prepare() {
  cd libqrtr-glib
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson libqrtr-glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libqrtr-glib() {
  provides=(libqrtr-glib.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_libqrtr-glib-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
