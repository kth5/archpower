# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=mandown
pkgver=1.0.0
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('Apache-2.0')
url="https://gitlab.com/kornelski/mandown"
pkgdesc="Create man pages from markdown markup"
depends=(
  gcc-libs
  glibc
)
makedepends=(rust cargo)
source=(https://gitlab.com/kornelski/mandown/-/archive/v${pkgver}/mandown-v${pkgver}.tar.gz)
sha512sums=('93b908170586f8b6f90d8f6a5d3fceabaab83cbc7ed6c42fcf27dbd39d0778c160235fa566d667d3fed6949f884d045462434ea319588cd941ea7f7a2357c303')

prepare() {
  cd "$pkgname-v$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-v$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname-v$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-v$pkgver"
  install -vDm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
}
