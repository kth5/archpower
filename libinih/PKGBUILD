# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alberto Oporto Ames <otreblain@gmail.com>

pkgname=libinih
pkgver=60
pkgrel=1
pkgdesc='A simple .INI file parser written in C'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/benhoyt/inih
license=(BSD-3-Clause)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
)
provides=(
  libinih.so
  libINIReader.so
)
_tag=9de2a5fe4956447a22a324e2efc0648c5aad5285
source=(git+https://github.com/benhoyt/inih.git#tag=${_tag})
b2sums=('6f423fec18aa67e3ea69e4b40031021ff1123512177a0107307ba4c39fc5bb725bb04da4ef5a1b281ca57b1f15f6805e310de0188f81875e1aedec5108afe86c')

pkgver() {
  cd inih
  git describe --tags | sed 's/^r//'
}

build() {
  arch-meson inih build \
    -Ddefault_library=shared \
    -Ddistro_install=true \
    -Dwith_INIReader=true
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm 644 inih/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libinih/
}

# vim: ts=2 sw=2 et:
