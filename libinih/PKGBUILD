# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alberto Oporto Ames <otreblain@gmail.com>

pkgname=libinih
pkgver=59
pkgrel=1
pkgdesc='A simple .INI file parser written in C'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url=https://github.com/benhoyt/inih
license=(BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
)
provides=(
  libinih.so
  libINIReader.so
)
_tag=23acf2dd5af5287b0f170908c607560ab3995dae
source=(git+https://github.com/benhoyt/inih.git#tag=${_tag})
b2sums=('e9d7bc8460dc0dd527416886bfcddda5675823f2b78236d6a582c91df89f7aec34d9340bf695e63f8d4a05637200aa4bc9c7617c2169d4186643370d48ff0882')

pkgver() {
  cd inih
  git describe --tags | sed 's/^r//'
}

build() {
  arch-meson inih build \
    -Ddefault_library=shared \
    -Ddistro_install=true \
    -Dwith_INIReader=true
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm 644 inih/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libinih/
}

# vim: ts=2 sw=2 et:
