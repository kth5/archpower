# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Cody P Schafer <archlinux@codyps.com>
# Contributor: John Eivind Helset <jehelset@gmail.com>

pkgname=debugedit
pkgver=5.2
pkgrel=1
pkgdesc='Tool to mangle source locations in .debug files'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://sourceware.org/debugedit/'
license=('GPL-3.0-or-later' 'GPL-2.0-or-later' 'LGPL-2.0-or-later')
depends=('glibc' 'libelf' 'xxhash')
source=("https://sourceware.org/ftp/debugedit/${pkgver}/debugedit-${pkgver}.tar.xz"{,.sig})
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A')
sha512sums=('0fe21d7576ca8ea8067f6afe5c02807ace77051249d8911531e6f9d077db59487ee29dfbdb5e9c80aebaa8bd22c6efe515d25a502d614bc058f24d174c7ebe4b'
            'SKIP')

prepare() {
    cd "debugedit-${pkgver}"
    autoreconf -fiv
}

build() {
    cd "debugedit-${pkgver}"
    ./configure --prefix=/usr
    make
}

package() {
    cd "debugedit-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
