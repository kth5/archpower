# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel LÃ¶bl <dloebl.2000@gmail.com>

pkgname=libcgif
pkgver=0.5.0
pkgrel=1
pkgdesc="A fast and lightweight GIF encoding library"
url="https://github.com/dloebl/cgif"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('MIT')
depends=('glibc')
makedepends=('meson')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('d6cb312c7da2c6c9f310811aa3658120c0316ba130c48a012e7baf3698920fe9')

build() {
	meson build "cgif-${pkgver}" --prefix=/usr
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "${pkgdir}"
	install -Dm 644 "cgif-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
