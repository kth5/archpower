# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Andre Schr√∂der <andre.schroedr at gmail dot com>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=bats
pkgver=1.11.0
pkgrel=2
pkgdesc='Bash Automated Testing System'
url='https://github.com/bats-core/bats-core'
arch=(any)
license=(MIT)
depends=(
  bash
  coreutils
  ncurses
  parallel
)
checkdepends=(
  procps-ng
)
provides=(bats-core)
replaces=(bash-bats)
source=(${url}/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('aeff09fdc8b0c88b3087c99de00cf549356d7a2f6a69e3fcec5e0e861d2f9063')
b2sums=('a86737e8f38e59031f926ce36e70417517095e2f34a9c6a8f1564879297dd097a44839343b66909e00338e53e8516a473e7b4bffc399166df87912e1e645809b')

prepare() {
  cd "bats-core-${pkgver}"
  # move everything to /usr/lib instead of /usr/libexec
  sed 's|libexec|lib|g' -i \
    bin/* \
    libexec/bats-core/* \
    lib/bats-core/* \
    test/*.bats \
    *install.sh
  mv -v libexec/bats-core/* lib/bats-core/
}

check() {
  cd "bats-core-${pkgver}"
  TERM=linux bin/bats --jobs "$(nproc)" --tap test
}

package() {
  cd "bats-core-${pkgver}"

  install -Dm 755 bin/bats -t "${pkgdir}/usr/bin"
  install -Dm 644 lib/bats-core/*.bash -t "${pkgdir}/usr/lib/bats-core"
  install -Dm 755 lib/bats-core/bats* -t "${pkgdir}/usr/lib/bats-core"

  install -Dm 644 man/bats.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 man/bats.7 -t "${pkgdir}/usr/share/man/man7"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
