# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

_pkg=orc
pkgname=apache-${_pkg}
pkgver=1.7.3
pkgrel=4
pkgdesc="Columnar storage for Hadoop workloads."
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://orc.apache.org"
license=(Apache)
depends=(lz4 protobuf snappy zlib zstd)
makedepends=(cmake gtest)
options=(!lto)
source=(https://archive.apache.org/dist/${_pkg}/${_pkg}-${pkgver}/${_pkg}-${pkgver}.tar.gz{,.asc}
        ${pkgname}-fix-ByteSize-Long.patch::https://github.com/apache/orc/commit/ffbd34115322cf3e0eb6963002c540ea449362d2.patch)
sha256sums=(535c4d7588172e85b8fc941cd0575d67f2155a55e4cd65d87d5b194b0bb28a31 SKIP
            cab14490e0cf8f229bb22b3d4269899a9c2522bcedba5878008b1900001d3b20)
validpgpkeys=(F28C9C925C188C35E345614DEDA00CE834F0FC5C) # Dongjoon Hyun (CODE SIGNING KEY) <dongjoon@apache.org>

prepare(){
  cd ${_pkg}-${pkgver}
  patch -p1 < ../${pkgname}-fix-ByteSize-Long.patch
  sed -i 's/orc STATIC/orc SHARED/' c++/src/CMakeLists.txt
  sed -i 's/target_link_libraries (orc_zlib INTERFACE ${ZLIB_STATIC_LIB}/target_link_libraries (orc_zlib INTERFACE ${ZLIB_LIBRARY}/' cmake_modules/ThirdpartyToolchain.cmake
}

build(){
  cmake -B build -S ${_pkg}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DLZ4_HOME="/usr" \
    -DPROTOBUF_HOME="/usr" \
    -DSNAPPY_HOME="/usr" \
    -DZLIB_HOME="/usr" \
    -DZSTD_HOME="/usr" \
    -DGTEST_HOME="/usr" \
    -DORC_PREFER_STATIC_PROTOBUF=OFF \
    -DORC_PREFER_STATIC_SNAPPY=OFF \
    -DORC_PREFER_STATIC_LZ4=OFF \
    -DORC_PREFER_STATIC_ZSTD=OFF \
    -DORC_PREFER_STATIC_ZLIB=OFF \
    -DBUILD_LIBHDFSPP=OFF \
    -DBUILD_JAVA=OFF \
    -DINSTALL_VENDORED_LIBS=OFF \
    -DSTOP_BUILD_ON_WARNING=OFF
  make -C build
}

check(){
  # Some tests failures https://github.com/apache/orc/issues/1068
  make -C build test-out || echo "Warning: Tests failed"
}

package(){
  make DESTDIR="${pkgdir}" -C build install
}
