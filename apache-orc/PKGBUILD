# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

_pkg=orc
pkgname=apache-${_pkg}
pkgver=1.9.2
pkgrel=1
pkgdesc="Columnar storage for Hadoop workloads."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://orc.apache.org"
license=(Apache)
depends=(lz4 protobuf snappy zlib zstd)
makedepends=(cmake gtest)
source=(https://dlcdn.apache.org/${_pkg}/${_pkg}-${pkgver}/${_pkg}-${pkgver}.tar.gz{,.asc})
sha256sums=('7f46f2c184ecefd6791f1a53fb062286818bd8710c3f08b94dd3cac365e240ee'
            'SKIP')
validpgpkeys=(F28C9C925C188C35E345614DEDA00CE834F0FC5C) # Dongjoon Hyun (CODE SIGNING KEY) <dongjoon@apache.org>

prepare(){
  cd ${_pkg}-${pkgver}
  sed -i 's/orc STATIC/orc SHARED/' c++/src/CMakeLists.txt
}

build(){
  case "${CARCH}" in
    powerpc|riscv64) CXXFLAGS+=' -latomic' ;;
  esac

  cmake -B build -S ${_pkg}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DSTOP_BUILD_ON_WARNING=OFF \
    -DLZ4_HOME="/usr" \
    -DPROTOBUF_HOME="/usr" \
    -DSNAPPY_HOME="/usr" \
    -DZLIB_HOME="/usr" \
    -DZSTD_HOME="/usr" \
    -DGTEST_HOME="/usr" \
    -DORC_PREFER_STATIC_PROTOBUF=OFF \
    -DORC_PREFER_STATIC_SNAPPY=OFF \
    -DORC_PREFER_STATIC_LZ4=OFF \
    -DORC_PREFER_STATIC_ZSTD=OFF \
    -DORC_PREFER_STATIC_ZLIB=OFF \
    -DBUILD_LIBHDFSPP=OFF \
    -DBUILD_JAVA=OFF \
    -DINSTALL_VENDORED_LIBS=OFF
  make -C build
}

check(){
  # Some tests failures https://github.com/apache/orc/issues/1068
  make -C build test-out || echo "Warning: Tests failed"
}

package(){
  make DESTDIR="${pkgdir}" -C build install
}
