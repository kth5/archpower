# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=xerces-c
pkgver=3.3.0
pkgrel=2
pkgdesc="A validating XML parser written in a portable subset of C++"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://xerces.apache.org/xerces-c/"
license=('APACHE')
depends=('gcc-libs' 'curl' 'libnsl' 'icu')
source=("https://archive.apache.org/dist/xerces/c/3/sources/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9555f1d06f82987fbb4658862705515740414fd34b4db6ad2ed76a2dc08d3bde')

build() {
  cd "${pkgname}-${pkgver}"

  [[ "${CARCH}" = "i686" ]] && SSE2="--disable-sse2"
  ./configure --prefix=/usr --sysconfdir=/etc ${SSE2}
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
