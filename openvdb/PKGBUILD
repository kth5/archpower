# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openvdb
pkgver=12.0.0
pkgrel=2
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/AcademySoftwareFoundation/openvdb'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('MPL-2.0')
depends=('boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc' 'log4cplus' 'imath')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python'
             'python-numpy' 'ninja' 'nanobind')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python-numpy: python module')
options=(!distcc)
case "${CARCH}" in
  powerpc) options+=(!debug !lto) ;;
esac
source=("$pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/openvdb/archive/v${pkgver}.tar.gz")
sha512sums=('7ea2997afa99ed1ed23422eb8b8420c7127c913432f94043ccf559b6720bba2f6e19376e955d8d9055ab765a821749936966f6e5925b9d36febaa724d866b90a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  local _pyversion=$(python -c "import sys; print(f'{sys.version_info.major}.{sys.version_info.minor}')")

  BUILD_DOCS=ON
  case "${CARCH}" in
    powerpc) CXXFLAGS+=' -latomic'; BUILD_DOCS=OFF ;;
    riscv64) CXXFLAGS+=' -latomic'; BUILD_DOCS=OFF ;;
  esac

  cmake \
    -Bbuild \
    -GNinja \
    -DUSE_IMATH_HALF=ON \
    -DUSE_NUMPY=ON \
    -DUSE_LOG4CPLUS=ON \
    -DNB_DIR=/usr/lib/python${_pyversion}/site-packages/nanobind \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=${BUILD_DOCS} \
    -DOPENVDB_BUILD_UNITTESTS=OFF

  ninja -C build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  DESTDIR="${pkgdir}" ninja install
  # Try to workaround FS#72653 for now
  mkdir -p "${pkgdir}"/usr/share/cmake/Modules/
  ln -s /usr/lib/cmake/OpenVDB/{OpenVDBUtils,FindOpenVDB,FindBlosc}.cmake "${pkgdir}"/usr/share/cmake/Modules/
}

# vim:set sw=2 sts=2 et:
