# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: SÃ©bastien Luttringer

pkgname=libnftnl
pkgver=1.3.1
pkgrel=1
pkgdesc='Netfilter library providing interface to the nf_tables subsystem'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://netfilter.org/projects/libnftnl/'
license=('GPL-2.0-or-later')
depends=('libmnl')
makedepends=('git')
validpgpkeys=('8C5F7146A1757A65E2422A94D70D1A666ACF2B21') # Netfilter Core Team
source=("git+https://git.netfilter.org/libnftnl.git#tag=${pkgname}-${pkgver}?signed")
sha256sums=('2a52dfe77129ff5997df5cbb2992b48d9d5e571dd72f59cfe8d18d359ff0939e')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
