# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=zvbi
pkgver=0.2.43
pkgrel=1
pkgdesc='VBI capture and decoding library'
url=http://zapping.sourceforge.net/cgi-bin/view/ZVBI/WebHome
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(
  libpng
  libx11
)
makedepends=(git)
license=(
  BSD
  GPL2
  LGPL2.1
  MIT
)
_tag=66a1c9bdf3a7f105a5c02070cc6de0d949513c58
#source=(git+https://github.com/zapping-vbi/zvbi.git?signed#tag=${_tag})
source=(git+https://github.com/zapping-vbi/zvbi.git#tag=${_tag})
b2sums=('ca6100f51f17820a974f4eacb922204e7b373261ee43dfb1d99b0eeea0241c94e3d42a32c073273fbe8da45d2faaf9266a72a7d56d506f027054409e7c9bfd93')
validpgpkeys=(FA26CA784BE188927F22B99F6570EA01146F7354) # Ileana Dumitrescu <ileanadumitrescu95@gmail.com>

prepare() {
  cd zvbi
  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd zvbi
  git describe --tags | sed 's/^v//'
}

build() {
  cd zvbi
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin
  make
}

package() {
  cd zvbi
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING.md -t "${pkgdir}"/usr/share/licenses/zvbi
}

# vim: ts=2 sw=2 et:
