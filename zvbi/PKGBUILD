# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=zvbi
pkgver=0.2.44
pkgrel=1
pkgdesc='VBI capture and decoding library'
url=http://zapping.sourceforge.net/cgi-bin/view/ZVBI/WebHome
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
depends=(
  libpng
  libx11
)
makedepends=(git)
license=(
  BSD
  GPL2
  LGPL2.1
  MIT
)
_tag=5169a428d51c3ae8ff7b0897e8a687d8e05e37b5
#source=(git+https://github.com/zapping-vbi/zvbi.git?signed#tag=${_tag})
source=(git+https://github.com/zapping-vbi/zvbi.git#tag=${_tag})
b2sums=('518476fa9dd0e924d1b560bd28b558d01692f55e09065b746fca208734c9bfd8a9a33e86810a1bb244c2297695c8ff3d1e587a0e6fe53afe110354cdae6e9fc4')
validpgpkeys=(FA26CA784BE188927F22B99F6570EA01146F7354) # Ileana Dumitrescu <ileanadumitrescu95@gmail.com>

prepare() {
  cd zvbi
  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd zvbi
  git describe --tags | sed 's/^v//'
}

build() {
  cd zvbi
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin
  make
}

package() {
  cd zvbi
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING.md -t "${pkgdir}"/usr/share/licenses/zvbi
}

# vim: ts=2 sw=2 et:
