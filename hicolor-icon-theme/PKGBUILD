# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=hicolor-icon-theme
pkgver=0.18
pkgrel=1
pkgdesc="Freedesktop.org Hicolor icon theme"
url="https://gitlab.freedesktop.org/xdg/default-icon-theme"
arch=(any)
license=(GPL-2.0-or-later)
makedepends=(
  git
  meson
)
source=("$pkgname::git+$url.git?signed#tag=v$pkgver")
b2sums=('d3a46022453e0301fa4dd8b76e3c761ef256e27a380ff1d87cc30d26c6745fc7403eaace80590bfaa98f8f58ddf39959b85f05e76c049fa7634d7e79165c7967')
validpgpkeys=(
  D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB # Matthias Klumpp <matthias@tenstral.net>
)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
