# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lynx
pkgver=2.9.0
pkgrel=2
pkgdesc='A text browser for the World Wide Web'
url='https://lynx.invisible-island.net/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-only)
depends=(brotli
         bzip2
         glibc
         libidn2
         ncurses
         openssl
         zlib)
backup=(etc/lynx.cfg)
options=(!lto)
_archive="$pkgname$pkgver"
source=("https://invisible-mirror.net/archives/lynx/tarballs/$_archive.tar.bz2"{,.asc})
sha256sums=('5bcae5e2e6043ca7b220963a97763c49c13218d849ffda6be7739bfd5a2d36ff'
            'SKIP')
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # Thomas Dickey <dickey@invisible-island.net>

build() {
	cd "$_archive"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-ssl=/usr \
		--enable-externs \
		--enable-nls \
		--enable-ipv6 \
		--mandir=/usr/share/man
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
	# FS#20404 - points to local help
	sed -i -e "s|^HELPFILE.*$|HELPFILE:file:///usr/share/doc/lynx/lynx_help/lynx_help_main.html|" "${pkgdir}/etc/lynx.cfg"
	install -d "$pkgdir/usr/share/doc/lynx"
	cp -rf lynx_help "$pkgdir/usr/share/doc/lynx"
}
