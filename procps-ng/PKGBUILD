# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Christian Hesse <eworm@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=procps-ng
pkgver=4.0.5
pkgrel=2
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=(GPL LGPL)
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
depends=(glibc ncurses libncursesw.so systemd-libs)
makedepends=(gettext git po4a systemd)
conflicts=(procps sysvinit-tools)
provides=(procps sysvinit-tools libproc2.so)
replaces=(procps sysvinit-tools)
options=('!emptydirs')
validpgpkeys=('5D2FB320B825D93904D205193938F96BDF50FEA5') # Craig Small <csmall@debian.org>
source=("git+https://gitlab.com/procps-ng/procps.git#tag=v${pkgver}?signed"
        '0001-fix-breakage-in-unhex.patch')
sha256sums=('640ad0085ee013efb94877072cfa7c0c025884dc8cc398e331b7efb6ebaefc01'
            '5386ca099f38dc1c0e0521f193896ff38c6c7a3855090ff4e245f1d6d165708d')
b2sums=('4f5d23ecd02434e83896e93840dddbe0896e321c7cb2294a54d280abf03b87302489ef7d99c56476952d1364eb4acfe7715b06b2b56a956373e037b0a273111b'
        '80c67d37768c56e9c9eff24ed51ffef115cdba8ed670294ac02241b752167fc595ecb7cfebfb203dc9ba2891350c4921660f694cd1acea49fb2f55d521ee99b2')

prepare() {
  cd procps

  # fix breakage in unhex
  # https://gitlab.com/procps-ng/procps/-/issues/369
  # https://gitlab.com/procps-ng/procps/-/merge_requests/247
  patch -Np1 < ../0001-fix-breakage-in-unhex.patch

  ./autogen.sh
}

build() {
  cd procps

  ./configure \
    --prefix=/usr \
    --exec-prefix=/ \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --enable-watch8bit \
    --with-systemd \
    --disable-modern-top \
    --disable-kill

  make
}

package() {
  cd procps

  make DESTDIR="$pkgdir" install
}
