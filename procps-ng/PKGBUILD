# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Christian Hesse <eworm@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=procps-ng
pkgver=4.0.6
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=(GPL LGPL)
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
depends=(glibc ncurses libncursesw.so systemd-libs)
makedepends=(gettext git po4a systemd)
conflicts=(procps sysvinit-tools)
provides=(procps sysvinit-tools libproc2.so)
replaces=(procps sysvinit-tools)
options=('!emptydirs')
validpgpkeys=('5D2FB320B825D93904D205193938F96BDF50FEA5') # Craig Small <csmall@debian.org>
source=("git+https://gitlab.com/procps-ng/procps.git#tag=v${pkgver}?signed")
sha256sums=('160aa30e2a133f44e7cc6e90c16cc018360a786f043aef84614ce595062fc894')
b2sums=('fe36c3c3c38882e09c47f7865015c5f8fc82e7432cb427ca21ad3712a112025daeba395b118ba81245727443407fb6666cf4ffea1f2ce1f605a8c5bbe8508db2')

prepare() {
  cd procps

  ./autogen.sh
}

build() {
  cd procps

  ./configure \
    --prefix=/usr \
    --exec-prefix=/ \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --enable-watch8bit \
    --with-systemd \
    --disable-modern-top \
    --disable-kill

  make
}

package() {
  cd procps

  make DESTDIR="$pkgdir" install
}
