# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=tracker3
pkgname=(
  tracker3
  tracker3-docs
)
pkgver=3.5.3
pkgrel=2
pkgdesc="Desktop-neutral user information store, search tool and indexer"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL)
depends=(
  glib2
  icu
  json-glib
  libsoup3
  libstemmer
  libxml2
  sqlite
)
makedepends=(
  asciidoc
  bash-completion
  dbus
  gi-docgen
  git
  gobject-introspection
  libsoup
  meson
  python-dbus
  python-gobject
  python-tappy
  systemd
  vala
)
_commit=d56637112f027aec9fcf069e1ce9218460f64e99  # tags/3.5.3^0
source=(
  "git+https://gitlab.gnome.org/GNOME/tracker.git#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/gvdb.git"
  0001-tests-Adapt-FTS-snippet-tests-to-run-before-after-SQ.patch
)
b2sums=('SKIP'
        'SKIP'
        '8c44e537e3e29e68ddb28fa125e8321255c10122cab080b2ba63bea3c6af07e699e9b9b4085b8fb5939e832e64931f4fad9e1eebf79f0beb29d652f872811f67')

pkgver() {
  cd tracker
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd tracker

  # Fix tests with SQLite 3.42
  # https://gitlab.gnome.org/GNOME/tracker/-/merge_requests/600
  git apply -3 ../0001-tests-Adapt-FTS-snippet-tests-to-run-before-after-SQ.patch

  git submodule init
  git submodule set-url subprojects/gvdb "$srcdir/gvdb"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D tests_tap_protocol=true
  )

  arch-meson tracker build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs -t 3
}

package_tracker3() {
  optdepends=('libsoup: Alternative remoting backend')
  provides=(libtracker-sparql-3.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p docs/usr/share
  mv {"$pkgdir",docs}/usr/share/doc
}

package_tracker3-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
