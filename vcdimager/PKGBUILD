# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Dan Johansen <strit@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=vcdimager
pkgver=2.0.1
pkgrel=7
pkgdesc="A full-featured mastering suite for authoring disassembling and analyzing Video CD's and Super Video CD's"
url="https://www.gnu.org/software/vcdimager/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL-2.0-or-later')
depends=(libcdio libxml2 popt)
source=(
  https://ftp.gnu.org/gnu/vcdimager/$pkgname-$pkgver.tar.gz{,.sig}
  libxml214.diff
)
sha256sums=('67515fefb9829d054beae40f3e840309be60cda7d68753cafdd526727758f67a'
            'SKIP'
            'a2fcf16c20df908889f8bc4287666dec54d1e2921a15d6edea5625c9579e8172')
validpgpkeys=(DAA63BC2582034A02B923D521A8DE5008275EC21) # R. Bernstein <rocky@panix.com>

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../libxml214.diff
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
