# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: John Sowiak <john@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

# For legal reasons, we can't separately package the Optix headers so we'll
# just build the package against them. I checked with NVIDIA and this way is
# fine with them.

pkgname=blender
pkgver=2.93.2
pkgrel=1
epoch=17
pkgdesc="A fully integrated 3D graphics creation suite"
arch=(x86_64 powerpc64le)
license=('GPL')
url="https://www.blender.org"
depends=('libpng' 'libtiff' 'openexr' 'python' 'desktop-file-utils' 'python-requests' 'potrace'
         'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils' 'glew' 'openjpeg2' 'python-numpy'
         'freetype2' 'openal' 'ffmpeg' 'fftw' 'boost-libs' 'opencollada' 'alembic' 'openxr'
         'openimageio' 'libsndfile' 'jack' 'opencolorio' 'openshadinglanguage'
         'jemalloc' 'libspnav' 'ptex' 'opensubdiv' 'openvdb' 'log4cplus' 'sdl2')
makedepends=('cmake' 'boost' 'mesa' 'git' 'llvm' 'ninja')
options=(!strip)
source=("git+https://git.blender.org/blender.git#tag=v$pkgver"
        "git+https://git.blender.org/blender-addons.git"
        "git+https://git.blender.org/blender-addons-contrib.git"
        "git+https://git.blender.org/blender-translations.git"
        "git+https://git.blender.org/blender-dev-tools.git"
        https://developer.download.nvidia.com/redist/optix/v7.0/OptiX-7.0.0-include.zip
        blender-openexr3.patch
        altivec-all.patch)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'b2cff73def3757d4259f4b4d318a8ccfe166bf7c215cbb2124f1c81bd6e742f96207285b24eb4d99b527b7b97dc6d5e8fdf2f16d78d5d1e2684c26d681328491'
            'e2dd7210d26b70445e9ae8e33dea35111ad0b0aca9d3b4ded3df560d0fcc8dc044868f729e0f090a04b6f316f083b5505a7056ce088e8702065add87b9b457e5'
            'dd87d718fb7189c7d03c2e5f3e3f02f956afef39d77b42ee6ce17ba3ac4e05ec6b9ad5be941c1d9c36f3a9e88ba523ec280ca27148a32203d74ff74c8d0cbf00')

case "${CARCH}" in
  x86_64)
    # these are conveniently x86 only, thanks Intel!
    depends+=('embree' 'openimagedenoise')
    # no support for Cuda on non-x86 yet
    makedepends+=('cuda')
    optdepends=('cuda: cycles renderer cuda support')
    CMAKE_EXTRA_FLAGS="-DWITH_CYCLES_EMBREE=ON"
    ;;
  # the rest of the world
  *)
    CMAKE_EXTRA_FLAGS="-DWITH_CYCLES=OFF -DWITH_CYCLES_EMBREE=OFF -DWITH_CYCLES_DEVICE_OPTIX=OFF"
esac


prepare() {
  cd "$pkgname"

  git submodule init
  git config submodule."release/scripts/addons".url "${srcdir}/blender-addons"
  git config submodule."release/scripts/addons_contrib".url "${srcdir}/blender-addons-contrib"
  git config submodule."release/datafiles/locale".url "${srcdir}/blender-translations"
  git config submodule."source/tools".url "${srcdir}/blender-dev-tools"
  git submodule update
  git submodule foreach git checkout v${pkgver}

  patch -p1 < "$srcdir"/blender-openexr3.patch # Fix build with OpenEXR 3

  patch -p1 < "$srcdir"/altivec-all.patch
}

build() {
  cd "$pkgname"

  local PYTHON_VER=3.9
 
  cmake . \
    -Bbuild \
    -GNinja \
    -Cbuild_files/cmake/config/blender_release.cmake \
    -DOPTIX_ROOT_DIR="$srcdir"/include \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_INSTALL_PORTABLE=OFF \
    -DWITH_PYTHON_INSTALL=OFF \
    -DPYTHON_LIBPATH=/usr/lib \
    -DPYTHON_LIBRARY=python$PYTHON_VER \
    -DPYTHON_INCLUDE_DIRS=/usr/include/python$PYTHON_VER \
    -DCMAKE_CXX_FLAGS="-I /usr/include/python$PYTHON_VER" ${CMAKE_EXTRA_FLAGS}
  ninja -C build ${MAKEFLAGS}
}

package() {
  cd "$pkgname"

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm755 release/bin/blender-softwaregl "${pkgdir}/usr/bin/blender-softwaregl"
  python -m compileall "${pkgdir}/usr/share/blender"
  python -O -m compileall "${pkgdir}/usr/share/blender"

  install -Dm644 release/freedesktop/org.blender.Blender.appdata.xml "${pkgdir}/usr/share/metainfo/org.blender.Blender.appdata.xml"

  # Manually install additional scripts (See FS#69351)
  cp -r release/scripts/addons_contrib/* "${pkgdir}"/usr/share/blender/2*/scripts/addons_contrib/
}
