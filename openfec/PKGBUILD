# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openfec
pkgver=1.4.2.12
pkgrel=1
pkgdesc="Open Forward Erasure Codecs"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="http://openfec.org/"
license=(
  CECILL-2.0
  CECILL-C
)
depends=(glibc)
makedepends=(cmake)
provides=(libopenfec.so)
# NOTE: we are using roc-streaming's version, because the original upstream is effectively dead:
# https://github.com/roc-streaming/openfec
source=(https://github.com/roc-streaming/openfec/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('8228dffe7be5ef687d1d38775eae077fd0f7011099580b9cdfc0963704ee7b1332f73e911f244d20876578a394773d2eaa51d54f5851905d7c99ba0c117da295')
b2sums=('3a53ee5330398d22b4f98038a4f9d0285b375f389e0fd80ed7e01f3fcb0e15fc78008dde98f33786e04a2707290a3ae39915f1b06ae686a0c5fe01fa76faa8fd')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DBUILD_SHARED_LIBS=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{LICENCE,Licence}* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
