# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux at free.fr>
# Contributor: Bartlomiej Piotrowski <nospam at bpiotrowski dot pl>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=discount
pkgver=3.0.0.d
_pkgver=3.0.0d
pkgrel=1
pkgdesc='A Markdown implementation written in C'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.pell.portland.or.us/~orc/Code/discount/'
license=(BSD-3-Clause)
source=(https://github.com/Orc/discount/archive/v$_pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('0ed8cc27ac5d46dc6a8beedd5e5673ac8b466a6474bdb7d35f37c815f670385f')
depends=(glibc)

build() {
  cd $pkgname-$_pkgver
  ./configure.sh \
    --prefix=/usr \
    --enable-all-features \
    --with-fenced-code \
    --shared
  make
}

package() {
  cd $pkgname-$_pkgver
  # fix ldconfig call
  sed -e 's|/sbin/ldconfig|/sbin/ldconfig -n|' -i librarian.sh

  make DESTDIR="$pkgdir" install.everything
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
