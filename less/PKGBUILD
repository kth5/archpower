# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=less
pkgver=685
pkgrel=1
epoch=1
pkgdesc='A terminal based program for viewing text files'
license=('GPL-3.0-or-later OR LicenseRef-Less')
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.greenwoodsoftware.com/less/'
depends=(
  glibc
  ncurses 
  pcre2)
validpgpkeys=('AE27252BD6846E7D6EAE1DD6F153A7C833235259') # Mark Nudelman
source=(
  https://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.sig::https://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.sig
  )
b2sums=('771ba2fb1f593e80d9e5e203bfe9b7a8a33833fcd3464a0c64c4056359e9ecc723eff87b783c9ab4c3090d7dac1973c1875eb7d926601c512dc0494e7e8adb5c'
        'SKIP')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $pkgname-$pkgver
  sh configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 {,"$pkgdir"/usr/share/licenses/$pkgname/}LICENSE
}

# vim:set ts=2 sw=2 et:
