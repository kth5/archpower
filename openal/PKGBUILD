# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgbase=openal
pkgname=(
  openal
  openal-examples
)
pkgver=1.23.1
pkgrel=3
pkgdesc="Cross-platform 3D audio library, software implementation"
url="https://github.com/kcat/openal-soft"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  alsa-lib
  cmake
  ffmpeg
  fluidsynth
  git
  jack
  libmysofa
  libpulse
  libsndfile
  ninja
  pipewire
  portaudio
  qt5-base
  sdl2
)
source=("git+https://github.com/kcat/openal-soft#tag=$pkgver")
b2sums=('5bf1c0c39b65de526ea5b4cf7a28ec72605d6ab2c97b8be2da462d6bec057c891890c88a77c441cff2c33667de3307a5db1016e2b5de34911dcc5bc492517011')

prepare() {
  cd openal-soft
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_LIBDIR=lib
  )

  cmake -S openal-soft -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_openal() {
  optdepends=(
    'qt5-base: alsoft-config GUI Configurator'
    'fluidsynth: MIDI rendering'
    'libmysofa: makemhr tool'
  )
  provides+=(libopenal.so)

  DESTDIR="$pkgdir" cmake --install build
  install -Dt "$pkgdir/usr/share/doc/openal" -m644 openal-soft/docs/*

  cd "$pkgdir"
  _pick examples usr/bin/al{ffplay,hrtf,latency,loopback,multireverb}
  _pick examples usr/bin/al{play,record,reverb,stream,tonegen}
}

package_openal-examples() {
  pkgdesc+=" (example programs)"
  depends+=(
    "openal=$pkgver-$pkgrel"
    ffmpeg
    libsndfile
    sdl2
  )

  mv examples/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
