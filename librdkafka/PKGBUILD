# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Andrew Sun <adsun701 at gmail dot com>
# Contributor: Joel Teichroeb <joel at teichroeb dot net>
# Contributor: Alim Gokkaya <alimgokkaya at gmail dot com>

pkgname=librdkafka
pkgver=2.6.1
pkgrel=1
pkgdesc='The Apache Kafka C/C++ library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/confluentinc/librdkafka'
license=(
  Apache-2.0
  BSD-2-Clause
  BSD-3-Clause
  MIT
  Zlib
)
depends=(
  curl
  gcc-libs
  glibc
  libsasl
  lz4
  openssl
  zlib
  zstd
)
makedepends=(
  cmake
  python
  rapidjson
)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  "remove-failing-tests.patch"
)
sha256sums=('0ddf205ad8d36af0bc72a2fec20639ea02e1d583e353163bf7f4683d949e901b'
            '23c8d030179724d13a91f13cf566b045b195d64a3219eb2c5122d06e1e1c6fd1')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir/remove-failing-tests.patch"
}

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure \
    --tests-regex RdKafkaTestBrokerLess
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
