# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=gn
pkgver=0.2174.b3a0bff4
_commit=feafd1012a32c05ec6095f69ddc3850afb621f3a
pkgrel=1
pkgdesc="Meta-build system that generates build files for Ninja"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gn.googlesource.com/gn/"
license=('BSD')
depends=('gcc-libs')
makedepends=('clang' 'ninja' 'python' 'git')
source=(git+https://gn.googlesource.com/gn#commit=$_commit)
sha256sums=('52992234bd8cee2a70f6e4920fb3131b3acf095fdafb90a15804685c613586b4')

pkgver() {
  cd $pkgname
  echo 0.$(git rev-list --count initial-commit..).$(git rev-parse --short=8 HEAD)
}

build() {
  cd $pkgname
  # works around:
  #   cc1plus: error: ‘-Wformat-security’ ignored without ‘-Wformat’ [-Werror=format-security]
  export CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  unset CFLAGS
  ./build/gen.py --allow-warnings
  ninja -C out
}

check() {
  cd $pkgname
  ./out/gn_unittests
}

package() {
  cd $pkgname
  install -D out/gn "$pkgdir/usr/bin/gn"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/*
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -r misc/vim/{autoload,ftdetect,ftplugin,syntax} \
    "$pkgdir/usr/share/vim/vimfiles/"
  install -Dm644 -t "$pkgdir/usr/share/emacs/site-lisp" misc/emacs/gn-mode.el
}

# vim:set ts=2 sw=2 et:
