# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=gn
pkgver=0.2238.a84e399f
_commit=a84e399f9999003472192286abc03d752c9cb1f0
pkgrel=1
pkgdesc="Meta-build system that generates build files for Ninja"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://gn.googlesource.com/gn/"
license=('BSD-3-Clause')
depends=('gcc-libs')
makedepends=('clang' 'ninja' 'python' 'git')
source=(git+https://gn.googlesource.com/gn#commit=$_commit)
sha256sums=('209c34c157d181ba81b78c1780657474768fac6ac6b37311f3722c6d7ced5732')

pkgver() {
  cd $pkgname
  echo 0.$(git rev-list --count initial-commit..).$(git rev-parse --short=8 HEAD)
}

build() {
  cd $pkgname
  # works around:
  #   cc1plus: error: ‘-Wformat-security’ ignored without ‘-Wformat’ [-Werror=format-security]
  export CXXFLAGS=${CXXFLAGS/-Werror=format-security/}
  unset CFLAGS

  ./build/gen.py --allow-warnings
  ninja -C out
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd $pkgname
  ./out/gn_unittests
}

package() {
  cd $pkgname
  install -D out/gn "$pkgdir/usr/bin/gn"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/*
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -r misc/vim/{autoload,ftdetect,ftplugin,syntax} \
    "$pkgdir/usr/share/vim/vimfiles/"
  install -Dm644 -t "$pkgdir/usr/share/emacs/site-lisp" misc/emacs/gn-mode.el
}

# vim:set ts=2 sw=2 et:
