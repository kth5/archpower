# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=gn
pkgver=0.2042.2ecd43a1
_commit=2ecd43a10266bd091c98e6dcde507c64f6a0dad3
pkgrel=1
pkgdesc="Meta-build system that generates build files for Ninja"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://gn.googlesource.com/gn/"
license=('BSD')
depends=('gcc-libs')
makedepends=('clang' 'ninja' 'python' 'git')
source=(git+https://gn.googlesource.com/gn#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  echo 0.$(git rev-list --count initial-commit..).$(git rev-parse --short=8 HEAD)
}

build() {
  cd $pkgname
  ./build/gen.py
  ninja -C out
}

check() {
  # skip for powerpc
  [ "${CARCH}" == 'powerpc' ] && return 0

  cd $pkgname
  ./out/gn_unittests
}

package() {
  cd $pkgname
  install -D out/gn "$pkgdir/usr/bin/gn"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/*
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -r misc/vim/{autoload,ftdetect,ftplugin,syntax} \
    "$pkgdir/usr/share/vim/vimfiles/"
  install -Dm644 -t "$pkgdir/usr/share/emacs/site-lisp" misc/emacs/gn-mode.el
}

# vim:set ts=2 sw=2 et:
