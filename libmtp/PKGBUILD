# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: damir <damir@archlinux.org>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=libmtp
pkgver=1.1.22
pkgrel=1
pkgdesc='Library implementation of the Media Transfer Protocol'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://libmtp.sourceforge.net'
license=(LGPL-2.1-or-later)
depends=(glibc
         libgcrypt
         libusb)
makedepends=(doxygen)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('c3fcf411aea9cb9643590cbc9df99fa5fe30adcac695024442973d76fa5f87bc'
            'SKIP')
validpgpkeys=(7C4AFD61D8AAE7570796A5172209D6902F969C95) # Marcus Mei√üner <meissner@suse.de>

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --with-udev=/usr/lib/udev
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

# Install man page
  install -Dm644 doc/man/man3/*.3 -t "$pkgdir"/usr/share/man/man3/
}
