# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: damir <damir@archlinux.org>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=libmtp
pkgver=1.1.19
pkgrel=2
pkgdesc='Library implementation of the Media Transfer Protocol'
arch=(x86_64 powerpc64le powerpc riscv64)
url='http://libmtp.sourceforge.net'
license=(LGPL)
depends=(libusb)
makedepends=(doxygen)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('deb4af6f63f5e71215cfa7fb961795262920b4ec6cb4b627f55b30b18aa33228'
            'SKIP')
validpgpkeys=(7C4AFD61D8AAE7570796A5172209D6902F969C95) # Marcus Mei√üner <meissner@suse.de>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

# Install man page
  install -Dm644 doc/man/man3/*.3 -t "$pkgdir"/usr/share/man/man3/
}
