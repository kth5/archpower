# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=strip-nondeterminism
pkgver=1.15.0
pkgrel=2
pkgdesc="Tool for stripping bits of non-deterministic information from files"
url="https://salsa.debian.org/reproducible-builds/strip-nondeterminism"
arch=(any)
license=('GPL-3.0-or-later')
depends=('perl' 'perl-archive-zip' 'perl-archive-cpio' 'perl-sub-override')
makedepends=('git')
options=('!emptydirs')
source=("${url}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "https://salsa.debian.org/reproducible-builds/reproducible-lfs/raw/master/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2.asc")
sha256sums=('cde5567a8e20f3f31ee25132d38f5803efc47c05551d321d1dd1e7666a780a07'
            'SKIP')
b2sums=('f2236837fc782aae458b323ba79fecabb9d715144b5a49b45b08f1fd3a12d9700647278e78bdb6a7c5fb2e1cd9d7ce0734c167144dd41b204a94d6ee614b349e'
        'SKIP')
validpgpkeys=('C2FE4BD271C139B86C533E461E953E27D4311E58'  # Chris Lamb <lamby@debian.org>
              'EF5D84C1838F2EB6D8968C0410378EFC2080080C'  # Andrew Ayer <agwa@andrewayer.name>
              '2F5DAF3FC1F793D94F3D900CA721DA055374AA4F'  # Reiner Herrmann <reiner@reiner-h.de>
              'B8BF54137B09D35CF026FE9D091AB856069AAA1C'  # Holger Levsen <holger@layer-acht.org>
              '66AE2B4AFCCF3F52DA184D184B043FCDB9444540') # Mattia Rizzolo <mattia@debian.org>

prepare() {
	cd "${pkgname}-${pkgver}"
	sed -r 's|bin/dh_strip_nondeterminism ||g' -i Makefile.PL
}

build() {
	cd "${pkgname}-${pkgver}"
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	# Don't install the internal handler documentation generated from pod documents
	# https://salsa.debian.org/reproducible-builds/strip-nondeterminism/-/commit/b9b84287c654bd14d6489282475868977dec50c6
	rm -r "${pkgdir}/usr/share/man/man3"
}
