# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=strip-nondeterminism
pkgver=1.14.1
pkgrel=2
pkgdesc="Tool for stripping bits of non-deterministic information from files"
url="https://salsa.debian.org/reproducible-builds/strip-nondeterminism"
arch=(any)
license=('GPL-3.0-or-later')
depends=('perl' 'perl-archive-zip' 'perl-archive-cpio' 'perl-sub-override')
makedepends=('git')
options=('!emptydirs')
source=("${url}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "https://salsa.debian.org/reproducible-builds/reproducible-lfs/raw/master/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2.asc")
sha256sums=('149e5e7585cd1d8e777564d5772fb1afa5ed7be4a049c52ffc3a31de2bc04b93'
            'SKIP')
b2sums=('ff7f7e6987031b92fa03785252c6042c56ce5114705d02d0f00a6e5189c30670baea10d2ac71f38c95d9efd8c6600fd10307249e9a232a4728f94382a9c93d88'
        'SKIP')
validpgpkeys=('C2FE4BD271C139B86C533E461E953E27D4311E58'  # Chris Lamb <lamby@debian.org>
              'EF5D84C1838F2EB6D8968C0410378EFC2080080C'  # Andrew Ayer <agwa@andrewayer.name>
              '2F5DAF3FC1F793D94F3D900CA721DA055374AA4F'  # Reiner Herrmann <reiner@reiner-h.de>
              'B8BF54137B09D35CF026FE9D091AB856069AAA1C'  # Holger Levsen <holger@layer-acht.org>
              '66AE2B4AFCCF3F52DA184D184B043FCDB9444540') # Mattia Rizzolo <mattia@debian.org>

prepare() {
	cd "${pkgname}-${pkgver}"
	sed -r 's|bin/dh_strip_nondeterminism ||g' -i Makefile.PL
}

build() {
	cd "${pkgname}-${pkgver}"
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	# Don't install the internal handler documentation generated from pod documents
	# https://salsa.debian.org/reproducible-builds/strip-nondeterminism/-/commit/b9b84287c654bd14d6489282475868977dec50c6
	rm -r "${pkgdir}/usr/share/man/man3"
}
