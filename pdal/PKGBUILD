# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.5.5
pkgrel=1
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'jsoncpp' 'libunwind')
makedepends=('cmake' 'python' 'python-numpy' 'ninja' 'postgresql-libs')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.bz2")
sha512sums=('a6bd1157f44a6f91ab44b3dc4d9318a070cdaaa9816aafb3f9aadfc72d15cf3ba54c0f37a9397f24702fcaec42a6dcd34f17130d8cc3609d718547cb559261d9')

build() {
  cd "PDAL-$pkgver-src/"

  case "${CARCH}" in
    powerpc64)
      CFLAGS='-O2 -pipe'
      CXXFLAGS='-O2 -pipe'
    ;;
  esac

  cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
      -DWITH_COMPLETION=ON

  ninja -C build
}

package() {
  cd "PDAL-$pkgver-src"

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
