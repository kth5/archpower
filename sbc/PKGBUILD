# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=sbc
pkgver=2.1
pkgrel=1
pkgdesc="Bluetooth Subband Codec (SBC) library"
url="https://git.kernel.org/pub/scm/bluetooth/sbc.git"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(
  GPL-2.0-or-later # programs
  LGPL-2.1-or-later # library
)
depends=(glibc)
makedepends=(git)
provides=(libsbc.so)
source=("git+$url#tag=$pkgver")
b2sums=('b40f82ead849cb047c1a95d76f42be8d03756bc0e400282a840543cd9c0fad2fa49229b64915f6e2e13123511e19cb009dd3e09144e4f6e413403873cafb624f')

prepare() {
  cd sbc
  autoreconf -fvi
}

build() {
  cd sbc
  ./configure --prefix=/usr --disable-static --disable-tester
  make
}

check() {
  cd sbc
  make check
}

package() {
  cd sbc
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
