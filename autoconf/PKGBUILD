# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=autoconf
pkgver=2.72
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=(any)
license=('GPL2' 'GPL3' 'custom')
url="https://www.gnu.org/software/autoconf"
depends=('awk' 'm4' 'diffutils' 'perl' 'sh')
checkdepends=('gcc-fortran')
source=(
	"https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig}
)
sha256sums=('ba885c1319578d6c94d46e9b0dceb4014caafe2490e437a0dbca3f270a223f5a'
            'SKIP')
validpgpkeys=('82F854F3CE73174B8B63174091FCC32B6769AA64')  # Zack Weinberg

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

check() {
	cd "${pkgname}-${pkgver}"
	# disable test 227 as it is broken
	make check TESTSUITEFLAGS="1-226 228-"
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# license exception
	install -Dm644 COPYING.EXCEPTION "$pkgdir"/usr/share/licenses/autoconf/COPYING.EXCEPTION

	# remove unwanted file
	rm -f "$pkgdir"/usr/share/info/standards.info
}
