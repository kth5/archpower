# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Rémy Oudompheng <oudomphe@clipper.ens.fr>

pkgname=libspiro
epoch=1
pkgver=20240903
pkgrel=1
pkgdesc='Library that simplifies the drawing of beautiful curves'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/fontforge/libspiro'
license=(GPL-3.0-or-later)
depends=(glibc)
makedepends=(git)
source=("git+https://github.com/fontforge/libspiro.git#tag=$pkgver")
b2sums=(6ec78aec4580a9f338d03ede7f028af5d60b9c572f46e2b068a085178617d042b10bce09079e45eb69f97b27e8fb38305ece538640b6f285977d57c8184bde3e)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
