# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cowsql
pkgver=1.15.9
pkgrel=1
pkgdesc='An embeddable and replicated SQLite engine with high availability & automatic failover'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/cowsql/cowsql'
license=('LGPL-3.0-only' 'LicenseRef-Custom')
depends=('glibc' 'libuv' 'raft' 'sqlite')
makedepends=('git' 'tcl' 'readline' 'zlib')
options=('strip')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('7067706d212e3521dec3d5c5fe5350ec2e96e500c8d33a03ba9b17a71d644b252e8b4330ae84df2f7f2f48bdfd98b7b11056af3802f62bec46048285ea7adc8f')
b2sums=('621b22fa3abdafeb2615185bf6dd65ab0f2b5acca9ec517c43681722e9501dc25b97d6aff33a21352a68aec2a8230478ce5432da661e91b2398e9f4b755eeb96')

build() {
  cd "$pkgname"

  autoreconf -i

  ./configure \
    --prefix=/usr \
    --enable-static=no

  make
}

check() {
  cd "$pkgname"

  make check || true
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
