# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
# LuaJIT has abandoned versioned releases and now advises using git HEAD
# https://github.com/LuaJIT/LuaJIT/issues/665#issuecomment-784452583
_commit=51fb2f2c3af778f03258fccee9092401ee4a0215
pkgver="2.1.0.beta3.r481.g${_commit::8}"
pkgrel=1
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=(x86_64 powerpc)
url='https://luajit.org/'
license=('MIT')
depends=('gcc-libs')
source=("LuaJIT-${_commit}.tar.gz::https://repo.or.cz/luajit-2.0.git/snapshot/${_commit}.tar.gz")
md5sums=('db33c5398500b5dfd36a659113f7781a')
sha256sums=('3ebb2f1f1d87f3986fd1ef81d074daf9cf0a0961c0b4b5afc7e936122f9e376f')
b2sums=('5f3f42b1ffda2430352750526e5dfec56a8718e042917b43a68bfee622925054958dd99e6e5a1bed601aa2be9bddc86405550a39098bd14a57742d440f1e81f0')

build() {
  cd "luajit-2.0-${_commit::7}"
  # Avoid early stripping
  make amalg PREFIX=/usr BUILDMODE=dynamic TARGET_STRIP=" @:"
}

package() {
  cd "luajit-2.0-${_commit::7}"

  make install DESTDIR="$pkgdir" PREFIX=/usr
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"

  ln -sf luajit-2.1.0-beta3 "$pkgdir/usr/bin/luajit"
}
