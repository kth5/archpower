# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgbase=libtool
pkgname=(libtool)
case "${CARCH}" in
  x86_64) pkgname+=(lib32-libltdl) ;;
esac
pkgver=2.6.0
_commit=e40fdc22cf727fb885f9df7d9affa827e3253d1c
pkgrel=3
_gccver=15.2.1
pkgdesc='A generic library support script'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.gnu.org/software/libtool'
license=('LGPL-2.0-or-later WITH Libtool-exception')
depends=(
  sh
  tar
)
makedepends=(
  "gcc>=$_gccver"
  git
  glibc
  help2man
)
makedepends_x86_64=(
  lib32-gcc-libs
  lib32-glibc
)
checkdepends=(
  gcc-fortran
)
source=(
  git+https://git.savannah.gnu.org/git/libtool.git#commit=$_commit
  git+https://git.savannah.gnu.org/git/gnulib.git
  gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git
)
b2sums=('944513327da07374709975c44ad8a80e953727ec11a68bdf6dd3a376acb02339000a0d5982011db9eeb863be0362991cbab1e2fe6c56d7710c1d527ea40bbcde'
        'SKIP'
        'SKIP')

pkgver() {
  cd libtool
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libtool

  git submodule init
  git config --local submodule.gnulib.url "${srcdir}"/gnulib
  git config --local submodule.gl-mod/bootstrap.url "${srcdir}"/gnulib-bootstrap
  git -c protocol.file.allow=always submodule update

  ./bootstrap

  cp -ar "${srcdir}"/libtool "${srcdir}"/libtool32
}

build() {
  cd libtool
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make

  case "${CARCH}" in
    x86_64)
      #lib32
      cd "${srcdir}"/libtool32
      export CC="gcc -m32" CXX="g++ -m32"
      ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32" --libdir=/usr/lib32
      make
    ;;
  esac
}

check() {
  cd libtool
  make check gl_public_submodule_commit=
}

package_libtool() {
  depends+=(
    glibc
  )
  provides=(
    "libltdl=$pkgver"
    "libtool-multilib=$pkgver"
  )
  conflicts=(
    libltdl
    libtool-multilib
  )
  replaces=(
    libltdl
    libtool-multilib
  )

  cd libtool
  make DESTDIR="$pkgdir" install
}

package_lib32-libltdl() {
  depends+=(
    lib32-glibc
    libtool
  )
  provides=(
    "lib32-libtool=$pkgver"
  )
  conflicts=(
    lib32-libtool
  )
  replaces=(
    lib32-libtool
  )

  cd libtool32
  make DESTDIR="$pkgdir" install-libLTLIBRARIES
}
