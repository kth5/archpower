# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libfabric
pkgver=1.20.0
pkgrel=1
pkgdesc="User-space API for OpenFabrics Interfaces (OFI)"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://ofiwg.github.io/libfabric/"
license=(GPL2)
depends=(glibc gcc-libs numactl)
options=(!lto)
source=(https://github.com/ofiwg/libfabric/releases/download/v${pkgver}/libfabric-${pkgver}.tar.bz2)
sha512sums=('8d6ce8959e9a8d1807e276e35ed0ca3b423247fce2cd6916d9dfec44aff2c665a9cc48571a8ffbf85fd87af3a021ced9b911bace8921a4bd73b8c923f8340f82')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}

  case "${CARCH}" in
    powerpc) CFLAGS+=' -latomic' ;;
  esac

  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
