# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Christian Neukirchen <chneukirchen@gmail.com>
# Contributor: Thomas Weißschuh <thomas_weissschuh@lavabit.com>

pkgname=mosh
pkgver=1.4.0
pkgrel=14
pkgdesc='Mobile shell, surviving disconnects with local echo and line editing'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://mosh.org/'
license=(GPL-3.0-only)
depends=(libutempter openssh openssl perl-io-tty protobuf python-absl)
makedepends=(git)
options=(!emptydirs)
source=("git+https://github.com/mobile-shell/mosh#tag=$pkgname-$pkgver")
b2sums=('a04f85c693f8e3d17dd4fff7cb5da9002a46e49d39cca21511588ea066da403c1d5c71ee36f10fd0eba977a391a8f31b91852f30344ef824ef353d0629c7b567')

prepare() {
  # protobuf 23 requires C++17
  sed -e 's|\[11\]|\[17\]|' -i $pkgname/configure.ac
}

build() {
  cd $pkgname
  ./autogen.sh
  ./configure --enable-ufw --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname install
  install -Dm644 "$pkgname/conf/bash-completion/completions/$pkgname" \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
