# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=add-determinism
_pkgname=add-det
pkgver=0.7.2
pkgrel=1
pkgdesc="Build postprocessor to reset metadata fields for build reproducibility"
url='https://github.com/keszybz/add-determinism'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'zlib' 'libz.so'
)
makedepends=('cargo')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/keszybz/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('232c4f9fdc482dee5e6d38ef45a2c983b88283fe23d6ff4bcb26164b4c9a2dcb'
            'd682210e59cdfc578c0e132f85c0147b2947656417ddbb29e09aa33352138874')
b2sums=('56ac61fa50d77885d5fc43a40155f6e5eefe866befcb39fcb0e2c34ce0a780caa6446802284488e6d67824b9a803fb5ef71f58a3cf35257c9a6d7008de88ed13'
        'e4665aa3125c13608c9929caaba88669dbdd613cd1160bf872f42407cc0fee3f41618dd6f1ee1c257cbd46fdeda7adabf006c266fac82a10a6dbee2c4ec941fa')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

updlockfiles() {
  cd "${pkgname}-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip test_handlers::test_pyc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${_pkgname}

  # For backwards compatibility
  # See https://github.com/keszybz/add-determinism/releases/tag/v0.7.0
  ln -sv "/usr/bin/${_pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
