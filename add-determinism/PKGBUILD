# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=add-determinism
_pkgname=add-det
pkgver=0.7.0
pkgrel=1
pkgdesc="Build postprocessor to reset metadata fields for build reproducibility"
url='https://github.com/keszybz/add-determinism'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'zlib' 'libz.so'
)
makedepends=('cargo')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/keszybz/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('ff23770171a16e8e9df97bfeacaccd9545e5a55157cada5f76ecc9aa5762698c'
            'fb24850fe83a01440bd1a1e90ad91ada471c3ee6a7280d70b8dd0c183cb4d9e6')
b2sums=('5eb52450c9a13c1af8940cf525eca7352804dc18fcf7cf5b3658926c1343e86778eb2416e357ab0263cba5c1f8fdabc8fd974eb2c2c5d4cfcca771741c6aa890'
        '8125b42087151fd7bb8184201335a9535687e27b5e5c7d8d8e62d5a690b3126673e8bed6ca8015cc2280c227258b5dedd054d6a7dfd8651d9cf4e46283eda38c')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "${pkgname}-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip test_handlers::test_pyc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${_pkgname}

  # For backwards compatibility
  # See https://github.com/keszybz/add-determinism/releases/tag/v0.7.0
  ln -sv "/usr/bin/${_pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
