# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jason A. Donenfeld <Jason@zx2c4.com>

pkgname=wireguard-tools
pkgver=1.0.20260223
pkgrel=1
pkgdesc='next generation secure network tunnel - tools for configuration'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.wireguard.com/'
license=('GPL')
depends=('bash')
makedepends=('git')
optdepends=('openresolv: for DNS functionality'
            'sudo: elevate privileges')
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE') # Jason A. Donenfeld <Jason@zx2c4.com>
source=("git+https://git.zx2c4.com/${pkgname}#tag=v${pkgver}?signed")
sha256sums=('dc2c44bdfd8ac11f7558e2e2536afa7e3da1381a3b17370b0798ecd83a4323eb')

prepare() {
	cd "${pkgname}/"

	find contrib/ -name '.gitignore' -delete
}

build() {
	cd "${pkgname}/"

	make -C src/
}

package() {
	cd "${pkgname}/"

	make -C src/ \
		DESTDIR="${pkgdir}/" \
		WITH_BASHCOMPLETION=yes \
                WITH_WGQUICK=yes \
                WITH_SYSTEMDUNITS=yes \
                install

	install -d -m0755 "${pkgdir}"/usr/share/${pkgname}/
	cp -r contrib/ "${pkgdir}"/usr/share/${pkgname}/examples/
}
