# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=wpewebkit
pkgname=(
  wpewebkit
  wpewebkit-docs
)
pkgver=2.46.0
pkgrel=1
pkgdesc="Embeddable web content engine"
url="https://wpewebkit.org"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(
  # :sort ui /\v^\s*['"]?/
  'AFL-2.0 OR GPL-2.0-or-later'
  Apache-2.0
  'Apache-2.0 WITH LLVM-exception'
  BSD-2-Clause
  BSD-2-Clause-Views
  BSD-3-Clause
  BSD-Source-Code
  BSL-1.0
  bzip2-1.0.6
  GPL-2.0-only
  'GPL-3.0-only WITH Autoconf-exception-3.0'
  'GPL-3.0-or-later WITH Bison-exception-2.2'
  ICU
  ISC
  LGPL-2.1-only
  LGPL-2.1-or-later
  MIT
  MPL-1.1
  MPL-2.0
  NCSA
  'NCSA OR MIT'
  OFL-1.1
  SunPro
  Unicode-TOU
)
depends=(
  at-spi2-core
  atk
  bubblewrap
  cairo
  fontconfig
  freetype2
  gcc-libs
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  harfbuzz
  harfbuzz-icu
  icu
  lcms2
  libavif
  libdrm
  libepoxy
  libgcrypt
  libinput
  libjpeg
  libjxl
  libpng
  libseccomp
  libsoup3
  libsystemd
  libtasn1
  libwebp
  libwpe
  libxkbcommon
  libxml2
  libxslt
  mesa
  openjpeg2
  sqlite
  wayland
  woff2
  wpebackend-fdo
  xdg-dbus-proxy
  zlib
)
makedepends=(
  cmake
  gi-docgen
  glib2-devel
  gobject-introspection
  gperf
  gst-plugins-bad
  ninja
  python
  ruby
  ruby-stdlib
  systemd
  unifdef
  wayland-protocols
)
options=(
  # https://gitlab.archlinux.org/archlinux/packaging/packages/webkit2gtk-4.1/-/issues/1
  # https://bugs.webkit.org/show_bug.cgi?id=278090
  !lto
)
source=(
  $url/releases/wpewebkit-$pkgver.tar.xz{,.asc}
  reduce-memory-overheads.patch
  fix-ftbfs-riscv64.patch
)
sha256sums=('301550fbd8703f3ba4c4a65fe596686960569f8a3b0f6668243179cbc77bbc50'
            'SKIP'
            '13df7f0b0a61866e767f280bbe77667126ae7de4ad4f463c682040da6366a864'
            'b42d36c275e5fbd6da8e0761381d9eac13b2d103a366a61d9d450421948385a3')
b2sums=('de8d64aa22eb3a544d8029c24a3e75bd17a452d217bdf61d0e287a129c48e371eef2f5c53026be483a305589bf8a294882aa9680f08299dac823890e2eb66484'
        'SKIP'
        'cc16462ce22df999b47ef477d4272ef3df2fbbfe5cdc585f3ce47e56cde65a1d7dd1105e6e7bbff41695f79b6394bc3e403e53a669bb7f456c733ed216372c9b'
        'cd80efecf12982e9e9335032a433e094614c8b95edce7114d37c6d651ee1f5bb3e331c3911d7901055cd59ca3ed5de38d14e15fab5488d3248bf5899623b2f8c')
validpgpkeys=(
  # https://wpewebkit.org/release/verify/
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B # Adrián Pérez de Castro <aperez@igalia.com>
  013A0127AC9C65B34FFA62526C1009B693975393 # Carlos Garcia Campos <cgarcia@igalia.com>
)

prepare() {
  cd wpewebkit-$pkgver
  patch -Np1 -i ../reduce-memory-overheads.patch
  patch -Np1 -i ../fix-ftbfs-riscv64.patch
}

build() {
  local cmake_options=(
    -DPORT=WPE
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DCMAKE_INSTALL_LIBEXECDIR=lib
    -DCMAKE_SKIP_RPATH=ON
    -DUSE_LIBBACKTRACE=OFF
    -DENABLE_DOCUMENTATION=ON
    -DENABLE_MINIBROWSER=ON
  )

  case "${CARCH}" in
    powerpc64*)
      export CFLAGS+=' -mminimal-toc'
      export CXXFLAGS+=' -mminimal-toc'
      export LDFLAGS+=' -Wl,--no-keep-memory'
    ;;
    powerpc)
      cmake_options+=(
        -DENABLE_WEBGL=OFF
        -DFORCE_32BIT=ON
      )
      export LDFLAGS+=" -Wl,--no-keep-memory"
    ;;
    riscv64)
      export LDFLAGS+=" -Wl,--no-keep-memory"
    ;;
    x86_64)
      # JITted code crashes when CET is used
      CFLAGS+=' -fcf-protection=none'
      CXXFLAGS+=' -fcf-protection=none'
    ;;
  esac

  # Produce minimal debug info: 4.3 GB of debug data makes the
  # build too slow and is too much to package for debuginfod
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  cmake -S wpewebkit-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package_wpewebkit() {
  depends+=(
    libWPEBackend-fdo-1.0.so
    libwpe-1.0.so
  )
  provides+=(
    libWPEPlatform-2.0.so
    libWPEWebKit-2.0.so
  )
  optdepends=(
    'geoclue: Geolocation support'
    'gst-libav: nonfree media decoding'
    'gst-plugins-bad: media decoding'
    'gst-plugins-good: media decoding'
  )

  DESTDIR="$pkgdir" cmake --install build

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  cd wpewebkit-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_wpewebkit-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
