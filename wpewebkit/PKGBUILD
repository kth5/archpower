# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=wpewebkit
pkgname=(
  wpewebkit
  wpewebkit-docs
)
pkgver=2.51.1
pkgrel=1
pkgdesc="Embeddable web content engine"
url="https://wpewebkit.org"
#arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
arch=(powerpc espresso)
license=(
  # :sort ui /\v^\s*['"]?/
  'AFL-2.0 OR GPL-2.0-or-later'
  Apache-2.0
  'Apache-2.0 WITH LLVM-exception'
  BSD-2-Clause
  BSD-2-Clause-Views
  BSD-3-Clause
  BSD-Source-Code
  BSL-1.0
  bzip2-1.0.6
  GPL-2.0-only
  'GPL-3.0-only WITH Autoconf-exception-3.0'
  'GPL-3.0-or-later WITH Bison-exception-2.2'
  ICU
  ISC
  LGPL-2.1-only
  LGPL-2.1-or-later
  MIT
  MPL-1.1
  MPL-2.0
  NCSA
  'NCSA OR MIT'
  OFL-1.1
  SunPro
  Unicode-TOU
)
depends=(
  at-spi2-core
  atk
  bubblewrap
  cairo
  expat
  fontconfig
  freetype2
  gcc-libs
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  harfbuzz
  harfbuzz-icu
  icu
  lcms2
  libavif
  libdrm
  libepoxy
  libgcrypt
  libinput
  libjpeg-turbo
  libjxl
  libpng
  libseccomp
  libsoup3
  libsystemd
  libtasn1
  libwebp
  libwpe
  libxkbcommon
  libxml2
  libxslt
  mesa
  openjpeg2
  simdutf
  sqlite
  wayland
  woff2
  wpebackend-fdo
  xdg-dbus-proxy
  zlib
)
makedepends=(
  cmake
  gettext
  gi-docgen
  glib2-devel
  gobject-introspection
  gperf
  gst-plugins-bad
  ninja
  python
  ruby
  ruby-stdlib
  systemd
  unifdef
  wayland-protocols
)
makedepends_powerpc=(
  distcc
)
makedepends_powerpc64=(
  gcc14
)
makedepends_powerpc64le=(
  gcc14
)
makedepends_x86_64=(
  clang
  lld
)
source=(
  $url/releases/wpewebkit-$pkgver.tar.xz{,.asc}
  missing-resource.patch
)
sha256sums=('983a25b20d79863e7b08b0519bb3216d7655b9eeb4af46cfe641a540ca0ce1a7'
            'SKIP'
            '2c29bc067cce281b5d97e89f91a967668a841714cb33afa965364d2493d48cde')
b2sums=('ded294cb5d335c25e583ef190a8e07a5a9cba03c80ba1aecffcbc021c3c5bfd65635a2c31f0fc0597950b5f3a4d9222aba86615c7aff759fd5d655e1adfb9439'
        'SKIP'
        'c854ffd6f2acd1c4a69d119ea19bb2fdfbc158cd9b23dddaba41977118483152b6dc4fcca9b14a950cb45da42d91bd3cce6b3dabcfc69502e4074a7348dcb957')
validpgpkeys=(
  # https://wpewebkit.org/release/verify/
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B # Adrián Pérez de Castro <aperez@igalia.com>
  013A0127AC9C65B34FFA62526C1009B693975393 # Carlos Garcia Campos <cgarcia@igalia.com>
)

case "${CARCH}" in
  powerpc*) options+=(!debug !lto) ;;
esac

prepare() {
  cd wpewebkit-$pkgver
  patch -Np1 -i ${srcdir}/missing-resource.patch
  sed -i -e "s:--warn-error::" Source/cmake/FindGI.cmake
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_INSTALL_LIBEXECDIR=lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_SKIP_RPATH=ON
    -D ENABLE_DOCUMENTATION=ON
    -D ENABLE_MINIBROWSER=ON
    -D ENABLE_SPEECH_SYNTHESIS=OFF
    -D PORT=WPE
    -D USE_FLITE=OFF
    -D USE_LIBBACKTRACE=OFF
  )

  case "${CARCH}" in
    powerpc64le)
      cmake_options+=(
        -DCMAKE_C_COMPILER=gcc-14
        -DCMAKE_CXX_COMPILER=g++-14
      )
      export CFLAGS+=' -mminimal-toc'
      export CXXFLAGS+=' -mminimal-toc'
      export LDFLAGS+=' -Wl,--no-keep-memory'
    ;;

    powerpc64*)
      cmake_options+=(
        -DCMAKE_C_COMPILER=gcc-14
        -DCMAKE_CXX_COMPILER=g++-14
      )
      export CFLAGS='-O2 -pipe -mminimal-toc'
      export CXXFLAGS='-O2 -pipe -mminimal-toc'
      export LDFLAGS+=' -Wl,--no-keep-memory'
    ;;
    powerpc)
      cmake_options+=(
        -DENABLE_WEBGL=OFF
      )
      export CFLAGS+=' -mminimal-toc'
      export CXXFLAGS+=' -mminimal-toc'
      export LDFLAGS+=" -Wl,--no-keep-memory"
      export PATH=/usr/lib/distcc:$PATH
    ;;
    x86_64)
      # Upstream prefers Clang
      # https://gitlab.archlinux.org/archlinux/packaging/packages/webkitgtk-6.0/-/issues/4
      export CC=clang CXX=clang++
      LDFLAGS+=" -fuse-ld=lld"

      # JITted code crashes when CET is used
      CFLAGS+=' -fcf-protection=none'
      CXXFLAGS+=' -fcf-protection=none'
    ;;
  esac

  # Produce minimal debug info: 4.3 GB of debug data makes the
  # build too slow and is too much to package for debuginfod
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  # Skia uses malloc_usable_size
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  export WK_USE_CCACHE=NO
  cmake -S wpewebkit-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build ${MAKEFLAGS}
}

package_wpewebkit() {
  depends+=(
    libWPEBackend-fdo-1.0.so
    libwpe-1.0.so
  )
  provides+=(
    libWPEWebKit-2.0.so
  )
  optdepends=(
    'geoclue: Geolocation support'
    'gst-libav: nonfree media decoding'
    'gst-plugins-bad: media decoding'
    'gst-plugins-good: media decoding'
  )

  DESTDIR="$pkgdir" cmake --install build

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  cd wpewebkit-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_wpewebkit-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
