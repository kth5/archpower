# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

_name=SCons
pkgname=scons
pkgver=4.8.0
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=(any)
url="https://scons.org"
license=(MIT)
depends=(
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pytest
)
optdepends=(
  'python-psutil: to wait for processes to exit'
)
source=(
  $pkgname::git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver
  https://downloads.sourceforge.net/project/scons/scons/$pkgver/$_name-$pkgver.tar.gz
)
sha512sums=('c75443c0808c03b0b2d3b9ea0932fde825acb81b9bebfb7d5dab65637951b34839cecfdc7f0488a8d1a47626cc8f8664ad6891381f81e48422ca937b1684784a'
            '6f1ab01b25752f1f43a1796fb68fb0508105b372cf082a57477af4685399f8b83372466d65070d5c928d81c9fd4bdf1e8ba357c72537f32c50f77121f52415fb')
b2sums=('bebbf54365a6ec7341b0f407615305ee8ba489965ee85ba1d8857c2ba6dd3e5f4af72c2d183f1d470069aa855ef306f18988ef1df189a64ae976c020e27892f9'
        '1ab4a63d14ecfd1984e41f6be11c87003413f74a0d6b03eb7d5fd7d7b66ad5cf904b1061b3cd91a33735fd6ea1ffda6aa388dbf42a7085caa900961bcf2340dd')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python runtest.py --all --unit-only
}

package() {
  # install man pages from prebuilt sources
  install -vDm 644 $pkgname-$pkgver/*.1 -t "$pkgdir/usr/share/man/man1/"

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {{CHANGES,RELEASE}.txt,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"

  # remove docbook dirs
  find "$pkgdir" -name 'docbook' -type d -exec rm -frv {} +
}
