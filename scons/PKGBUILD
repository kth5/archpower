# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

_name=SCons
pkgname=scons
pkgver=4.9.1
pkgrel=3
pkgdesc="Extensible Python-based build utility"
arch=(any)
url="https://scons.org"
license=(MIT)
depends=(
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pytest
)
optdepends=(
  'python-psutil: to wait for processes to exit'
)
source=(
  $pkgname::git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver
  https://downloads.sourceforge.net/project/scons/scons/$pkgver/$_name-$pkgver.tar.gz
)
sha512sums=('a8b91dc8805bf30f938388726b15ac9eb5fb021680dbf635e817c291fc81fff48355ce33ad0c580e798acdb429632128c173b2d55612abe31e0ab28e19a369f4'
            '754704d59096480d30f38271f43e4b0dccd3974b4da385f798bf60654a27eb165693907691245da3d8fedfb9a6cc6cb0cfa3af811227b46eb269de3f525fea2a')
b2sums=('e6e3c28bc6c0e9ffcbf232d6509e44f4bd66c2cee1b7ae5ff8e9ead829f69201ce83e8dc5f2ad83a0a462ed7fd8cf9f6c82b9edeb23dae62a57e31fa94f53788'
        'dfb38fdb70ba5bb62211efebf8ae2170e06886efb5c01c8661aef89b9fdf47fef97a1385ccab14180c92d64ef729a0de58b1623d54a19b9e65cb2e46d2836036')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python runtest.py --all --unit-only
}

package() {
  # install man pages from prebuilt sources
  install -vDm 644 $pkgname-$pkgver/*.1 -t "$pkgdir/usr/share/man/man1/"

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {{CHANGES,RELEASE}.txt,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"

  # remove docbook dirs
  find "$pkgdir" -name 'docbook' -type d -exec rm -frv {} +
}
