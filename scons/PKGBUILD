# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

_name=SCons
pkgname=scons
pkgver=4.8.1
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=(any)
url="https://scons.org"
license=(MIT)
depends=(
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pytest
)
optdepends=(
  'python-psutil: to wait for processes to exit'
)
source=(
  $pkgname::git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver
  https://downloads.sourceforge.net/project/scons/scons/$pkgver/$_name-$pkgver.tar.gz
)
sha512sums=('73d0ad2b91165cf43c369aaf33ee4bb386af6bbef3dfa244d678429569c8e5fb2a255cd04a6c8a151f824e2b15bc93902bf1e3c2a98fdf1b72ee2ec67005054f'
            'dad76d7f16b4cc1d9e25c9d8969773604e23accbb4e94451f0fc725919bf223cf04265f5d2b64a1b7e96cc63496cba574df3a634fd6779f2eb5fb7246fded120')
b2sums=('1d93a90fe6e6b088957bd0d866f950252d67f61b11a8d7426749e423ccc23c773e4fe73cf15eea845a4eb23119ddf32d7315a09f601ea289b3df64efcf897896'
        '31441e16ca96b844b1bcc388cd430a4a087bb5f09a8951166fc44b7422611d416fc6311d79370d768b52a93ad417369682e2a77ada3fe77e82d59c81bfd7b4dd')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python runtest.py --all --unit-only
}

package() {
  # install man pages from prebuilt sources
  install -vDm 644 $pkgname-$pkgver/*.1 -t "$pkgdir/usr/share/man/man1/"

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {{CHANGES,RELEASE}.txt,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"

  # remove docbook dirs
  find "$pkgdir" -name 'docbook' -type d -exec rm -frv {} +
}
