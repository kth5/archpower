# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

_name=SCons
pkgname=scons
pkgver=4.5.2
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=(any)
url="https://scons.org"
license=(MIT)
depends=(
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pytest
)
optdepends=(
  'python-psutil: to wait for processes to exit'
)
source=(
  https://github.com/$pkgname/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz
  https://downloads.sourceforge.net/project/scons/scons/$pkgver/$_name-$pkgver.tar.gz
  $pkgname-4.4.0-dont_install_manpages.patch
)
sha512sums=('aa788d9365a8b5119fef404869562f61cb854be7ceadb5f5d47523e262c07f93ea7476166b577edec25339a64dc6a1f4a6f391fa5aeeab3f086a6659fc72e1ce'
            'a9675f4b4dbedf8c7375d4d099216cd935c38944d57b0a08de2b9c133bb53184de0d5803edf5cb9f800f205b1252ceca3aaf33a10bf5d8b48eacd58866cf776c'
            '97fb0e54274753b8c94a184a8b811371d314a371abb933b0380b2f990962c341d4d896050501c6889f0568b6d45867dc854ce16d23e9a8540cf4957df7af15a1')
b2sums=('c0f42994a178bb0fb329cfa05328bdae89b374a33a7806276e7529a81b37a4ea9f52d07fb2afc55adbe3c1e00661ea3c3d53e981be4a8a095079fc35403328f1'
        'c27d00e4bc319e3b8d5d35d50d5f03df15e2b72256e26000299ac95c5bdaffbe850942df9e3c23a6b0dbf55ea000f768a7938cfedc451cc7bd3e2a521292d49e'
        'b7b611138f0c672d29f9f56a46802289fed7daac25b12822e60c3d8e076aae3b1b88f144e7b9b1fea3a5092ca80f75ed02480250de65e664e18ec9b8ab5f120e')

prepare() {
  # don't try to install the (non-existent) man pages
  # we'll use them from prebuilt sources, because building them drags in a lot of dependencies, is very brittle and painful
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-4.4.0-dont_install_manpages.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python runtest.py --all --unit-only
}

package() {
  # install man pages from prebuilt sources
  install -vDm 644 $_name-$pkgver/*.1 -t "$pkgdir/usr/share/man/man1/"

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {{CHANGES,RELEASE}.txt,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"

  # remove docbook dirs
  find "$pkgdir" -name 'docbook' -type d -exec rm -frv {} +
}
