# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

_name=SCons
pkgname=scons
pkgver=4.9.0
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=(any)
url="https://scons.org"
license=(MIT)
depends=(
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pytest
)
optdepends=(
  'python-psutil: to wait for processes to exit'
)
source=(
  $pkgname::git+https://github.com/$pkgname/$pkgname.git#tag=$pkgver
  https://downloads.sourceforge.net/project/scons/scons/$pkgver/$_name-$pkgver.tar.gz
)
sha512sums=('483112afe1f41fb4240139543c7904df82513cb88adcd3dd85c48b978a86981be61eeede22e1fa471b854ee99d0fcaa29c1590393f81061be50a59c5b9cedafb'
            '74d3d782433394fbdd1010c2a6c4a4026431753f72be1e6f61c7c00ad37ff1cb4c5e149c4418a7d7325b5553e3f827b32bf0f8f1727b08def5a8f90e54ff3439')
b2sums=('1d241a73e448219db8f0641b4fa5228080aaf1e25f97144544fa135098ccfb4401be689e3e60cbbfe6f25ab4563fa8f13bcb559bbab25e04b19f8c9edb0df065'
        'fafce07d3e6f946344298f66d8530f79dce08e221ccd9957d29fbea9f23c7048f651c1c6735ac3dd51dd2c07f1540d77f16301e9c253a9f3087446a2a4856666')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python runtest.py --all --unit-only
}

package() {
  # install man pages from prebuilt sources
  install -vDm 644 $pkgname-$pkgver/*.1 -t "$pkgdir/usr/share/man/man1/"

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {{CHANGES,RELEASE}.txt,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"

  # remove docbook dirs
  find "$pkgdir" -name 'docbook' -type d -exec rm -frv {} +
}
