# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-osi
pkgver=0.108.10
pkgrel=1
pkgdesc='COIN-OR Open Solver Interface'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://github.com/coin-or/Osi'
license=(EPL)
groups=(coin-or)
depends=(coin-or-coinutils)
makedepends=(gcc-fortran)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Osi/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('614c2b329caf57c00326412266299fdfd93c5691492034fbb46990b5e71cc5a7')

build() {
  cd Osi-releases-$pkgver
  ./configure --prefix=/usr --enable-dependency-linking \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --build=${CHOST}
  make
}

package() {
  cd Osi-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
