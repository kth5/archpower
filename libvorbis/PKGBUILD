# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgbase=libvorbis
pkgname=(
  libvorbis
  libvorbis-docs
)
pkgver=1.3.7
pkgrel=4
pkgdesc='Reference implementation of the Ogg Vorbis audio format'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://www.xiph.org/vorbis/
license=(BSD-3-Clause)
depends=(
  glibc
  libogg
)
makedepends=(git)
_tag=0c55fa38933fd4bdb7db7c298b27e7bf2f2c5e98
source=(git+https://github.com/xiph/vorbis.git?signed#tag=${_tag})
sha256sums=('f58777e598c5ef1c9f21698e581f6e62729f3d343ec4aa270051fb304a56fe26')
validpgpkeys=(
  B7B00AEE1F960EEA0FED66FB9259A8F2D2D44C84 # Ralph Giles <giles@thaumas.net>
)

pkgver() {
  cd vorbis

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd vorbis

  ./autogen.sh
}

build() {
  cd vorbis

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-shared
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -C vorbis check -j1
}

package_libvorbis() {
  depends+=(libogg.so)
  provides=(
    libvorbis.so
    libvorbisenc.so
    libvorbisfile.so
  )

  make DESTDIR="${pkgdir}" -C vorbis install

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  install -Dm 644 vorbis/COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_libvorbis-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"

  install -Dm 644 vorbis/COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set sw=2 sts=-1 et:
