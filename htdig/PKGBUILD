# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# $Id: PKGBUILD 260530 2016-03-02 18:25:02Z foutrelis $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=htdig
pkgver=3.2.0b6
pkgrel=12
pkgdesc="Scripts and HTML code needed for using ht://Dig as a web search engine"
url="http://www.htdig.org/"
arch=(i686 x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL')
depends=('openssl')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        drop-bogus-assignment.patch
        fix-implicit-function-declaration.patch
        gcc-11.patch
        gcc-6.patch
        htdig_3.2.0b6-13.patch)
md5sums=('8b9b9587a411ac7dd278fa5413428960'
         '5fb98c71dffd17159a47991d00663fab'
         'b4eaf906899d89a4cd1eadc0f1f9c47e'
         '1d901a46a78482e26c3a41906000c337'
         'd06461873131bfac0ba3e0bc2ba6fdbe'
         'bc2daea4e04d1d32aece993d4fdfca17')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ${srcdir}/drop-bogus-assignment.patch
  patch -Np1 -i ${srcdir}/fix-implicit-function-declaration.patch
  patch -Np1 -i ${srcdir}/gcc-11.patch
  patch -Np1 -i ${srcdir}/gcc-6.patch
  patch -Np1 -i ${srcdir}/htdig_3.2.0b6-13.patch

  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}

  CFLAGS+=" -Wno-narrowing" \
  CXXFLAGS+=" -Wno-deprecated -Wno-narrowing" \
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --with-pic \
    --with-gnu-ld \
    --with-config-dir=/etc/htdig \
    --with-default-config-file=/etc/htdig/htdig.conf \
    --with-database-dir=/var/lib/htdig/db \
    --with-cgi-bin-dir=/usr/bin \
    --with-common-dir=/usr/share/doc/htdig \
    --with-search-dir=/usr/share/doc/htdig \
    --with-image-dir=/usr/share/doc/htdig \
    --localstatedir=/var/lib/htdig \
    --with-ssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
