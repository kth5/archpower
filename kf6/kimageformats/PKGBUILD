# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kimageformats
pkgver=6.23.0
pkgrel=1
pkgdesc='Image format plugins for Qt 6'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://community.kde.org/Frameworks'
license=(LGPL-2.0-only LGPL-3.0-only)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(extra-cmake-modules
             jxrlib
             karchive
             libavif
             libheif
             libjxl
             libraw
             openjpeg2
             openexr)
optdepends=('imath: EXR format support'
            'jxrlib: JXR format support'
            'karchive: plugin for Krita and OpenRaster images'
            'libavif: AVIF format support'
            'libheif: HEIF format support'
            'libjxl: JPEG-XL format support'
            'libraw: RAW format support'
            'openjpeg2: JPEG 2000 format support'
            'openexr: EXR format support')
groups=(kf6)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6590379e0ffbf4d4c7562e0c3d81f9b042533b4701bab9c0d44312c29ae75b5e'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB # David Faure <faure@kde.org>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D # Jonathan Esk-Riddell <jr@jriddell.org>
              90A968ACA84537CC27B99EAF2C8DF587A6D4AAC1 # Nicolas Fella <nicolas.fella@kde.org>
              )

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DKIMAGEFORMATS_HEIF=ON \
    -DKIMAGEFORMATS_JXR=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
