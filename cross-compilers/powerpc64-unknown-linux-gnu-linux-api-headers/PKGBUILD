# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Contributor: toha257 <toha257@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>
# AUR Maintainer: Stefan Schmidt <thrimbor.github@gmail.com>

_target_arch=powerpc64
_target=${_target_arch}-unknown-linux-gnu
pkgname=${_target}-linux-api-headers
pkgver=6.0.16
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace (${_target})"
arch=(any)
url='http://www.gnu.org/software/libc'
license=(GPL2)
makedepends=(rsync make)
source=(https://www.kernel.org/pub/linux/kernel/v6.x/linux-${pkgver}.tar.xz)
sha256sums=('842071bca611c1f080cbc39c7ab3a6b58d7951f4f41e553b3db4fbe3e0705ce4')
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'   # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E')  # Greg Kroah-Hartman

build() {
  cd linux-${pkgver}

  make ARCH=powerpc mrproper
}

package() {
  cd linux-${pkgver}
  make INSTALL_HDR_PATH="$pkgdir/usr/$_target" ARCH=powerpc headers_install

  # use headers from libdrm
  rm -rf "$pkgdir/usr/$_target/include/drm"

  # clean-up unnecessary files generated during install
  find ${pkgdir} \( -name .install -o -name ..install.cmd \) -delete
}
