# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Emil Renner Berthing <aur@esmil.dk>

_target=powerpc-unknown-linux-gnu
pkgname=$_target-binutils
pkgver=2.45.1
_commit=48324fde1e284293dd3d570dba597cb644921c92
pkgrel=1
pkgdesc='Assemble and manipulate binary and object files for powerpc'
arch=(x86_64 powerpc64le powerpc64)
url='https://gnu.org/software/binutils/'
license=(GPL)
depends=(glibc zlib libelf jansson)
makedepends=(git setconf)
options=(!debug)
source=(git+https://sourceware.org/git/binutils-gdb.git#commit=${_commit}
	gold-warn-unsupported.patch)
sha256sums=('3e2351760a32f1739f0683da1d4b43e74b42e117867225b88c41c07af6aeec9e'
            '2d430b66f84a19c154725ec535280c493be1d34f1a90f95208dce02fecd0e4e4')
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)

pkgver() {
  cd binutils-gdb
  git describe --abbrev=12 --tags | sed 's/[^-]*-//;s/[^-]*-/&r/;s/-/+/g;s/_/./g'
}

prepare() {
  cd ${srcdir}
  [[ ! -d binutils-gdb ]] && ln -s binutils-$pkgver binutils-gdb

  cd "binutils-gdb"
  # Creds @Fedora
  # Change the gold configuration script to only warn about
  # unsupported targets.  This allows the binutils to be built with
  # BPF support enabled.
  patch -Np1 -i "${srcdir}"/gold-warn-unsupported.patch

  setconf libiberty/configure ac_cpp "'\$CPP \$CPPFLAGS -O2'"
}

build() {
  cd "binutils-gdb"

  unset CPPFLAGS
  ./configure \
    --disable-nls \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-ld=default \
    --enable-multilib \
    --enable-plugins \
    --prefix=/usr \
    --target=$_target \
    --with-gnu-as \
    --with-gnu-ld \
    --with-sysroot=/usr/$_target/usr \
    --with-system-zlib \
    --disable-werror \
    --disable-sim \
    --enable-colored-disassembly \
    --enable-targets=no
  make

  # Extract the FSF All Permissive License
  # <https://www.gnu.org/prep/maintain/html_node/License-Notices-for-Other-Files.html>
  # used for some linker scripts.
  tail -n 5 "${srcdir}"/binutils-gdb/ld/scripttempl/README > FSFAP
}

package() {
  make -C "binutils-gdb" DESTDIR="$pkgdir" install

  install -d -m755 "$pkgdir/usr/$_target/usr"
  mv $pkgdir/usr/$_target/{bin,lib} "$pkgdir/usr/$_target/usr/"

  # Remove info documents that conflict with host version
  rm -r "$pkgdir/usr/share/info"
  rm -r "$pkgdir/usr/share/gdb"
  rm "$pkgdir/usr/include/gdb/jit-reader.h"

  rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so

  # install FSF All Permissive License
  install -Dm644 -t "${pkgdir}"/usr/share/licenses/${pkgname}/ binutils-gdb/FSFAP
}

# getver: gnu.org/software/binutils
