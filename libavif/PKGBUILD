# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libavif
pkgver=0.10.1
pkgrel=2
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://github.com/AOMediaCodec/libavif"
license=(BSD)
depends=(glibc aom dav1d rav1e svt-av1 zlib libpng libjpeg libyuv)
makedepends=(cmake pkgconf gdk-pixbuf2)
makedepends_x86_64=(nasm)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('66e82854ceb84a3e542bc140a343bc90e56c68f3ecb4fff63e636c136ed9a05e')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=ON \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
