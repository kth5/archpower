# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libavif
pkgver=1.2.0
pkgrel=2
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/AOMediaCodec/libavif"
license=(LicenseRef-libavif)
depends=(aom
         dav1d
         gcc-libs
         glibc
         libjpeg-turbo
         libpng
         libwebp
         libyuv
         rav1e
         svt-av1)
makedepends=(cmake
             gdk-pixbuf2
             git
)
makedepends_x86_64=(
             nasm
             pandoc-cli
)
source=(git+https://github.com/AOMediaCodec/libavif#tag=v$pkgver)
sha256sums=('c017ff5f47eca5f43259ca169db7a54ac2a2a4664c785911f9add257355849f6')

build() {
  # disable pandoc on non-x86_64 so we don't need GHC
  case "${CARCH}" in
    x86_64) _cmake_options=(-DAVIF_BUILD_MAN_PAGES=ON) ;;
  esac

  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=SYSTEM \
    -DAVIF_CODEC_DAV1D=SYSTEM \
    -DAVIF_CODEC_RAV1E=SYSTEM \
    -DAVIF_CODEC_SVT=SYSTEM \
    -DAVIF_LIBSHARPYUV=SYSTEM \
    -DAVIF_BUILD_GDK_PIXBUF=ON ${_cmake_options[@]}
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install
  install -Dm644 $pkgname/LICENSE -t "${pkgdir}"/usr/share/licenses/$pkgname
}
