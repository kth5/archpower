# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: userwithuid <userwithuid@gmail.com>

pkgbase=rust
pkgname=(
  rust
  rust-src
  rust-wasm
)
epoch=1
pkgver=1.89.0
pkgrel=1
pkgdesc="Systems programming language focused on safety, speed and concurrency"
url=https://www.rust-lang.org/
#arch=(powerpc64le powerpc64 powerpc espresso)
arch=(powerpc64le)
license=("Apache-2.0 OR MIT")
options=(
  !emptydirs
  !lto
)
[ "${CARCH}" == 'powerpc' ] && options+=(!debug)
depends=(
  curl
  gcc
  gcc-libs
  libssh2
  llvm-libs
)
makedepends=(
  clang
  cmake
  libffi
  lld
  llvm
  llvm-libs
  ninja
  perl
  python
  rust
  wasi-libc
  wasm-component-ld
)
checkdepends=(
  gdb
  procps-ng
)
source=(
  "https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
  # Patch bootstrap so that rust-analyzer-proc-macro-srv
  # is in /usr/lib instead of /usr/libexec
  0001-bootstrap-Change-libexec-dir.patch

  # Put bash completions where they belong
  0002-bootstrap-Change-bash-completion-dir.patch

  # Use our *-pc-linux-gnu targets, making LTO with clang simpler
  0003-compiler-Change-LLVM-targets.patch

  # Use our wasm-ld
  0004-compiler-Use-wasm-ld-for-wasm-targets.patch

  # Use our aarch64-linux-gnu-gcc
  0005-compiler-Use-aarch64-linux-gnu-gcc-to-link-aarch64-t.patch

  # Prefer "lib" over "lib64"
  0006-compiler-Swap-primary-and-secondary-lib-dirs.patch

  # Fix build with glibc 2.42
  0001-compiler-rt-Fix-compilation-with-glibc-2.42.patch

  # Fix build with system rustc
  # https://github.com/rust-lang/rust/issues/143735
  0007-bootstrap-Workaround-for-system-stage0.patch

  # ArchPOWER powerpc64 is ELFv2
  hotfix-abi.powerpc64.elfv2.patch
)
b2sums=('5cc32e9e7f766ec3631f369c1b60d4a9bcb39c21ede5ef6364cccf32d0d4105cab3fd10dfdf5f38db304db0dc771ae783303ed94d6277013770b59ee569a48c1'
        'SKIP'
        '17e7f6e73936137507e387cd2761b0b98f9457391e4f534beb872effaa9f442e72351bb39d5e34963d9f18a31fd9f07139cfbde4df9c4c1d8cc6e5d3a7f2c8f7'
        'fed39e683a0fa4b7730c629f2453bccb70d2b37329ee8853e0c8e8cbc07a227a6c15bdf81c7285a2a2debeffde24ef14abcfccee6383024ac6bff7405e725482'
        '1f664dc8156ebe48ae49e7651312ca8879b40fb65471bd9e4180e84fc8b2f6e94b50b7ff7db598e8f298058fae7bcc591532fe087084330650c69df131d2bb85'
        '2317343e6b986d1ec1fb6d035fb6d8933245704b5be1b3e4a032ad14300d8a338087c52e53a6dff4ceda52232ce7f21dd8ad536c9d4da04faee6a9b79a9670b6'
        '5db7f8a8320e6900c35c0f3db270f70e5ee00b466038914b474ad33a822f20c2e518934dffb2d4391ae8a5ef94b49ab21ef7f57ab86686a51499d9f5516faa94'
        '8be938ad0016244e86707396ee147fdff7a7ee4d2a776607ff316a9264512faba99d03a7a6b5ba48744c3ae93b09a2eaede567554142a30570346e177131099f'
        'b9fdc6e4849dd167568322a09c645e1dfbbb4ceb1d6057744fa170c22f29195f3a6a3e6e71807f24124d6c7af659f7a91611f9b1b1f3702ceaba34f1fd153aca'
        'f7f43c42856be95b658963275f6ebd94a500f24f23c925e3d9e5fe917f6d509bfd75d2fca072c987e93cd7d5a3776800ba633406cd77fe387b39252b5b7dfbc2'
        '68398863593631ccb46be8887e612f13b77fc97c624fa2827895c8ba67d8ff8bce3813bbce1edbfd4088e1238f068ec4a85b9b3cffa103dc5e595d50d9bb04bb')
validpgpkeys=(
  108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE  # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
)

# Make sure the duplication in rust-wasm is found
COMPRESSZST+=(--long)

prepare() {
  cd rustc-$pkgver-src

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    src="${src%.zst}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  sed -i 's/\("files":{[^,]*\),[^}]*}/\1}/' vendor/*/.cargo-checksum.json

  _rust_target="\"${CHOST}\""

  _lto="fat"
  case "${CARCH}" in
    powerpc) _lto="off" ;;
  esac

  cat >bootstrap.toml <<END
# see src/bootstrap/defaults/
profile = "dist"

# see src/bootstrap/src/utils/change_tracker.rs
change-id = 140732

[llvm]
download-ci-llvm = false
link-shared = true

[build]
target = [
  ${_rust_target},
  "wasm32-unknown-unknown",
  "wasm32-wasip1",
  "wasm32-wasip1-threads",
  "wasm32-wasip2",
  "wasm32v1-none"
]
cargo = "/usr/bin/cargo"
description = "Arch POWER $pkgbase $epoch:$pkgver-$pkgrel"
rustc = "/usr/bin/rustc"
rustfmt = "/usr/bin/rustfmt"
locked-deps = true
vendor = true
tools = [
  "cargo",
  "clippy",
  "rustdoc",
  "rustfmt",
  "rust-analyzer-proc-macro-srv",
  "analysis",
  "src",
]
sanitizers = true
profiler = true

# Generating docs fails with the wasm32-* targets
docs = false

[install]
prefix = "/usr"

[rust]
codegen-units = 1
codegen-units-std = 1
#debuginfo-level = 2
#debuginfo-level-std = 2
channel = "stable"
rpath = false
frame-pointers = true
lld = false
#use-lld = "external"
llvm-bitcode-linker = false
deny-warnings = false
backtrace-on-ice = true
jemalloc = false
remap-debuginfo = false
lto = "${_lto}"

[dist]
compression-formats = ["gz"]
compression-profile = "fast"

[target.${_rust_target}]
cc = "/usr/bin/${CHOST}-gcc"
cxx = "/usr/bin/${CHOST}-g++"
ar = "/usr/bin/${CHOST}-gcc-ar"
ranlib = "/usr/bin/${CHOST}-gcc-ranlib"
llvm-config = "/usr/bin/llvm-config"
linker = "${CHOST}-gcc"

[target.wasm32-unknown-unknown]
cc = "/usr/bin/clang"
cxx = "/usr/bin/clang++"
ar = "/usr/bin/llvm-ar"
ranlib = "/usr/bin/llvm-ranlib"
linker = "/usr/bin/wasm-ld"
default-linker = "wasm-ld"
sanitizers = false
profiler = false

[target.wasm32v1-none]
cc = "/usr/bin/clang"
cxx = "/usr/bin/clang++"
ar = "/usr/bin/llvm-ar"
ranlib = "/usr/bin/llvm-ranlib"
linker = "/usr/bin/wasm-ld"
default-linker = "wasm-ld"
sanitizers = false
profiler = false

[target.wasm32-wasip1]
cc = "/usr/bin/clang"
cxx = "/usr/bin/clang++"
ar = "/usr/bin/llvm-ar"
ranlib = "/usr/bin/llvm-ranlib"
linker = "/usr/bin/wasm-ld"
default-linker = "wasm-ld"
sanitizers = false
profiler = false
wasi-root = "/usr/share/wasi-sysroot"

[target.wasm32-wasip1-threads]
cc = "/usr/bin/clang"
cxx = "/usr/bin/clang++"
ar = "/usr/bin/llvm-ar"
ranlib = "/usr/bin/llvm-ranlib"
linker = "/usr/bin/wasm-ld"
default-linker = "wasm-ld"
sanitizers = false
profiler = false
wasi-root = "/usr/share/wasi-sysroot"

[target.wasm32-wasip2]
cc = "/usr/bin/clang"
cxx = "/usr/bin/clang++"
ar = "/usr/bin/llvm-ar"
ranlib = "/usr/bin/llvm-ranlib"
linker = "/usr/bin/wasm-ld"
default-linker = "wasm-ld"
sanitizers = false
profiler = false
wasi-root = "/usr/share/wasi-sysroot"
END
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  #rustup toolchain update --profile default 1.88.0
  #rustup default 1.88.0

  case "${CARCH}" in
    powerpc)
      # fails to link with -O3 due to 32bit related relocation problems
      export RUSTFLAGS="-C opt-level=1"
    ;;
  esac

  cd rustc-$pkgver-src

  export RUST_BACKTRACE=full
  unset CFLAGS CXXFLAGS LDFLAGS

  DESTDIR="$srcdir/dest-rust" python ./x.py install -j "$(nproc)"

  cd ../dest-rust

  # delete unnecessary files, e.g. files only used for the uninstall script
  rm -v usr/lib/rustlib/{components,install.log,rust-installer-version,uninstall.sh}
  rm -v usr/lib/rustlib/manifest-*

  # licenses for main rust package
  local ldir="usr/share/licenses/rust" f d
  mkdir -p "$ldir"
  for f in usr/share/doc/*/{COPYRIGHT,LICENSE}*; do
    d="$(dirname "$f")"
    case $f in
      */LICENSE-APACHE) rm -v "$f" ;;
      *) mv -v "$f" "$ldir/${f##*/}.${d##*/}" ;;
    esac
    rmdir -p --ignore-fail-on-non-empty "$d"
  done

  _pick dest-src  usr/lib/rustlib/src
  _pick dest-wasm usr/lib/rustlib/wasm32{,v1}-*
}

package_rust() {
  optdepends=(
    'gdb: rust-gdb script'
    'lldb: rust-lldb script'
  )
  provides=(
    cargo
    rustfmt
  )
  conflicts=(
    cargo
    'rust-docs<1:1.56.1-3'
    rustfmt
  )
  replaces=(
    cargo
    cargo-tree
    'rust-docs<1:1.56.1-3'
    rustfmt
  )

  cp -a dest-rust/* "$pkgdir"
}

package_rust-wasm() {
  pkgdesc="WebAssembly targets for Rust"
  depends=(
    lld
    rust
    wasm-component-ld
  )

  cp -a dest-wasm/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 \
    rustc-$pkgver-src/{COPYRIGHT,LICENSE-MIT}
}

package_rust-src() {
  pkgdesc="Source code for the Rust standard library"
  depends=(rust)

  cp -a dest-src/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 \
    rustc-$pkgver-src/{COPYRIGHT,LICENSE-MIT}
}

# vim:set ts=2 sw=2 et:
