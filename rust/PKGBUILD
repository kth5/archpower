# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: userwithuid <userwithuid@gmail.com>

pkgbase=rust
pkgname=(rust rust-src)
epoch=1
pkgver=1.64.0
pkgrel=1
pkgdesc="Systems programming language focused on safety, speed and concurrency"
url=https://www.rust-lang.org/
arch=(x86_64 powerpc64le powerpc riscv64)
license=(MIT Apache)
options=(!emptydirs !strip !lto)
_llvm_ver=14.0.6
depends=(gcc-libs llvm-libs curl libssh2 gcc)
makedepends=(rust "llvm=$_llvm_ver" libffi perl python cmake ninja wasi-libc lld)
makedepends_x86_64=(lib32-gcc-libs musl)
checkdepends=(procps-ng gdb)
source=(
  "https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
  "https://github.com/llvm/llvm-project/releases/download/llvmorg-$_llvm_ver/compiler-rt-$_llvm_ver.src.tar.xz"{,.sig}
  "rust-1.64.0-regression.patch::https://github.com/rust-lang/rust/commit/263edd43c5255084292329423c61a9d69715ebfa.patch"
  0001-cargo-Change-libexec-dir.patch
  0001-bootstrap-Change-libexec-dir.patch
  0002-compiler-Change-LLVM-targets.patch
  0003-compiler-Use-wasm-ld-for-wasm-targets.patch
  remove-include-linux-fs.h.patch
  replace_patched_libc_cc.patch
)
sha256sums=('b3cd9f481e1a2901bf6f3808d30c69cc4ea80d93c4cc4e2ed52258b180381205'
            'SKIP'
            '88df303840ca8fbff944e15e61c141226fe79f5d2b8e89fb024264d77841a02e'
            'SKIP'
            '710a0f0f35da1951a3ae5eb63ed4a86e85eaf9a248ff56ef734d993a698bedd3'
            'c901a9bb036c29ca092f7dbc8b447efdd9aa1044a902a88f9d024cb22681dc19'
            '60c6dfc100d292284af2187cfefc1d0d928139ca557d27bc6bef88edefccff70'
            '8dcb566da29a92ecf3eb6afe094d9c7766d08230d90fd666bfd022c018ae676a'
            '26bf232d144b81a303ed91f6a331ddf004c5d9fefd41a11e36b61812a34459aa'
            '34ed866e313e4580130a50118a4410d36fa0159123982521b6ef049439fc32ad'
            '908de2d6d84358289ff2667aca3360a3bc91cbe8d2687ee5b02474088282ff37')
validpgpkeys=(108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE  # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
              474E22316ABF4785A88C6E8EA2C794A986419D8A  # Tom Stellard <tstellar@redhat.com>
              B6C8F98282B944E3B0D5C2530FC3042E345AD05D) # Hans Wennborg <hans@chromium.org>

_jemalloc='true'
case "${CARCH}" in
  powerpc64le)
    _rust_target='powerpc64le-unknown-linux-gnu'
    _build_tools='"cargo", "rls", "clippy", "rustfmt", "analysis", "src", "rust-demangler"'
    _config_targets='"powerpc64le-unknown-linux-gnu","wasm32-unknown-unknown","wasm32-wasi"'
    _config_target_extras=''
    pkgname+=(rust-wasm)
  ;;
  powerpc)
    _rust_target='powerpc-unknown-linux-gnu'
    _build_tools='"cargo", "clippy", "rustfmt", "analysis", "src", "rust-demangler"'
    _config_targets='"powerpc-unknown-linux-gnu"'
    _config_target_extras=''
  ;;
  riscv64)
    _rust_target='riscv64gc-unknown-linux-gnu'
    _build_tools='"cargo", "rls", "clippy", "rustfmt", "analysis", "src", "rust-demangler"'
    _config_targets='"riscv64gc-unknown-linux-gnu"'
    _config_target_extras=''
    _jemalloc=false
  ;;
  x86_64)
    _rust_target="${CHOST}"
    _build_tools='"cargo", "rls", "clippy", "rustfmt", "analysis", "src", "rust-demangler"'
    _config_targets='"x86_64-unknown-linux-gnu","i686-unknown-linux-gnu","x86_64-unknown-linux-musl","wasm32-unknown-unknown","wasm32-wasi"'
    _config_target_extras='[target.x86_64-unknown-linux-musl]
sanitizers = false
musl-root = "/usr/lib/musl"'
    pkgname+=(rust-wasmi lib32-rust-libs rust-musl)
  ;;
esac

prepare() {
  cd rustc-$pkgver-src

  # Patch bootstrap and cargo so credential helpers
  # are in /usr/lib instead of /usr/libexec
  patch -d src/tools/cargo -Np1 < ../0001-cargo-Change-libexec-dir.patch
  patch -Np1 -i ../0001-bootstrap-Change-libexec-dir.patch

  # Use our *-pc-linux-gnu targets, making LTO with clang simpler
  patch -Np1 -i ../0002-compiler-Change-LLVM-targets.patch

  # Use our wasm-ld
  patch -Np1 -i ../0003-compiler-Use-wasm-ld-for-wasm-targets.patch

  # https://github.com/llvm/llvm-project/issues/56421                                                                                                                                                                                    
  patch -Np1 -d src/llvm-project < ../remove-include-linux-fs.h.patch

  case "${CARCH}" in
    riscv64)
      patch -Np1 -i "../replace_patched_libc_cc.patch"
      patch -Rp1 -i "../rust-1.64.0-regression.patch"
    ;;
  esac


  cat >config.toml <<END
changelog-seen = 2
profile = "user"

[llvm]
link-shared = true

[build]
target = [
  ${_config_targets}
]
cargo = "/usr/bin/cargo"
rustc = "/usr/bin/rustc"
rustfmt = "/usr/bin/rustfmt"
locked-deps = true
vendor = true
tools = [${_build_tools}]
sanitizers = true
profiler = true

# Generating docs fails with the wasm32-* targets
docs = false

[install]
prefix = "/usr"

[rust]
debuginfo-level-std = 2
channel = "stable"
description = "Arch POWER $pkgbase $epoch:$pkgver-$pkgrel"
rpath = false
backtrace-on-ice = true
remap-debuginfo = true
jemalloc = ${_jemalloc} 

# LLVM crashes when passing an object through ThinLTO twice.  This is triggered
# when using rust code in cross-language LTO if libstd was built using ThinLTO.
# http://blog.llvm.org/2019/09/closing-gap-cross-language-lto-between.html
# https://github.com/rust-lang/rust/issues/54872
codegen-units-std = 1

# musl target produces warnings
deny-warnings = false

[dist]
compression-formats = ["gz"]

[target.${_rust_target}]
llvm-config = "/usr/bin/llvm-config"

${_config_target_extras}

[target.wasm32-unknown-unknown]
sanitizers = false
profiler = false

[target.wasm32-wasi]
sanitizers = false
profiler = false
wasi-root = "/usr/share/wasi-sysroot"
END

  cd src/bootstrap
  cargo update -p cc
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/$f"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  cd rustc-$pkgver-src

  export RUST_BACKTRACE=1
  export RUST_COMPILER_RT_ROOT="$srcdir/compiler-rt-$_llvm_ver.src"
  [[ -d $RUST_COMPILER_RT_ROOT ]]

  DESTDIR="$srcdir/dest-rust" python ./x.py install -j "$(nproc)"

  cd ../dest-rust

  # delete unnecessary files, e.g. files only used for the uninstall script
  rm usr/lib/rustlib/{components,install.log,rust-installer-version,uninstall.sh}
  rm usr/lib/rustlib/manifest-*

  # rustbuild always installs copies of the shared libraries to /usr/lib,
  # overwrite them with symlinks to the per-architecture versions
  ln -srft usr/lib   usr/lib/rustlib/${_rust_target}/lib/*.so

  case "${CARCH}" in
    x86_64)
      mkdir -p usr/lib32
      ln -srft usr/lib32 usr/lib/rustlib/i686-unknown-linux-gnu/lib/*.so
    ;;
  esac

  mkdir -p usr/share/bash-completion
  mv etc/bash_completion.d usr/share/bash-completion/completions

  mkdir -p usr/share/licenses/rust
  mv -t usr/share/licenses/rust usr/share/doc/rust/{COPYRIGHT,LICENSE*}

  case "${CARCH}" in
    x86_64)
      _pick dest-i686 usr/lib/rustlib/i686-unknown-linux-gnu usr/lib32
      _pick dest-musl usr/lib/rustlib/x86_64-unknown-linux-musl
    ;;
  esac

  [[ ! "${CARCH}" = 'powerpc' ]] && _pick dest-wasm usr/lib/rustlib/wasm32-*
  _pick dest-src  usr/lib/rustlib/src
}

package_rust() {
  optdepends=('lldb: rust-lldb script'
              'gdb: rust-gdb script')
  provides=(cargo rustfmt)
  conflicts=(cargo rustfmt 'rust-docs<1:1.56.1-3')
  replaces=(cargo rustfmt cargo-tree 'rust-docs<1:1.56.1-3')

  cp -a dest-rust/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s rust "$pkgdir/usr/share/licenses/$pkgname"
}

package_lib32-rust-libs() {
  pkgdesc="32-bit target and libraries for Rust"
  depends=(rust lib32-gcc-libs)
  provides=(lib32-rust)
  conflicts=(lib32-rust)
  replaces=(lib32-rust)

  cp -a dest-i686/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s rust "$pkgdir/usr/share/licenses/$pkgname"
}

package_rust-musl() {
  pkgdesc="Musl target for Rust"
  depends=(rust)

  cp -a dest-musl/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s rust "$pkgdir/usr/share/licenses/$pkgname"
}

package_rust-wasm() {
  pkgdesc="WebAssembly targets for Rust"
  depends=(rust lld)

  cp -a dest-wasm/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s rust "$pkgdir/usr/share/licenses/$pkgname"
}

package_rust-src() {
  pkgdesc="Source code for the Rust standard library"
  depends=(rust)

  cp -a dest-src/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s rust "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
