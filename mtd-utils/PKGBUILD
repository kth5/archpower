# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Phil Pirozhkov <pirjsuka@gmail.com>

pkgname=mtd-utils
pkgver=2.2.0
pkgrel=1
pkgdesc='Utilities for dealing with MTD devices'
url='http://www.linux-mtd.infradead.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-only)
depends=(
  lzo
  util-linux
  zstd
  zlib
)
makedepends=(
  cmocka
)
source=(ftp://ftp.infradead.org/pub/mtd-utils/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha512sums=('985d1a600cc6d5c69dd4bd47c67612fcd35c009d772051c5e05b132a13c8a62534e0808930a398f78b3ef83e6f14a198169df71ac4c61c6415d879ddf64202de'
            'SKIP')
validpgpkeys=('13063F723C9E584AEACD5B9BBCE5DC3C741A02D1')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sbindir=/usr/bin \
    --enable-unit-tests \
    --enable-tests
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check -j1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
