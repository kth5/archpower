# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx
pkgver=1.15.2
pkgrel=2
pkgdesc='VP8 and VP9 codec'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://www.webmproject.org/
license=(custom:BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
)
makedepends_x86_64=(
  nasm
)
provides=(libvpx.so)
_tag=9aec918bf47611de2ffc7df61624c3d1b58be4ec
source=(git+https://chromium.googlesource.com/webm/libvpx#tag=${_tag})
b2sums=('7dac94f25867bf023b5d15662996f867e435533b038da881352730b88f8fc9ae657c45b17c135579c3e4bd4039f450cb4f7e5e3e3f2738c68e230f901f7c6456')

pkgver() {
  cd libvpx
  git describe --tags | sed 's/^v//'
}

build() {
  cd libvpx
  ./configure \
    --prefix=/usr \
    --disable-install-docs \
    --disable-install-srcs \
    --disable-unit-tests \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-vp9-temporal-denoising
  make
}

package() {
  cd libvpx
  make DIST_DIR="${pkgdir}"/usr install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx/
}

# vim:set sw=2 sts=-1 et:
