# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx
pkgver=1.14.1
pkgrel=1
pkgdesc='VP8 and VP9 codec'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://www.webmproject.org/
license=(custom:BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
)
makedepends_x86_64=(
  nasm
)
provides=(libvpx.so)
_tag=12f3a2ac603e8f10742105519e0cd03c3b8f71dd
source=(git+https://chromium.googlesource.com/webm/libvpx#tag=${_tag})
b2sums=('2ee22fba5822c0f3667c0c74de799536724ce88694b31e0493ce10c6f9a11c6ac563e39f5dcaf9fb4378346276610008dcbfc47f9407411b7c35ba4c71fb5f66')

pkgver() {
  cd libvpx

  git describe --tags | sed 's/^v//'
}

build() {
  cd libvpx
  ./configure \
    --prefix=/usr \
    --disable-install-docs \
    --disable-install-srcs \
    --disable-unit-tests \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-vp9-temporal-denoising
  make
}

package() {
  cd libvpx
  make DIST_DIR="${pkgdir}"/usr install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx/
}

# vim:set sw=2 sts=-1 et:
