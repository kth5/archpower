# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx
pkgver=1.15.0
pkgrel=1
pkgdesc='VP8 and VP9 codec'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://www.webmproject.org/
license=(custom:BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
)
makedepends_x86_64=(
  nasm
)
provides=(libvpx.so)
_tag=5a65405dde363f5c50b568cf85bd7d6d874ce145
source=(git+https://chromium.googlesource.com/webm/libvpx#tag=${_tag})
b2sums=('6b2db5d77c0f6f8f0353dc9ae021a55e32ff8412d0cea9f2c0c5b258497d77c93968535bf98abb8108bf639fed303d961016817902767875d959fd5f21c8a2d3')

pkgver() {
  cd libvpx
  git describe --tags | sed 's/^v//'
}

build() {
  cd libvpx
  ./configure \
    --prefix=/usr \
    --disable-install-docs \
    --disable-install-srcs \
    --disable-unit-tests \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-vp9-temporal-denoising
  make
}

package() {
  cd libvpx
  make DIST_DIR="${pkgdir}"/usr install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx/
}

# vim:set sw=2 sts=-1 et:
