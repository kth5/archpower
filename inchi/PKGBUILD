# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=inchi
pkgver=1.07.4
pkgrel=1
pkgdesc="The IUPAC International Chemical Identifier"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/IUPAC-InChI/InChI"
license=(MIT)
depends=(
  glibc
)
makedepends=(
  cmake
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
b2sums=('17db0c262e00966079193135de5f7aca3ffd79f5d1c83123f2554925b365cfef1873d51d1bbeb8760c05f08e8a0b0cd04a37da988a51703acae680f3d989c6e1')

build() {
  local cmake_options=(
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_SKIP_RPATH=ON
  )

  # main API
  cmake -B build-main -S InChI-$pkgver/INCHI-1-SRC/INCHI_API/demos/inchi_main/src "${cmake_options[@]}"
  cmake --build build-main

  # CLI
  cmake -B build-cli -S InChI-$pkgver/INCHI-1-SRC/INCHI_EXE/inchi-1/src "${cmake_options[@]}"
  cmake --build build-cli
}

package() {
  # upstream build system has no install targets :-/
  install -vDm 755 build-main/bin/inchi_main -t "$pkgdir"/usr/bin/
  install -vDm 644 build-main/bin/libinchi.so -t "$pkgdir"/usr/lib/
  install -vDm 755 build-cli/bin/inchi-1 -t "$pkgdir"/usr/bin/

  # install headers
  for h in InChI-$pkgver/INCHI-1-SRC/INCHI_BASE/src/*.h InChI-$pkgver/INCHI-1-SRC/INCHI_API/libinchi/src/*.h; do
    install -vDm 644 "$h" -t "$pkgdir"/usr/include/inchi/
  done

  # install license
  install -vDm 644 InChI-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
