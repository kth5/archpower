# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jkqtplotter
pkgver=5.0.0git20251013
pkgrel=1
_commit=d243218119b1632987df26baea0d4bc6ccdee533
pkgdesc='A Qt Plotting Library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='http://jkriege2.github.io/JKQtPlotter/'
license=(LGPL-2.1-or-later)
depends=(gcc-libs
         glibc
         qt6-base
         qt6-svg)
makedepends=(cmake
             git)
source=(git+https://github.com/jkriege2/JKQtPlotter#commit=$_commit)
sha256sums=('f5fdc43c64ad5ddb9be9c7b9a33098848d8103d15b3633c0242090579f82d5bb')

build() {
  cmake -B build -S JKQtPlotter \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DJKQtPlotter_BUILD_EXAMPLES=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 JKQtPlotter/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
