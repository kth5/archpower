# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=('libvirt-python')
epoch=1
pkgver=10.5.0
pkgrel=1
pkgdesc="libvirt python binding"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL-2.1-or-later')
makedepends=('python' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'libvirt')
depends=('python' 'libvirt')
options=('emptydirs')
source=("https://libvirt.org/sources/python/libvirt-python-${pkgver}.tar.gz"{,.asc})
sha512sums=('1114405f52c9bfcfd733a26d7d2a51439fca6012313c22f40c4b488f55690d2564a26ab9376c285245e9899ef68fa136bb943ebb3702aa679874d0b98f3cebd7'
            'SKIP')
validpgpkeys=('453B65310595562855471199CA68BE8010084C9C') # Jiří Denemark <jdenemar@redhat.com>

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
