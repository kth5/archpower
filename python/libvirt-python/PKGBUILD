# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=('libvirt-python')
epoch=1
pkgver=10.9.0
pkgrel=2
pkgdesc="libvirt python binding"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL-2.1-or-later')
makedepends=('python' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'libvirt')
depends=('python' 'libvirt')
options=('emptydirs')
source=("https://libvirt.org/sources/python/libvirt-python-${pkgver}.tar.gz"{,.asc})
sha512sums=('56466ed43df9517288696677289f87b410b9b08ce056dc0edd4340ccd694b13f8f179953833680987d65bdfd961409cb82493016fb4aa130ab92a269c46cf7ad'
            'SKIP')
validpgpkeys=('453B65310595562855471199CA68BE8010084C9C') # Jiří Denemark <jdenemar@redhat.com>

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
