# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-betamax
pkgname=python-betamax
pkgver=0.9.0
pkgrel=1
pkgdesc="A VCR imitation for python-requests"
arch=(any)
license=('GPL')
url='https://github.com/betamaxpy/betamax'
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/betamaxpy/betamax/archive/$pkgver.tar.gz")
sha512sums=('6a85f21dd7ccd0d53e6c87ff4f159d295d40729098c13c435c1715252e4cbbeabb82e6347e84a16094f13a1a0249dd1c067ddc82fd36c292913ecbdb22148371')

prepare() {
  (cd betamax-$pkgver
   # Disable some failing tests; taken from Gentoo
   sed -i -e 's:test_records:_&:' -e 's:test_replaces:_&:' -e 's:test_replays:_&:' \
     tests/integration/test_record_modes.py
  )
}

check() {
  cd betamax-$pkgver
  python setup.py pytest
}

package() {
  cd betamax-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
