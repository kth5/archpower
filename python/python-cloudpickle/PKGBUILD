# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Chris Warrick <aur@chriswarrick.com>

_pkg=cloudpickle
pkgname=python-${_pkg}
pkgver=3.1.0
pkgrel=3
pkgdesc="Extended pickling support for Python objects"
arch=(any)
url="https://github.com/cloudpipe/cloudpickle"
license=(BSD-3-Clause)
depends=(python)
makedepends=(
    python-build
    python-installer
    python-flit-core
    python-wheel
)
checkdepends=(
    python-pytest
    python-psutil
    python-tornado
    python-numpy
)
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('3a6cf611c6cc12b93dcafe0d539da7c5e3d7aac52b041a452980fdf3b8753946')

build() {
  cd ${_pkg}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac
  cd ${_pkg}-${pkgver}
  PYTHONPATH=. pytest .
}

package() {
  cd ${_pkg}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
