# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-aiosignal
pkgver=1.3.2
pkgrel=1
pkgdesc='List of registered asynchronous callbacks'
arch=(any)
url='https://github.com/aio-libs/aiosignal'
license=('Apache-2.0')
depends=(
  'python'
  'python-frozenlist'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-asyncio'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9a66e79d43e0aecd1195721db07d4e21da6ea9be81198b965471dd01beda44791d56400839f4ea30ba4d0c106f0b9bf6f91277c310c219669de45a4f874957e5')
b2sums=('253022824866f47658fd5e2ebfc0e234e31541c38679695a2645ae0612888134edab3c5f36a01242db87c28ba37d50d45edfc782b48278860a72c3f3aadcdf73')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" CHANGES.rst README.rst
}

# vim: ts=2 sw=2 et:
