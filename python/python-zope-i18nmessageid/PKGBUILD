# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-i18nmessageid
_pkgname=zope.i18nmessageid
pkgver=7.0
pkgrel=2
pkgdesc="Message Identifiers for internationalization"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL-2.1')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('5c5e255c426ab5a560847efaaf1f5aacaf7ed034f05bf4a44c38d81b0cba9474328577323826574725223aa7674a18e6fbd0e93c4c024e0bd594ea6c234fd04c')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$PWD/tmp_install/$site_packages"
  zope-testrunner --test-path=src -vc
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
