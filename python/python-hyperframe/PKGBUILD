# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hyperframe
pkgver=6.0.1
pkgrel=5
pkgdesc="HTTP/2 framing layer for Python"
arch=(any)
license=('MIT')
url="https://hyper.rtfd.org"
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-hyper/hyperframe/archive/v$pkgver.tar.gz")
sha512sums=('b4170194ef7504f0897a9cee689b32e22784e9b0d98d6da267b62983f013758acbfe53df9d6f6fad5f9709299e95ed32e1e73213f9515a4de20bd5c36fa39814')

build() {
  cd "$srcdir"/hyperframe-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd "$srcdir"/hyperframe-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package_python-hyperframe() {
  cd hyperframe-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
