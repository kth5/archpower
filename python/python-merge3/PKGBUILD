# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Luis Martinez <luis.martinez@disroot.org>
# Contributor: Mark Wagie <mark.wagie@tutanota.com>

pkgname=python-merge3
pkgver=0.0.15
pkgrel=1
pkgdesc='Python implementation of 3-way merge'
arch=(any)
url=https://github.com/breezy-team/merge3
license=(GPL2)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=deea4acaacb3c4a53cab286490550c32d5d0c2b4
#source=(git+https://github.com/breezy-team/merge3.git?signed#tag=${_tag})
source=(git+https://github.com/breezy-team/merge3.git#tag=${_tag})
b2sums=('4873a8a57a2c04e3c597acf4a4571b75f3229c37c891544ed5740c4df346f6d15ee6de39ca6caecb5d33138e8c5e44a37a8f95f1bbc08b84b43f00b9805198c8')
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>

pkgver() {
  cd merge3
  git describe --tags | sed 's/^v//'
}

build() {
  cd merge3
  python -m build --wheel --no-isolation
}

check() {
  cd merge3
  python -m unittest discover
}

package() {
  python -m installer --destdir="${pkgdir}" merge3/dist/*.whl
}

# vim: ts=2 sw=2 et:
