# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Luis Martinez <luis.martinez@disroot.org>
# Contributor: Mark Wagie <mark.wagie@tutanota.com>

pkgname=python-merge3
pkgver=0.0.16
pkgrel=2
pkgdesc='Python implementation of 3-way merge'
arch=(any)
url=https://github.com/breezy-team/merge3
license=(GPL2)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=6cc145d479cee3517ae47dc41f8ac66b731f520f
#source=(git+https://github.com/breezy-team/merge3.git?signed#tag=${_tag})
source=(git+https://github.com/breezy-team/merge3.git#tag=${_tag})
b2sums=('f13afaf0f2015370bfd0eb95a3160b460506bde4b8b95254ce839ff5026b3d2e458280db0e8ad65e0edb5ff3d42ffb0debacfc92c2e82023b19e100703985c91')
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>

pkgver() {
  cd merge3
  git describe --tags | sed 's/^v//'
}

build() {
  cd merge3
  python -m build --wheel --no-isolation
}

check() {
  cd merge3
  python -m unittest discover
}

package() {
  python -m installer --destdir="${pkgdir}" merge3/dist/*.whl
}

# vim: ts=2 sw=2 et:
