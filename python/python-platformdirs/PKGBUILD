# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Tobias Roettger <toroettg@gmail.com>

pkgname=python-platformdirs
pkgver=4.5.1
pkgrel=3
pkgdesc='A library to determine platform-specific system directories'
arch=(any)
url='https://github.com/tox-dev/platformdirs'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
  python-hatch-vcs
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-appdirs
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('0064f4dcfc42b8703fc20423f4b5fb3f6c40ab36e776514f5297b60858b41ff056932f0c36e70b65cb018a55c0405d82caf2c5789174ed3868b6e47b9a44b8c5')
b2sums=('5d151eb8f7237a4f29c9a47a0b652e196df353696d6e00479324705ac06e4915c18605310f796578a0f83633c82db2e6d33d6dc951678b73ca0134b73382d180')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  return 0
  cd "$pkgname"

  PYTHONPATH="$(pwd)/src" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
