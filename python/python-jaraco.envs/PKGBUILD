# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.envs
pkgver=2.6.0
pkgrel=1
pkgdesc="Classes for orchestrating Python (virtual) environments"
url="https://github.com/jaraco/jaraco.envs"
license=('MIT')
arch=(any)
depends=('python-path' 'python-virtualenv' 'python-tox')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-enabler' 'python-pytest-mypy')
source=("https://github.com/jaraco/jaraco.envs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0f00451a43a65d34ac4eaf95a1adebe24a5011b55500b65283ea5720378d77037d3eca14734a90b823d63abc46722f604cf6a2fb8e3a13b0797e407289b34bab')

build() {
  cd jaraco.envs-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver} \
  python -m build -wn
}

check() {
  cd jaraco.envs-$pkgver
  python -m pytest
}

package() {
  cd jaraco.envs-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
