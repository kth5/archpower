# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testscenarios
pkgver=0.5.0
pkgrel=19
pkgdesc="Testscenarios, a pyunit extension for dependency injection"
arch=(any)
license=('Apache')
url="https://github.com/testing-cabal/testscenarios"
depends=('python-pbr' 'python-testtools')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/t/testscenarios/testscenarios-$pkgver.tar.gz" "assertEqual-assertEquals.patch")
sha512sums=('f08b868babf504482369c5cb945d3e95bca1cc00f92d5013f6898c488051be0c26dc49aaabcef362aea45450a8d041d9826fb6b5c848beec013fb102d6935521'
            'fb421b781cfd17035c449ecda652b379a8d74f4b2120451131bf2b6f5a07c3a704f83eb0de5fca1434906145a126525a06015fcb7172a7595d03cad8eaa8dfe3')

prepare() {
  cd testscenarios-$pkgver
  patch -Np1 -i ${srcdir}/assertEqual-assertEquals.patch
}

build() {
  cd testscenarios-$pkgver
  python setup.py build
}

check() {
  cd testscenarios-$pkgver
  python -m testtools.run testscenarios.test_suite
}

package() {
  cd testscenarios-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}
