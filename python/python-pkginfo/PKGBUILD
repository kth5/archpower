# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgname=python-pkginfo
_pkgname="${pkgname#python-}"
pkgver=1.12.0
pkgrel=1
pkgdesc='Query metadata from sdists / bdists / installed packages'
arch=(any)
url='https://pypi.python.org/pypi/pkginfo'
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=(
  'python-pytest'
  'python-packaging'
  'python-wheel'
)
source=("$pkgname-$pkgver.tar.gz::https://pypi.io/packages/source/p/pkginfo/pkginfo-$pkgver.tar.gz")
sha512sums=('871d85cfbc69380033dc071bfb32dc3da4a5c44fefda8c3be9c7ae5d8ed17f2800f82f9ce3b81e4c0ebb3a1956581093a1379d00a9eb6a42e0821253586aaceb')
b2sums=('4a0e30c71c7ce283f16fb8595343ac7f473ce604e47a4863cc107cab96db1db9de0eb1c6ccd9430cea1eb6b94903f00356d75c5b9acfa50129d9c4c7ee24577d')

build() {
  cd "$_pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$pkgver"

  # HACK: needs upstream big report
  pytest -v -k 'not test_installed_ctor_w_dist_info'
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
