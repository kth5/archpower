# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=python-rfc3986
pkgver=2.0.0
pkgrel=6
pkgdesc='Validating URI References per RFC 3986'
arch=(any)
url='https://rfc3986.readthedocs.org'
license=(Apache-2.0)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+https://github.com/python-hyper/rfc3986#tag=$pkgver")
sha512sums=('925359747261cec2b30f6ec18060cda651b2c7530bf4b357062235f4cb5903cef894fba81c5b3ae18cc9adac84f9d5a147670bbeda972d101f01851d63d51590')
b2sums=('1c26b52fa03bc0f6becb6097da86125b033b5486e8d9faec52d829f14a6a4b34cfcbe6b31019e39b3c41457dd4738bd8e798658487b373e0663da3c8133754f0')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
