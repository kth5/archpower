# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mako
pkgver=1.3.2
pkgrel=1
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=(any)
url="https://www.makotemplates.org/"
license=('MIT')
depends=('python-markupsafe')
makedepends=('python-setuptools')
# python-lingua isn't packaged
checkdepends=('python-pytest' 'python-babel' 'python-beaker' 'python-dogpile.cache'
              'python-pygments')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz")
sha512sums=('acde7b4be929fdd97f2a3fc133966ffc44b5706dbdd3f1af0fee16620b3c82d12b1a67b7292a229084ebb4f0c392e8db1b2844112baa3c6a5fd2be8777b04897')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1'   # Michael Bayer
              '2CA9B722133AF20453401FD1A33AC2044BFDF51E')  # Mako Maintainer

build() {
  cd Mako-$pkgver
  python setup.py build
}

check() {
  cd Mako-$pkgver
  python -m pytest
}

package() {
  cd Mako-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
