# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mako
_pkgname=${pkgname#python-}
pkgver=1.3.8
_pkgver=rel_${pkgver//./_}
pkgrel=1
pkgdesc="A template library written in Python"
arch=(any)
url="https://github.com/sqlalchemy/mako"
license=(MIT)
depends=(
  python
  python-markupsafe
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  # python-lingua isn't packaged
  python-babel
  python-beaker
  python-dogpile.cache
  python-pygments
  python-pytest
)
optdepends=(
  'python-babel: for i18n features'
  'python-beaker: for caching support'
  'python-dogpile.cache: for caching support'
  'python-pygments: for syntax highlighting'
  'python-pytest: for testing utilities'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgver.tar.gz")
sha512sums=('7f0dab83970895dac9daf48c4b3ee176cf6ac296e04ed8148feccebe7475f92db3e6940814d4a9913c3b0ad1da7c566fd841b7d526e99adc6f2a88327074e6d4')

build() {
  cd "$_pkgname-$_pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$_pkgver"
  pytest
}

package() {
  cd "$_pkgname-$_pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
