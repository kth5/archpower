# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mako
pkgver=1.3.3
pkgrel=2
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=(any)
url="https://www.makotemplates.org/"
license=('MIT')
depends=('python-markupsafe')
makedepends=('python-setuptools')
# python-lingua isn't packaged
checkdepends=('python-pytest' 'python-babel' 'python-beaker' 'python-dogpile.cache'
              'python-pygments')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz")
sha512sums=('fa8bf3997f7a3edc18057f53e8450d72d5c86019229fef91eaa141f0b65a081ca9425c14245227b35aedcccc7dbe52e90bc7dfee34598b490e93cd01fc4a8321')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1'   # Michael Bayer
              '2CA9B722133AF20453401FD1A33AC2044BFDF51E')  # Mako Maintainer

build() {
  cd Mako-$pkgver
  python setup.py build
}

check() {
  cd Mako-$pkgver
  python -m pytest
}

package() {
  cd Mako-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
