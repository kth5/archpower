# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-brotlicffi
pkgver=1.2.0.0
pkgrel=2
pkgdesc='Python bindings to the Brotli compression library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/python-hyper/brotlicffi
license=(MIT)
depends=(
  brotli
  python
  python-cffi
)
makedepends=(
  git
  python-setuptools
)
_tag=c42980ada3c6df78660109f42cac4774f0c8d0dd
source=(git+https://github.com/python-hyper/brotlicffi.git#tag=${_tag})
b2sums=('1bf3edbd632e4220c16e407c25f64f685a3eb5a5b9bf9fb7607aa02988228cc294575d476979a4829158a2bf7ed4f1d5a0e6eeeece4fa11bea9888b08abaf10c')

pkgver() {
  cd brotlicffi
  git describe --tags | sed 's/^v//'
}

build() {
  cd brotlicffi
  USE_SHARED_BROTLI=1 python setup.py build
}

package() {
  cd brotlicffi
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-brotlicffi
}

# vim: ts=2 sw=2 et:
