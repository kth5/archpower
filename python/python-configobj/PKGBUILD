# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.com.ve>
# Contributor: Stefan Husmann  <stefan-husmann@t-online.de>
# Contributor: Manuel C.

pkgname=python-configobj
pkgver=5.0.9
pkgrel=4
pkgdesc='Simple config file reader and writer'
arch=(any)
url='https://github.com/DiffSK/configobj'
license=(BSD)
depends=(python)
checkdepends=(python-pytest)
makedepends=(git python-setuptools)
source=("git+$url#tag=v$pkgver")
b2sums=('a741249a7116b42f099caea58ab4081f38ee34a449d6f082064e379d54b2d394c977a2f5a10d879a19b9717fe1c71afc36b8c4b9467c33e5c643f9556fdc923a')

prepare() {
  cd configobj
  # Avoid the need for a local configobj install just for VERSION
  local _version=$(grep -oP "__version__ = '\K[^']+" src/configobj/_version.py)
  sed -i "/from validate/d;s/VERSION/\"$_version\"/" setup_validate.py
}

check() {
  cd configobj
  PYTHONPATH=src pytest src/tests
}

package() {
  cd configobj
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python setup_validate.py install --root="$pkgdir" --optimize=1
}
