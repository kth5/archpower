# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgname=python-apsw
pkgver=3.49.2.0
pkgrel=1
pkgdesc="Python wrapper for SQLite"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/rogerbinns/apsw"
license=('MIT')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'git')
depends=('python' 'sqlite')
source=("git+https://github.com/rogerbinns/apsw.git#tag=${pkgver}")
sha512sums=('5df31238abcbb51945a515f0f946336009efcbc165e44de51ca1f7eded5504090ba91de5db16b59e57b5639501f1d0cb0cd5dacc02aca3c6b011c145c97606a5')
b2sums=('dc0e5cbf7f01e6d5dbe798de352560a4b6e9717651d06e909fbc1e866b3aa547e8e6988475a2e1b71eabc6c1dd4c76c03f5c4a95f990cc7fff6be9d50f5e0e47')

build() {
  cd apsw

  python -m build --wheel --no-isolation
}

check() {
  return 0
  cd apsw

  gcc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -fPIC -shared -o testextension.sqlext src/testextension.c
  # do glob expansion in variable assignment
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-${python_version}" python -m unittest discover -vs apsw
}

package() {
  cd apsw

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
