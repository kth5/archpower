# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgname=python-apsw
pkgver=3.50.4.0
pkgrel=1
pkgdesc="Python wrapper for SQLite"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/rogerbinns/apsw"
license=('MIT')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'git')
depends=('python' 'sqlite')
source=("git+https://github.com/rogerbinns/apsw.git#tag=${pkgver}")
sha512sums=('e4a7f8c81872f95a77a1aebffa241811017f394e6a4a099cabab3702de7745c18d233bb1126be63e19d6da94363963f9987e43810d8de01c875e1f124419eaa1')
b2sums=('340dbde4252e4c9f930d4d094bc79cfdf66ebddccdc89a80bbabe95bd892adf769808ef1810bc14b9ea4fcf4c0221fa0f2f4bb4bb4a8f1cc3dc2d0c8cbcdce00')

MYARCH=${CARCH}
case "${CARCH}" in
  powerpc*) MYARCH=${CARCH/powerpc/ppc} ;;
esac

build() {
  cd apsw

  python -m build --wheel --no-isolation
}

check() {
  cd apsw

  gcc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -fPIC -shared -o testextension.sqlext src/testextension.c
  # do glob expansion in variable assignment
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  cd "build/lib.linux-$MYARCH-cpython-${python_version}"
  python -m apsw.tests -v
}

package() {
  cd apsw

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
