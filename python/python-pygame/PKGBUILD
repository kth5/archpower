# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: Matthew McGinn <mamcgi@gmail.com>
# Contributor: Gryffyn
# Contributor: Tetsumi

pkgname=python-pygame
pkgver=2.6.0
pkgrel=1
pkgdesc="Python game library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://www.pygame.org"
license=('LGPL')
depends=('glibc' 'libpng' 'python' 'sdl2' 'sdl2_mixer' 'sdl2_ttf' 'sdl2_image' 'portmidi')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel' 'sdl2')
optdepends=(
    'python-numpy: for examples'
)
source=("https://pypi.io/packages/source/p/pygame/pygame-$pkgver.tar.gz")
sha256sums=('722d33ae676aa8533c1f955eded966411298831346b8d51a77dad22e46ba3e35')
b2sums=('d2c0af1aa6c785709c3239bc274ecb82c3c1f5f9c9b3857098acf6f5a3f0075e0425224d4a36a68221ab2c7dbed01c648b51e158b8a9c87e7606179f1fb131b8')

build() {
    cd pygame-"$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd pygame-"$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
