# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=python-ansi2html
_libname=${pkgname/python-/}
pkgver=1.9.2
pkgrel=3
pkgdesc="Convert text with ANSI color codes to HTML"
arch=(any)
url="https://github.com/pycontribs/ansi2html"
license=('LGPL-3.0-or-later')
depends=(python)
makedepends=(python-build python-installer python-wheel python-setuptools python-setuptools-scm)
checkdepends=(python-pytest)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha256sums=('d4a12e0d0ed06af467882bcc24fad34ddcf7d4d61f237125ce7713f7f1341c3f')
b2sums=('a43f25a47af819a4302217fcfbe39081b40b2a94bb8a186f2ae3b52aceac9ab1a789e5c8848da2de00ec817cc8e182b36aec69d2a5d042838db100a2e6d693ee')
validpgpkeys=(94504C3AE11DD197920058ABA90ED7DE971095FF)

build() {
    cd "$_libname-$pkgver"
    SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
    local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

    cd "$_libname-$pkgver"
    python -m installer --destdir=test_dir dist/*.whl
    export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
    export PATH="$PWD/test_dir/usr/bin:$PATH"
    pytest -vv
}

package() {
    cd "$_libname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
