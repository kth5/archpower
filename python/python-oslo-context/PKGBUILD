# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=5.7.0
pkgrel=1
pkgdesc="OpenStack Context library"
arch=(any)
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-debtcollector')
makedepends=('git' 'python-setuptools')
checkdepends=('python-oslotest' 'python-stestr')
source=("git+https://github.com/openstack/oslo.context.git#tag=$pkgver")
sha512sums=('2fd7b5cfb084e77c64a1e06e23f3fab82c3e7949c0c4fb10d2432bd07b8a3a478a7e80135920d34cd2442218b4fd28aaacffb217bf5acf24aa1aa9cddd5be114')

build() {
  cd oslo.context
  python setup.py build
}

check() {
  cd oslo.context
  stestr run
}

package() {
  cd oslo.context
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
