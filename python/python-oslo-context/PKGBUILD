# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=5.5.0
_commit=e31a7a1af27ccaddce4cde9a6d273ea029fd92e7
pkgrel=1
pkgdesc="OpenStack Context library"
arch=(any)
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-debtcollector')
makedepends=('git' 'python-setuptools')
checkdepends=('python-oslotest' 'python-stestr')
source=("git+https://github.com/openstack/oslo.context.git#commit=$_commit")
sha512sums=('c96f57e8a2e7e55e303677365e8840d631fc163de77f4208b3dc2b8151cebffc0f16b41df7ed467e701ccd208cdc534783b6e36114a899e1242290b889f246ac')

build() {
  cd oslo.context
  python setup.py build
}

check() {
  cd oslo.context
  stestr run
}

package() {
  cd oslo.context
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
