# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=6.1.0
pkgrel=1
pkgdesc="OpenStack Context library"
arch=(any)
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache-2.0')
depends=('python-pbr' 'python-typing_extensions')
makedepends=('git' 'python-build' 'python-installer')
checkdepends=('python-oslotest' 'python-stestr')
source=("git+https://github.com/openstack/oslo.context.git#tag=$pkgver")
sha512sums=('389bf425d3d6ecba6dbc5afa65fa19d2bb75adde46f504ca80c743ce4b28269c24c344f5a865d301fdef51321dbbeb780b55dc63e8925d1a4b70be6ebfffb49e')

build() {
  cd oslo.context
  python -m build --wheel --no-isolation
}

check() {
  cd oslo.context
  stestr run
}

package() {
  cd oslo.context
  python -m installer --destdir "$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
