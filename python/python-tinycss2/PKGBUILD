# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-tinycss2
pkgver=1.5.1
pkgrel=2
pkgdesc="Low-level CSS parser for Python"
url="https://pypi.python.org/pypi/tinycss2"
license=('BSD-3-Clause')
arch=(any)
depends=('python-webencodings')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest')
_css_tests_commit=43e65b244133f17eb8a4d4404d5774672b94824f
source=("https://pypi.io/packages/source/t/tinycss2/tinycss2-$pkgver.tar.gz"
        css-parsing-tests-$_css_tests_commit.tar.gz::https://github.com/SimonSapin/css-parsing-tests/archive/$_css_tests_commit.tar.gz)
sha512sums=('1c2e1818c27c90f78b1e0af9b4c3c2a0944999691dcea678d0f5ed8a141d6ebd272b8b6763c1eac9d824db0d539e1e238f1e5e6abd2c02d97bb86002da637a10'
            'ed83bf447a595d754bbae9fcf25c3d2b4812f99b65c5e46b39d5f7ade57d49b4ffded9bc9d1cfef0a50b078f8ceb9bedb1d4f2f9ebf6db875eaad6c956b4f1d5')

prepare() {
  cd tinycss2-$pkgver
  cp ../css-parsing-tests-$_css_tests_commit/* tests/css-parsing-tests/
}

build() {
  cd tinycss2-$pkgver
  python -m build -nw
}

check() {
  cd tinycss2-$pkgver
  PYTHONPATH=. pytest
}

package() {
  cd tinycss2-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
