# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=3.0.0
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events"
arch=(any)
url="https://github.com/gorakhargosh/watchdog"
license=('Apache')
depends=('python')
optdepends=('python-yaml: for watchmedo')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-timeout')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('de4811f45a63b2e8c36c0daaf43c59844481e84b70a7732893a4340d2d7ed15249f993bd7b1c96b22d4b8f6ed3868fdf36df4cc4fcdcb24653d34ee6efec76ae')

prepare() {
  cd watchdog-$pkgver
  sed -i '/--cov/d' setup.cfg
}

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  # Invokes sudo
  PYTHONPATH="$PWD/build/lib" pytest --deselect tests/test_inotify_buffer.py::test_unmount_watched_directory_filesystem
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
