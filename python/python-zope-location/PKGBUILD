# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-location
_pkgname=zope.location
pkgver=5.1
pkgrel=1
pkgdesc="Special objects that have a structural location"
arch=('any')
url="https://github.com/zopefoundation/zope.location"
license=('ZPL-2.1')
depends=(
  'python'
  'python-zope-interface'
  'python-zope-proxy'
  'python-zope-schema'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.location/archive/$pkgver.tar.gz")
sha512sums=('8591c946ca0304ff0229c741f38713664152bbc669b83174b8941d03aecbe42025b74cf45613e8f8ff3a2c9e06d3def3f29068c55ef077723c94de7667572aca')

build() {
  cd $_pkgname-$pkgver
  sed -i 's/"setuptools <= 75.6.0"/"setuptools"/' pyproject.toml
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
