# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-lexicon
pkgver=2.0.1
pkgrel=5
pkgdesc="Powerful dict subclass(es) with aliasing & attribute access"
arch=(any)
url="https://github.com/bitprophet/lexicon"
license=('BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest-relaxed')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ce63ca85d823851c757c4e7292f4e7d6d6a7714bd48ef4df1a6f792ecf03965ea89aa09f81411128e8e734d72e6dd062a8afb03602c2511be951756216c823ec')

build() {
  cd ${pkgname#python-}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # Deselect failing tests, not sure why they fail.
  test-env/bin/python -m pytest \
    --deselect tests/alias_dict.py::AliasDict_::aliases_are_not_real_keys \
    --deselect tests/alias_dict.py::AliasDict_::aliases_of
}

package() {
  cd ${pkgname#python-}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
