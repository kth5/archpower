# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pluggy
pkgver=1.4.0
_commit=2efd28ef5dcd8b7ad44a41777ce640debbbf9c39
pkgrel=2
pkgdesc="Plugin and hook calling mechanisms for python"
arch=(any)
url="https://github.com/pytest-dev/pluggy"
license=('MIT')
depends=('python')
makedepends=('git' 'python-setuptools-scm' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/pytest-dev/pluggy.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd pluggy
  python -m build --wheel --no-isolation
}

check() {
  cd pluggy
  PYTHONPATH="$PWD"/src pytest
}

package() {
  cd pluggy
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
