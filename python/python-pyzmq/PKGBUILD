# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd at gmail dot com>

pkgname=python-pyzmq
pkgver=25.1.0
pkgrel=1
pkgdesc='Python bindings for ZeroMQ'
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://zeromq.org/languages/python/'
license=('LGPL')
depends=('zeromq' 'python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
  'cython'
)
_commit='5728a227d08333d6c24f431b23d3b110a4c92322'
source=("$pkgname::git+https://github.com/zeromq/pyzmq#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
