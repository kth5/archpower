# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd at gmail dot com>

pkgname=python-pyzmq
pkgver=27.0.2
pkgrel=1
pkgdesc='Python bindings for ZeroMQ'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://zeromq.org/languages/python/'
license=('BSD-3-Clause')
depends=('zeromq' 'python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-scikit-build-core'
  'python-wheel'
  'cython'
)
source=("$pkgname::git+https://github.com/zeromq/pyzmq#tag=v$pkgver")
sha512sums=('22fd115841404cfe6645b6d7a34b725e8c099ce1079f714bdf99f6b78590f12cd7db2c2f9cb4293625458d55264954ecb699e3c6d094ff6171d4cbaff02545fb')
b2sums=('3b09f249b14c9c79cfd22b25e4125152799ab6ddbb54ae09b5a7680ccdd545739c51b345e60cbb7c53c51057fb675c022165232e4b1d73c40d05261410962553')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
