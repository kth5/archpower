# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd at gmail dot com>

pkgname=python-pyzmq
pkgver=26.2.0
pkgrel=1
pkgdesc='Python bindings for ZeroMQ'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://zeromq.org/languages/python/'
license=('BSD-3-Clause')
depends=('zeromq' 'python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-scikit-build-core'
  'python-wheel'
  'cython'
)
source=("$pkgname::git+https://github.com/zeromq/pyzmq#tag=v$pkgver")
sha512sums=('c87c189003bccacbde90c214b64c4214b74a6daee9fdab8dcb5d1bac336a6f9ba5465a097008d038978ec7061dcb32254f1a9e6bed45ddd4b7a68a51372ee18f')
b2sums=('fe158692bca52a80eb825a79a6363feaf4bd84fada5792c9661b5ee6cf06f168bb1c009d3fc384483615e662404ccea582e61896807d769a10de0ba776c227b8')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
