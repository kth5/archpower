# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd at gmail dot com>

pkgname=python-pyzmq
pkgver=25.1.2
pkgrel=1
pkgdesc='Python bindings for ZeroMQ'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://zeromq.org/languages/python/'
license=('LGPL')
depends=('zeromq' 'python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
  'cython'
)
_commit='21f32cc9789415b7655eb62e5e5cb72ecc18cf04'
source=("$pkgname::git+https://github.com/zeromq/pyzmq#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
