# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net

pkgname=python-configshell-fb
pkgver=2.0.2
pkgrel=1
pkgdesc="Python framework for building simple CLI applications (free branch)"
arch=(any)
url="https://github.com/open-iscsi/configshell-fb"
license=('Apache-2.0')
depends=('python' 'python-pyparsing')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling' 'python-hatch-vcs')
provides=('python-configshell')
conflicts=('python-configshell')
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('f5307847a11d647c0e15d93a316550fea0904fc5d41667c778c7f73d48edbceaf76a5a1258b2ecdd7654932347ca1fe8ac67de06efeca3b5efcadd37a467b4cb')

build() {
  cd configshell-fb
  python -m build --wheel --no-isolation
}

check() {
  cd configshell-fb
  echo exit | PYTHONPATH="$PWD" examples/myshell
}

package() {
  cd configshell-fb
  python -m installer --destdir="$pkgdir" dist/*.whl
}
