# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-scrypt
pkgver=0.9.4
pkgrel=2
pkgdesc='Bindings for the scrypt key derivation function library'
url='https://github.com/holgern/py-scrypt'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('BSD-2-Clause')
depends=('python' 'glibc' 'openssl')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8a8a0bbbf23538538d7efaf40e7fd36bf0a05d15ed9dfe9245f2d79751e30562e680c63f3157c8473881225c73554e385f34523be5bcc089e7949d483f01e8ef')
b2sums=('819230b4b7b47221d2ba3e89ebb2e6401256ffbb735c514d4e8e68df55d388e02fffa2b434b60b5ab71fc759c28952492eb85c54fb3fb3244e4b6172a5248e38')

MYARCH=${CARCH}
case "${CARCH}" in
	powerpc*) MYARCH=${CARCH/powerpc/ppc} ;;
esac

build() {
  cd py-scrypt-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd py-scrypt-${pkgver}
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$MYARCH-cpython-${python_version}" pytest .
}

package() {
  cd py-scrypt-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/scrypt/tests
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
