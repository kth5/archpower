# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: j605

_name=pyperf
pkgname='python-perf'
pkgver=2.9.0
pkgrel=1
pkgdesc='Toolkit to run Python benchmarks'
arch=(any)
url='https://github.com/psf/pyperf'
license=('MIT')
depends=(
  'python'
  'python-psutil'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
provides=(python-pyperf)
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
sha512sums=('b8e54d974ea8b4a5ad0f0d343767f7ddb0eb43689f4e11ada8641a1258cf3feeb2d479797c94af62682dd4ffcae11452fb311c4b8b1e0dbfe8a965898050a3e6')
b2sums=('d9166c8c861dc845c0970c177af8046854fa62174c41ba4237bb8936994028ceb7e8e8ae5a13cd9fb6397c31c34ae0b80c0e82d9b6d0d33e9c6fbf4507599d61')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m unittest discover -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}

# vim: ts=2 sw=2 et:
