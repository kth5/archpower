# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: j605

_name=pyperf
pkgname='python-perf'
pkgver=2.6.2
pkgrel=1
pkgdesc='Toolkit to run Python benchmarks'
url='https://github.com/psf/pyperf'
arch=(any)
license=('MIT')
depends=('python' 'python-psutil')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
provides=(python-pyperf)
source=($url/archive/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('56a47ebe0fcf5e14434ebcf01264c119247e6bfd1ac3e9a93f785f77ad033857c47899c923a4d95d29ba40700b75a29e25335b2276df4b564d8a4df55ff7fd1f')
b2sums=('9df5d6a38984925764a685ee98c841eb7b063c65e5e7b542dceafb13546bf41ec3b3c17e172308fbac36458b523245f895f439c572c0dc5c6e29fad18fecbb0f')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"

  python -m unittest discover -s $_name/tests/ -v
}

package(){
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
