# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configupdater
pkgver=3.2
pkgrel=2
pkgdesc="Parser like ConfigParser but for updating configuration files"
url="https://github.com/pyscaffold/configupdater"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-setuptools-scm' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/pyscaffold/configupdater.git#tag=v$pkgver")
sha512sums=('1cdcce14b8d5445b3c10dde49f3887868db0cf85a1841a4735be36385508c5042418f5b4237e978c6a2136578836b4882c8a62966d6005de6e650f9a85cad5f0')

prepare() {
  sed -i '/--cov configupdater/d' configupdater/setup.cfg
}

build() {
  cd configupdater
  python -m build --wheel --no-isolation
}

check() {
  cd configupdater
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd configupdater
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
