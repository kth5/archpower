# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-fastbencode
_pkgname=fastbencode
pkgver=0.3.7
pkgrel=1
pkgdesc="Implementation of bencode with optional fast C extensions"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/breezy-team/fastbencode"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools-rust
  python-wheel
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('8e7b30a9811f8b3229077513006f189d2fcc8e09765fd1783bb773ad11147b1a')

build() {
  cd $_pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  python -m unittest discover -v
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
