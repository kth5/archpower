# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-fastbencode
pkgver=0.3.9
pkgrel=1
pkgdesc="Implementation of bencode with optional fast C extensions"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/breezy-team/fastbencode"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools-rust
  python-wheel
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('b86208909f809bb48a9d4809decb59810680f599f44c3894c28c4cb8f9c19bf3ad49212bed9ae68508ebd7c4cf16df07edce1b681d50a4c09a63a3d0ec42a210')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m unittest discover -v
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
