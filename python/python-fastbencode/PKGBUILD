# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-fastbencode
_pkgname=fastbencode
pkgver=0.3.5
pkgrel=1
pkgdesc="Implementation of bencode with optional fast C extensions"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/breezy-team/fastbencode"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools-rust
  python-wheel
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('6320746099f5a1ed4037e8c4498254012ab62b5f5518a5aeb5160f5528f7984e')

build() {
  cd $_pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  python -m unittest discover -v
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
