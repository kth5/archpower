# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Germ√°n Osella Massa <gosella@gmail.com>

pkgname=python-more-itertools
_name=${pkgname#python-}
pkgver=10.5.0
pkgrel=1
pkgdesc='More routines for operating on iterables, beyond itertools'
arch=(any)
url='https://github.com/more-itertools/more-itertools'
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
  'python-flit-core'
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('07a6fa54c0e251441689bef3ffdd7f290b6e54688f93cd8d0aab4fe097a16b051d81e49ba200d9d853e1ef9274746c792893954c2be2f2171f66a7578463397e')
b2sums=('649153d5ef4b5d805f00bf3ebd25a8e1a7301f20648a7acaba1789eeb20ed58f430c5e8e45263342de4723e3cfd1cdd869dd58a32e1bd28ec2e6ac6a8d0075de')

build() {
  cd "$_name-$pkgver"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  cd "$_name-$pkgver"

  python -m unittest
}

package() {
  cd "$_name-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
