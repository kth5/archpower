# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Germ√°n Osella Massa <gosella@gmail.com>

pkgname=python-more-itertools
pkgver=10.8.0
pkgrel=1
pkgdesc='More routines for operating on iterables, beyond itertools'
arch=(any)
url='https://github.com/more-itertools/more-itertools'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-flit-core
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('ff10bc70de440507ae2566697b981f7edc00d825e2dc8caaf4aeb5a385dcdf56daf57ed8d0e8096273499be7f9233aeb081a92ce8d771e30241ed4f06645f866')
b2sums=('71ef10c75b82ad62e546e92d1fa36f106fa4187dd5ca07a36a68345cb8d7ad80a28f29e49284e648f2c308fd1a83208a45bcbd83c3f8b0e217c89e0fd7b15e12')

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
