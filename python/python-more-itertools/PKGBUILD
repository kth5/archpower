# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Germ√°n Osella Massa <gosella@gmail.com>

pkgname=python-more-itertools
pkgver=10.7.0
pkgrel=1
pkgdesc='More routines for operating on iterables, beyond itertools'
arch=(any)
url='https://github.com/more-itertools/more-itertools'
license=('MIT')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-flit-core'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('5351deb484498fb44ab1915fdb17f6cf32b3319393332c66e7d60a2b4632bd5059039a64b7c786dfd76c4013c155dfd286dc9eade50dea791b98274cac25d558')
b2sums=('ad05e6a2f412ebe9af97491f2a8a078fc3d065afbe65c2fc8fc24acbde9b4691435271c529cf1a0c026a11c466eae64a686aabef75702220ad7439b5c88f17b7')

build() {
  cd "$pkgname"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
