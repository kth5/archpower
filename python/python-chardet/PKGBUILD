# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=python-chardet
pkgver=5.2.0
pkgrel=1
arch=(any)
url="https://github.com/chardet/chardet"
license=('LGPL')
depends=('python')
pkgdesc="Python3 module for character encoding auto-detection"
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/c/chardet/chardet-${pkgver}.tar.gz")
sha256sums=('1b3b6ff479a8c414bc3fa2c0852995695c4a026dcd6d0633b2dd092ca39c1cf7')
b2sums=('931653d176edd6045ce5f850018bd7bee70e29bcbec2313aaaa786974d1001deaa00402e22f0e7a14f662d3193da35ab97d65eba3fda68338c9a67131b2706fe')

build() {
  cd chardet-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd chardet-$pkgver
  python -m pytest
}

package() {
  cd chardet-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
