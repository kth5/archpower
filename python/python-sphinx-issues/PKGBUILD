# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-sphinx-issues
_pyname=${pkgname/python-/}
pkgver=5.0.0
pkgrel=2
pkgdesc="Sphinx extension for linking to your project's issue tracker"
url='https://github.com/sloria/sphinx-issues'
arch=(any)
license=('MIT')
depends=('python' 'python-sphinx')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest')
source=(https://github.com/sloria/sphinx-issues/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('fbe39e27b0df0e8e07b04030cc08b3aed7c51bd91dab145af5cd184c88180e20c472578821bdaaf047c6ecf9ca58523bc3e63aeba6e683b81ae0914b4b8f45af')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir=tmp-install dist/*.whl
  PYTHONPATH="$PWD"/tmp-install`python -c "import site; print(site.getsitepackages()[0])"` \
  py.test -v
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
