# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

# TODO: move optional python-requests-cache

pkgname=python-pandas-datareader
pkgver=0.10.0
pkgrel=5
pkgdesc="Data readers extracted from the pandas codebase"
arch=('any')
url="https://github.com/pydata/pandas-datareader"
license=('BSD')
depends=('python-pandas' 'python-lxml' 'python-requests')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydata/pandas-datareader/archive/v${pkgver}.tar.gz"
         python-3.12.patch)
sha256sums=('73385b23d42599c6f6f34966f0f22287acd8b775917d5694ae6df4adaf0d0add'
            'c0b83992b347d8f23d0ed3606e75a538d2aab57089064fa7c1276ad9a2c846e6')

prepare() {
  patch -d pandas-datareader-$pkgver -p1 < python-3.12.patch # Fedora patch
}

build() {
  cd "$srcdir"/pandas-datareader-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package() {
  cd "$srcdir"/pandas-datareader-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
