# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_name=cachecontrol
pkgname=python-cachecontrol
pkgver=0.13.1
pkgrel=1
epoch=1
pkgdesc="httplib2 caching for requests"
arch=(any)
url="https://github.com/ionrock/${_name}"
license=(Apache)
depends=(
  python
  python-filelock
  python-msgpack
  python-requests
  python-urllib3
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel)
checkdepends=(
  python-cherrypy
  python-mock
  python-pytest
  python-redis
)
optdepends=(
  'python-lockfile: for filecache'
  'python-redis: for redis cache'
)
# not tests in dist tarball on pypi.org: https://github.com/ionrock/cachecontrol/issues/281
# source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
source=($_name-$pkgver.tar.gz::https://github.com/ionrock/$_name/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('d385996b1874b7a12b34bf4735bd1df6cec3abed83252e0fc7095c9bfb5f7e0a')
b2sums=('67179719c224aaee52b3b61a128caadd25b26137e8ee6bbd77f36074f8d8889bf27a56a6bfe41ad376361b4055c49be046f46b964cd6a0cff3b670c9d8e5af9d')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
