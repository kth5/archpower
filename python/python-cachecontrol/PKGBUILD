# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_name=cachecontrol
pkgname=python-cachecontrol
pkgver=0.14.3
pkgrel=1
epoch=1
pkgdesc="Port of the caching algorithms in httplib2 for use with requests session object"
arch=(any)
url="https://github.com/ionrock/cachecontrol"
license=(Apache-2.0)
depends=(
  python
  python-filelock
  python-msgpack
  python-requests
  python-urllib3
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel)
checkdepends=(
  python-cherrypy
  python-pytest
  python-redis
)
optdepends=(
  'python-lockfile: for filecache'
  'python-redis: for redis cache'
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('822e4f1e048f35c9edd19426b4fe8260daf4168732c65c6ff9bc0d8acea24d7b')
b2sums=('4536434fee21407ba57cafd376ff2c1fb5dddec7bf45109ea3b7cf7b85a842146144a4e78f553fe2e64437b35d6c32a24fe0967ff5978262af948f7b83f3e5f5')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
