# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_name=cachecontrol
pkgname=python-cachecontrol
pkgver=0.14.1
pkgrel=2
epoch=1
pkgdesc="Port of the caching algorithms in httplib2 for use with requests session object"
arch=(any)
url="https://github.com/ionrock/cachecontrol"
license=(Apache-2.0)
depends=(
  python
  python-filelock
  python-msgpack
  python-requests
  python-urllib3
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel)
checkdepends=(
  python-cherrypy
  python-pytest
  python-redis
)
optdepends=(
  'python-lockfile: for filecache'
  'python-redis: for redis cache'
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('012e5eb94b9a0b7261572f07675f8f0cb78723e37e4c79ddd5f4e1e6ba413834')
b2sums=('6d84d94d0fb630e9db0b4d4da5ae0045a0704775e55ae632ab00859efe9034dcb7fb2caf1736ae13618b9d5a484afd288afb45cf5fad5f0f251fa967c7e7b930')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
