# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>

pkgname=ufmt
pkgver=2.8.0
pkgrel=1
pkgdesc='Safe, atomic formatting with black and µsort'
arch=(any)
url='https://github.com/omnilib/ufmt'
license=('MIT')
depends=('python-black' 'python-click' 'python-libcst' 'python-moreorless'
         'python-tomlkit' 'python-trailrunner' 'python-typing_extensions' 'usort' 'python-pygls')
makedepends=('python-build' 'python-installer' 'python-flit-core' 'python-wheel')
checkdepends=('python-ruff-api' 'usort')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('6f1f9129865c92cc6cd7d75872b2bc50544d37a512138ac01ad442a2f50fb34d8c9385270ff13f682f8ea0639a9356d517ee5d65e0e2acb9b24cd8a4ec2946f5')

build() {
  cd $pkgname-$pkgver

  python -m build -nwx
}

check() {
  cd $pkgname-$pkgver

  python -m ufmt.tests
}

package() {
  cd $pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
