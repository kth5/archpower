# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_bootstrap=0
_bootstrap_version=3.13.0
_pkgname=installer
pkgname=python-$_pkgname
pkgver=0.7.0
pkgrel=10
pkgdesc='Low-level library for installing a Python package from a wheel distribution'
arch=(any)
url='https://github.com/pypa/installer'
license=('MIT')
depends=('python')
if (( _bootstrap == 0 )); then
  makedepends=(
    'python-build'
    'python-flit-core'
    'python-installer'
  )
else
  makedepends=('git')
fi
checkdepends=('python-pytest')
if (( _bootstrap == 0 )); then
  source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
else
  source=(
    python-bootstrap::git+https://gitlab.archlinux.org/archlinux/python-bootstrap.git#tag=$_bootstrap_version
    python-build::git+https://github.com/pypa/build.git
    python-flit::git+https://github.com/pypa/flit.git
    python-installer::git+https://github.com/pypa/installer.git
    python-wheel::git+https://github.com/pypa/wheel.git
    python-packaging::git+https://github.com/pypa/packaging
    python-pyproject-hooks::git+https://github.com/pypa/pyproject-hooks
    python-setuptools::git+https://github.com/pypa/setuptools.git
  )
fi
sha512sums=('a509c6ea9d88b8527cce0428ca6109077820cb9aa352967a389012bac40f8ec04039ab73710f4fb32b32ed20affd520ce0ba16ba18d9d380ce0af1f51fe8e2c6')

prepare() {
  if (( _bootstrap == 0 )); then
    cd $_pkgname-$pkgver
  else
    cd python-bootstrap
    git submodule init

    git config submodule."external/build".url ../python-build
    git config submodule."external/flit".url ../python-flit
    git config submodule."external/installer".url ../python-installer
    git config submodule."external/wheel".url ../python-wheel
    git config submodule."external/packaging".url ../python-packaging
    git config submodule."external/pyproject-hooks".url ../python-pyproject-hooks
    git config submodule."external/setuptools".url ../python-setuptools

    git -c protocol.file.allow=always submodule update
    git submodule update --init --recursive
  fi
}

build() {
  if (( _bootstrap == 0 )); then
    cd $_pkgname-$pkgver
    python -m build -wn --skip-dependency-check
  else
    cd python-bootstrap
    python -m bootstrap.build
  fi
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=src pytest
}

package() {
  if (( _bootstrap == 0 )); then
    cd $_pkgname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  else
    cd python-bootstrap
    python -m bootstrap.install dist/installer-*-py3-none-any.whl -d "$pkgdir"
    install -vDm 644 external/installer/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  fi

  # remove windows entrypoint scripts executables
  rm "$pkgdir"/usr/lib/python*/site-packages/installer/_scripts/*.exe
}
