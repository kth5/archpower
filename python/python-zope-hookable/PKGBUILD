# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-hookable
_pkgname=zope.hookable
pkgver=8.1
pkgrel=1
pkgdesc="Represents the core of the Zope hookable Architecture"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/zopefoundation/zope.hookable"
license=('ZPL-2.1')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('bc6a0e0ef66445176f7179ecb867bd0147717b8e1124a4f2de506651644ec4d35e4a12bf970b759c97b126d5d92655b0e5386e91a3824d17aa067fd7774387b2')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m zope.testrunner -vc --test-path src/
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
