# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=python-sphinxext-opengraph
pkgver=0.13.0
pkgrel=1
pkgdesc='Sphinx extension to generate unique OpenGraph metadata'
arch=(any)
url='https://github.com/sphinx-doc/sphinxext-opengraph'
license=(BSD-3-Clause)
depends=(
  python
  python-sphinx
  python-matplotlib
  python-docutils
)
makedepends=(
  git
  python-build
  python-installer
  python-flit-core
)
checkdepends=(
  python-pytest
  python-beautifulsoup4
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3fbbe884bdf5c86b2f9c460105456178580c49b896dbe82038d947ec67ac905711056852a97b3b136407202f71bf5306666d913896b528c07d6775acdbc51aab')
b2sums=('fe88d5b6eae99428bd2afb52fb6f2bbc8713c463ab43642de610f680dcc71008203c74b9a6f1430114bb2d7a1352c641782ac1bdfb08d5c910fa5d37dd4c407f')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  return 0
  cd "$pkgname"

  # temporary install
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENCE.rst
}

# vim:set ts=2 sw=2 et:
