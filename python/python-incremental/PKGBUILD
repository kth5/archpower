# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-incremental
pkgver=24.7.2
pkgrel=1
pkgdesc='A small library that versions your Python projects'
arch=(any)
license=('MIT')
depends=('python-click' 'python-setuptools' 'python-twisted')
makedepends=('python-build' 'python-installer' 'python-wheel')
url='https://github.com/hawkowl/incremental'
checkdepends=('python-pytest')
source=("https://github.com/hawkowl/incremental/archive/incremental-$pkgver.tar.gz")
sha512sums=('d76a4192299bfa94a84f2e126800f2898c9c56c6831b6fe945fb291151f62958416d6d8118c0ea7c3cefbb85da6a97dfe9a2f8b95b31062a0831e84d522014d8')

build() {
  cd incremental-incremental-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd incremental-incremental-$pkgver
  pytest src
}

package() {
  cd incremental-incremental-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
