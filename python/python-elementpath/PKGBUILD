# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=5.0.0
pkgrel=1
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("git+https://github.com/sissaschool/elementpath.git#tag=v$pkgver")
sha512sums=('ea53092d20f060b8f3a17bf98511769eb189d66577cd33c6babd211d4c29617d80d568133b7cba628d89f7c29ea19bf1d185285991f782a40f4a880494fa1c64')

build() {
  cd elementpath
  python -m build -nw
}

check() {
  cd elementpath
  python -m pytest
}

package() {
  cd elementpath
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
