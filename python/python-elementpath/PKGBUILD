# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=5.1.1
pkgrel=1
pkgdesc='XPath 1.0/2.0/3.0/3.1 parsers and selectors for ElementTree and lxml'
arch=(any)
url='https://github.com/sissaschool/elementpath'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-lxml
  python-xmlschema
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('de4562eb9eddaea7b1ee1c0a23e453ad6d2f7331b4747e9cdffa67ce71ee5095a257065670145642f7457487cdfb5666eeade2bb291385269943343c52770872')
b2sums=('2ede91b8e55aa97de6e2ea5e7b63a25ad40aeda6d869e32647639ed0f75ddc7110a71dad0b5e2d2dccb8e3b6244415ba7834d466297b5800199023986730749f')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
