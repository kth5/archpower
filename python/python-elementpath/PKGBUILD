# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=4.7.0
pkgrel=1
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('git' 'python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("git+https://github.com/sissaschool/elementpath.git#tag=v$pkgver")
sha512sums=('92ac1663d4d2c71fcf610301cc46d8ab0d98fb00a67577e6bbb77770b87a08c057b93ea359ce3c50ea3196741374a78e3454eb6daa3ae4810faa95644643f1f6')

build() {
  cd elementpath
  python setup.py build
}

check() {
  cd elementpath
  python -m pytest
}

package() {
  cd elementpath
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
