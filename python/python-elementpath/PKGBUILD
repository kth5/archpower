# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=4.8.0
pkgrel=1
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('git' 'python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("git+https://github.com/sissaschool/elementpath.git#tag=v$pkgver")
sha512sums=('2aa9bc9a76aabcefd422a44ace984f651a3418bf67aa8ea4538f1b6f8142e1de880ab91c204316a10df87722cf1741eace931d45cb03da77b9fe806eeae320b9')

build() {
  cd elementpath
  python setup.py build
}

check() {
  cd elementpath
  python -m pytest
}

package() {
  cd elementpath
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
