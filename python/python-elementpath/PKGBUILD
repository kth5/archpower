# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=4.6.0
pkgrel=2
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('git' 'python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("git+https://github.com/sissaschool/elementpath.git#tag=v$pkgver")
sha512sums=('31c33b799413fdc7d1e0b48b9aa81a0bfa5ea39efcdf5930293c3cfcc3e2dcc3d4bc7d46970dbf4d337298c308dea6673aa8343619df3c761c1f0b5feb8d2802')

build() {
  cd elementpath
  python setup.py build
}

check() {
  cd elementpath
  python -m pytest
}

package() {
  cd elementpath
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
