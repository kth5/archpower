# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rst2pdf
pkgver=0.102
pkgrel=1
pkgdesc="Use a text editor. Make a PDF"
arch=(any)
url="https://github.com/rst2pdf/rst2pdf"
license=(MIT)
depends=(
  python
  python-docutils
  python-importlib-metadata
  python-jinja
  python-packaging
  python-pillow
  python-pygments
  python-reportlab
  python-smartypants
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
# TODO: package python-xhtml2pdf
# https://github.com/xhtml2pdf/xhtml2pdf/issues/560
checkdepends=(
  graphviz
  plantuml
  python-aafigure
  python-matplotlib
  python-pytest
  python-pymupdf
  python-sphinx
  python-svglib
)
# TODO: package python-xhtml2pdf
optdepends=(
  'plantuml: for UML diagram support'
  'python-aafigure: for ASCII image support'
  'python-pyphen: for hyphenation support'
  'python-matplotlib: for math support'
  'python-sphinx: for sphinx support'
  'python-svglib: for SVG support'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('39b6e280741ee58a36e34328a11edf26644b470cefc270b0fc6a50cdf20fb15bb068aa52c5e8da2a58e06085ecbaf730c00b58db4bcc42c2682cf0efb6f5c7be')
b2sums=('1765d2b469218a3909c809aa96d89bf82bbf972d3a1c99a3b56de9ba61e3e51c140e62418683ba7584fd13d176713615414d8aed713e033fb8a5e3b24cd3f731')

build() {
  cd $pkgname-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # https://github.com/rst2pdf/rst2pdf/issues/1067
    --deselect tests/input/test_raw_html.rst::test_raw_html
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="test_dir" dist/*.whl

  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  export PATH="$PWD/test_dir/usr/bin:$PATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,MAINTAINERS,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
