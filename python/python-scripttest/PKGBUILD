# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-scripttest
pkgver=2.0
pkgrel=1
pkgdesc="Utilities to help with testing command line scripts"
url="https://github.com/pypa/scripttest"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pypa/scripttest/archive/$pkgver.tar.gz")
sha512sums=('c3a2ccb1856c11a377287829683e63f3c8b4653ee778596a606195712bb8c01dcfb6e98408ac1012bfb3d0b45853f3076301f6a42e1f03bebf1df8a06e9d15f9')

build() {
  cd "$srcdir"/scripttest-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/scripttest-$pkgver
  pytest
}

package() {
  cd scripttest-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 docs/license.rst $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
