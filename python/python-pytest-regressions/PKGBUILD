# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pytest-regressions
pkgname=python-$_pkgname
pkgver=2.5.0
pkgrel=3
pkgdesc='Pytest plugin for regression testing'
arch=('any')
url='https://github.com/ESSS/pytest-regressions'
license=('MIT')
depends=('python-pytest' 'python-pytest-datadir' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('e2cb432d02584fbae099d4c84a1464a179dfe829845bb5e1d91f3082db8a4fdcc36b0ae985fe9ce3dae772527b985a00c9887f291df7ab6b79d1a428a78c4554')

build() {
  cd $_pkgname-$pkgver

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build -nw
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
