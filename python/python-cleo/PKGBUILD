# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=cleo
pkgname=python-cleo
pkgver=2.2.1
pkgrel=2
pkgdesc="create beautiful and testable command-line interfaces"
arch=(any)
url="https://github.com/python-poetry/$_pkgname"
license=(MIT)
depends=(python-crashtest
         python-rapidfuzz
         python-typing_extensions)
makedepends=(python-{build,installer,wheel}
             python-poetry-core)
checkdepends=(python-pytest
              python-pytest-mock)
_archive="$_pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('ee0a700bc9547fff1c31fc61fc5155412147b198e183498c3d926d8b1eb7ebb2')
b2sums=('2f5da066672ecf7067801cd6287cd81055be893001a00067ada150c76363dde1ad3dc715d030963a15cee56080a15d1843d34499a92a55a4d4a3dfe340239ce5')

prepare() {
    cd "$_archive"
    # we do not use overly strict version constraints
    sed -e 's/\^/>=/g' \
        -e 's/~=/>=/g' \
        -i pyproject.toml
}

build(){
    cd "$_archive"
    python -m build -wn
}

check() {
    cd "$_archive"
    export PYTHONPATH="$PWD/src"
    pytest -vv
}

package() {
    cd "$_archive"
    python -m installer -d "$pkgdir" dist/*.whl
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
