# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-cryptography
pkgver=46.0.3
pkgrel=1
pkgdesc="A package designed to expose cryptographic recipes and primitives to Python developers"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('Apache-2.0 OR BSD-3-Clause')
url="https://github.com/pyca/cryptography"
depends=(
  'gcc-libs'
  'glibc'
  'openssl'
  'python'
  'python-cffi'
)
makedepends=(
  'clang'
  'git'
  'lld'
  'llvm'
  'python-build'
  'python-installer'
  'python-maturin'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-certifi'
  'python-hypothesis'
  'python-pretend'
  'python-pytest'
)
source=("git+https://github.com/pyca/cryptography.git#tag=$pkgver")
sha512sums=('9a209885c5b3fcaf5581655f05d091d42f5d71a3bbb7e1328b0cf8d18c10f863f3257c75c40fab1385aa0b9f317ffb46e72cc70f740b9750780a20c6a8b612b7')

prepare() {
  cd cryptography
  # Drop all benchmark tests, this means we don't have to checkdepends on
  # pytest-benchmark nor are benchmark tests interesting for a distribution.
  rm -rf tests/bench
}

build() {
  cd cryptography
  echo "$RUSTFLAGS"
  # https://github.com/pyca/cryptography/issues/9023
  CC=clang RUSTFLAGS+=" -Clinker-plugin-lto -Clinker=clang -Clink-arg=-fuse-ld=lld"
  case "${CARCH}" in
    powerpc)
      CC=gcc
      RUSTFLAGS=""
    ;;
  esac

  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd cryptography
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir="tmp_install" dist/*.whl
  PYTHONPATH="$PWD/tmp_install/$site_packages:$PWD/vectors" \
    pytest --override-ini="addopts="
}

package() {
  cd cryptography
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.BSD
}
