# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=python-license-expression
pkgver=30.4.0
pkgrel=1
pkgdesc='Utility to parse, normalize and compare license expressions'
arch=(any)
url='https://github.com/nexB/license-expression'
license=('Apache-2.0')
depends=(
  'python'
  'python-boolean.py'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-xdist'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('74103443d6f16e2ba1b8ea83fa48c5df1ebf86c2a67096f19b521f4d03da06f46ca6ceddd5ca9297196bd27aa7aede6f408f1709e1c9ad8b43ad3bf3cadd3063')
b2sums=('8daa40e555b093c461659ec01257f2f692f6f24f5fb6d2474199bff298c2256c72509299395cbc5f765f69c3dea577db324ebdc1b8f7a19740b5bde8526d2ad4')

prepare() {
  cd "$pkgname"

  # Fix file to comply with PEP-440
  sed \
    -i pyproject.toml \
    -e "s/^fallback_version =.*/fallback_version = \"$pkgver\"/"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
