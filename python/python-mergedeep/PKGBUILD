# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux>org>
# Contributor: KokaKiwi <kokakiwi+git@kokakiwi.net>

pkgname=python-mergedeep
pkgver=1.3.4
pkgrel=2
pkgdesc='A deep merge function for Python'
arch=(any)
url=https://github.com/clarketm/mergedeep
license=(MIT)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=ab871aff9eccb5e1bf0ea237c3cff8f6787ebc56
source=(git+https://github.com/clarketm/mergedeep.git#tag=${_tag})
b2sums=('82cceebc42d8a7f429f281b197c90a58f2a95f11590f21e833be0e09d43ebac03b4b4670576c5194afa2c0cd682640a0c4c99784631ba9ed4432256524cf6978')

pkgver() {
  cd mergedeep
  git describe --tags | sed 's/^v//'
}

build() {
  cd mergedeep
  python setup.py build
}

package() {
  cd mergedeep
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 0644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-mergedeep/
}

# vim: ts=2 sw=2 et:
