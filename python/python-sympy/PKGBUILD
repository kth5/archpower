# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgname=python-sympy
pkgver=1.13.3
pkgrel=2
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='https://sympy.org/en/index.html'
license=('BSD')
depends=('python-mpmath')
optdepends=('ipython: user friendly interface for isympy'
            'python-symengine: optimized backend, set USE_SYMENGINE=1 to use')
makedepends=('git'
             'python-build'
             'python-installer'
             'python-setuptools'
             'python-wheel')
checkdepends=('python-hypothesis'
              'python-pytest')
source=(git+https://github.com/sympy/sympy#tag=sympy-$pkgver)
sha512sums=('abc0d547ccdd43469aa546c09491df756a0f447bc63e6828ad68f340e303c735ca3bb1f34a7bb2f0c52435cc699083b678e5dc721a45bc03e47509bfda0489b0')

build() {
  cd sympy
  python -m build --wheel --no-isolation
}

check() {
  cd sympy
  bin/test -v
}

package() {
  cd sympy

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
