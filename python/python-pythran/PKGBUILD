# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ashwin Vishnu <ashwinvis+arch at pr0t0nm4il dot com>
# Contributor: xantares

pkgname=python-pythran
pkgver=0.14.0
pkgrel=1
pkgdesc='Ahead of Time compiler for numeric kernels'
arch=(any)
url='https://pythran.readthedocs.io/'
license=(BSD)
depends=(python-ply python-numpy python-beniget xsimd boost)
makedepends=(python-setuptools python-pytest-runner)
source=(https://files.pythonhosted.org/packages/source/p/pythran/pythran-$pkgver.tar.gz)
sha256sums=('42f3473946205964844eff7f750e2541afb2006d53475d708f5ff2d048db89bd')

build() {
  cd pythran-$pkgver
  python setup.py build
}

package() {
  cd pythran-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  rm -r "$pkgdir"/usr/lib/python*/site-packages/pythran/{boost,xsimd} # Remove bundled boost and xsimd
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
