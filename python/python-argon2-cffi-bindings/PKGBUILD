# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-argon2-cffi-bindings
pkgver=25.1.0
pkgrel=2
pkgdesc='Low-level CFFI bindings for Argon2'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/hynek/argon2-cffi-bindings'
license=(MIT)
depends=(
  argon2
  glibc
  python
  python-cffi
)
makedepends=(
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
checkdepends=(python-pytest)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('204ac0e36e4f4fe97c7fa07b2ce1e5587633999786b055d1372cb27bb0402d4c')

build() {
  cd ${pkgname#python-}-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  export ARGON2_CFFI_USE_SYSTEM=1
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
