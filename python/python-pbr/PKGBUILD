# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=python-pbr
pkgver=6.0.0
_commit=11151154b94f5e116c0ec8cd312df8d341553d50
pkgrel=1
pkgdesc="Python Build Reasonableness"
arch=(any)
url='https://pypi.python.org/pypi/pbr'
license=('Apache')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-six' 'python-stestr' 'python-testresources' 'python-testscenarios'
              'python-testtools' 'python-virtualenv' 'python-sphinx' 'python-testrepository')
source=("git+https://github.com/openstack-dev/pbr.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd pbr
  python -m build --wheel --no-isolation
}

check() {
  cd pbr
  stestr run
}

package() {
  cd pbr
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
