# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-psycopg2
pkgver=2.9.9
pkgrel=4
pkgdesc="A PostgreSQL database adapter for the Python programming language."
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://www.psycopg.org/"
license=('LGPL-3.0-or-later')
depends=('python' 'glibc' 'postgresql-libs')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
source=("https://github.com/psycopg/psycopg2/archive/refs/tags/${pkgver}.tar.gz" "python3-13.patch")
b2sums=('0bf4c9e7b07915c52b7581d02c100ca1539b63ce46ec92cc299d4998112f6146a45a7c5a5806c99d5483f1b91cea9e577402a920690b1ada3ac5560e0fcc743e'
        '150497aa9a86297e5f1abf8aa3fe1150801d6a062df6720a9a0ff294aa205e4dbbe03e5831b6f48d4c3819ad1260f9c87b27c89107c6318660e164dd4e5fca40')

prepare(){
  cd "psycopg2-${pkgver}"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
  patch -Np1 -i "${srcdir}/python3-13.patch"
}

build(){
  cd "psycopg2-${pkgver}"
  CFLAGS+=" -Wno-implicit-function-declaration" \
  python -m build --wheel --no-isolation
}

check() {
  cd "psycopg2-${pkgver}"
  # TODO: run unit tests which don't require a postgresql server, for now try to import the module to verify
  # it can be imported with a newer Python version.
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" python3 -c 'import psycopg2'
}

package() {
  cd "psycopg2-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
