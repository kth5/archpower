# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-psycopg2
pkgver=2.9.7
pkgrel=1
pkgdesc="A PostgreSQL database adapter for the Python programming language."
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://www.psycopg.org/"
license=('LGPL3')
depends=('python' 'postgresql-libs')
makedepends=('python-setuptools' 'postgresql-libs')
source=("https://github.com/psycopg/psycopg2/archive/refs/tags/${pkgver}.tar.gz")
b2sums=('303125e2155dbb48545fef3e239ff0dc28f8405a23ee449c2ca9c234eb3a2028223404c2e83ee32eb3a32cc0f452dc49abeb2f886cb916ba79617bb91d5a2665')
build(){
  cd "psycopg2-${pkgver}"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
  python setup.py build
}

package() {
  cd "psycopg2-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
