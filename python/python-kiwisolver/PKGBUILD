# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-kiwisolver
pkgver=1.4.6
pkgrel=1
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('Artistic-1.0-Perl')
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
depends=('python')
makedepends=('git' 'python-cppy' 'python-setuptools-scm' 'python-wheel' 'python-build'
             'python-installer')
checkdepends=('python-pytest')
source=("git+https://github.com/nucleic/kiwi.git#tag=$pkgver")
sha512sums=('9b57f5661112a592b818328e511be664a757fe2f683cbad8d23a04aba87ef5fcd1249baf285e5480ba36761bf8c185ceb619bfd1a0bd17068fdde1b993fe10f1')

build() {
  cd kiwi
  python -m build --wheel --no-isolation
}

check() {
  MYARCH=${CARCH}
  case "${CARCH}" in
    powerpc) MYARCH=ppc ;;
    powerpc64) MYARCH=ppc64 ;;
    powerpc64le) MYARCH=ppc64le ;;
  esac

  cd kiwi
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-${MYARCH}-cpython-$python_version" pytest
}

package() {
  cd kiwi
  python -m installer --destdir="$pkgdir" dist/*.whl
}
