# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=rjsmin
pkgname=python-rjsmin
pkgver=1.2.3
pkgrel=3
pkgdesc="Fast javascript minifier for Python"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/ndparker/rjsmin"
license=(Apache-2.0)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('43a5445f6507cf5d751c0fad180f1c3391ce8b7fbe04d4ac98703ad19a72e0c0733a7c51d7090f5cd60acd8eedad23958d6cf5b291aee421179655f2f17d2c4a')
b2sums=('da7313f48b957b78f61e889d6bf8ce8738b1dc9204d51ef147c8b40fa3ad49999b60854e9b26f6836d6463c03e38c2f30332c036abe846c93a1ef54b12677d8c')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {docs/DESCRIPTION,CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
