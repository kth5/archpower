# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=rjsmin
pkgname=python-rjsmin
pkgver=1.2.4
pkgrel=1
pkgdesc="Fast javascript minifier for Python"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/ndparker/rjsmin"
license=(Apache-2.0)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('fe0f7736ce5dc5a9828d20aeaf7b403cac526e7edc92b71e5d40d0d1b9a49f8eb2fe33105b825c7760c4ff82506ad231017e25c47775a50bf983827f4c7626cf')
b2sums=('e6c62929a8a2d4e59f2d57957768865ab37463199319ec3e399861e857ebf16feeb0c9e41d88c8ee57425334cc7e1476c0c706063b6504d1d447398bdd753ed4')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {docs/DESCRIPTION,CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
