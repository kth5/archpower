# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deepdiff
pkgver=6.3.0
pkgrel=1
pkgdesc="Deep Difference and Search of any Python object/data."
url="https://github.com/seperman/deepdiff"
license=('MIT')
arch=(any)
depends=('python-ordered-set')
optdepends=('python-click: for cli'
            'python-toml: for cli'
            'python-yaml: for YAML support'
            'python-clevercsv: for more rubust CSV parsing'
            'python-orjson: for speed and memory optimized parsing')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-clevercsv' 'python-click' 'python-dateutil'
              'python-jsonpickle' 'python-numpy' 'python-toml' 'python-yaml' 'python-orjson')
source=("https://github.com/seperman/deepdiff/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6c4bc7367b7a7b2ab87319efa069aa3795944867c6f5130691af69b684bf042e79867bc66a6cb3834f428feb6221f14105771c32562765c2d753953a1ca10f9e')

build() {
  cd deepdiff-$pkgver
  python setup.py build
}

check() {
  cd deepdiff-$pkgver
  pytest tests
}

package() {
  cd deepdiff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
