# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deepdiff
pkgver=7.0.1
pkgrel=1
pkgdesc="Deep Difference and Search of any Python object/data."
url="https://github.com/seperman/deepdiff"
license=('MIT')
arch=(any)
depends=('python-ordered-set')
optdepends=('python-click: for cli'
            'python-tomli-w: for cli'
            'python-yaml: for YAML support'
            'python-clevercsv: for more rubust CSV parsing'
            'python-orjson: for speed and memory optimized parsing')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-clevercsv' 'python-click' 'python-dateutil'
              'python-jsonpickle' 'python-numpy' 'python-tomli-w' 'python-yaml' 'python-orjson'
	      'python-pydantic')
source=("https://github.com/seperman/deepdiff/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2f3a7f7d6493e040e6253ec6349518f01dd8648d002b53694df56fa409df7e6685aee2e2839cf4120a003c7aa77760a9ac67417ff069bfa168bd101ea1338b82')

build() {
  cd deepdiff-$pkgver
  python setup.py build
}

check() {
  cd deepdiff-$pkgver
  pytest tests
}

package() {
  cd deepdiff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
