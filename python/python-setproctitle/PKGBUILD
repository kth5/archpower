# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-setproctitle
pkgver=1.3.5
pkgrel=1
pkgdesc="Allows a python process to change its process title"
license=("BSD")
url="https://github.com/dvarrazzo/py-setproctitle"
depends=('python')
makedepends=(python-setuptools python-wheel python-build python-installer)
checkdepends=('python-pytest' 'procps-ng')
arch=(x86_64 powerpc64le powerpc riscv64 espresso)
source=(https://files.pythonhosted.org/packages/source/s/setproctitle/setproctitle-$pkgver.tar.gz)
sha256sums=('1e6eaeaf8a734d428a95d8c104643b39af7d247d604f40a7bebcf3960a853c5e')

build() {
    cd "$srcdir"/setproctitle-$pkgver
    python -m build --wheel --no-isolation
}

check() {
    case "${CARCH}" in
      powerpc*) return 0 ;;
    esac

    local _pyver=$(python -c "import sys; print('{0}{1}'.format(*sys.version_info[:2]))")
    cd "$srcdir"/setproctitle-$pkgver/build/lib.linux-$CARCH-cpython-$_pyver
    python -m pytest ../../tests
}

package_python-setproctitle() {
    cd "$srcdir"/setproctitle-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -m0644 -D COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
