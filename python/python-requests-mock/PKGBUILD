# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requests-mock
pkgver=1.10.0
pkgrel=3
pkgdesc="A mock of useful classes and functions to be used with python-requests."
arch=(any)
url="https://github.com/jamielennox/requests-mock"
license=('Apache')
depends=('python-requests' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-purl' 'python-pytest' 'python-requests-futures'
              'python-testrepository')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jamielennox/requests-mock/archive/$pkgver.tar.gz"
	"stop-using-python-mock.patch")
sha512sums=('24e34407651a14d89ce24b4fcd32556d7a522c02a3855466c6523c45e88dbc751d560478cc9af1eafc903dc8fdfc8715e1effedd116e33dee96063c980050560'
            'd3616feea24bd17c000664fa809098609376c5220859217b3c0caa193804511d76275d40164f8905f0a863f6634375c7e022464016f6fe64a4124030f4df9307')

export PBR_VERSION=$pkgver

prepare() {
  cd requests-mock-$pkgver
  patch -Np1 -i ${srcdir}/stop-using-python-mock.patch
}

build() {
  cd requests-mock-$pkgver
  python setup.py build
}

check() {
  cd requests-mock-$pkgver
  python -m pytest tests/pytest
  python setup.py testr
}

package() {
  cd requests-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
