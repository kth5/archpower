# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-semantic-version
pkgver=2.10.0
pkgrel=9
pkgdesc="A library implementing the 'SemVer' scheme"
arch=(any)
url='https://github.com/rbarrois/python-semanticversion'
license=(BSD-2-Clause)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('25b04cc7432d310abffc639778d0c50386ced826bf85abebbf1616a2080a018d52414f14d4894d9d087615abad7f8219d98b7c226e1cddb333da7d2e87d14450')
b2sums=('8ae1b92bd83e8f6bc9ff0a96a13fc1c48264fdc1c085d60c247d2f4aac1fb34ca11b354cdf945587a8c0b34d463f80045952188283b43b3bb8ee05bad8b43058')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}
check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
