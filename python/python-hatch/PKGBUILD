# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Santiago Torres-Arias <santiago @ usualplace>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>
# Contributor: Phillip A. (flying-sheep)

pkgname=python-hatch
pkgver=1.13.0
pkgrel=2
pkgdesc="A modern project, package, and virtual env manager"
arch=(any)
url="https://github.com/pypa/hatch"
license=('MIT')
depends=(
  'python'
  'python-click'
  'python-hatchling'
  'python-httpx'
  'python-hyperlink'
  'python-keyring'
  'python-packaging'
  'python-pexpect'
  'python-platformdirs'
  'python-rich'
  'python-shellingham'
  'python-tomli-w'
  'python-tomlkit'
  'python-userpath'
  'python-uv'
  'python-virtualenv'
)
makedepends=(
  'python-build'
  'python-hatch-vcs'
  'python-installer'
  'python-wheel'
)
checkdepends=(
  'git'
  'libxcrypt-compat'
  'python-pyfakefs'
  'python-pytest'
  'python-pytest-mock'
  'python-zstandard'
  'rust'
)
_name=${pkgname/python-/}
source=("$url/archive/refs/tags/$_name-v$pkgver.tar.gz")
sha256sums=('f293eefd006a2dab3982f1c34d1a9160e03a4822cd91c5020be97fa1c0eae661')
b2sums=('2d07d6273d2cff6d73bf6a4fd110080910a4b57123033cdf98b4a63afcc141d6841278626a6bb051ba4b1563d8ec9078e84ae3b92f7e26c5090046dfb2ad1b6b')

build() {
  cd $_name-$_name-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local pytest_options=(
    -vv
    --deselect tests/backend/builders/test_custom.py::test_default
    --deselect tests/backend/builders/test_custom.py::test_explicit_path
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_default_auto_detection
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_default
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_default_extra_dependencies
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_default_force_include
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_default_force_include_option
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_default_symlink
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_exact
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_exact_extra_dependencies
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_exact_force_include
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_exact_force_include_option
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_exact_force_include_build_data_precedence
    --deselect tests/backend/builders/test_wheel.py::TestBuildStandard::test_editable_pth
    --deselect tests/cli/self/test_report.py::TestDefault::test_open
    --deselect tests/cli/self/test_report.py::TestDefault::test_no_open
    --deselect tests/cli/self/test_report.py::test_pyenv
  )

  cd $_name-$_name-v$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  sed -e "s|'sh'|'bash'|g" -i tests/cli/python/conftest.py
  pytest "${pytest_options[@]}"
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$_name-v$pkgver
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  export PYTHONPATH="$pkgdir/$site_packages:$PYTHONPATH"
  export PATH="$pkgdir/usr/bin:$PATH"
  _HATCH_COMPLETE=bash_source "$_name" | install -vDm 644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$_name"
  _HATCH_COMPLETE=fish_source "$_name" | install -vDm 644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$_name.fish"
  _HATCH_COMPLETE=zsh_source "$_name" | install -vDm 644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$_name"
}
