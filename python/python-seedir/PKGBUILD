# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-seedir
pkgver=0.5.0
pkgrel=2
pkgdesc="A Python package for creating, editing, and reading folder tree diagrams"
url="https://github.com/earnestt1234/seedir"
license=('MIT')
arch=('any')
depends=('python-natsort')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('python-emoji: for emoji style')
checkdepends=('python-emoji' 'python-pytest')
source=("git+https://github.com/earnestt1234/seedir.git#tag=v$pkgver")
sha512sums=('481503870a54246670b2889049a274d4d019e0078ea59d2a4862de99df88b66e66cd1e130f3bcaeeb98885698c8a64e0d4ab7da370784bd05d59711e5208cea4')

build() {
  cd seedir
  python -m build -nw
}

check() {
  cd seedir
  pytest
}

package() {
  cd seedir
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
