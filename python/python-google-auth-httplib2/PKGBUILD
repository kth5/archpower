# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth-httplib2
pkgver=0.2.0
pkgrel=1
pkgdesc="Google Authentication Library: httplib2 transport"
url="https://github.com/googleapis/google-auth-library-python-httplib2"
license=('Apache')
arch=(any)
depends=('python-google-auth' 'python-httplib2')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-flask' 'python-pytest-localserver')
source=("https://pypi.io/packages/source/g/google-auth-httplib2/google-auth-httplib2-$pkgver.tar.gz")
sha512sums=('82ffe87172056cd6d15e3d5f12c155ff9e09fdea9d8e6385fd3ddc2db374df41a74dcfb3aff3df1d046d2b537f0a98ee9b5c7dd7818d2274af9462df48e7bdf8')

build() {
  cd google-auth-httplib2-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd google-auth-httplib2-$pkgver
  pytest
}

package() {
  cd google-auth-httplib2-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
