# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

_pkg=arrow
_pkgname=pyarrow
pkgname=python-$_pkgname
pkgver=22.0.0
pkgrel=1
# parquet-testing and arrow-testing projects have no releases, commits may need to be updated on pkgver bumps
_parquet_testing_commit=4cf674fc589309ba8651ad676f3fb557582d9cab
_arrow_testing_commit=fbf6b703dc93d17d75fa3664c5aa2c7873ebaf06
pkgdesc="Columnar in-memory analytics layer for big data — Python module."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://arrow.apache.org"
license=(Apache-2.0)
depends=(
  arrow
  gcc-libs
  glibc
  python
  python-numpy
)
optdepends=(
  'python-cffi: interact with C code'
  'python-pandas: Pandas integration'
  'python-fsspec: Filesystem Spec support'
)
makedepends=(
  cmake
  cython
  git
  ninja
  python-build
  python-cffi
  python-installer
  python-pandas
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-pytest
  python-pytz
)
source=(
  https://archive.apache.org/dist/$_pkg/$_pkg-$pkgver/apache-$_pkg-$pkgver.tar.gz{,.asc}
  git+https://github.com/apache/parquet-testing.git#commit=$_parquet_testing_commit
  git+https://github.com/apache/arrow-testing.git#commit=$_arrow_testing_commit
)
sha512sums=('8ec9ddaf7917c0e35c8bb32831fe6ea0a7b81de5723828a1289ba1b9e104b42af688d0f427a0ceff6f617d5f7ac67769431184b137e54f6987779e467c59d3ec'
            'SKIP'
            'fa20be39a69081d8c59add20a26cac4f201decda216c0e358f3bb72134e0015098596467d80dda2ba27bbf9e1a0c9b4a21a16aaaa0b6064821d1f4aaa4b93b1b'
            '444838ecf5f0ec22fb748632e16b67f29d7730050bc91c54a97455150caa77abe1006d8aa11536c15cc6b939d009f978262213dc3ae14c96d1983934e262dc1b')
validpgpkeys=(265F80AB84FE03127E14F01125BCCA5220D84079  # Krisztian Szucs (apache) <szucs.krisztian@gmail.com>
              08D3564B7C6A9CAFBFF6A66791D18FCF079F8007  # Kouhei Sutou <kou@cozmixng.org>
              AF6AADA4C9835B75973FF5DA275C532289DD0F4A  # Raúl Cumplido Domínguez (CODE SIGNING KEY) <raulcd@apache.org>
              A2AC7132B5DA7C273A7A147665F4A8CA9769ECD7) # Apache Arrow Automated Release Signing <private@arrow.apache.org>

build() {
  cd apache-$_pkg-$pkgver/python
  # Documentation for building from source:
  # https://github.com/apache/arrow/blob/main/docs/source/developers/python.rst#relevant-components-and-environment-variables
  # Note that components being disabled or enabled when building PyArrrow is by default based on how Arrow C++ is build.
  PYARROW_BUILD_TYPE="relwithdebinfo" \
  PYARROW_CMAKE_GENERATOR=Ninja \
  PYARROW_CMAKE_OPTIONS="-DARROW_SIMD_LEVEL=NONE -DARROW_RUNTIME_SIMD_LEVEL=MAX" \
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
     powerpc*) return 0 ;;
  esac

  local pytest_options=(
    -vv
    --pyargs pyarrow
    # ignore test that fails in version 22.0.0
    -k 'not test_timezone_absent'
  )

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer apache-$_pkg-$pkgver/python/dist/*.whl
  PARQUET_TEST_DATA="$srcdir"/parquet-testing/data \
  ARROW_TEST_DATA="$srcdir"/arrow-testing/data \
  test-env/bin/python -m pytest "${pytest_options[@]}"
}

package() {
  cd apache-$_pkg-$pkgver/python
  python -m installer --destdir="$pkgdir" dist/*.whl

  # drop tests from install
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rfv "$pkgdir$site_packages"/$_pkgname/{conftest.py,tests}

  # move python include files
  install -vdm 755 "$pkgdir"/usr/include/arrow/
  mv -v "$pkgdir$site_packages"/$_pkgname/include/arrow/python/ "$pkgdir"/usr/include/arrow/
  rm -rfv "$pkgdir$site_packages"/$_pkgname/include/
  ln -sTv /usr/include/arrow/ "$pkgdir$site_packages"/$_pkgname/include
}
