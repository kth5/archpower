# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requests-kerberos
pkgver=0.15.0
pkgrel=1
pkgdesc="A kerberos of useful classes and functions to be used with python-requests."
arch=(any)
url="https://github.com/requests/requests-kerberos"
license=('ISC')
depends=('python-requests' 'python-pyspnego' 'python-cryptography')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("git+https://github.com/requests/requests-kerberos.git#tag=v$pkgver")
sha512sums=('f1ceecd16267b049722b8b39bc58db2458a9e69a1e5d1d0b1092273cee37995a68c3232c6f7f4030b05c21d8a29069ea2ab3c7635a7e47ceeb74328100c591bc')

build() {
  cd requests-kerberos
  python -m build --wheel --no-isolation
}

check() {
  cd requests-kerberos
  pytest
}

package() {
  cd requests-kerberos
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
