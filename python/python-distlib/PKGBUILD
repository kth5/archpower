# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-distlib
pkgver=0.3.9
pkgrel=2
pkgdesc='Low-level functions that relate to packaging and distribution of Python software'
arch=(any)
url='https://distlib.readthedocs.io'
license=('PSF-2.0')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-tests')
source=("$pkgname::git+https://github.com/pypa/distlib.git#tag=$pkgver")
b2sums=('816f3d98d7e5f8bb12ae65685cf731c6a67c596d337474d618c90cb5d83a82f21fbee40d6d76f39791fa8770cb0282b2c45ff212d42e0e535403f9990380fd97')

prepare() {
  cd "$pkgname"

  # do not bundle executables of unknown provenance
  rm distlib/*.exe
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python tests/test_all.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
