# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>

pkgname=python-icecream
pkgver=2.1.7
pkgrel=1
pkgdesc='Sweet and creamy print debugging'
arch=(any)
url='https://github.com/gruns/icecream'
license=(MIT)
depends=(
  python
  python-colorama
  python-pygments
  python-executing
  python-asttokens
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('f4a9b8a0b90d297e655d78465e3ef11052d2d40793a8e276a519e1c18a0a6279289b4cfd9dd48f5f277eb04036def833b7974f2488239163b601cd0b7f757e94')
b2sums=('47228cc5a463783772cf9f22e2aa857cdf26fbbb6ce8f54a5b4586d9d9b27aa5d1be34aa5b0759620854eb0dc55191c32a80e5f3d99d6d2c7c782a7c13379464')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
