# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-copy
_pkgname=zope.copy
pkgver=5.0
pkgrel=3
pkgdesc="Pluggable object copying mechanism"
arch=('any')
url="https://github.com/zopefoundation/zope.copy"
license=('ZPL-2.1')
depends=(
  'python'
  'python-zodbpickle'
  'python-zope-interface'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-zope-location'
  'python-zope-testing'
  'python-zope-testrunner'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.copy/archive/$pkgver.tar.gz")
sha512sums=('5ce5907036e1572cdb4143030b25fc89ca738dec04fc91acf423b1570ab01e8906f8ffb2a389cc7f1edd58e25ad4ebaa324925032f05a21dbc07c94160086918')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" zope-testrunner --test-path=src
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
