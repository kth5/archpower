# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=comm
pkgname=python-$_pyname
pkgver=0.2.1
pkgrel=1
pkgdesc='Python Comm implementation for the Jupyter kernel protocol'
arch=(any)
url='https://pypi.org/project/comm/'
license=(BSD)
depends=(python
         python-traitlets)
makedepends=(python-build
             python-hatchling
             python-installer)
checkdepends=(python-pytest)
source=(https://github.com/ipython/$_pyname/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('7946d8ca485089dffcbbd11279b9195d30bc9dd0a654a7b084702049ebad7403')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  PYTHONPATH="$PWD" \
  pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
