# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Your Name <youremail@domain.com>

_name=annotated_types
pkgname=python-annotated-types
pkgver=0.5.0
pkgrel=1
pkgdesc="Reusable constraint types to use with typing.Annotated"
arch=(any)
url="https://github.com/annotated-types/annotated-types"
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-hatchling
  python-installer
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-pytest-sugar
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('d59c00ff84d510d87149aa9a33deb26e2c3aee98f0f99b63cc0eba83a212c4c1567fcb81370e8bf9add32fd23cc9ea2a5a8692a840d316c44ab7a550bd4349b3')
b2sums=('6c13267b2bdcafabc57e734e3a92133c140ed34560625ed067e01b19f4c8e250b01dacfd5518d0e7283fe469d053a1c1baed173f215231e7303defc3e444667d')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
