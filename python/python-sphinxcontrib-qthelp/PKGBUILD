# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-sphinxcontrib-qthelp
_name=${pkgname#python-}
pkgver=2.0.0
pkgrel=1
pkgdesc='Sphinx extension which outputs QtHelp document'
arch=(any)
url=https://github.com/sphinx-doc/sphinxcontrib-qthelp
license=(BSD-2-Clause)
depends=(python)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
)
checkdepends=(
  python-defusedxml
  python-pytest
  python-sphinx
)
source=("git+$url.git#tag=$pkgver")
b2sums=('e7b102bad614c0793a6aeed26fce0260b06553045fec1f5bd69d1df850cfb8692e635e70ebedb0531447fe984234234b763a3cb5b1b89034071ac9da2fffab3d')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"${_name//-/_}"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
