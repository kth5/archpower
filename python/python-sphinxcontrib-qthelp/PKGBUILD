# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-sphinxcontrib-qthelp
_name=${pkgname#python-}
pkgver=1.0.7
pkgrel=3
pkgdesc='Sphinx extension which outputs QtHelp document'
arch=(any)
url=https://github.com/sphinx-doc/sphinxcontrib-qthelp
license=('BSD-2-Clause')
depends=('python')
makedepends=('git' 'python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-pytest' 'python-sphinx')
source=("git+$url.git#tag=$pkgver")
b2sums=('0bb18d9323c175bfdf7ec34d62e5638c7c9bb0c42ccb4cda78bd03eb898d188ca150e16ba5cc09e82bf2731a797e73c065c90bc5cde70a119ba26fd23a272a26')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"${_name//-/_}"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
