# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinxcontrib_qthelp
pkgname=python-sphinxcontrib-qthelp
pkgver=1.0.6
pkgrel=1
pkgdesc='Sphinx extension which outputs QtHelp document'
arch=(any)
url=https://github.com/sphinx-doc/sphinxcontrib-qthelp
license=('BSD')
makedepends=('python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-pytest' 'python-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('62b9d1a186ab7f5ee3356d906f648cacb7a6bdb94d201ee7adf26db55092982d')
b2sums=('7fabbd35f5ccf5895f16fd17e335268445e32ee0cb8c596c68e537169d4ad0aa3dc102ee2474c136c62676cff6ab639288c5fff4892d78a257a85a7ab963fe9d')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
