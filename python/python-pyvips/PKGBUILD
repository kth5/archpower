# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Butui Hu <hot123tea123@gmail.com>
# Contributor: Aaron Liu <aaronliu0130 at gmail dot com

pkgname=python-pyvips
_pkgname=pyvips
pkgver=3.0.0
pkgrel=1
pkgdesc='Python binding for libvips using cffi'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/libvips/pyvips'
license=(MIT)
depends=(
  glib2
  glibc
  libvips
  python-cffi
  python-numpy
  python-pkgconfig
)
makedepends=(
  imagemagick
  libheif
  libjxl
  openslide
  poppler-glib
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/libvips/pyvips/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('252819173f4d3737c516c5b81aa3c82a92a21f666633705ecf239a0b33e95899')

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
