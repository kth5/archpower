# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-enabler
_pkgname=${pkgname#python-}
pkgver=3.3.0
pkgrel=2
pkgdesc="Enable installed pytest plugins"
arch=(any)
license=(MIT)
url="https://github.com/jaraco/pytest-enabler"
depends=(
  python
  python-jaraco.context
  python-jaraco.functools
  python-pytest
  python-toml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
# python-pytest-cov is an actual dependency for one of the tests.
checkdepends=(python-pytest-cov)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('1b0a9eddc1f67939b4a66d5bd29c4343fbe6842df94528bb04a77a7a5df9ccfb')

build() {
  cd "$_pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname"
  PYTHONPATH=$PWD pytest
}

package() {
  cd "$_pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
