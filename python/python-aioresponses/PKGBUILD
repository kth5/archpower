# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aioresponses
pkgver=0.7.6
_commit=444aa498cd2f625edd7923355fa373972d5fcf44
pkgrel=2
pkgdesc="A helper to mock/fake web requests in python aiohttp package."
url="https://github.com/pnuckowski/aioresponses"
license=('MIT')
arch=(any)
depends=('python-aiohttp')
makedepends=('git' 'python-pbr' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-ddt')
source=("git+https://github.com/pnuckowski/aioresponses.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd aioresponses
  python -m build --wheel --no-isolation
}

check() {
  cd aioresponses
  pytest
}

package() {
  cd aioresponses
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
