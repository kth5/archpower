# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-syrupy
pkgver=4.6.1
pkgrel=4
pkgdesc="Pytest Snapshot Test Utility"
arch=(any)
license=('Apache-2.0')
url="https://github.com/tophat/syrupy"
depends=('python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
source=("git+https://github.com/tophat/syrupy.git#tag=v$pkgver")
sha512sums=('15a83d0a5d7eed41b756df7749a35bea34f78710d2af2eac368492659d282e53d707a3c0ff9d3c3d106468fb9dde318dd5a6f0cd757a65ef75be6aa73ad90584')

build() {
  cd syrupy
  python -m build -nw
}

check() {
  cd syrupy
  python -m venv tmpenv --system-site-packages
  tmpenv/bin/python -m installer dist/*.whl
  tmpenv/bin/python -m pytest
}

package() {
  cd syrupy
  python -m installer -d "$pkgdir" dist/*.whl
}
