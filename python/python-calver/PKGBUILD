# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-calver
pkgver=2025.04.01
pkgrel=1
pkgdesc="Setuptools extension for CalVer package versions"
url="https://github.com/di/calver"
license=('Apache-2.0')
arch=(any)
depends=('python')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-pretend')
source=("git+https://github.com/di/calver.git#tag=$pkgver")
sha512sums=('f8e1e3072c46c226eebdde52b6341a878265261079ecfdab6d68a34c7b8d21eaf89debc52931d0e88d9593517fd03d81da9ecceba3a8e01b6fb427f448eb0370')

prepare() {
  cd calver
  echo "Version: $pkgver" > PKG-INFO
}

build() {
  cd calver
  python -m build --wheel --no-isolation
}

check() {
  cd calver
  mv PKG-INFO PKG-INFO.bak
  (
    # unset SOURCE_DATE_EPOCH to workaround test failures
    unset SOURCE_DATE_EPOCH
    PYTHONPATH="$PWD"/build/lib pytest
  )
  mv PKG-INFO.bak PKG-INFO
}

package() {
  cd calver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
