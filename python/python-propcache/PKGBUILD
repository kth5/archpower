# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-propcache
pkgver=0.3.2
pkgrel=1
pkgdesc='Fast property caching'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aio-libs/propcache'
license=(Apache-2.0)
depends=(
  glibc
  python
)
makedepends=(
  cython
  python-build
  python-expandvars
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$url/archive/v$pkgver/${pkgname#python-}-$pkgver.tar.gz")
sha512sums=('8b169fc528bff0d45f1948a2b76a146b34d7be968ba567fee37243c6804e9cd0012736023f0580b5fd28b69f1e6778cfda44d92db3edb93ff0dc8b116042c055')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
