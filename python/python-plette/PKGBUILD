# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plette
pkgver=1.0.0
pkgrel=1
pkgdesc="Structured Pipfile and Pipfile.lock models"
url="https://github.com/sarugaku/plette"
license=('ISC')
arch=(any)
depends=('python-tomlkit')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-cerberus')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/plette/archive/v$pkgver.tar.gz")
sha512sums=('72a3f270f76dbf32cb418628f1b24046111a46633603b07fd44fd4ed50d946653afaec070bce8e67a1ecff3ec8607f2476d3b25760c76726218873898bef97ed')

build() {
  cd plette-$pkgver
  python -m build -nw
}

check() {
  cd plette-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd plette-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
