# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Philipp A. <flying-sheep@web.de>

_pkgname=aiohttp
pkgname=python-aiohttp
_gitcommit=996de2629ef6b4c2934a7c04dfd49d0950d4c43b
pkgver=3.8.6
pkgrel=1
pkgdesc='HTTP client/server for asyncio'
url='https://aiohttp.readthedocs.io'
arch=(x86_64 powerpc64le powerpc64 riscv64)
license=('Apache')
depends=('python' 'python-multidict' 'python-async-timeout'
         'python-yarl' 'python-attrs' 'python-charset-normalizer'
         'python-aiosignal' 'python-frozenlist')
makedepends=('git' 'cython' 'nodejs' 'python-build' 'python-installer'
             'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-pytest-mock'
              'python-async_generator' 'python-brotli'
              'python-pytest-xdist' 'python-pytest-cov'
              'python-trustme' 'python-freezegun' 'gunicorn' 'python-re-assert'
              'python-proxy.py' 'python-aiodns')
optdepends=('gunicorn: to deploy using Gunicorn'
            'python-aiodns: for fast DNS resolving'
            'python-brotli: for Brotli transfer-encodings support')
source=(${pkgname}::"git+https://github.com/aio-libs/aiohttp#commit=${_gitcommit}"
        git+https://github.com/nodejs/llhttp.git
        yarl-1.9.patch)
sha512sums=('SKIP'
            'SKIP'
            'd82603b24d2525e801878e807cef7bc3f69c3bb263581e1f0078e6794e39f07ad714254fc7257a6ca5f1e165977ada3b1116e00478b7206b329d9e277b3c1829')

pkgver() {
  cd ${pkgname}
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."vendor/llhttp".url "${srcdir}/llhttp"
  git -c protocol.file.allow=always submodule update --recursive
  sed 's|.install-cython ||' -i Makefile

  # This test calls the Python interpreter, we need to make sure that the path
  # for the C extensions is correct there as well
  sed -i "s/import {import_path!s}/import sys; sys.path.insert(0, '{os.environ['PYTHONPATH']}'); &/" \
      tests/test_circular_imports.py

  # yarl 1.9 compatibility
  patch -p1 -i ../yarl-1.9.patch
}

build() {
  cd ${pkgname}
  make generate-llhttp cythonize
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}
  local _python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-${_python_version}" pytest -k 'not test_default_loop[pyloop] and not test_ctor_global_loop and not test_set_loop_default_loop'
}

package() {
  cd ${pkgname}
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
