# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinxcontrib_devhelp
pkgname=python-sphinxcontrib-devhelp
pkgver=1.0.5
pkgrel=1
pkgdesc='Sphinx extension which outputs Devhelp document'
arch=(any)
url=https://github.com/sphinx-doc/sphinxcontrib-devhelp
license=('BSD')
makedepends=('python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-pytest' 'python-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('63b41e0d38207ca40ebbeabcf4d8e51f76c03e78cd61abe118cf4435c73d4212')
b2sums=('f9c46d07888eab1bddcecce3a862fe503a0187620768a40f360fa985dee9cfaaf8d28f03c6a08e0b174788639675ee715141287bd7bf04ff403459e9858e4f9c')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
