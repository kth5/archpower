# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-argparse-manpage
pkgver=4.6
pkgrel=2
pkgdesc='Automatically build man-pages for your Python project'
url='https://github.com/praiskup/argparse-manpage'
arch=(any)
license=('Apache')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-pip')
source=("https://github.com/praiskup/argparse-manpage/releases/download/v${pkgver}/argparse-manpage-${pkgver}.tar.gz")
sha512sums=('ba9149609b3724811b4cb780c2ebcbfde7f0850153df679fad9e7c12a4a25f84635669ef8f6011ed023228c57e3917cb4850b2d7c395c38191a179edfc288028')

build() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python -m build --wheel
}

check() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python -m unittest discover -vs tests
}

package() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python -m installer --prefix=/usr --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
