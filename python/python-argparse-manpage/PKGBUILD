# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-argparse-manpage
pkgver=4.7
pkgrel=2
pkgdesc='Automatically build man-pages for your Python project'
url='https://github.com/praiskup/argparse-manpage'
arch=(any)
license=('Apache')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-pip')
source=("https://github.com/praiskup/argparse-manpage/releases/download/v${pkgver}/argparse_manpage-${pkgver}.tar.gz")
sha512sums=('88c2e0e0747f5950b131d5456b1bc153e64d11ca41d87e3c995d91f7ffb662685a5c58f7ea99f011151aa72e45a5bcab98ac6729e20a19d086a7798d9c783452')

build() {
  cd "$srcdir/argparse_manpage-$pkgver"
  python -m build --wheel
}

check() {
  cd "$srcdir/argparse_manpage-$pkgver"
  python -m unittest discover -vs tests
}

package() {
  cd "$srcdir/argparse_manpage-$pkgver"
  python -m installer --prefix=/usr --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
