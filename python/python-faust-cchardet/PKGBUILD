# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=python-faust-cchardet
pkgver=2.1.19
pkgrel=2
pkgdesc="Fork of the original cChardet"
url="https://github.com/faust-streaming/cChardet"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(
  GPL
  LGPL
  MPL
)
depends=(
  python
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-pkgconfig
  python-setuptools
  python-wheel
)
provides=("python-cchardet=$pkgver")
conflicts=(python-cchardet)
source=(
  "faust-cchardet::git+$url#tag=v$pkgver"
  "pyyoshi-uchardet::git+https://github.com/PyYoshi/uchardet"
)
b2sums=('e4dcee1147eaa0247db5d3d75b805337819bb372c10e7e1b6d93195fe73f8a88a0db73c8fc5769b7ddbc4deff5eba424c07835c49bd7ebb5ff6cfafb551f15e4'
        'SKIP')

prepare() {
  cd faust-cchardet

  git submodule init
  git submodule set-url src/ext/uchardet "$srcdir/pyyoshi-uchardet"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  cd faust-cchardet
  python -m build --wheel --no-isolation
}

package() {
  cd faust-cchardet
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set sw=2 sts=-1 et:
