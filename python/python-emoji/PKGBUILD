# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-emoji
pkgver=2.15.0
pkgrel=1
pkgdesc="Emoji for Python"
url="https://github.com/carpedm20/emoji"
license=('BSD-3-Clause')
arch=(any)
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/carpedm20/emoji.git#tag=v$pkgver")
sha512sums=('94ceba743b2babd639a561354b9b379decb5ed10bcf1b0f49c6023873bd7d13609b1d8e8a11a884638f8ac94fb5c2b616c8bdad5b431c15e78bbfab58957fefc')

build() {
  cd emoji
  python -m build -nw
}

check() {
  cd emoji
  pytest
}

package() {
  cd emoji
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
