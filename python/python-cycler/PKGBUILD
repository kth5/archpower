# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cycler
pkgver=0.12.0
pkgrel=1
pkgdesc="Composable style cycles"
arch=(any)
license=('BSD')
url="https://github.com/matplotlib/cycler"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/matplotlib/cycler/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('dcbaf8c8d66ae0a302f9dc7507d7064ded133ef5349f2d08cfb52beb3d7a81aa7cd0bebd5da357934431d39b6141e0e2ce344ab7f6e12b6ec6f61649bef3e5c3')

build() {
  cd cycler-$pkgver
  python -m build -wn
}

check() {
  cd cycler-$pkgver
  pytest
}

package() {
  cd cycler-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
