# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=u-msgpack-python
pkgname='python-u-msgpack'
pkgver=2.8.0
pkgrel=2
arch=(any)
url="https://github.com/vsergeev/${_pkgname}"
license=('MIT')
pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python"
depends=('python')
makedepends=('python' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('b801a83d6ed75e6df41e44518b4f2a9c221dc2da4bcd5380e3a0feda520bc61a')
b2sums=('9cd0bf2a7dca8eac4c990e320a5d3c325ea31867c82a46d588c8e9bfae1feef504916f8cb75d3f2ed2adb3fa13c03c07fca91071d5d9a6d6ab47686539d704bf')

build() {
    cd ${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd ${_pkgname}-${pkgver}

    pytest
}

package() {
    cd ${_pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
