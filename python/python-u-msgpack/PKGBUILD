# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-u-msgpack
pkgver=2.8.0
pkgrel=5
pkgdesc='A portable, lightweight MessagePack serializer and deserializer written in pure Python'
arch=(any)
url='https://github.com/vsergeev/u-msgpack-python'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('20bd837869e856e327dea5b6fff166c4d1eb28199afc57f6e145eaa738af53418ceb8003c091fac1ee5b2ad7477b0d9fde7d8f6b2df74e37a7d397b795936220')
b2sums=('427c31d2042b01a3043e2d156e966406be4dc8da67a8eaf318e091a750e2ff214fbea3f337b1337b929279ad665aef098877b153c6a102b5c0e35b396d540555')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
