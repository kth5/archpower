# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgname=python-dateutil
pkgver=2.8.2
_sha=6b035517571e63b6a63a493740c5506ec1e5da44
pkgrel=6
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=(any)
license=(BSD Apache)
url="https://github.com/dateutil/dateutil"
depends=(python-six)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm
             python-six git)
checkdepends=(python-freezegun
              python-hypothesis
              python-pytest)
source=("git+$url.git#commit=$_sha?signed")
sha256sums=('SKIP')
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23') # Mario Corchero <mariocj89@gmail.com>

build() {
  cd dateutil
  python -m build -wn
}

check() {
  cd dateutil
  # https://github.com/pytest-dev/pytest/issues/5678
  pytest -W ignore::pytest.PytestUnknownMarkWarning \
    --deselect dateutil/test/test_internals.py::test_parser_parser_private_not_warns
}

package() {
  cd dateutil
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
