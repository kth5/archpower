# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-blosc2
pkgver=3.8.0
pkgrel=1
pkgdesc='Wrapper for the blosc2 compressor'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/Blosc/python-blosc2'
license=('BSD-3-Clause')
depends=(
  'blosc2'
  'glibc'
  'python'
  'python-msgpack'
  'python-ndindex'
  'python-numexpr'
  'python-numpy'
  'python-platformdirs'
  'python-py-cpuinfo'
  'python-requests'
)
makedepends=(
  'cmake'
  'cython'
  'git'
  'ninja'
  'python-build'
  'python-installer'
  'python-scikit-build-core'
  'python-setuptools'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3eb91be34bc36ab7e9d25ea687cb075ae18887cb913cd78de67bb0d604f03c37f06b3fe5676e05d628955f4c672049fb279ab7f9aa998010fe7db68a15973edf')
b2sums=('628da31d9691e258cc63dfad642f76663b5e9c4ef77d0ff4fc562495ddd346a206a4f807f8132a1c36e6880b82e4c567164fa1275371e99ec467f58ab7e5be9b')

build() {
  cd $pkgname
  export CMAKE_ARGS="-DUSE_SYSTEM_BLOSC2=ON"
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
