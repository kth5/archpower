# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgname=python-keyring
pkgver=25.6.0
pkgrel=1
pkgdesc='Store and access your passwords safely'
arch=(any)
url='https://github.com/jaraco/keyring'
license=('MIT')
depends=(
  'python'
  'python-jaraco.classes'
  'python-jaraco.context'
  'python-jaraco.functools'
  'python-secretstorage'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-shtab'
  'python-wheel'
)
checkdepends=(
  'python-pyfakefs'
  'python-pytest'
)
optdepends=(
  'libsecret: libsecret backend'
  'python-dbus: kwallet backend'
  'python-gobject: libsecret backend'
  'python-keyrings-alt: Alternative backends'
  'python-pluggy: devpi client'
)
source=("git+$url.git#tag=v$pkgver")
sha512sums=('d8c2625bbb0a4a837aa240647ea9a5b9fe24aebfbe1317b03ef8f08987fbc35a1a4dc7e52a7a9d7f1501b2387045691e7bb1c33e003d2adca6fff7e077f2913c')
b2sums=('2aae2cc7d43379966d5b59b445978e3daf3f9d76ce0960ee8b563cf96059950e615a430b5fed02bc587fb695379033fa2e9e21094c3ac652c54e9fb2fe9e790f')

build() {
  cd ${pkgname#python-}
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation

  # Completions
  python -m venv --system-site-packages completion-env
  completion-env/bin/python -m installer dist/*.whl
  completion-env/bin/keyring --print-completion bash > keyring.bash
  completion-env/bin/keyring --print-completion zsh > keyring.zsh
}

check() {
  cd ${pkgname#python-}
  pytest --deselect tests/test_packaging.py::test_entry_point
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vDm644 keyring.bash "$pkgdir/usr/share/bash-completion/completions/keyring"
  install -vDm644 keyring.zsh "$pkgdir/usr/share/zsh/site-functions/_keyring"
}
