# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-numpydoc
pkgver=1.9.0
pkgrel=2
pkgdesc="Numpy's Sphinx extensions"
url="https://github.com/numpy/numpydoc"
license=('Apache')
arch=(any)
depends=('python-sphinx' 'python-jinja' 'python-tabulate')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-matplotlib' 'python-numpy' 'python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/numpy/numpydoc/archive/v$pkgver.tar.gz")
sha512sums=('7ae73a5bbce11e674fe8ca16e8ddce7914498f97dc8ebe85c26ae0cf240299596c20a6915e3a547a394e0e3f9ae6e9ac0694a2de7baa34918df98f523a8bfdf0')

build() {
  cd numpydoc-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd numpydoc-$pkgver
  PYTHONPATH="$PWD" pytest --override-ini="addopts="
}

package() {
  cd numpydoc-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
