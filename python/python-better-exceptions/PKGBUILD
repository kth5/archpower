# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-better-exceptions
pkgver=0.3.3
pkgrel=2
pkgdesc='Pretty and useful exceptions in Python, automatically'
arch=(any)
url='https://github.com/Qix-/better-exceptions'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/Qix-/better-exceptions.git#tag=$pkgver")
b2sums=(26f02a4f64f4baa6e61b679f3eda483346b5a0b535693748b08fc11869d1be7765e5884b2f7939cad9831f09d392629b72ff6c377f112ba3d7b62e6981f4614b)

build() {
  cd better-exceptions
  python -m build --wheel --no-isolation
}

package() {
  cd better-exceptions
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
