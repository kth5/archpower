# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

_pkgname=coveragepy
pkgname=python-coverage
pkgver=7.4.1
pkgrel=1
pkgdesc="A tool for measuring code coverage of Python programs"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://nedbatchelder.com/code/coverage/"
license=('Apache-2.0')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-flaky' 'python-hypothesis'
              'python-pytest' 'python-pytest-xdist'
              'python-unittest-mixins' 'python-virtualenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nedbat/coveragepy/archive/$pkgver.tar.gz")
sha512sums=('5df67a35ca37107ba89afb89868e7e448585f2741a448dccc3418017a10f951e93699958b4a1a2b2153dc5bee8b88b95bf3ce7a6ff5424f5cc4009de3829f371')
b2sums=('7b402d4587ce9b4ce7dd2b57fb77abad4755aab9e30cfa6ef9e35093ff1c1557868af5871c1f79750c6d66f8aeff3dcdae456bbfe323ea6c0aa3be908033c70a')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  (
    virtualenv "test_dir" --system-site-packages
    . "test_dir/bin/activate"
    export PYTHONPATH="/usr/lib/python$python_version/site-packages:$PYTHONPATH"
    export COVERAGE_NO_CONTRACTS=1
    python setup.py --quiet clean develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  ) || echo "Known failing tests."
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="$pkgdir"
}
