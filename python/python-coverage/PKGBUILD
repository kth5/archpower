# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

_pkgname=coveragepy
pkgname=python-coverage
pkgver=7.11.0
pkgrel=1
pkgdesc="A tool for measuring code coverage of Python programs"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://coverage.readthedocs.io/en/latest/"
license=('Apache-2.0')
depends=('glibc' 'python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nedbat/coveragepy/archive/$pkgver.tar.gz")
sha512sums=('029411427fd43f83c6d09ae3f6511ee42ecd6bb66341875aaf811069ac7e6f1f5dbfdcba7cdb9bcbfe118559d606d17bd02637b2c58c22363175e2a0e886ec99')
b2sums=('5353de6696ab9ebb859a81e8d1b36feab665de7de63070e32b0f86a1a97c09da5c719546af31de10d3ed2d2bce757524c7db35b84f90342d61187baccbdbf8e8')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  # Upstream test suite doesn't currently run in our build environment.
  cd $_pkgname-$pkgver
}

package() {
  cd $_pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
}
