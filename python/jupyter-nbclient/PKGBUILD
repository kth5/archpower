# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbclient
pkgver=0.10.4
pkgrel=2
pkgdesc='A tool for running Jupyter Notebooks in different execution contexts'
arch=(any)
url='https://github.com/jupyter/nbclient'
license=(BSD-3-Clause)
depends=(jupyter-nbformat
         python
         python-jupyter-client
         python-jupyter-core
         python-traitlets)
makedepends=(git
             python-build
             python-hatchling
             python-installer)
checkdepends=(jupyter-nbconvert
              python-flaky
              python-ipywidgets
              python-pytest-asyncio
              python-testpath
              python-xmltodict)
source=(git+https://github.com/jupyter/nbclient#tag=v$pkgver)
sha256sums=('7939623ac9d73353f7d3370ed917dfd8d74ab216ae7c10ba86a725ddc8cf02e6')

build() {
  cd nbclient
  python -m build --wheel --no-isolation
}

check() {
  cd nbclient
  pytest -v -k 'not test_cli_simple' # fails on build server
}

package() {
  cd nbclient
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
