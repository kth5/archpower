# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastjsonschema
pkgname=python-fastjsonschema
pkgver=2.18.0
pkgrel=1
pkgdesc="Fast JSON schema validator for Python"
arch=(any)
url="https://github.com/horejsek/python-fastjsonschema"
license=(BSD)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('848ea4587edb9d694d4f42cee35c8a6a785f601b3e84daae5a5b155ca61c0c7de1b9164e86c0a48166d429a4a737799480dfdeaad7e81eab28f68f71dd46988f')
b2sums=('d2b79eb442351487ffebc59ff48af1e8f07fb5e2ca97059cc98cc8f02eda3523fcf60b2aa6b46edc1d3d019ed04d65d2fc5c3eee58471ea9b2a0bd0bf7bf40ed')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv -m "not benchmark"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
