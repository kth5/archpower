# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-fastjsonschema
pkgver=2.18.1
pkgrel=1
pkgdesc="Fast JSON schema validator for Python"
arch=(any)
url="https://github.com/horejsek/python-fastjsonschema"
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('4e020cfff690a39c2660718d6bf7303ecc0535153b9c0d601ffdf07b2de28eaeded241759fb3889ade84f73237e2bf08dad0718b315fb604932ee6c5bc284ac1')
b2sums=('cbcb99345141daddd45294ec6d5b665380d5517107da5ed7b2666f497ed90d5d7624cf2c5e2756b41c27a4706733931aa085a0083c58bd3f3cd99ab090db4f54')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -vv -m "not benchmark"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
