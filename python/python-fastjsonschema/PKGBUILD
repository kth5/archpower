# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastjsonschema
pkgname=python-fastjsonschema
pkgver=2.21.1
pkgrel=1
pkgdesc="Fast JSON schema validator for Python"
arch=(any)
url="https://github.com/horejsek/python-fastjsonschema"
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('5d6b27a5c914007d716594b13b05251d986c0b922f47336ca875928eecb6df40c3473026243eeee18d3dc1b3276de6f64eb1529c3f697771f755f8531233eed3')
b2sums=('14b5e67ee23a85aa62f0a8208ff9dd2f5e6f4af2d4807b2452fbbc4888051c3cd3d206195695fca66743dd17905933bff0a4b77b916b66714c9b8bc26be92228')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -m "not benchmark"
    # broken with pytest 8
    --deselect tests/test_pattern_properties.py::test_pattern_with_escape_no_warnings
    --deselect tests/test_string.py::test_pattern_with_escape_no_warnings
  )

  cd $pkgname-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
