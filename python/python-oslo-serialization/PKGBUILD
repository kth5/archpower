# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=5.6.0
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=(any)
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-pbr' 'python-msgpack' 'python-oslo-utils')
makedepends=('git' 'python-setuptools')
checkdepends=('python-stestr' 'python-oslotest' 'python-oslo-i18n')
source=("git+https://github.com/openstack/oslo.serialization.git#tag=$pkgver")
sha512sums=('5f6154273f874748a9db85efc20043cb9f27da9f6b281dcafe683ee8b8eea7c0cacbeeb500d1c65505f081f65c275abe7ada20e7a0746eee3fc1afa8c08707ec')

prepare() {
  cd oslo.serialization
  # We have system tzdata
  sed -i '/tzdata/d' requirements.txt
}

build() {
  cd oslo.serialization
  python setup.py build
}

check() {
  cd oslo.serialization
  stestr run
}

package() {
  cd oslo.serialization
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
