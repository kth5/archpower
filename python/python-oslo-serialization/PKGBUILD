# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=5.7.0
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=(any)
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-pbr' 'python-msgpack' 'python-oslo-utils')
makedepends=('git' 'python-setuptools')
checkdepends=('python-stestr' 'python-oslotest' 'python-oslo-i18n')
source=("git+https://github.com/openstack/oslo.serialization.git#tag=$pkgver")
sha512sums=('a56575f9bde183efe510a0ff72305bf786468a8196cb9f96fdc780ed3a1fc2621bee883b8193b3d4ad9a28f1d83aef11ebe9de3371f5465bab42177ec6473b3b')

prepare() {
  cd oslo.serialization
  # We have system tzdata
  sed -i '/tzdata/d' requirements.txt
}

build() {
  cd oslo.serialization
  python setup.py build
}

check() {
  cd oslo.serialization
  stestr run
}

package() {
  cd oslo.serialization
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
