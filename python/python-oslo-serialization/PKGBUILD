# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=5.8.0
pkgrel=4
pkgdesc="Oslo Serialization library"
arch=(any)
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache-2.0')
depends=('python-msgpack' 'python-oslo-utils')
makedepends=('git' 'python-build' 'python-installer' 'python-pbr')
checkdepends=('python-stestr' 'python-oslotest' 'python-oslo-i18n')
source=("git+https://github.com/openstack/oslo.serialization.git#tag=$pkgver")
sha512sums=('a9478cb7d04dc89dad158ce58a079ef2eb3b68d5d89ed80f850be329e218c3b1280a8a057bf7d8bd7514334a3f02277aae97a70c5f54554fef5d70c1dc21735a')

prepare() {
  cd oslo.serialization
  # We have system tzdata
  sed -i '/tzdata/d' requirements.txt
}

build() {
  cd oslo.serialization
  python -m build -nw
}

check() {
  cd oslo.serialization
  stestr run
}

package() {
  cd oslo.serialization
  python -m installer --destdir "$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
