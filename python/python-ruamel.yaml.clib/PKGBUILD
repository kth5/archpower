# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Samuel Littley <aur@toastwaffle.com>
# Contributor: rnons <remotenonsense at gmail dot com>

_pkg=ruamel.yaml.clib
pkgname=python-$_pkg
pkgver=0.2.14
pkgrel=1
pkgdesc='C version of reader, parser and emitter for ruamel.yaml derived from libyaml'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://sourceforge.net/projects/ruamel-yaml-clib/'
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=("$pkgname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/source/r/${_pkg}/${_pkg}-${pkgver}.tar.gz")
sha512sums=('0be9be1c26c0d5cdb686ced8b16b0eb4efe59cafa6eb8d3378cdbcdbb0a5d70ebb578e5e48c4c8171817fde7c503c75396829c6dad8dcff9e3724dedd6363cb5')
b2sums=('588eea54fcf133249255a2e0376f9d4e2aa5a4d15e49aeec4fc1a524eba02af70232eeaec113b49a8d7b17d538fd4ebb8973d2e7d666206fd3119d4f0be56abf')

build() {
  cd "${_pkg}-${pkgver}"

  # Workaround for GCC 14, see https://sourceforge.net/p/ruamel-yaml-clib/tickets/22/
  CFLAGS="$CFLAGS -Wno-error=incompatible-pointer-types" \
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${_pkg}-${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
