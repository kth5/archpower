# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinxcontrib_htmlhelp
pkgname=python-sphinxcontrib-htmlhelp
pkgver=2.0.4
pkgrel=1
pkgdesc='Sphinx extension which renders HTML help files'
arch=(any)
url=https://github.com/sphinx-doc/sphinxcontrib-htmlhelp
license=('BSD')
makedepends=('python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-html5lib' 'python-pytest' 'python-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('6c26a118a05b76000738429b724a0568dbde5b72391a688577da08f11891092a')
b2sums=('ef5f5c26696cadc37feefda8b5343562efd45ab7685d472d820f4e391a1699993f26634269087c24971b176ef7b05e0191f90dc6b07093ca25a424bbf7a2a69d')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
