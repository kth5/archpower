# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Tarmo Heiskanen <turskii@gmail.com>

pkgname=python-flask
pkgver=3.1.0
pkgrel=1
pkgdesc='Micro webdevelopment framework for Python'
url='http://flask.pocoo.org/'
arch=(any)
license=('BSD-3-Clause')
depends=(
  'python'
  'python-blinker'
  'python-click'
  'python-itsdangerous'
  'python-jinja'
  'python-werkzeug'
)
makedepends=(
  'python-build'
  'python-flit-core'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-pytest')
optdepends=(
  'python-asgiref: async functions and views'
  'python-dotenv: loading env vars from files'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/pallets/flask/archive/${pkgver}.tar.gz")
sha512sums=('b70efc2d8bf7de810b0b42dfd5a792c1958739dd84fdc533eae76cf6d805b5de311f9324327d12f00c0d596ca5d43e468f2f52b31f09e04f63a38997ce0a312f')

build() {
  cd "flask-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "flask-$pkgver"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd "flask-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}

# vim:set ts=2 sw=2 et:
