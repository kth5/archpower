# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>
# Contributor: Anton Eliasson <devel@antoneliasson.se>

pkgname=python-httplib2
pkgver=0.22.0
pkgrel=6
pkgdesc='Comprehensive HTTP client library, supporting many features'
arch=(any)
url='https://github.com/httplib2/httplib2'
license=('MIT')
depends=(
  'ca-certificates'
  'python'
  'python-pyparsing'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=(
  'flake8'
  'python-pytest-forked'
  'python-pytest-timeout'
  'python-pytest-xdist'
  'python-pytest-randomly'
  'python-future'
  'python-cryptography'
  'python-six'
)
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'use-system-certificates.patch'
  'remove-cov.patch'
)
sha512sums=('19c5ad43141c9e042cf81ee84ac5c6ee8f57a89656042708e86f8b098456df1752bdcddf0830b3514871cf9a488ebd374d3742cc21621169f1ea15c881b2a4ad'
            '9699f17938703d1975417d244240d2df0062d309a1af91b33b438158e60d28ea3a38f439a774286a4090977f021d0ed3adc8c5c8c450bcbd4417649c735646f6'
            'e3a37f1db327dd1c96a24c581a833e0447fa0e1cc1aaaff58f558a2bb923a50581b74cb0b33fe56240597bdf47ac0fba564bb5f6c79f2ee9e84e4a3d5ef0c027')
b2sums=('2f7dd351018fdbaff3f63b3406442510bdf584c31fa57f6f39dd4cb4496a9fcf95dec40e4f961352baae6839317d3b325524402ab9bd9c1612b651a198d1ab58'
        '5616fb29c36d107c6e064761f56793c0e618dd355158d3360fc7e596d90295863d83bc039ff494bc2de11a1db8976d35e7828a66dd6fd0f7ea96eda7aa21edb0'
        '22ccdcf65d2f9303c8c9834db48e5c006d8081673e989e1d00c34621a0b99a10389ce0dfdbcaef38015cdc435dad311d427df61119fa4e45ee78405fdb212e73')

prepare() {
  cd "$pkgname"

  # use system ca certificates
  patch -p1 -i "$srcdir/use-system-certificates.patch"
  rm -vf python{2,3}/httplib2/cacerts.txt

  # do not use python-pytest-cov
  patch -p1 -i "$srcdir/remove-cov.patch"

  # patch requirements to allow newer versions of checkdepends
  sed -e 's/==/>=/' -i requirements-test.txt
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"
  # test_client_cert_password_verified fails with cryptography 36 https://github.com/httplib2/httplib2/issues/221
  PYTHONPATH=build/lib pytest -k 'not test_client_cert_password_verified'
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
