# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deprecated
pkgver=1.2.15
pkgrel=2
pkgdesc="Python @deprecated decorator to deprecate old python classes, functions or methods"
url="https://github.com/tantale/deprecated"
license=('MIT')
arch=(any)
depends=('python' 'python-wrapt')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tantale/deprecated/archive/v$pkgver.tar.gz")
sha512sums=('d15bce4370ef20ad455202c9f1be453aab04e61f6967368a7ae32bed98ae0f3fdb1abee944c7afa203fe11e100e85c7cfa6d7d0cf800b1d70e51e0efca092075')

build() {
  cd deprecated-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd deprecated-$pkgver
  pytest
}

package() {
  cd deprecated-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
