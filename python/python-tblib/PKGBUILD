# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-tblib
pkgver=3.1.0
pkgrel=1
pkgdesc='Python traceback fiddling library'
arch=(any)
url='https://github.com/ionelmc/python-tblib'
license=('BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('766d117cf64fc19ce426cb2ef83153b2112093e5fb8393810203b9885b50e62380cecc534c3be2ad8bd9861a76f3c879927b965ef60c2c66b4fa7626eb6edca8')
b2sums=('7f08264e1d12132e853592617a8c56dcc4cf058e201126ce873223f6cd7e7e5f8e69efecc6b240fec965bb0fa83dcaadf1b6edfa1dd05f24cf3ba5f2a3098607')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  export PYTHONNODEBUGRANGES=yes
  test-env/bin/python -m pytest --override-ini="addopts=" \
    --deselect tests/test_perf.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
