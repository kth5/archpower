# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-tblib
_pyname=tblib
pkgver=2.0.0
pkgrel=3
pkgdesc='Python traceback fiddling library'
url='https://github.com/ionelmc/python-tblib'
arch=(any)
license=('BSD-2-Clause')
depends=('python-six')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-six' 'python-wheel')
checkdepends=('python-pytest' 'python-twisted')
source=("https://github.com/ionelmc/python-tblib/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('13f9800647aacc516f893fed6717b13b53102af367bb3c21224b5545dedc3b8f1b73173b2fe26a7e2325235071ed534f8bafe28d2c8ba38d2f7a69bd3fb5419a')
b2sums=('e26c224f6ca20cba5d8ce654da4069ed4735e2da77cb6ccb27040c67b762df32f50676932c49ed2adf71ca7ce135a5d0ea3821df926ee7d64e865c034fe9442f')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "${site_packages}/${_pyname}-${pkgver}.dist-info/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
