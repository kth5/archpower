# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-tblib
pkgver=3.2.1
pkgrel=1
pkgdesc='Python traceback fiddling library'
arch=(any)
url='https://github.com/ionelmc/python-tblib'
license=('BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('198a0263b38b7c177ce1f612da1db394d7e5a6d35aa137d9af0d7bc2240667bf44b57361d3fa52a3e95f8ee64309ee6e8722cef16904939c137a5027d68067e4')
b2sums=('cc83fbf112a855dd178e4be29f75f0151684f9009f8505676cf47ccd7c1df26943e8a9060e98894b67ba178a666273b7c994aa022ba5c71bece013e4b937ed56')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  export PYTHONNODEBUGRANGES=yes
  test-env/bin/python -m pytest --override-ini="addopts=" \
    --deselect tests/test_perf.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
