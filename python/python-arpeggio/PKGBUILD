# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-arpeggio
pkgver=2.0.3
pkgrel=1
pkgdesc='Packrat parser interpreter'
arch=(any)
url='https://github.com/textX/Arpeggio'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('1b9c91affa77b7263afe69a4801069958d4f1be9c548fcfd9571653bd338853e424b78755d2f1850598013110334fcf08bd272a2b71f8807f767f68ed9819140')
b2sums=('698dd234b4f9501ae1016538c209b1a17fb88f4ca87080b54e9b7e1a8e6c4b2289781bef4b0540f5f15df48bbe129866a25ce91831e0903b95cded63def60ddf')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
