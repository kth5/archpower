# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Carl George < arch at cgtx dot us >

pkgname=python-click
_realname=click
pkgver=8.2.1
pkgrel=1
pkgdesc="Simple wrapper around optparse for powerful command line utilities"
arch=(any)
url='https://click.palletsprojects.com/'
license=("BSD")
depends=("python")
makedepends=('python-flit-core' 'python-build' 'python-installer')
checkdepends=('python-pytest')
source=("https://github.com/pallets/click/archive/${pkgver}/$pkgname-$pkgver.tar.gz")
sha512sums=('420ba1989410af0949317f9651c6b292da68605b86b4cf0e2382eaba1defbc9eb4b9d97e0a71e5cdb51a61a1f578e81fd864e1abb0cdcd0a8ee26a899c802afe')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # https://github.com/pallets/click/issues/2489
  PYTHONPATH="src" pytest -k 'not test_bytes_args'
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 "LICENSE.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
