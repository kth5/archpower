# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-spec-validator
pkgver=0.7.2
pkgrel=2
pkgdesc="OpenAPI 2.0 (aka Swagger) and OpenAPI 3 spec validator"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache-2.0')
arch=('any')
depends=(
  'python'
  'python-jsonschema'
  'python-jsonschema-path'
  'python-lazy-object-proxy'
  'python-openapi-schema-validator'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-poetry-core'
)
checkdepends=('python-pytest')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8642c2d98baebe122e1ea56b960f73e6cc5ff2dd3c5f6d82362ef1846b4a1a79b652668d7b42277fc7062a0b84556f38dd0e7c53e5e791a88d11b6dd4711c898')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  PYTHONPATH="$PWD" pytest --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
