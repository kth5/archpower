# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

pkgname=python-websocket-client
pkgver=1.6.1
pkgrel=1
pkgdesc="WebSocket client library for Python"
arch=(any)
url="https://github.com/websocket-client/websocket-client"
license=('LGPL')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/w/websocket-client/websocket-client-$pkgver.tar.gz")
sha512sums=('eb784dffb2ad26b63e5890685ed46ccadee40687f4433a1b28392d468fdc1099192095899b56129f72b28e88960744bb5d4991d826476d77ec98bbf18df3f608')

build() {
  cd websocket-client-$pkgver
  python setup.py build
}

check() {
  cd websocket-client-$pkgver
  pytest
}

package() {
  cd websocket-client-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 ft=sh et:
