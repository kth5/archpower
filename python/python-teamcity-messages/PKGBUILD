# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-teamcity-messages
pkgver=1.32
pkgrel=4
pkgdesc="Send test results to TeamCity continuous integration server from unittest, nose, py.test, twisted trial, behave"
url="https://github.com/JetBrains/teamcity-messages"
license=('Apache-2.0')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-virtualenv')
source=("https://github.com/JetBrains/teamcity-messages/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b9733edb1e2aef4ca69773070e927a2b6162b34d8bea4b4fd22c5d7858de2d6416dcc39102aa718b4858635900519cfd7e02016143ac6a19fa82d1fd6780c0a3')

prepare() {
  cd teamcity-messages-$pkgver
  sed -i "s/virtualenv_version = 'virtualenv==20.16.5'/virtualenv_version = 'virtualenv'/" setup.py
}

build() {
  cd teamcity-messages-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd teamcity-messages-$pkgver
  PYTHONPATH="$PWD" pytest tests/unit-tests*
}

package() {
  cd teamcity-messages-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
