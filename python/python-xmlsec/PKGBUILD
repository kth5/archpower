# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.13
_commit=b483b644b6033e539f303509f5ee85bebf178a73
pkgrel=4
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('python-lxml' 'xmlsec')
makedepends=('git' 'python-build' 'python-installer' 'python-pkgconfig' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis' 'python-pytest')
source=("git+https://github.com/mehcode/python-xmlsec.git#commit=$_commit"
        0001-Add-compatibility-with-xmlsec-1.3.patch
        0002-Add-compatibility-with-xmlsec-1.3.3.patch
        0003-enable-python-3.11-builds.patch)
sha512sums=('SKIP'
            '2390d1ea29d5af92833fc157d13b80818cf1bef1b599e3443936214c3b3481dfc0a54ba2fec766c086725185416aa6aecf69cd30ecb26f40d9c622e668c83f62'
            '1fbf4f856dd1c6da2805186f7b54d954a66cc1b9eaad600baaa697f6389fdecee0ed766dfd638dbdbefe3aae44d49808a3dff99509efb92dd4e17aa4c886f12c'
            'cfbfb92e7ab60790a2824aa5cad5ce5132ea9f765ccb11b2d158ffec74051df87e98c3107c57ed0617e92eb3758784eb2477e1334a90901b50bb94f7b9f8da92')

prepare() {
  cd $pkgname
  # fix build with python 3.11 and later xmlsec releases
  patch -Np1 -i ../0003-enable-python-3.11-builds.patch
  # https://github.com/xmlsec/python-xmlsec/pull/264
  patch -Np1 -i ../0001-Add-compatibility-with-xmlsec-1.3.patch
  # https://github.com/xmlsec/python-xmlsec/pull/280
  patch -Np1 -i ../0002-Add-compatibility-with-xmlsec-1.3.3.patch
}

build() {
  cd python-xmlsec
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    # https://github.com/xmlsec/python-xmlsec/issues/84
    --deselect 'tests/test_doc_examples.py::test_doc_example[encrypt.py]'
    --deselect 'tests/test_doc_examples.py::test_doc_example[sign.py]'
    --deselect 'tests/test_doc_examples.py::test_doc_example[verify.py]'
    # https://github.com/xmlsec/python-xmlsec/issues/244
    --deselect tests/test_ds.py::TestSignContext::test_sign_case5
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv "${pytest_options[@]}"
}

package() {
  cd python-xmlsec
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
