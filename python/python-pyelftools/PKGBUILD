# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=python-pyelftools
pkgver=0.32
pkgrel=2
pkgdesc='Python library for analyzing ELF files and DWARF debugging information'
arch=(any)
url='https://github.com/eliben/pyelftools'
license=('Unlicense')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
options=('!strip')
source=("$url/archive/v$pkgver/${pkgname#python-}-$pkgver.tar.gz")
sha512sums=('798da482d97bb759e9d54aa6c1be80bcfc118b7c0b2011b053564763ba3866946da8ad4b472a0b28c4e71f2b9023130ff044886f5d665d143b4922dbd87ba47e')
b2sums=('4763302d019acf8904b4ca4d8114eacc4c9bb9a7128f040951a190e619ba384967a0999b437e4e32135edc519ea0c0766934c6a4b2a699fd9227b4519a41685e')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python test/run_all_unittests.py
  python test/run_examples_test.py
  if [ "$CARCH" == "x86_64" ]; then
    python test/run_readelf_tests.py
  else
    echo "Skipping readelf tests (require x86_64)"
  fi
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst CHANGES
  cp -vr examples "$pkgdir/usr/share/doc/$pkgname"
}
