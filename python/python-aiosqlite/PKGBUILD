# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-aiosqlite
pkgver=0.22.1
pkgrel=1
pkgdesc="Asyncio bridge to the standard sqlite3 module"
arch=(any)
url="https://github.com/omnilib/aiosqlite"
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-flit-core
  python-installer
)
checkdepends=(python-aiounittest)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('b3cf6c41ca00c5ff701433e89fef7c33a486f274c5b2fd44ca5c31edef0307c9cd0ac4d3858842393d26a34ce1780c0922874e75667abf275f83cf9640b4784e')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m unittest discover -v
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
