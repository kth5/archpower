# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: renek <aur@spaceshore.net>

pkgname=python-multidict
pkgver=6.4.4
pkgrel=1
pkgdesc='Asyncio-based multidict implementation for Python'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aio-libs/multidict'
license=('Apache-2.0')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'cython'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-objgraph'
  'python-perf'
  'python-psutil'
  'python-pytest'
  'python-pytest-codspeed'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ccf07263672abfcaa44b4e1b89e82d7a2843da9805cfb52644f44363b196bb025427478e7cf355d1c2692a1713ccd612761939ac32e30c21f5ad2d99012435c5')
b2sums=('6fbe211f13810464b591d7198c0529c0d7443270b25b2358e10042721abc40feddbf8b7c0525f20810bed510fb50c8ffe3d2e29b0ff0711b4dd5337bb54ac46a')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  cd tests
  ../test-env/bin/python -m pytest -v --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
