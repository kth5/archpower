# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aniso8601
pkgver=9.0.1
pkgrel=7
pkgdesc="A library for parsing ISO 8601 strings."
url="https://bitbucket.org/nielsenb/aniso8601"
license=('BSD-3-Clause')
arch=(any)
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-dateutil' 'python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/nielsenb/aniso8601/get/v$pkgver.tar.gz")
sha512sums=('82b3c943fe3be845ee375e232c6aafb20d4ede560831f594e91ad75e93fd6d12316ad7285c6ebb796587ace27f56c4b0c17059d9c2d1aa9525394802ea19d00c')

prepare() {
  mv nielsenb-aniso8601-* aniso8601-$pkgver
}

build() {
  cd "$srcdir"/aniso8601-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd "$srcdir"/aniso8601-$pkgver
  pytest .
}

package() {
  cd aniso8601-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
