# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-os-client-config
pkgver=2.3.0
pkgrel=1
arch=(any)
pkgdesc='OpenStack Client Configuation Library'
url='https://github.com/openstack/os-client-config'
license=('Apache')
depends=('python-pbr' 'python-openstacksdk')
makedepends=('git' 'python-build' 'python-installer')
checkdepends=('python-stestr' 'python-oslotest' 'python-jsonschema' 'python-glanceclient')
source=("git+https://github.com/openstack/os-client-config.git#tag=$pkgver")
sha512sums=('4e2ab21bf10661d6d0969ff82c4ba7be0318387ae6e143e8c023a84614047ffd8f1322de1f8fcf0976179f7dcb4553e96a126e8b323de19875178fb72817906f')

build() {
  cd os-client-config
  python -m build --wheel --no-isolation
}

check() {
  cd os-client-config
  stestr run
}

package() {
  cd os-client-config
  python -m installer --destdir "$pkgdir" dist/*.whl
}
