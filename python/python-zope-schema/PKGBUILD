# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-schema
_pkgname=zope.schema
pkgver=8.1
pkgrel=1
pkgdesc="zope.interface extension for defining data schemas"
arch=(any)
url="https://github.com/zopefoundation/zope.schema"
license=('ZPL-2.1')
depends=(
  'python'
  'python-zope-event'
  'python-zope-interface'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-zope-i18nmessageid'
  'python-zope-testing'
  'python-zope-testrunner'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5f479938797bfb8d157873006c889f24d08c597cb8c4a90140a03739c9e63251b7519f058ed6aab473d0b41181d001302dcd4a6b82972c57a517ada8e7b69156')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m zope.testrunner -vc --test-path src/
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
