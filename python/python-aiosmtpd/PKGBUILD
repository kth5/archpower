# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=aiosmtpd
pkgname=python-aiosmtpd
pkgver=1.4.5
pkgrel=1
pkgdesc="An asyncio based SMTP server"
arch=(any)
url="https://github.com/aio-libs/aiosmtpd"
license=(Apache-2.0)
depends=(
  python
  python-atpublic
  python-attrs
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-mock
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('d33441aa3179cab0711dbbf14a9b243d292195cce41b730e9fd965ffaa40c4bbf4a61acdc4e209c93f370a058dd1f207258cc4207746dbe5e14fa4c83837f137')
b2sums=('d355c4e68c3ceb0abed9d8195979b514c75c97163bd4b836db37b606211d886e42ea3833ef28d6188c6fe3ec8f51b00f08f8aed2643338f2a04ede8b9c9fafbb')

prepare() {
  # we are not interested in code coverage
  sed -i '/--cov/d' $_name-$pkgver/pytest.ini
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --deselect aiosmtpd/qa/test_0packaging.py::TestVersion::test_ge_master
  )

  cd $_name-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove unneeded stuff: https://github.com/aio-libs/aiosmtpd/issues/356
  rm -frv "$pkgdir/$_site_packages/$_name/"{docs,qa,tests}
}
