# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configargparse
pkgver=1.5.5
pkgrel=2
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/bw2/ConfigArgParse'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-tests' 'python-yaml')
optdepends=('python-yaml: for YAML support')
source=("https://github.com/bw2/ConfigArgParse/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fc9229d12fcc8da3b001d75d10829e1eef4ec0624b89ad319e840bea9a795d3d575a3760538f13a30bd1e778a27a7620486d7cb199fff9df5535017a7fef4a4c')

build() {
  cd ConfigArgParse-$pkgver
  python setup.py build
}

check() {
  cd ConfigArgParse-$pkgver
  pytest
}

package() {
  cd ConfigArgParse-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
