# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configargparse
pkgver=1.7.1
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=(any)
url='https://github.com/bw2/ConfigArgParse'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-yaml') # HACK: run tests without python-tests as they fail
optdepends=('python-yaml: for YAML support')
source=("https://github.com/bw2/ConfigArgParse/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ca9741cf04a444b86c589a0097838cf9d6e1e752287587dda103cf59d9603890067b418ac26323aac4749787ac28c9764f1a41be839055756420e51363db10c7')

build() {
  cd ConfigArgParse-$pkgver
  python setup.py build
}

check() {
  cd ConfigArgParse-$pkgver
  # https://github.com/bw2/ConfigArgParse/issues/294
  PYTHONPATH=. pytest -v -k "not TestMisc and not testBasicCase2 and not testMutuallyExclusiveArgs and not test_unrecognized_args"
}

package() {
  cd ConfigArgParse-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
