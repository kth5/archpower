# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-testrunner
_pkgname=zope.testrunner
pkgver=6.6.1
pkgrel=1
pkgdesc="Zope testrunner script"
arch=(any)
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL-2.1')
depends=(
  'python'
  'python-setuptools'
  'python-zope-exceptions'
  'python-zope-interface'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-zope-testing')
source=(
  "$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
  "dont-package-tests.patch"
)
sha512sums=('343a34af5335eec43dd1bc46792a5d676a9ebd3645a1db29f6e8c6ed8494ebe23bcb10d00a713738a54a042de1756150dea123433f47b2bc14a766dcab6dace7'
            '46be2d2002c45e9e5fa9ea60e1b5832fae29e0438c07fd47017043a662a4c943b24535c9bce49ed215065866f6a5651b5b79a4a4e09484707b81347f7b2ab1df')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../dont-package-tests.patch
  sed -i 's/setuptools<74/setuptools/' pyproject.toml
}

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -r src/zope/testrunner/tests "$PWD/tmp_install/$site_packages/zope/testrunner"
  export PYTHONPATH="$PWD/tmp_install/$site_packages"
  ./tmp_install/usr/bin/zope-testrunner \
    --test-path="$PWD/tmp_install/$site_packages" -vc
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
