# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Pierre Buard <pierre.buard@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgbase=python-pyenchant
pkgname=(
  python-pyenchant
  python-pyenchant-docs
)
pkgver=3.2.2
pkgrel=5
pkgdesc='Python bindings for the Enchant spellchecker'
arch=(any)
url='https://pyenchant.github.io/pyenchant/'
license=(LGPL-2.1-or-later)
depends=(
  enchant
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-sphinx_rtd_theme
  python-wheel
)
checkdepends=(
  hunspell
  hunspell-en_us
  python-pytest
)
source=("git+https://github.com/pyenchant/pyenchant.git#tag=v$pkgver")
b2sums=(2e60d31bd6a409d1d90609383dcad00873809b300de15e052d6c39904770b53ce5f83d6d31546bc144530bbe1293f244b9cbb3aa57fb3e0a36669aacc3997178)

build() {
  cd pyenchant
  python -m build --wheel --no-isolation
}

check() {
  cd pyenchant
  PYTHONPATH="$PWD/build/lib" \
    pytest --ignore=tests/test_pwl.py
}

package_python-pyenchant() {
  cd pyenchant
  python -m installer --destdir="$pkgdir" dist/*.whl
}

package_python-pyenchant-docs() {
  pkgdesc='Developer documentation for python-pyenchant'
  depends=()

  cd pyenchant
  python -m sphinx -b html -c website website/content "$pkgdir/usr/share/doc/$pkgbase/html"
  rm -r "$pkgdir/usr/share/doc/$pkgbase/html/.doctrees"
}
