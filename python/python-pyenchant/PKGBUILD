# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Pierre Buard <pierre.buard@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgbase=python-pyenchant
pkgname=(
  python-pyenchant
  python-pyenchant-docs
)
pkgver=3.3.0
pkgrel=1
pkgdesc='Python bindings for the Enchant spellchecker'
arch=(any)
url='https://pyenchant.github.io/pyenchant/'
license=(LGPL-2.1-or-later)
depends=(
  enchant
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-sphinx_rtd_theme
  python-wheel
)
checkdepends=(
  hunspell
  hunspell-en_us
  python-pytest
)
source=("git+https://github.com/pyenchant/pyenchant.git#tag=v$pkgver")
b2sums=('6e2bfcb578c931119f6c9a24b4be26575c5a1fe083a0af16a08ab3475063b283f837242ce0806cd3c1f478ded21a8b71622dceeb62da709bdd256e3657ac6033')

build() {
  cd pyenchant
  python -m build --wheel --no-isolation
}

check() {
  cd pyenchant
  PYTHONPATH="$PWD/build/lib" \
    pytest --ignore=tests/test_broker.py --ignore=tests/test_pwl.py
}

package_python-pyenchant() {
  cd pyenchant
  python -m installer --destdir="$pkgdir" dist/*.whl
}

package_python-pyenchant-docs() {
  pkgdesc='Developer documentation for python-pyenchant'
  depends=()

  cd pyenchant
  python -m sphinx -b html -c website website/content "$pkgdir/usr/share/doc/$pkgbase/html"
  rm -r "$pkgdir/usr/share/doc/$pkgbase/html/.doctrees"
}
