# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Blair Bonnett <blair.bonnett@gmail.com>

pkgname=python-scikit-build-core
_name=${pkgname#python-}
pkgdesc='Next generation Python CMake adaptor and Python API for plugins'
url='https://github.com/scikit-build/scikit-build-core'
pkgver=0.10.7
pkgrel=2
arch=(any)
license=(Apache-2.0)
depends=(
  cmake
  ninja
  python
  python-packaging
  python-pathspec
)
makedepends=(
  python-build
  python-hatchling
  python-hatch-vcs
  python-installer
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('299533c671e3b9579f58c2bfd6a24ba43daf213fb662e5f11e0ce7a38993eeb7ff7b783fc50b16ba11bf7855b026cf9ab3f636cc04bc0ba51d0ef8744ec7a027')
b2sums=('8ed13c48126bd4af1a31f794c933a12b6c2bfee518d13e1aeb5e2491d0cccbf5c3a89cb7af216532357702ec75447b1e733fec208c1bb585e1c1792972fae839')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

# NOTE: upstream hardcodes downloading the internet with pip: https://github.com/scikit-build/scikit-build-core/issues/720

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
