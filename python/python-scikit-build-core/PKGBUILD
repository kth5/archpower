# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Blair Bonnett <blair.bonnett@gmail.com>

pkgname=python-scikit-build-core
_name=${pkgname#python-}
pkgdesc='Next generation Python CMake adaptor and Python API for plugins'
url='https://github.com/scikit-build/scikit-build-core'
pkgver=0.10.6
pkgrel=1
arch=(any)
license=(Apache-2.0)
depends=(
  cmake
  ninja
  python
  python-packaging
  python-pathspec
)
makedepends=(
  python-build
  python-hatchling
  python-hatch-vcs
  python-installer
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('01f41f987969189fa10c92edccfa38bdb637db31eef43eda2891edc1175fc005f0454b9f11a0d22dc4e306aa384ecd3f628f59184817d42711144f9d748ab4a1')
b2sums=('31743c3aa150250bb6582e064050b267c053a9a7dc15d84919c84f2bdc4d90051def6fd07fe7655fff975e18f5b53178c51e02f63daa247dd4d502a8d7b169f5')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

# NOTE: upstream hardcodes downloading the internet with pip: https://github.com/scikit-build/scikit-build-core/issues/720

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
