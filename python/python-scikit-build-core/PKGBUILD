# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Blair Bonnett <blair.bonnett@gmail.com>

pkgname=python-scikit-build-core
pkgver=0.11.1
pkgrel=1
pkgdesc='Next generation Python CMake adaptor and Python API for plugins'
arch=(any)
url='https://github.com/scikit-build/scikit-build-core'
license=(Apache-2.0)
depends=(
  cmake
  ninja
  python
  python-packaging
  python-pathspec
)
makedepends=(
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
)
checkdepends=(
  pybind11
  python-cattrs
  python-fastjsonschema
  python-hatch-fancy-pypi-readme
  python-numpy
  python-pytest
  python-pytest-subprocess
  python-setuptools-scm
  python-validate-pyproject
  python-virtualenv
)
source=("$url/archive/v$pkgver/${pkgname#python-}-$pkgver.tar.gz")
sha512sums=('b4f52fc796c95284e691539f02355e926c0399bd18bbb2477ab63edaa73f2a224f8eebae80732b63bd61ef047b29b3204184472a0b98d006b7dc2db153c9b74c')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

# NOTE: upstream hardcodes downloading the internet with pip:
# https://github.com/scikit-build/scikit-build-core/issues/720
check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -k 'not network'
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
