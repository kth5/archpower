# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cmd2
pkgver=2.5.11
pkgrel=1
pkgdesc="A tool for building interactive command line apps"
arch=(any)
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
depends=('python-attrs' 'python-colorama' 'python-pyperclip' 'python-wcwidth')
makedepends=('git' 'python-setuptools-scm' 'python-build' 'python-installer' 'python-wheel')
source=("git+https://github.com/python-cmd2/cmd2.git#tag=$pkgver")
sha512sums=('6a1424f48a188e5760ee537a3f0a317b9bcc38242c33ae2e4ce471a9cce06888aeb02f738d9af4753fb43acee13899e55fc56f037443dbc9189803f8af3efaef')

build() {
  cd cmd2
  python -m build --wheel --no-isolation
}

package() {
  cd cmd2
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
