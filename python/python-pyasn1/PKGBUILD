# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-pyasn1
pkgdesc="ASN.1 library for Python 3"
pkgver=0.6.1
pkgrel=2
arch=(any)
url="https://github.com/pyasn1/pyasn1"
license=('BSD-2-Clause')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
replaces=('pyasn1')
provides=('pyasn1')
source=("https://github.com/pyasn1/pyasn1/archive/v$pkgver/pyasn1-$pkgver.tar.gz")
sha512sums=('a7f995413643d4f9283a9878a1bc3c8d606d018a31209309e4eeda42461eee89efcedd7af4c75fd9e8c9d7e628d86229afbbd0c1bf3862982e45524845c7a833')

build() {
  cd pyasn1-$pkgver
  python -m build -nw
}

check() {
  cd pyasn1-$pkgver
  python -m unittest discover -s tests
}

package() {
  cd pyasn1-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl

  # Symlink license file
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
