# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-pyasn1
pkgdesc="ASN.1 library for Python 3"
pkgver=0.5.0
pkgrel=1
arch=(any)
url="https://github.com/pyasn1/pyasn1"
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
replaces=('pyasn1')
provides=('pyasn1')
source=(https://github.com/pyasn1/pyasn1/archive/v$pkgver/pyasn1-$pkgver.tar.gz)
sha512sums=('50e29b4c0a55367c4ba3a48bc8935ec967f23822bf89bc848d4c910572f5351be23d8f696dc6f451c8eee4e97ccce888fd7d89d12362a3c52fc247fec7d47b5b')

build() {
  cd pyasn1-$pkgver
  python -m build -nw
}

check() {
  cd pyasn1-$pkgver
  python -m unittest discover -s tests
}

package() {
  cd pyasn1-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
