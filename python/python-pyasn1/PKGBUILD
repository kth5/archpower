# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-pyasn1
pkgdesc="ASN.1 library for Python 3"
pkgver=0.5.1
pkgrel=1
arch=(any)
url="https://github.com/pyasn1/pyasn1"
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
replaces=('pyasn1')
provides=('pyasn1')
source=(https://github.com/pyasn1/pyasn1/archive/v$pkgver/pyasn1-$pkgver.tar.gz)
sha512sums=('570880ba571a054da9b64aab8cc1646be3cb6c9ccf71f2039feb04114a0a2ddca07b1a55c116736a286426461762304fb3b7819c3e7e78ed7ff1291e02f7f9cc')

build() {
  cd pyasn1-$pkgver
  python -m build -nw
}

check() {
  cd pyasn1-$pkgver
  python -m unittest discover -s tests
}

package() {
  cd pyasn1-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
