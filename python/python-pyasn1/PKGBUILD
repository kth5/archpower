# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-pyasn1
pkgdesc="ASN.1 library for Python 3"
pkgver=0.6.0
pkgrel=1
arch=(any)
url="https://github.com/pyasn1/pyasn1"
license=('BSD-2-Clause')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
replaces=('pyasn1')
provides=('pyasn1')
source=("https://github.com/pyasn1/pyasn1/archive/v$pkgver/pyasn1-$pkgver.tar.gz")
sha512sums=('a62a847cb1a6d4cdf200a05105b9473fdb0af76223465250fd2675bb4205db4d30a7d580b2140369397f04c6e3eb7a69f9cc01d49a7782aead588dd24b9fc199')

build() {
  cd pyasn1-$pkgver
  python -m build -nw
}

check() {
  cd pyasn1-$pkgver
  python -m unittest discover -s tests
}

package() {
  cd pyasn1-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pyasn1-$pkgver.dist-info/LICENSE.rst \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
