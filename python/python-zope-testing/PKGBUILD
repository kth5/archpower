# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-testing
_pkgname=zope.testing
pkgver=5.1
pkgrel=1
pkgdesc="Zope testing helpers"
arch=(any)
url="https://github.com/zopefoundation/zope.testing"
license=('ZPL-2.1')
depends=(
  'python'
  'python-zope-testrunner'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('74e9aa73d778783cb67fb1f67c0903512a4b623b024e29bdfdc86800d3c1651d2d46c1563ef4db0cf4223e3fabd51819e29b0e52a644fa7099e1395e30e3e61b')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$PWD/tmp_install/$site_packages"
  zope-testrunner --test-path=src -vc
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
