# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zope-testing
_pkgname=zope.testing
pkgver=6.1
pkgrel=1
pkgdesc="Zope testing helpers"
arch=(any)
url="https://github.com/zopefoundation/zope.testing"
license=('ZPL-2.1')
depends=(
  'python'
  'python-zope-testrunner'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('20cc4006fee44a9d86fd3a7f26507247731c1a32541a5676ab45bd74e75199fb856cd9382cd80bf75eb6ba50b7f18bd3718cdfec005e78ba75d250875c890e33')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m zope.testrunner -vc --test-path src/
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
