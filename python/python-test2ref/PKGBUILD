# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=python-test2ref
pkgver=0.8.2
pkgrel=1
pkgdesc="Testing Against Learned Reference Data"
arch=('any')
url="https://test2ref.readthedocs.io/"
license=('MIT')
depends=('python' 'python-binaryornot')
makedepends=('python-build' 'python-installer' 'git' 'python-pdm-backend')
checkdepends=('python-pytest' 'python-pytest-cov')
source=(
  "$pkgname::git+https://github.com/nbiotcloud/test2ref#tag=v$pkgver"
)
b2sums=('4ffcb02954c274f99db43e6fa906a6ef69d0751bd4442e8c88d52e870edad9ae2c4b058b132b902cdb3c3cdf8ff1e05c269cbebad1f5c7f0dbb490a75cf8f296')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"
  python -m pytest
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
