# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-jaraco.functools
# https://github.com/jaraco/jaraco.functools/blob/main/NEWS.rst
pkgver=3.9.0
# curl https://api.github.com/repos/jaraco/jaraco.functools/git/ref/tags/v$pkgver | jq -r .object.sha
_tag=a51deece0e54c37c5811565a2997bce2a77fb0a7
pkgrel=1
pkgdesc='Functools like those found in stdlib'
arch=(any)
url='https://github.com/jaraco/jaraco.functools'
license=('MIT')
depends=('python' 'python-more-itertools')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'python-jaraco.classes')
conflicts=('python-jaraco')
replaces=('python-jaraco')
source=("git+https://github.com/jaraco/jaraco.functools?signed#tag=$_tag")
sha512sums=('SKIP')
validpgpkeys=(
  'CE380CF3044959B8F377DA03708E6CB181B4C47E' # https://github.com/jaraco.gpg
)

pkgver() {
  cd jaraco.functools
  git describe --tags | sed 's/^v//'
}

build() {
  cd jaraco.functools
  python -m build --wheel --no-isolation
}

check() {
  cd jaraco.functools
  pytest
}

package() {
  cd jaraco.functools
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
