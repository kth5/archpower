# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgver=3.2.2
pkgrel=1
pkgdesc='General parsing module for Python'
arch=(any)
url='https://github.com/pyparsing/pyparsing/'
license=('MIT')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-jinja' 'python-railroad-diagrams' 'python-pytest' 'python-matplotlib')
optdepends=('python-railroad-diagrams: for generating Railroad Diagrams'
            'python-jinja: for generating Railroad Diagrams')
source=("git+https://github.com/pyparsing/pyparsing.git#tag=$pkgver")
sha512sums=('5f8f123729b51b98138f0c8bd8faa67ec43489f0771a714f0297da7c46c64b816c9bca1aec9a1bbd44dd83b3b656c5e59420daeb597059d49afd24548fc93d78')

build() {
  cd pyparsing
  python -m build -nw
}

check() {
  cd pyparsing
  python -m unittest
}

package() {
  cd pyparsing
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
