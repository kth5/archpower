# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgver=3.2.5
pkgrel=1
pkgdesc='General parsing module for Python'
arch=(any)
url='https://github.com/pyparsing/pyparsing/'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-flit-core
)
checkdepends=(
  python-jinja
  python-railroad-diagrams
  python-pytest
  python-matplotlib
)
optdepends=('python-railroad-diagrams: for generating Railroad Diagrams'
            'python-jinja: for generating Railroad Diagrams')
source=("$pkgname::git+https://github.com/pyparsing/pyparsing.git#tag=$pkgver")
sha512sums=('fa496039fed41503bbbdd93d1b936b91b990fda946d6d0c5a7867146f84ff56fa4455c62f4a3dafbe3e184f82f7f1fbbcb5dd04377ff19705f68a08f7cdd3ffc')
b2sums=('01a58a0ff7acd6e52720333aefdd127c62b0cc058e11fa35f37c3050edbdb25725f7f1601ed0231959473e60064b594524eb04485353c26ee08991f638578e83')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
