# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-rencode
pkgver=1.0.8
pkgrel=3
pkgdesc="A Module similar to bencode from the BitTorrent project"
url="https://github.com/aresch/rencode"
license=('GPL-3.0-or-later')
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
depends=('glibc' 'python')
makedepends=('git' 'cython' 'python-build' 'python-installer' 'python-poetry-core' 'python-setuptools')
checkdepends=('python-pytest')
source=(
  "git+https://github.com/aresch/rencode.git#tag=v$pkgver"
  0001-Remove-extra-compile-args.patch
)
sha512sums=('974d3efb1e1f7a54902c3772663a5d122399b2e825592634cc88ba1f1a51ebf97470c368e6891d9e52af8a700a50749d428d273de2c5b21b6334fa456f664443'
            '43088a23f0832a39efcd0acba90163c46e53dde263217693b1b9a35d7b85170b0427ea82bb40fbdefc10508bead3abc2cc2260f898d760cfcdc40edb90b773ef')

prepare() {
  cd rencode

  # https://gitlab.archlinux.org/archlinux/packaging/packages/python-rencode/-/issues/1
  # https://github.com/aresch/rencode/issues/38
  git apply -3 ../0001-Remove-extra-compile-args.patch
}

build() {
  # Running in the rencode dir won't work, as
  # ./build.py will be ran instead of python-build
  python -m build --wheel --no-isolation rencode
}

check() {
  python -m venv --system-site-packages testenv
  testenv/bin/python -m installer rencode/dist/*.whl
  testenv/bin/python -m pytest
}

package() {
  python -m installer --destdir="$pkgdir" rencode/dist/*.whl
}
