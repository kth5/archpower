# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyasn1-modules
pkgver=0.4.0
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=(any)
license=('BSD-2-Clause')
url="https://github.com/pyasn1/pyasn1-modules"
depends=('python-pyasn1')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://github.com/pyasn1/pyasn1-modules/archive/v$pkgver/pyasn1-modules-$pkgver.tar.gz")
sha512sums=('63469221598f7aa5eebebbcd847f41f26b8fbbf4ea4ebfa062ab7d43c68c32659293fece68733c56e1818c83ecceb4ee067786a57ea29faed99e65bed9d46201')

build() {
  cd pyasn1-modules-$pkgver
  python -m build -nw
}

check() {
  cd pyasn1-modules-$pkgver
  PYTHONPATH=. python tests/__main__.py
}

package() {
  cd pyasn1-modules-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pyasn1_modules-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
