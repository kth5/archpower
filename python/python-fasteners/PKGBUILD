# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Tomislav Ivek <tomislav.ivek@gmail.com>

pkgname=python-fasteners
_name="${pkgname#python-}"
pkgver=0.20
pkgrel=1
pkgdesc="A python package that provides useful locks."
arch=(any)
url="https://github.com/harlowja/fasteners"
license=('Apache-2.0')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=(
  # 'python-diskcache'  # TODO: package
  'python-eventlet'
  'python-more-itertools'
  'python-pytest'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/harlowja/fasteners/archive/$pkgver.tar.gz")
sha512sums=('539ce5b16f70a73fe6b7296a1bab3b751eff7b7385b980eba3e4d6e42eb263448fd074f325529a30d982e1c366a122aef46bc359578a370ef1278225c09a5f71')

build() {
  cd $_name-$pkgver
  python3 -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  # ignored test requires python-diskcache
  pytest -vv --ignore tests/test_reader_writer_lock.py
}

package() {
  cd $_name-$pkgver
  python3 -m installer --destdir="$pkgdir" dist/*.whl
}
