# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=typing_inspect
pkgname=python-$_pkgname
pkgver=0.9.0
pkgrel=6
pkgdesc='Runtime inspection utilities for Python typing module'
arch=(any)
url='https://github.com/ilevkivskyi/typing_inspect'
license=('MIT')
depends=('python-mypy_extensions' 'python-typing_extensions')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        5bc59c61.patch
        ebc6f92d.patch)
sha512sums=('106597b100c3abda89cc15e0f0cd1fbcc771cb04e15e1d85da2134b9937d1a9b8d1fb396aa94fc350d8e21eeaa7f96f4c98c08923360f7eb1fb5b7b7d1a32df5'
            '6418c685b8c0b298e350bb5cc28a185229d14f6c716c6999d5fb5211c93c4cb1bb66a69c1c3508f5c2818dd6100fbeda742778cfa1406cdab0f30b64b7639a1c'
            'b832b44f6428fbe102f79659e20639435e895aca756fd8c0affd732df78b33cc77b6476ffa35b8e775df860629714db1eb4ed53dea1af43856ee254ff742524f')

prepare() {
  cd $_pkgname-$pkgver
  patch -p1 -i ../5bc59c61.patch
  patch -p1 -i ../ebc6f92d.patch # Fix tests with python 3.14
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  pytest .
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
