# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-objgraph
pkgver=3.6.1
pkgrel=2
pkgdesc="Draws Python object reference graphs with graphviz"
url="https://github.com/mgedmin/objgraph"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('graphviz')
source=("objgraph-$pkgver.tar.gz::https://github.com/mgedmin/objgraph/archive/$pkgver.tar.gz")
sha512sums=('619be7a56add7e1ddf3edce4d153bc3592af85c36d6ef03c443a5150f35cce593eac3e50cb98384c2834059594e12fa2a236a89ee3dd0dd4ef385932bfb1da2f')

build() {
  cd objgraph-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd objgraph-$pkgver
  python tests.py
}

package() {
  cd objgraph-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
