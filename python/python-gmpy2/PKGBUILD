# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgname=python-gmpy2
pkgver=2.2.2
pkgrel=2
pkgdesc='Provides C-coded Python modules for fast multiple-precision arithmetic'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aleaxit/gmpy'
license=(LGPL-2.1-only)
depends=(glibc
         gmp
         libmpc
         mpfr
         python
         python-packaging)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-hypothesis
              python-pytest)
source=(git+https://github.com/aleaxit/gmpy#tag=v$pkgver)
sha256sums=('75869c26cad60e9f53d0e1605e4f1108552d065e778f837e028257f73344cbca')

build() {
  cd gmpy
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
    powerpc64le) return 0 ;;
  esac

  cd gmpy
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  rm -r gmpy2
  test-env/bin/python -m pytest -v
}

package() {
  cd gmpy 
  python -m installer --destdir="$pkgdir" dist/*.whl
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}
