# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Samuel Laur√©n <samuel.lauren@iki.fi>

pkgname=python-gssapi
pkgver=1.10.1
pkgrel=1
pkgdesc='Provides both low-level and high level wrappers around the GSSAPI C libraries'
url='https://github.com/pythongssapi/python-gssapi'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('ISC')
depends=(
  'glibc'
  'krb5'
  'python'
  'python-decorator'
)
makedepends=(
  'cython'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-k5test'
  'python-parameterized'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('607cc3119a214803e290162960a1202935b1706a050ab15472f4ab33a08382bc962d63e8fa880eefca05992bade94e6d774b257c90c857c65944da2de41b9370')
b2sums=('3e0cf9ff4ff2b3fdfa0d041e33158f4def3b646a0164f73231cce1b13a50150673b5de6f84732317fd58d690a912d6a90852ae9dadd3c0c0c4963060b2f3745e')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  case "${CARCH}" in
    powerpc64le) return 0 ;;
  esac

  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  python -m unittest discover -v \
    -t test-env/lib/python*/site-packages \
    -s test-env/lib/python*/site-packages
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.txt
  install -vDm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
