# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: PiC0 <guill.p.linux@gmail.com>

pkgname=python-xmltodict
pkgver=0.14.2
pkgrel=1
pkgdesc='Python module that makes working with XML feel like you are working with JSON'
arch=(any)
url='https://github.com/martinblech/xmltodict'
license=('MIT')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-nose')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('8e87101cf38412f63c127d1c9bcacd13c20d86125137b36708ea6d69c9732e0d801297d84f65cd6fb387eaeb56e0e2ec3bc2fc1b9aff8025b223afa05f6691ac')
b2sums=('928ef3615363cd8b5d0a91efe306c6ded96567d02101a1769aa3425d8bfd62f8eaa05158d9698b846c5bbb9bb270ef1afbec26875e5473e32f7b6c8bbcd93ea4')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  nosetests
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
