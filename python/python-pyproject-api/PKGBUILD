# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyproject-api
pkgname=python-pyproject-api
pkgver=1.9.0
pkgrel=1
pkgdesc="API to interact with the python pyproject.toml based projects"
arch=(any)
url="https://github.com/tox-dev/pyproject-api"
license=(MIT)
depends=(
  python
  python-packaging
)
makedepends=(
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-mock
  python-virtualenv
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('01f0b52bd6a12253da0034a1921084aea0e9f900b6de674e81179765ed8dff3976c27b9674b5edb7be9cadc65ac275f1f4cf3ba39e54dc6bba5af27b175bcf38')
b2sums=('4d7e08472cf44332adf85abe3835c5f5c33244763f8eb168ac38016c18a268488be1d8d04f15c15efd6cdc2631e2e2b6b0799dcb68676fa401910944e4d950ba')

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # unclear why that fails
    --deselect tests/test_frontend.py::test_missing_backend
    # probably some setuptools weirdness
    --deselect tests/test_frontend_setuptools.py::test_setuptools_get_requires_for_build_wheel
    # metadata tests for setuptools fail: https://github.com/tox-dev/pyproject-api/issues/153
    --deselect tests/test_frontend_setuptools.py::test_setuptools_prepare_metadata_for_build_wheel
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
