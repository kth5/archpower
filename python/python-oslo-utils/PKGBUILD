# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-utils
pkgver=7.4.0
pkgrel=5
pkgdesc="Oslo Utility library"
arch=(any)
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
depends=('python-iso8601' 'python-oslo-i18n' 'python-netaddr' 'python-netifaces'
         'python-debtcollector' 'python-pyparsing' 'python-packaging' 'python-yaml' 'python-psutil')
makedepends=('git' 'python-setuptools')
checkdepends=('python-eventlet' 'python-testscenarios' 'python-testtools' 'python-oslotest'
              'python-ddt' 'python-stestr' 'qemu-img')
source=("git+https://github.com/openstack/oslo.utils.git#tag=$pkgver")
sha512sums=('5c31f88b364d7f5282e76941250182337656eedafcd0641c815bc6cafbf8e0b1557d6cb8cd54d6017d3a53d391e4889c654667e7b25f818cf3fd3f2f6641e19b')

prepare() {
  cd oslo.utils
  # We have system tzdata
  sed -i '/tzdata/d' requirements.txt
}

build() {
  cd oslo.utils
  python setup.py build
}

check() {
  cd oslo.utils
  stestr run
}

package() {
  cd oslo.utils
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
