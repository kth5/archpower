# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-warlock
pkgver=2.1.0
pkgrel=1
pkgdesc="Python object model built on JSON schema and JSON patch."
arch=(any)
license=('Apache-2.0')
url="https://github.com/bcwaldon/warlock"
depends=('python-jsonschema' 'python-jsonpatch')
makedepends=('git' 'python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest')
source=("git+https://github.com/bcwaldon/warlock.git#tag=$pkgver")
sha512sums=('854bf5e841f97e878b876519f321b528b92ab4c6d7b786c056d9ee8d32b971b73e1a55e3f49f7ff07acaaec595bd4355295485d67be5afc8b8b3c3f193076d9d')

prepare() {
  cd warlock
  sed -i '/--cov/d' pytest.ini
}

build() {
  cd warlock
  python -m build --wheel --no-isolation
}

check() {
  cd warlock
  pytest
}

package() {
  cd warlock
  python -m installer --destdir="$pkgdir" dist/*.whl
}
