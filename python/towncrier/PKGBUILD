# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=towncrier
pkgver=24.8.0
pkgrel=1
pkgdesc="Utility to produce useful, summarised news files for your project"
arch=(any)
url="https://github.com/twisted/towncrier"
license=('MIT')
depends=(
  'python'
  'python-click'
  'python-jinja'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-hatchling'
)
checkdepends=(
  'python-twisted'
)
source=("git+https://github.com/twisted/towncrier.git#tag=$pkgver")
sha512sums=('abb35985b5e5bdfd1a038aa4ede1e9027ef47589f7d309913ff9614e31bca29136215ae827051b64367824e8a869fbee94ca133f25929e4cb777a8dd88e1bbaa')

build() {
  cd towncrier
  python -m build --wheel --no-isolation
}

check() {
  cd towncrier
  python -m installer --destdir=tmp_install dist/*.whl
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/tmp_install/usr/lib/python${python_version}/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" trial towncrier
}

package() {
  cd towncrier
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
