# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=towncrier
pkgver=24.7.0
pkgrel=2
pkgdesc="Utility to produce useful, summarised news files for your project"
arch=(any)
url="https://github.com/twisted/towncrier"
license=('MIT')
depends=(
  'python'
  'python-click'
  'python-jinja'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-hatchling'
  'python-wheel'
)
checkdepends=(
  'python-twisted'
)
source=("git+https://github.com/twisted/towncrier.git#tag=$pkgver")
sha512sums=('d7b8e82b0df30f0766860f0912b114f02f26a2cbfd670a8b5c0800d3e5d396186da31c54955d255d1219dde28da118e5891da9e4a91512dfedb66bf9670a602c')

build() {
  cd towncrier
  python -m build --wheel --no-isolation
}

check() {
  cd towncrier
  python -m installer --destdir=tmp_install dist/*.whl
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/tmp_install/usr/lib/python${python_version}/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" trial towncrier
}

package() {
  cd towncrier
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
