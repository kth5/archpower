# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-argparse
pkgname=python-sphinx-argparse
pkgver=0.5.2
pkgrel=1
pkgdesc="Sphinx extension that automatically documents argparse commands and options"
arch=(any)
url="https://github.com/ashb/sphinx-argparse"
license=(MIT)
depends=(
  python
  python-docutils
  python-sphinx
)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
checkdepends=(
  python-lxml
  python-pytest
)
optdepends=('python-commonmark: markdown support')
source=($pkgname-$pkgver.tar.gz::https://github.com/sphinx-doc/sphinx-argparse/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('dbf6c5e87a3c864d16eab5ecf644754b71e240ae823ffb82a17950e17b2a916188fbe44c4b0551a1c31763accd4eec7e9ce8fc99f8b47a22a7ae4e69133a75ee')
b2sums=('d4af0a872f3036f6bee071524beab2ad312eaf3464ff0b604d06c60dac8e4135b9c7a2a28fbe8c5dbd2b3a4dcf8458b26b77a70fabdde5d739358d0ee2f626c7')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENCE.rst -t "$pkgdir/usr/share/licenses/$pkgname/"
}
# vim:set ts=2 sw=2 et:
