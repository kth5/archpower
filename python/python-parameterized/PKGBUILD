# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parameterized
pkgver=0.8.1
pkgrel=5
pkgdesc="Parameterized testing with any Python test framework"
url="https://github.com/wolever/parameterized"
license=('BSD')
arch=(any)
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://github.com/wolever/parameterized/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
	"replace-mock-usage.patch")
sha512sums=('fb80d75c3d20bb9fe043072e14c89827ad94907d822edac90bee154088fe17d1ecd5df01e98e6a4764c8cdf85deb5eca9bbcdf9dc3756bf2c849453832b3c46e'
            '8fea559b165ce8de3381b65dfaf25b4af6a53fb5f09bfdae20ce17ec15a362d69dda6472b0de6181a627113d31310a2fc119aec8f0f3cce35af84688056355d9')

prepare() {
  cd parameterized-$pkgver
  patch -Np2 -i ${srcdir}/replace-mock-usage.patch
}

build() {
  cd parameterized-$pkgver
  python setup.py build
}

check() {
  cd parameterized-$pkgver
  python setup.py nosetests
}

package() {
  cd parameterized-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
