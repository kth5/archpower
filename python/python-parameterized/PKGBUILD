# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parameterized
pkgver=0.9.0
pkgrel=1
pkgdesc="Parameterized testing with any Python test framework"
url="https://github.com/wolever/parameterized"
license=('BSD')
arch=(any)
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/wolever/parameterized/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
	"replace-mock-usage.patch" "unittest-3.12.patch")
sha512sums=('31a7859117eda89705a7031fd183927432346bfa6bd5e4e29b2bccc9d5602672c8b408275690dffaa7e692122021938e4023d49ee830d2893030a818cf77a00c'
            'beb6af683da0183611e7997f0ac0919e3f35f7ddbe8ac5ca7eb81fcbb80ed7918b1304e0003f10bf4a653c617cc58294f2cf9eff1f4a9d37e2bd08c234b67a0c'
            'bb0cd5bc191e454a5a50a8d0ecc536936ca2e4e9342c43e98583f1b7dde465fa6600244db28f7582e030bdd28b1e0912b20b9681e4b415c1fe6c0ce3f79d7a8a')

prepare() {
  cd parameterized-$pkgver
  patch -Np1 -i ${srcdir}/replace-mock-usage.patch
  patch -Np1 -R -i ${srcdir}/unittest-3.12.patch
}

build() {
  cd parameterized-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd parameterized-$pkgver
  # Hopeless.. pytest 4 era tests
  # PYTHONPATH=. pytest parameterized/test.py
}

package() {
  cd parameterized-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
