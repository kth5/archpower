# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgname=python-unidecode
pkgver=1.4.0
pkgrel=1
pkgdesc='ASCII transliterations of Unicode text'
arch=(any)
url=https://github.com/avian2/unidecode
license=(GPL)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=8d83b7c70c39678c2b95cb0caf3d3c4425cb2cc2
source=(git+https://github.com/avian2/unidecode.git#tag=${_tag})
sha256sums=('a33c1751226d4cca24d0b3cf457a31932ee496f161713d654cfc07c10ef13c28')

pkgver() {
  cd unidecode

  git describe --tags | sed 's/^unidecode-//'
}

build() {
  cd unidecode

  python setup.py build
}

package() {
  cd unidecode

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
