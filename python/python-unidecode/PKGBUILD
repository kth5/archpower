# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgname=python-unidecode
pkgver=1.4.0
pkgrel=3
pkgdesc='ASCII transliterations of Unicode text'
arch=(any)
url='https://github.com/avian2/unidecode'
license=(GPL-2.0-or-later)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("$pkgname::git+$url#tag=unidecode-$pkgver")
sha512sums=('a1b3814130464b2bad1875d1c3baa5dfb1fb8424bec395a1a71d9d84bf57d41f8e4be4f25f934da224c0e07e6b9090a02323d7bb52ba700d606eafdd4e7319a2')
b2sums=('cf765b22ddfd12e4ece7c10598aa123b20330519d800dcf37fdabe5a191042bffeaf89124ae9ab0335f37d0166c080c612362864a23b9fad39b99523a42b0082')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
# vim: ts=2 sw=2 et:
