# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cachetools
pkgver=5.3.2
pkgrel=1
pkgdesc="Extensible memoizing collections and decorators"
url="https://github.com/tkem/cachetools"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tkem/cachetools/archive/v$pkgver.tar.gz")
sha512sums=('0688d6caa25af2440a2ca4535adf99f275ea793521a9462df0a662c0833c608075239e8b7458f908fa742819e99a98452206c5e652c15e6c2c72458e3c1b10a5')

build() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py pytest
}

package() {
  cd cachetools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
