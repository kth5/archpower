# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-gevent
pkgver=24.11.1
pkgrel=1
pkgdesc='Python network library that uses greenlet and libenv for easy and scalable concurrency'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://www.gevent.org'
license=('MIT')
depends=(
  'c-ares'
  'libev'
  'python-greenlet'
  'python-setuptools'
  'python-zope-event'
  'python-zope-interface'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  'cython'
  'python-cffi'
  'libuv'
)
checkdepends=(
  'lsof'
  'python-perf'
  'python-objgraph'
  'python-dnspython'
  'python-requests'
)
source=("$pkgname::git+https://github.com/gevent/gevent#tag=$pkgver")
sha512sums=('00caeeab87dc398e224dee71f44a3a1d5dc2fa16616315ecffe718eca7176c0fc0500ce7ef2348d79a705cc03469ed7a91c170d7b5a3ffcaa1e23710bdd5c174')
b2sums=('214fc287d802b0533d236b353775bc8c84ade44669c1b502b5820c1c28a4130e033578b26d068b5d95b1a5864ac22a86f21b1bdd6b2e3fa5b3ecc83feacc8afe')

build() {
  cd "$pkgname"

  # http://www.gevent.org/development/installing_from_source.html#embedding-libraries
  export GEVENTSETUP_EMBED=0

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m gevent.tests || echo "Tests failed"
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
