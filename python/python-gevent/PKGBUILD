# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-gevent
pkgname=(python-gevent python2-gevent)
pkgver=20.6.2
pkgrel=1
arch=(x86_64 powerpc64le)
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
makedepends=('python-greenlet' 'python2-greenlet' 'cython' 'cython2' 'c-ares' 'libev')
checkdepends=('lsof' 'python-perf' 'python2-perf' 'python2-virtualenv' 'python-objgraph'
              'python2-objgraph' 'python-zope-interface' 'python2-zope-interface' 'python-dnspython'
              'python2-dnspython' 'python-requests' 'python2-requests' 'python-zope-event'
              'python2-zope-event' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('9c95033e109b3f91ee772bd11889c7edf0ee85a076a2e52574c1140c498a656f79ab3d4a034c90f0a72e9432d0aa779522b47904727b74b648437ec35f26e267')

export EMBED=0

prepare() {
  cp -a gevent-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/gevent-$pkgver

  CYTHON=cython \
  PYTHON=python \
  python setup.py build

  cd "$srcdir"/gevent-$pkgver-py2

  CYTHON=cython2 \
  PYTHON=python2 \
  python2 setup.py build
}

check() {
  (
    cd "$srcdir"/gevent-$pkgver
    python -m venv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py install
    python -m gevent.tests
  ) || warning "Tests failed"

  (
    cd "$srcdir"/gevent-$pkgver-py2
    virtualenv2 "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python2.7/site-packages"
    python setup.py install
    python2 -m gevent.tests
  ) || warning "Tests failed"
}

package_python-gevent() {
  depends=('c-ares' 'libev' 'python-greenlet' 'python-zope-event' 'python-zope-interface')

  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gevent() {
  depends=('c-ares' 'libev' 'python2-greenlet' 'python2-zope-event' 'python2-zope-interface')

  cd gevent-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
