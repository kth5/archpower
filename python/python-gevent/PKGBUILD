# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-gevent
pkgver=23.9.1
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
depends=('c-ares' 'libev' 'python-greenlet' 'python-setuptools' 'python-zope-event' 'python-zope-interface')
makedepends=('cython')
checkdepends=('lsof' 'python-perf' 'python-objgraph' 'python-dnspython' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('c620543a2d347b9cad8f6d602d2d2b48bfcbd0f66df685751b37f14515199f6aa031b588210453715560323cfbb07981e211d073a7a13c34940ee204ab031e43')

export GEVENTSETUP_EMBED=0

build() {
  cd gevent-$pkgver
  python setup.py build
}

check() {
  (
    cd gevent-$pkgver
    python -m venv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
    export PYTHONPATH="$PYTHONPATH:/$site_packages"
    python setup.py install
    python -m gevent.tests
  ) || echo "Tests failed"
}

package() {
  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
