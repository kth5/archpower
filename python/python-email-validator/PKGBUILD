# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-email-validator
pkgver=2.2.0
pkgrel=2
pkgdesc="A robust email address syntax and deliverability validation library"
arch=(any)
url="https://github.com/JoshData/python-email-validator"
license=(Unlicense)
depends=(
  python
  python-dnspython
  python-idna
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('9a18de7b50a92a76c9d8fe85cf70f231a10457d7a77e49cf456c38c4cbffd702ac8d62099244cd2d7cc46918cc7aa3eccd49baceb65602c7216a886b2e0aa8cb')
b2sums=('d63c816529d95846cec46a53240bc08be50c3f1d773d11bdb8ff9621e4f0cd5275e85384bb99c608b01f9f14db05767e1a421e1356d07daba07ec807181084b4')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m pytest
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
