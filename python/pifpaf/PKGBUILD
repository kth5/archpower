# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=pifpaf
pkgver=3.3.0
pkgrel=1
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=(any)
url='https://github.com/jd/pifpaf'
license=('Apache-2.0')
depends=(
  'python'
  'python-click'
  'python-daiquiri'
  'python-fixtures'
  'python-jinja'
  'python-packaging'
  'python-psutil'
  'python-xattr'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  # 'couchdb' # Test skipped even if installed
  # 'influxdb' # Fails
  # 'kafka' # Fails
  'consul'
  'httpbin'
  'memcached'
  'postgresql'
  'python-requests'
  'python-testtools'
  'rabbitmq'
  'redis'
)
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
source=(
  "git+$url.git#tag=$pkgver"
  "$pkgname-skip-failing-tests.patch"
)
sha512sums=('ffc72d7213904c0053f62e7c0e10c4319fa678c36df5e2c34df1ff60115ec1562fb587387a9ecf1727ec9636ae509cd1e834f3ab7d1c5dd35c886e7304da0ad2'
            'd3d470971accce527213d3e811240c867ffcf3c4ba9a62bc60f5991b2eac93d17b9365e7ce0d5f845b3b2fab7667481120f437d6b0e745889ed3543e34f51ece')
# Versions 3.2.0+ are not signed.
# validpgpkeys=('5361BD40015B74382739101A611BA9508B78A5C2') # Julien Danjou <julien@danjou.info>

prepare() {
  cd $pkgname
  patch -Np1 -i ../$pkgname-skip-failing-tests.patch
  # Move tests out of package to avoid installing.
  mv pifpaf/tests tests
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" \
    test-env/bin/python -m unittest discover -v
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
