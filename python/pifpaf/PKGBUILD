# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pifpaf
pkgver=3.1.5
pkgrel=8
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=(any)
url='https://github.com/jd/pifpaf'
license=('Apache-2.0')
# setuptools is required for entry points queried by pkg_resources
depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-fixtures'
         'python-psutil' 'python-xattr' 'python-setuptools')
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-testrepository' 'python-requests' 'python-testtools' 'python-oslotest'
              'memcached' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'consul' 'couchdb' 'httpbin' 'rabbitmq')
source=("git+$url.git#tag=$pkgver?signed")
sha512sums=('f80296b849cf6e3eece508d5c4f00f26747660d0a51655eb80b6019c11af56a73e6a53b8599c54fadb467475913440844debcbf30cab07db0dc5b92f9b3f470b')
validpgpkeys=('5361BD40015B74382739101A611BA9508B78A5C2') # Julien Danjou <julien@danjou.info>

build() {
  cd pifpaf
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  # Hack entry points by installing it

  cd pifpaf
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  # TODO: fix test failures
  PYTHONPATH="$PWD/tmp_install/$site_packages" \
  PATH="$PWD/tmp_install/usr/bin:$PATH" \
  python setup.py testr || echo "Tests failed"
}

package() {
  cd pifpaf
  python -m installer --destdir="$pkgdir" dist/*.whl
}
