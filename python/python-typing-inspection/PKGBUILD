# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Heusel <gromit@archlinux.org>

_name=typing-inspection
pkgname=python-typing-inspection
pkgver=0.4.1
pkgrel=1
pkgdesc="Runtime typing introspection tools"
arch=(any)
url="https://github.com/pydantic/typing-inspection"
license=(MIT)
depends=(
    python
    python-typing_extensions
)
makedepends=(
    git
    python-build
    python-installer
    python-setuptools
    python-setuptools-scm
    python-wheel
    python-hatchling
)
checkdepends=(
    python-pytest
)
source=("git+https://github.com/pydantic/typing-inspection#tag=v${pkgver}")
sha512sums=('94ea2d05786ab4c15a999fbbb117ed0dd04cfc52683fb766e01c63b9557cc0b7c37df8510941621ec212fce8fb9e41af644b2d24ab64bdb6cae1adade84efc00')
b2sums=('597f85b24287e0953f389f4ec3415dc0070f0936b5b0be2576a82fbbcd0695834c856f0b3463d135fc497f5a83cb32096025706e8e24a4f46e9fcd9937826cc1')

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  python -m installer --destdir="tmp_install" dist/*.whl
  export PYTHONPATH="$PWD/tmp_install/$_site_packages/:$PYTHONPATH:$PWD/tests"
  pytest -vv -k 'not test_mysql'
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}
