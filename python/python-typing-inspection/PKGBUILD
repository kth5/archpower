# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Heusel <gromit@archlinux.org>

_name=typing-inspection
pkgname=python-typing-inspection
pkgver=0.4.0
pkgrel=1
pkgdesc="Runtime typing introspection tools"
arch=(any)
url="https://github.com/pydantic/typing-inspection"
license=(MIT)
depends=(
    python
    python-typing_extensions
)
makedepends=(
    git
    python-build
    python-installer
    python-setuptools
    python-setuptools-scm
    python-wheel
    python-hatchling
)
checkdepends=(
    python-pytest
)
source=("git+https://github.com/pydantic/typing-inspection#tag=v${pkgver}")
sha512sums=('9b1691c3419fcc6b106316fdb0db01c41b4fb4488a427ad0528a7e3b70383e95cf8ec71a0cf45405d23bec96e912235fe2818c3ec39dfdf2d86110ba11c6d240')
b2sums=('e66bf911076b031c8650e56dfa2a775d19dc99712389e8b69d772fa279de9781e0105cadf651c3c7ba0a40b88ed7651a85922b7ebe835741e9b6efbb6abc2464')

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  python -m installer --destdir="tmp_install" dist/*.whl
  export PYTHONPATH="$PWD/tmp_install/$_site_packages/:$PYTHONPATH:$PWD/tests"
  pytest -vv -k 'not test_mysql'
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}
