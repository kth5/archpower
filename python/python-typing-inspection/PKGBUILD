# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Heusel <gromit@archlinux.org>

_name=typing-inspection
pkgname=python-typing-inspection
pkgver=0.4.2
pkgrel=2
pkgdesc="Runtime typing introspection tools"
arch=(any)
url="https://github.com/pydantic/typing-inspection"
license=(MIT)
depends=(
    python
    python-typing_extensions
)
makedepends=(
    git
    python-build
    python-installer
    python-setuptools
    python-setuptools-scm
    python-wheel
    python-hatchling
)
checkdepends=(
    python-pytest
)
source=("git+https://github.com/pydantic/typing-inspection#tag=v${pkgver}")
sha512sums=('ebc188b6c3b99e0d17bae5a9788caae028eb3e10e1d3c3e2a19a2f1ddb59b6bcc5d49df888a755ac02d227cf51bc411fb366821238bf5f082ff904b82b1d247c')
b2sums=('605cadd95c782cdc119ac234a1aa84a53ba35edf36527f4a8ab39e0fcbb0b889a2ca2e32aa8be9229736121f8b2774c223c44562c7e0725f31e24b944ea794c8')

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  python -m installer --destdir="tmp_install" dist/*.whl
  export PYTHONPATH="$PWD/tmp_install/$_site_packages/:$PYTHONPATH:$PWD/tests"
  pytest -vv -k 'not test_mysql'
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}
