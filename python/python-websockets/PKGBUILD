# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sherlock Holo <sherlockya@gmail.com>
# Contributor: user6553591 <Message on Reddit>

pkgname=python-websockets
pkgver=15.0.1
pkgrel=1
pkgdesc='Python implementation of the WebSocket Protocol (RFC 6455)'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aaugustin/websockets'
license=('BSD-3-Clause')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-werkzeug')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('878a5c19e393b4f5994bbe7a4c9d1266d97eaa02e2610f307a62ee9af2386a1210387d2cb81c19b4795ab76b80de4e9e2bfcad89e0bca3f7b9db76ce0a148ebb')
b2sums=('c3bb0fb19624dbe7d5474584b360415931677ea553170483caee142e8a78120e449902b8ccff76f37ced1959f2758480e536d196fd9488c9a54281e2d82e2820')

build() {
  cd ${pkgname#python-}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${pkgname#python-}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m unittest discover -v
}

package() {
  cd ${pkgname#python-}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
