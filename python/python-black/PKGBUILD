# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxim Baz <archlinux at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

pkgname=python-black
pkgver=25.1.0
pkgrel=1
pkgdesc='Uncompromising Python code formatter'
arch=(any)
url='https://github.com/psf/black'
license=('MIT')
depends=(
  'python'
  'python-click'
  'python-mypy_extensions'
  'python-packaging'
  'python-pathspec'
  'python-platformdirs'
)
makedepends=(
  'git'
  'python-build'
  'python-hatch-fancy-pypi-readme'
  'python-hatch-vcs'
  'python-hatchling'
  'python-installer'
)
optdepends=(
  'ipython: for Jupyter notebook support'
  'python-tokenize-rt: for Jupyter notebook support'
  'python-aiohttp: for the blackd HTTP server'
  'python-colorama: for colored diffs'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('7a3f2752fb01ec7ec5abf316bfdd4950c2bc6e1ac7a45da2d933929a58e251a9620710380f64701634391f072c982b86aae38cfe572246b6878dbdf0f79eda47')
b2sums=('4457ee9fef2e55e71a6f379356bfee861740b82b3904555cb122729e2970e8eae4809d2f01e02e3441244e08c6d9770af93c3b2281fd9cba4aa1d40bdeb6d836')

build() {
  cd "$pkgname"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # vim plugin
  install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/black.vim
  install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/autoload" autoload/black.vim
}

# vim:set ts=2 sw=2 et:
