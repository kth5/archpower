# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgname=python-portend
pkgver=3.2.1
pkgrel=1
pkgdesc="Use portend to monitor TCP ports for bound or unbound states."
arch=(any)
url="https://github.com/jaraco/portend"
license=('MIT')
depends=(
  'python'
  'python-tempora'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("git+$url#tag=v$pkgver")
sha512sums=('053bcd4d588f6ed0e48d5e384ff5cee23f6e9dbd2f17cecafa8d4e5240924a4476e699815b70e3cf175188a573dc9d3e4cf503b54f3a10bf3912e7cad480910e')

prepare() {
  cd ${pkgname#python-}
  git revert -n 9a81db3c77bc106017dcd4b0853a5a94f43ae33c
}

build() {
  cd ${pkgname#python-}
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd ${pkgname#python-}
  pytest --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
