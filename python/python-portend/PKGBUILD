# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgname='python-portend'
_pkgbase="${pkgname//python-/}"
pkgver=3.2.0
pkgrel=3
pkgdesc="Use portend to monitor TCP ports for bound or unbound states."
arch=(any)
url="https://github.com/jaraco/portend"
license=('MIT')
depends=(
  'python'
  'python-tempora'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c32794ec0d7220bc97a6956289c9a5f31bb51a17be4014b8e5d508efc3516aeb2a7c790496dad49af3ad19afc37b71bca4aa5dfde9bfa998e415df65866b41b5')

build() {
  cd "$_pkgbase-$pkgver"
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgbase-$pkgver"
  pytest --override-ini="addopts="
}

package() {
  cd "$_pkgbase-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
