# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgname='python-portend'
_pkgbase="${pkgname//python-/}"
pkgver=3.2.0
pkgrel=1
pkgdesc="Use portend to monitor TCP ports for bound or unbound states."
arch=(any)
url="https://github.com/jaraco/portend"
license=('MIT')
depends=('python' 'python-tempora')
makedepends=('python-setuptools' 'python-setuptools-scm'
             'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('56790cdb6d1619e8873acc2498f9bee758a64ad8f8e47f31e48ae1ac7b6f038fc81af5d52cbc4bc4797dd6dc25d453d391d73d454c98559b1d6a8e9061837d7f'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

prepare() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    # remove linting/style tests, not relevant for downstream packaging
    for pointless_flag in \
      --flake8 \
      --black \
      --cov \
      --cov-append \
      --doctest-modules
    do
      sed -i "s/${pointless_flag}//g" pytest.ini
    done
}

build() {
    # setuptools wont find version from git tag
    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    # tox
    py.test -vvv -s -ra --showlocals --noconftest .
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python -m installer --destdir="$pkgdir" dist/*.whl

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
