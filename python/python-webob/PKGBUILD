# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase='python-webob'
pkgname=('python-webob' 'python-webob-docs')
pkgver=1.8.8
pkgrel=1
pkgdesc="WSGI request and response object"
arch=(any)
url="https://webob.org/"
license=('MIT')
depends=('python' 'python-legacy-cgi')
makedepends=('git' 'python-setuptools' 'python-sphinx' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
optdepends=('python-webob-docs: documentation')
source=("git+https://github.com/Pylons/webob.git#tag=$pkgver")
sha512sums=('88b4f8803260d94a452a80635aa0bfe77f6be956f66163fd669064ba8f22bedd8f1b1b91703e58199cc5e4e5847170530020de04c659d553292dcc3d4b320127')

prepare() {
  cp -a webob{,-docs}
  sed -i "s/pkg_resources.get_distribution('webob').version/'$pkgver'/" webob-docs/docs/conf.py
}

build() {
  cd "$srcdir"/webob
  python -m build --wheel --no-isolation

  cd "$srcdir"/webob-docs
  PYTHONPATH="$PWD/src" make -C docs html
}

check() {
  cd webob
  PYTHONPATH="$PWD/src" pytest --ignore=docs
}

package_python-webob() {
  cd webob
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 "docs/license.txt" \
                   "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python-webob-docs() {
  optdepends=()

  cd webob-docs
  _doc_dir="$pkgdir"/usr/share/doc/python-webob
  mkdir -p "$_doc_dir"
  cp -a "docs/_build/html" "$_doc_dir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
