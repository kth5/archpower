# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase='python-webob'
pkgname=('python-webob' 'python-webob-docs')
pkgver=1.8.9
pkgrel=3
pkgdesc="WSGI request and response object"
arch=(any)
url="https://webob.org/"
license=('MIT')
depends=('python' 'python-legacy-cgi')
makedepends=('git' 'python-setuptools' 'python-sphinx' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
optdepends=('python-webob-docs: documentation')
source=("git+https://github.com/Pylons/webob.git#tag=$pkgver")
sha512sums=('50934d79bd31a1b0472649f4b150ad2105357a6310d06d3ee741bc88e47b8d9d64e98b77225bf77fb3ac22cf525ab2182004d1c4168ba659053b05849d1fd823')

prepare() {
  cp -a webob{,-docs}
  sed -i "s/pkg_resources.get_distribution('webob').version/'$pkgver'/" webob-docs/docs/conf.py
}

build() {
  cd "$srcdir"/webob
  python -m build --wheel --no-isolation

  cd "$srcdir"/webob-docs
  PYTHONPATH="$PWD/src" make -C docs html
}

check() {
  cd webob
  # Deselected test fails under Python 3.14 due to DisconnectionError being
  # raised before IOError.
  PYTHONPATH="$PWD/src" pytest --ignore=docs \
    --deselect=tests/test_in_wsgiref.py::test_interrupted_request
}

package_python-webob() {
  cd webob
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 "docs/license.txt" \
                   "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python-webob-docs() {
  optdepends=()

  cd webob-docs
  _doc_dir="$pkgdir"/usr/share/doc/python-webob
  mkdir -p "$_doc_dir"
  cp -a "docs/_build/html" "$_doc_dir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
