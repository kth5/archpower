# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pkgname=poetry-plugin-export
pkgname=python-poetry-plugin-export
pkgver=1.9.0
pkgrel=2
pkgdesc="Poetry plugin to export the dependencies to various formats "
url="https://github.com/python-poetry/poetry-plugin-export"
license=('MIT')
arch=(any)
depends=('python-poetry')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest-mock' 'python-pytest-xdist')
source=("https://github.com/python-poetry/poetry-plugin-export/archive/$pkgver/$pkgname-$pkgver.tar.gz"
         https://github.com/python-poetry/poetry-plugin-export/commit/16637f19.patch
         https://github.com/python-poetry/poetry-plugin-export/commit/39ff1fb0.patch)
sha512sums=('c4ae0320256576e6ef72b1958d56871c09e4c1e0d9556e381cbd438b2346f15aa6ef8583e284c0320b9aeac64aed8e8360394a0140abeda6ca55e18c612c5028'
            '5f6b1ac67bf33b02c88c3f1a4283c1971cbc489d7715d1a9d804d39d9e9959c665c94f43e6624331546cce1a5f9caa3423fb68e5dd77328a09ea25cff8918bd6'
            '87e125aa570302d29d1c6d2e3fb67d8db815582506e9aa11e78aa539a77ad08da8d92cabd7569bf43c660dbb7a8eddf11b50a95ea8f7debe7a4527d9a2f24809')

prepare() {
  cd poetry-plugin-export-$pkgver
  patch -p1 -i ../16637f19.patch
  patch -p1 -i ../39ff1fb0.patch # Fix tests
}

build() {
  cd poetry-plugin-export-$pkgver
  python -m build -wn
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd poetry-plugin-export-$pkgver
  python -m installer -d tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest
}

package() {
  cd poetry-plugin-export-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
