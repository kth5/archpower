# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=httpbin
pkgver=0.10.1
pkgrel=1
pkgdesc="HTTP Request and Response Service"
arch=(any)
url="https://github.com/psf/httpbin"
license=('MIT' 'custom:ISC')
depends=('python' 'python-flask' 'python-decorator' 'python-six'
         'python-brotlicffi' 'python-werkzeug' 'python-flasgger'
         # Not directly used. TODO: ask upstream if it's really needed
         'python-greenlet')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/psf/httpbin/archive/release-v$pkgver.tar.gz")
sha512sums=('4fc9551a44de6d2dc9d9d1a430bf6f1be6a5d850397fa69cc02e6ffbd537d1b21f2b47310c770af1070c24b27179b6ccd52b62490003c43de680906e072120c8')

build() {
  cd httpbin-release-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd httpbin-release-v$pkgver
  pytest tests
}

package() {
  cd httpbin-release-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
