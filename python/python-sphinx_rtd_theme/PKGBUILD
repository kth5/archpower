# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgname=python-sphinx_rtd_theme
_name=${pkgname/python-/}
pkgver=1.3.0
pkgrel=1
pkgdesc='Read the Docs theme for Sphinx'
arch=(any)
url=https://github.com/readthedocs/sphinx_rtd_theme
license=('MIT')
depends=('python-sphinx' 'python-sphinxcontrib-jquery')
makedepends=('python-setuptools-scm' 'python-build' 'python-installer' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('590b030c7abb9cf038ec053b95e5380b5c70d61591eb0b552063fbe7c41f0931')
b2sums=('f9849a72941024de7537a3517e226e2cdc6b2067e043bcdf8bbb4cc29cdf8b0be67121f721d92d30ffccea367af62ff0ce71d5d00d8eafbb65a2ebf4ef8e5159')

build() {
  cd "$_name"-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name"-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
