# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.path
pkgver=3.7.1
pkgrel=1
pkgdesc="Miscellaneous path functions"
url="https://github.com/jaraco/jaraco.path"
license=('MIT')
arch=(any)
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-enabler' 'python-pytest-mypy')
source=("git+https://github.com/jaraco/jaraco.path.git#tag=v$pkgver")
sha512sums=('a8cf1b9df71c97dd122a46f6a7f1c55f502a5be2d883422e30336d554ea559ff5e838852cf8faf447c3daa0a1386358f7aa7715b128a124489ac733fde514851')

build() {
  cd jaraco.path
  python -m build -wn
}

check() {
  cd jaraco.path
  python -m pytest
}

package() {
  cd jaraco.path
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
