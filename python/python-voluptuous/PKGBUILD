# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-voluptuous
pkgver=0.15.0
pkgrel=1
pkgdesc="A Python data validation library"
url="https://github.com/alecthomas/voluptuous"
license=('BSD-3-Clause')
arch=(any)
depends=('python')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
source=(git+https://github.com/alecthomas/voluptuous.git#tag=$pkgver)
sha512sums=('99a940f9e9253459d806d604db931731ed320b78581cce0180c29b5997ffb2db26b74c461a1ec69bc2b542d66a190d6ce6c6698a0ab543b2a545df1fcf48ffb7')

build() {
  cd voluptuous
  python setup.py build
}

check() {
  cd voluptuous
  pytest
}

package() {
  cd voluptuous
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
