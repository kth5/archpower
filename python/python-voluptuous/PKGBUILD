# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-voluptuous
pkgver=0.16.0
pkgrel=1
pkgdesc="A Python data validation library"
url="https://github.com/alecthomas/voluptuous"
license=('BSD-3-Clause')
arch=(any)
depends=('python')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
source=(git+https://github.com/alecthomas/voluptuous.git#tag=$pkgver)
sha512sums=('9017ff11b95668973a97ae62624934abab7766948ae2d5b1728d9208c692ba60486f3b9ff784aae47e62605d9e7f4d58447678d5d216975b148c07dd24e9ef42')

build() {
  cd voluptuous
  python setup.py build
}

check() {
  cd voluptuous
  pytest
}

package() {
  cd voluptuous
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
