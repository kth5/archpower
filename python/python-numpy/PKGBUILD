# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=python-numpy
pkgver=2.4.2
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('custom')
url="https://www.numpy.org/"
depends=('cblas' 'lapack' 'python')
optdepends=('blas-openblas: faster linear algebra')
makedepends=('git' 'python-build' 'python-installer' 'meson-python' 'cmake' 'gcc-fortran' 'cython')
checkdepends=('python-pytest' 'python-hypothesis')
source=(git+https://github.com/numpy/numpy#tag=v$pkgver
        numpy-meson::git+https://github.com/numpy/meson.git
        numpy-highway::git+https://github.com/google/highway.git
        numpy-x86-simd-sort::git+https://github.com/intel/x86-simd-sort
        numpy-pocketfft::git+https://github.com/mreineck/pocketfft
        numpy-svml::git+https://github.com/numpy/SVML.git
        numpy-pythoncapi-compat::git+https://github.com/python/pythoncapi-compat)
sha512sums=('13fa8aa7b69a57921cf709c0146fb835cb2cd20b2953b8b58ca82ba49e21d22084a806c416a01842f399619f2b67e143516744d62f948c972ca7176dbeeaf89c'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd numpy

  git submodule init
  git submodule set-url vendored-meson/meson "$srcdir"/numpy-meson
  git submodule set-url numpy/_core/src/highway "$srcdir"/numpy-highway
  git submodule set-url numpy/_core/src/npysort/x86-simd-sort "$srcdir"/numpy-x86-simd-sort
  git submodule set-url numpy/fft/pocketfft "$srcdir"/numpy-pocketfft
  git submodule set-url numpy/_core/src/umath/svml "$srcdir"/numpy-svml
  git submodule set-url numpy/_core/src/common/pythoncapi-compat "$srcdir"/numpy-pythoncapi-compat
  git -c protocol.file.allow=always submodule update \
      vendored-meson/meson \
      numpy/_core/src/highway \
      numpy/_core/src/npysort/x86-simd-sort \
      numpy/fft/pocketfft \
      numpy/_core/src/umath/svml \
      numpy/_core/src/common/pythoncapi-compat
}

build() {
  cd numpy
  CFLAGS+=" -ffat-lto-objects" \
  CXXFLAGS+=" -ffat-lto-objects" \
  python -m build --wheel --no-isolation \
    -Csetup-args=-Dblas=cblas \
    -Csetup-args=-Dlapack=lapack \
    -Csetup-args=-Denable-openmp=true \
    -Csetup-args=-Dcpu-baseline=NONE
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd numpy
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl
  cd "$PWD/tmp_install"
  PATH="$PWD/usr/bin:$PATH" PYTHONPATH="$PWD/$site_packages:$PYTHONPATH" python -c 'import numpy; numpy.test()'
}

package() {
  cd numpy
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/numpy-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
