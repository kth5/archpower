# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=2.2.0
pkgrel=4
pkgdesc="Pythonic task execution"
arch=(any)
url="https://github.com/pyinvoke/invoke"
license=('BSD-2-Clause')
depends=(
  'python'
  'python-fluidity'
  'python-lexicon'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-icecream'
  'python-pytest'
  'python-pytest-relaxed'
)
source=("$url/archive/$pkgver/${pkgname#python-}-$pkgver.tar.gz")
sha512sums=('3d0adabec4825fb45524d3ab4f2a790d31245a89e038c08c770fc9f33c81ed182769e8f17d3d6f33a913b431cba475b975826ddfd533a945d481b52deba759a2')

prepare() {
  cd ${pkgname#python-}-$pkgver
  rm -r invoke/vendor
}

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  [ ! "${CARCH}" == 'x86_64' ] && return 0
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -s \
    --deselect tests/runners.py::Local_::env::uses_execve_for_pty_True \
    --deselect tests/runners.py::Local_::pty \
    --deselect tests/runners.py::Local_::shell::defaults_to_bash_or_cmdexe_when_pty_True \
    --deselect tests/runners.py::Local_::shell::may_be_overridden_when_pty_True
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
