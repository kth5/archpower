# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=2.1.3
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=(any)
depends=('python-fluidity' 'python-lexicon' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-pytest-relaxed' 'python-icecream')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('6fe101c1ce52cf3cf5c8f273ae0dede093b8b84f0117bc02fc2fefd680d6fc44a5a0dbfa236048a42e5389d7a7e5af0556530ee43fd3d2cfe37c1637570d9122')

prepare() {
  rm -r invoke-$pkgver/invoke/vendor
}

build() {
  cd invoke-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd invoke-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest -s tests -k 'not pty'
}

package() {
  cd invoke-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
