# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Bruno Santos <brunomanuelsantos@tecnico.ulisboa.pt>

pkgname=python-sphinx-hawkmoth
pkgver=0.21.0
pkgrel=1
pkgdesc="Sphinx autodoc C extension"
url="https://github.com/jnikula/hawkmoth"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(BSD-2-Clause)
depends=(
  clang
  python
  python-docutils
  python-sphinx
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-xdist
  python-strictyaml
)
source=(
  "git+https://github.com/jnikula/hawkmoth?signed#tag=v$pkgver"
)
b2sums=('054566db1fd936e9ae1d10449251b356569d492488a39911944ae715bd723aa6e1f6585775743c74364c3f38c68239c3e1c69aac80b86d72861fc47f787c04f3')
validpgpkeys=(
  3FE6F7606966ED87707760B6A14C59DFEDB4CE88 # Jani Nikula <jani@nikula.org>
)

prepare() {
  cd hawkmoth
}

build() {
  python -m build --wheel --no-isolation hawkmoth
}

check() {
  cd hawkmoth
  PYTHONPATH="$PWD/src" make test-verbose
}

package() {
  python -m installer --destdir="$pkgdir" hawkmoth/dist/*.whl
  install -Dm644 hawkmoth/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
