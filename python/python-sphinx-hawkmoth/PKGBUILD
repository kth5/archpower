# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Bruno Santos <brunomanuelsantos@tecnico.ulisboa.pt>

pkgname=python-sphinx-hawkmoth
pkgver=0.19.0
pkgrel=2
pkgdesc="Sphinx autodoc C extension"
url="https://github.com/jnikula/hawkmoth"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(BSD-2-Clause)
depends=(
  clang
  python
  python-docutils
  python-sphinx
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-xdist
  python-strictyaml
)
source=(
  "git+https://github.com/jnikula/hawkmoth?signed#tag=v$pkgver"
  0001-Reapply-test-query-system-include-args-from-clang.patch
)
b2sums=('c3b816fcd8259ba64e2269926ff4120a385ea45be226a1f3ca91e61108b8086e0a72e39f1186a0cdf4e5f9dab5624268ac4d2e68c850b1d77305878cc6b51ae6'
        '6fca617b2d07c8a16ad8606b53cf5469062b1630a5491647c4953613d3ed40e326a3dfdf5abba0789e70dd84a2908d9969178267b657efd07dc79e402fe4ec98')
validpgpkeys=(
  3FE6F7606966ED87707760B6A14C59DFEDB4CE88 # Jani Nikula <jani@nikula.org>
)

prepare() {
  cd hawkmoth

  # Unbreak tests. Clang issue?
  git apply -3 ../0001-Reapply-test-query-system-include-args-from-clang.patch
}

build() {
  cd hawkmoth
  python -m build --wheel --no-isolation
}

check() {
  cd hawkmoth
  PYTHONPATH="$PWD/src" make test-verbose
}

package() {
  cd hawkmoth
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
