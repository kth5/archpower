# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Bruno Santos <brunomanuelsantos@tecnico.ulisboa.pt>

pkgname=python-sphinx-hawkmoth
pkgver=0.20.0
pkgrel=1
pkgdesc="Sphinx autodoc C extension"
url="https://github.com/jnikula/hawkmoth"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(BSD-2-Clause)
depends=(
  clang
  python
  python-docutils
  python-sphinx
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-xdist
  python-strictyaml
)
source=(
  "git+https://github.com/jnikula/hawkmoth?signed#tag=v$pkgver"
)
b2sums=('d37a9040139dd35b2c030dd023cf9cf4f568f9ac69787225f7d4f00b7dea11822f6b26dfdf1be3693b4a7a40b0b3e761f4a861f46acf58f243734b420bb23abd')
validpgpkeys=(
  3FE6F7606966ED87707760B6A14C59DFEDB4CE88 # Jani Nikula <jani@nikula.org>
)

prepare() {
  cd hawkmoth
}

build() {
  cd hawkmoth
  python -m build --wheel --no-isolation
}

check() {
  cd hawkmoth
  PYTHONPATH="$PWD/src" make test-verbose
}

package() {
  cd hawkmoth
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
