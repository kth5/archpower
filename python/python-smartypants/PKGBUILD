# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jiachen Yang <farseerfc@gmail.com>
# Contributor: Aetf <aetf at unlimitedcodeworks dot xyz>
# Contributor: carstene1ns <arch carsten-teibes de>

pkgname=python-smartypants
_pkgname=smartypants.py
pkgver=2.0.1
pkgrel=14
pkgdesc="Python with the SmartyPants"
arch=(any)
url="https://github.com/justinmayer/smartypants.py"
license=('BSD-2-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-docutils'
  'python-pygments'
  'python-pytest'
)
source=(
  "$url/archive/v$pkgver/$_pkgname-$pkgver.tar.gz"
  "$pkgname-python-3.12.patch::$url/commit/ea46bf36343044a7a61ba3acce4a7f188d986ec5.patch"
)
sha512sums=('d47a866a5478c3520251f87a93a468a5eea10318b24b2e8d4bc918d533b5a5789aa56d3a8d5fb8ccff9572fb63e5b6f2eafc44f93fb57a19e6621ebef5d64d9d'
            'a4a8a6aa6896aee6369b35e7f0aafda0c96e1e667daa1c866be6665f56dbfa8dd63f51f0b07a8767e9e0673b8b67a678b78bbfa0155b8ec823107195148f7c24')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 < ../$pkgname-python-3.12.patch
}

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  pytest
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
