# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-outcome
pkgver=1.3.0
pkgrel=1
pkgdesc='Capture the outcome of Python function calls'
arch=(any)
url=https://github.com/python-trio/outcome
license=(MIT)
depends=(
  python
  python-attrs
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
)
_tag=4037dd7fa89b3a26f41594607bc94aaea422d7c3
source=(git+https://github.com/python-trio/outcome.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd outcome
  git describe --tags | sed 's/^v//'
}

build() {
  cd outcome
  python setup.py build
}

check() {
  cd outcome
  PYTHONPATH=$PWD/build/lib pytest
}

package() {
  cd outcome
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-outcome/
}

# vim: ts=2 sw=2 et:
