# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-expandvars
pkgver=1.1.1
pkgrel=1
pkgdesc='Expand system variables Unix-style'
arch=(any)
url='https://github.com/sayanarijit/expandvars'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('f357227482616e808840ba835c451beaabee0a70ffca7792f02d9d3c361a7da0ed186d0a551d4320ad91fec2e2562fcd0f662b4a7f6be42ba08b309b5d27accb')
b2sums=('aa008823eec46320696ee928a70023d45b49140c366e983226e3affe18aa2cdb7b3ec3a741a11ba8d8fff9adb1ac6991dd46a6bdb88207a7be11be41d294595b')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
