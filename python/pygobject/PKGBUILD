# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(
  python-gobject
  python-gobject-docs
)
pkgver=3.52.3
pkgrel=3
pkgdesc="Python bindings for GLib/GObject/GIO/GTK"
url="https://pygobject.gnome.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  gobject-introspection-runtime
  libffi
  python
)
makedepends=(
  git
  gobject-introspection
  meson
  python-cairo
  python-setuptools
  python-sphinx
  python-sphinx-copybutton
  python-sphinx-furo
)
checkdepends=(
  gtk3
  python-pytest
  xorg-server-xvfb
)
optdepends=('python-cairo: Cairo bindings')
source=(
  "git+https://gitlab.gnome.org/GNOME/pygobject.git?#tag=$pkgver"
  "git+https://gitlab.gnome.org/GNOME/gobject-introspection-tests.git#commit=33dca8ac76c4ccae76e462ce069853e7c12247c"
  "git+https://github.com/python/pythoncapi-compat#commit=2d18aecd7b2f549d38a13e27b682ea4966f37bd8"
  0001-docs-Use-furo-theme.patch
)
b2sums=('5d86ac25d085e51b328e901397a8200f4b9aead10b536f038d37fb8d8639135c5d8797a28baa0c9eeab9125c0f22d2b516864b296d9674f1431f1f260f4816e2'
        '4b682ce3201eec67006a8f953649651ce807c05036d7239da6a64a32c19691d9c68766f809213b5755adee45cb8f4849d4c39335f19c94ff84833a0d43ae1a27'
        '97ab530cd8256e0c64be5789980af00430a822bb17721a08dcf242512c28ec7b6debd0c5a6b4abd7f555a36c03a88b91d12d734e18b394322372b3c9139fdb6c'
        '6deeb5fff97f7d98fb27d7ba5df3941bab75d81407fec278f66a6d9bbf06a981047c249a1ab7906077a94bfa6c07b4bedd85d963b24d33ca8fc15c5707128f86')
validpgpkeys=(
  0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC # Christoph Reiter <reiter.christoph@gmail.com>
  12B7C9847B4AD5787318A831A61779569ADCE5E6 # Arjan Molenaar <gaphor@gmail.com>
)

prepare() {
  cd pygobject

  # Fix docs build
  git apply -3 ../0001-docs-Use-furo-theme.patch
}

build() {
  # Inject subprojects
  export MESON_PACKAGE_CACHE_DIR="$srcdir"

  arch-meson pygobject build
  meson compile -C build
}

check() {
  PYTEST_ADDOPTS="-v" PYGI_TEST_VERBOSE=1 \
    xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_python-gobject() {
  provides=("pygobject-devel=$pkgver")
  conflicts=(pygobject-devel)
  replaces=('pygobject-devel<=3.36.1-1')

  meson install -C build --destdir "$pkgdir"
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

package_python-gobject-docs() {
  pkgdesc="Developer documentation for PyGObject"
  depends=()
  optdepends=()

  python -m sphinx -b html pygobject/docs "$pkgdir/usr/share/doc/pygobject/html"
  rm -r "$pkgdir/usr/share/doc/pygobject/html/.doctrees"
}


# vim:set sw=2 sts=-1 et:
