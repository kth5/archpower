# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-interpreters-pep-734
pkgver=0.5.0
pkgrel=1
pkgdesc='Pure-Python implementation of PEP 734, along with several other backports'
arch=(any)
url='https://github.com/ericsnowcurrently/interpreters'
license=(BSD-2-Clause)
depends=(python)
makedepends=(git
             python-build
             python-installer
             python-setuptools)
source=(git+https://github.com/ericsnowcurrently/interpreters#tag=v$pkgver)
sha256sums=('96c02f9a7cccf44dd25a93ef9e20ef9122c1f6de9c58b6541ca76e6dbee2879d')

build() {
  cd interpreters
  python -m build --wheel --no-isolation
}

package() {
  cd interpreters
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
