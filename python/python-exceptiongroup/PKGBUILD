# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Padraic Fanning <fanninpm AT miamioh DOT edu>

pkgname=python-exceptiongroup
_pyname=${pkgname#python-}
pkgver=1.2.1
pkgrel=2
pkgdesc='Backport of PEP 654 (exception groups)'
arch=(any)
url="https://github.com/agronholm/$_pyname"
license=(MIT)
depends=('python')
makedepends=(python-{build,installer,wheel}
             python-flit-scm)
checkdepends=(python-pytest)
_archive="$_pyname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('b59b2c0cee4423a01ca6dba773efac4dd6390e3908e36ed222d683ce827c8f3b')

build() {
	cd "$_archive"
	export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
	python -m build -wn
}

check() {
	cd "$_archive"
	PYTHONPATH=src pytest
}

package() {
	cd "$_archive"
	export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
