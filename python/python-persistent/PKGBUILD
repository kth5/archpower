# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=6.1.1
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('ZPL-2.1')
url="https://github.com/zopefoundation/persistent/"
depends=('python-cffi' 'python-zope-interface')
makedepends=('git' 'python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("git+https://github.com/zopefoundation/persistent.git#tag=$pkgver")
sha512sums=('75631f10707bce7fcfa34b4b6e39eb80761f71788263202a19dfb8ea25411b58f0c169d9f0e258073a836ffc6e168dc52ebcfcafd718d9bb302ef76073250c95')

build() {
  cd persistent
  python setup.py build
}

check() {
  cd persistent
  python -m venv "$srcdir"/pyvenv --system-site-packages
  "$srcdir"/pyvenv/bin/python setup.py develop
  "$srcdir"/pyvenv/bin/python -m zope.testrunner --test-path=src
}

package() {
  cd persistent
  python setup.py install --root="$pkgdir" --optimize=1
}
