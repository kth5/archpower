# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-absl
pkgver=2.1.0
pkgrel=2
pkgdesc="Abseil Python Common Libraries"
arch=('any')
url='https://github.com/abseil/abseil-py'
provides=('absl-py')
conflicts=('absl-py')
replaces=('absl-py')
license=('APACHE')
makedepends=(python-{build,installer,wheel} python-setuptools)
source=("https://pypi.io/packages/source/a/absl-py/absl-py-$pkgver.tar.gz")
sha512sums=('3f28c73c63c9a648a3df73f367725bb4f294b1858daa9e63711a3dfb1c7a7d14b3929afdc384a3e4a3da79820c3b2bb19daf068c0628bf1ea5cfba5b7f8645b7')

build() {
  cd "$srcdir/absl-py-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/absl-py-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
