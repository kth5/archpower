# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zipp
pkgver=3.19.2
pkgrel=1
pkgdesc="Pathlib-compatible object wrapper for zip files"
url="https://github.com/jaraco/zipp"
license=('MIT')
arch=(any)
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-big-o'
  'python-jaraco.functools'
  'python-jaraco.itertools'
  'python-jaraco.test'
  'python-more-itertools'
  'python-pytest'
  'python-pytest-enabler'
  'python-pytest-ignore-flaky'
  'python-tests'
)
source=(
  "git+$url.git#tag=v$pkgver"
  'do-not-use-compat.patch'
)
sha512sums=('0540b9b2fefc10b5c6720e9e8d2fedacd0c4c0e4b14745f5680f46b2e624718eac9cfc881c7dc8ce55df5a208357940b7af2e82cf8a50369f5d9f129fc22ee2a'
            '1dc5e9ce426a24474a06b64dab2f6c40d515116bc9fb3c3b19a0da2a481bc4ded904eff08b04bee00f39e84d80ad09f452ac278b533bb5c3948b4d06ff05d23a')

prepare() {
  cd zipp
  patch --forward --strip=1 --input=../do-not-use-compat.patch
}

build() {
  cd zipp
  python -m build -nw
}

check() {
  cd zipp
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd zipp
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/zipp-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
