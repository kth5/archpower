# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgname=python-xcffib
pkgver=1.3.0
_commit=76b0c03597ee59fd4109a671d8c536842f904b4c
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=(any)
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-cffi' 'libxcb')
makedepends=('git' 'python-setuptools' 'parallel' 'python-wheel')
checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-xeyes')
source=("git+https://github.com/tych0/xcffib.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cd xcffib
  # Remove redundant six import
  git cherry-pick -n 96eb6d4a6fa0b1404bf4ba072d0bb8f9e58af306
}

build() {
  cd xcffib
  make xcffib CABAL=false
  make
  python setup.py build
}

check() {
  cd xcffib
  xvfb-run pytest
}

package() {
  cd xcffib
  python setup.py install --root="$pkgdir/" --optimize=1
}
