# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname='python-et-xmlfile'
pkgver=2.0.0
pkgrel=3
pkgdesc='A low memory library for creating large XML files'
arch=(any)
url='https://foss.heptapod.net/openpyxl/et_xmlfile'
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-lxml')
source=("https://foss.heptapod.net/openpyxl/et_xmlfile/-/archive/${pkgver}/et_xmlfile-${pkgver}.tar.gz" "update-stdlib-tests-3.14.patch")
sha512sums=('b23c0d110b85aba2881e4bb2702efc7a7d411aa1528e1096fd11c09b54e1afed01bf8ae5231874eeb64011ae0ebd6342dd89add45237bfc98ff19f2a895eb013'
            'a838d1292f33b0b1850f4b2c970da46763728c153d07bab5832ddddf11757762e267b1b31cb5c67ad1f1c99482ba429dbc6bdc9bcff362c11b02b3e56a22c065')
b2sums=('810cb9119558bd139e19e5ad9df90db0b1445f47dc410040d319d5a761a357eb07776e9a4d2515d7ddd10c2b845ea834769d7bddc7cebff54e1085261203a936'
        'a8f6bd188d8d93a67cefabb5f76e3d8a0836add8d53e9a0f650ed33e2399a05d781ac0456e6922c41af6edfdf5a7b231ee4503083a4f0430f9ec4a892b4cb7a4')

prepare() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  # fix deprecation warning
  sed -i "s/strict$/strict-markers/" pytest.ini

  patch -Np1 -i ${srcdir}/update-stdlib-tests-3.14.patch
}

build() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  PYTHONPATH="$PWD/et_xmlfile" pytest
}

package() {
  cd "$srcdir"/et_xmlfile-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENCE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
