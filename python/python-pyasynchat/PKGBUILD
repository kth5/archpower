# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-pyasynchat
pkgver=1.0.5
pkgrel=1
pkgdesc="Make asynchat available for Python 3.12 onwards"
arch=(any)
url="https://github.com/simonrob/pyasynchat"
license=(PSF-2.0)
depends=(
  python
  python-pyasyncore
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-tests)
provides=(python-asynchat)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('c9babb64f3bdb67532bd8594abef044c01742b6ccbb4b713297b1daf20514fa88f5bc41c4e4cb6ca45e5d943d62e16f1ae1f5af096d1cf62b658f4e137ef5428')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m unittest discover -v
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
