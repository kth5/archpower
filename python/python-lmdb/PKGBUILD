# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=py-lmdb
pkgname=python-lmdb
pkgver=1.7.5
pkgrel=2
pkgdesc='Universal Python binding for the LMDB Lightning Database'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/jnwatson/py-lmdb/'
license=('OLDAP-2.8')
depends=(
    'glibc'
    'lmdb'
    'python'
    'python-cffi')
makedepends=(
    'python-build'
    'python-installer'
    'python-patch-ng'
    'python-setuptools'
    'python-wheel')
checkdepends=(
    'python-pytest')
source=("https://github.com/jnwatson/py-lmdb/archive/${_srcname}_${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('715a642f6d8df1dbd2ad47d30a567982350213388ab4559fb418be4c1eb3a320')

build() {
    cd "${_srcname}-${_srcname}_${pkgver}"
    
    export LMDB_FORCE_SYSTEM='1'
    python -m build --wheel --no-isolation
}

check() {
    cd "${_srcname}-${_srcname}_${pkgver}"
    
    local _pyver
    _pyver="$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')"
    
    export LMDB_FORCE_SYSTEM='1'
    export PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-cpython-${_pyver}"
    pytest -vv --color='yes'
}

package() {
    export LMDB_FORCE_SYSTEM='1'
    python -m installer --destdir="$pkgdir" "${_srcname}-${_srcname}_${pkgver}/dist"/*.whl
    
    local _sitepkgs
    _sitepkgs="$(python -c 'import site; print(site.getsitepackages()[0])')"
    
    install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -sr "${pkgdir}${_sitepkgs}/lmdb-${pkgver}.dist-info/licenses/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
