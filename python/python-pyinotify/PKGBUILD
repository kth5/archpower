# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=python-pyinotify
pkgver=0.9.6
pkgrel=14
pkgdesc='Python module used for monitoring filesystems events on Linux platforms with inotify.'
arch=(any)
url="https://github.com/seb-m/pyinotify"
license=('custom:MIT')
depends=('python')
makedepends=('python' 'python-setuptools')
source=("https://github.com/seb-m/pyinotify/archive/${pkgver}.tar.gz"
	"pyinotify-python-3.12-fix.patch")
sha512sums=('144db691c1cdfd4e52b838b8ed839b8d50d84d26a91a59b7460c3170f58a1c96b3ce4f51e6273835835291781215a87b8f2d2d87d1abf012e4c14b2cd4b3f4cf'
            '5da85499b8a3e89dc13ce609a08804d778df53006b382c8e1ca2a5212bf5b62e006275b81d232fd6d3a554c392a1d756c1fca2b56bfd1124b9d6591feb70ea63')

prepare() {
  cd pyinotify-${pkgver}
  patch -Np1 -i ${srcdir}/"pyinotify-python-3.12-fix.patch"
}

build() {
  cd pyinotify-${pkgver}
  python3 setup.py build
}

package() {
  cd pyinotify-${pkgver}
  python3 setup.py install --root="${pkgdir}" -O1

  install -d "${pkgdir}"/usr/share/licenses/python-pyinotify
  install -m644 COPYING \
    "${pkgdir}"/usr/share/licenses/python-pyinotify
}
