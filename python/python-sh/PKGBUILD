# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=python-sh
pkgver=2.0.6
pkgrel=2
pkgdesc='Subprocess interface that allows you to call any program as if it were a function'
arch=(any)
url='https://pypi.python.org/pypi/sh'
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'python-poetry-core')
checkdepends=('lsof' 'python-pytest')
source=("https://pypi.io/packages/source/s/sh/sh-$pkgver.tar.gz")
sha512sums=('717dae58ebd08b3a86e0d30ba2a6f3055592844d064265280b97a1d29bd7d0422c9e5500b6eb999cc1a97c1c2af04cf8ed214670bc08cb2bc45e418f69560c1d')
b2sums=('2352aa1c560a95e59446473658d0a95609f594417c8840c84cf1d65590aa94c2225dcd8782e75d82e92cdbf86f21dbbe03a1818a5ef1800a39444ce2918bc90b')

build() {
  cd sh-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd sh-$pkgver
  # export PYTHONPATH="$(pwd):${PYTHONPATH}"
  pytest tests -k 'not test_environment'
}

package() {
  cd sh-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/python-sh/LICENSE
  install -vDm 644 {README.rst,CHANGELOG.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
