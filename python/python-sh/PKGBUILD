# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=python-sh
pkgver=2.2.2
pkgrel=2
pkgdesc='Subprocess interface that allows you to call any program as if it were a function'
arch=(any)
url='https://github.com/amoffat/sh'
license=('MIT')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel'
             'python-poetry-core')
checkdepends=('lsof' 'python-pytest')
source=("git+https://github.com/amoffat/sh.git#tag=$pkgver")
sha512sums=('c06c5ed027ba73062c0822eb4bdb2c4ed104f8b423fe9b610ae86bad6d67df4525a73b37974164d292e7dccd0057948ef52fecd6738226da1a2e0852974b8ad8')
b2sums=('e3d436d851dc7ec5286dbc6514810be733a9776f13dc5a8c5f0a5c4b481c0debdfa647022e0c2e83bb45165f2898ceecc1b2a554521260d6f6a390a9930a8d21')

build() {
  cd sh
  python -m build --wheel --no-isolation
}

check() {
  cd sh
  # export PYTHONPATH="$(pwd):${PYTHONPATH}"
  pytest tests -k 'not test_environment'
}

package() {
  cd sh
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -vDm644 {README.rst,CHANGELOG.md} \
    -t "$pkgdir"/usr/share/doc/$pkgname/
}

# vim:set ts=2 sw=2 et:
