# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-relaxed
pkgver=2.0.1
pkgrel=1
pkgdesc='Relaxed test discovery for pytest'
arch=(any)
license=('BSD')
url='https://github.com/bitprophet/pytest-relaxed'
depends=('python-pytest' 'python-decorator')
makedepends=('python-setuptools')
source=("https://github.com/bitprophet/pytest-relaxed/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b62458aaf96cd7d70291c0c3ef19430ebeeee3e3c033bfda3c320f40ea2c4b1147e3edd49aa389dba393cee4b70f1ecd477a8b4ca17b37a9ef8d9766895d1c69')

build() {
  cd pytest-relaxed-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd pytest-relaxed-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install$site_packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-relaxed-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
