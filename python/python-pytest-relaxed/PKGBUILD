# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-relaxed
pkgver=2.0.2
pkgrel=2
pkgdesc='Relaxed test discovery for pytest'
arch=(any)
license=('BSD')
url='https://github.com/bitprophet/pytest-relaxed'
depends=('python-pytest' 'python-decorator')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/bitprophet/pytest-relaxed.git#tag=$pkgver")
sha512sums=('95b31156d52abf54009164bb989bf50abb2a09103d267906e3dea9e83f41050009b82b2bfebb5085a63b2bdf2183c8dbbf1b3ae4877cfe0d88e483d4c965e080')

build() {
  cd pytest-relaxed
  python setup.py build
}

check() {
  # Hack entry points by installing it

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd pytest-relaxed
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install$site_packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-relaxed
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
