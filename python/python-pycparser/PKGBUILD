# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: lang2 <wenzhi.liang@gmail.com>

pkgname=python-pycparser
pkgver=2.23
pkgrel=2
pkgdesc='C parser and AST generator written in Python'
url='https://github.com/eliben/pycparser'
depends=('python')
makedepends=('git' 'python-setuptools')
arch=(any)
license=('BSD-3-Clause')
source=("git+https://github.com/eliben/pycparser.git#tag=release_v$pkgver")
sha512sums=('1526d7874c52c99d25af2c52154bace3051fcc8a0ab57558a4251dca7718b6298eddb277de4c0826aac3f61f76cc8dd0219fc20955e3c83face7ec6741a1438c')

build() {
  cd pycparser
  python setup.py build

  cd pycparser
  python _build_tables.py
}

check() {
  cd pycparser
  python -m unittest discover
}

package() {
  cd pycparser

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
