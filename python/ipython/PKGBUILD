# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=ipython
pkgver=8.15.0
pkgrel=2
pkgdesc='Enhanced Interactive Python shell'
arch=(any)
url=https://ipython.org
license=('BSD')
depends=(
  'python-backcall'
  'python-jedi'
  'python-matplotlib-inline'
  'python-pexpect'
  'python-pickleshare'
  'python-prompt_toolkit'
  'python-pygments'
  'python-stack-data'
  'python-traitlets'
  'sqlite'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'git'
  'jupyter-nbformat'
  'python-curio'
  'python-ipykernel'
  'python-matplotlib'
  'python-numpy'
  'python-pandas'
  'python-pytest'
  'python-pytest-asyncio'
  'python-testpath'
  'python-trio'
  'tcsh'
  'texlive-bin'
  'texlive-latex'
)
optdepends=(
  'python-black: to auto format with Black'
  'yapf: to auto format with YAPF'
)
source=(
  "https://github.com/ipython/ipython/archive/$pkgver/$pkgname-$pkgver.tar.gz"
  'IPython-icon.png::https://www.packal.org/sites/default/files/public/styles/icon_large/public/workflow-files/nkeimipynbworkflow/icon/icon.png'
)
b2sums=('9b2f10686a3c65e4085994904d38552bd263b809a7c6714823bda43f2d66976d9f094aded5b40a416ca10e0261583bdd590b2ca534a58737d8dd9ee9024b82ad'
        'd445e2bc7a037db8715ea103611720e965987e155c32e445b0ef783e519fca8a0301b16c5763fd9a5d8d169c3b0d7b4db6c0bd0f9772842258b135dcb1d6d5a2')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  # FS#47046
  install -Dm644 IPython-icon.png "$pkgdir"/usr/share/pixmaps/ipython.png

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  cd 'examples/IPython Kernel'
  # FS#45120
  sed -i 's/gnome-netstatus-idle/ipython/' ipython.desktop
  install -Dm644 -t "$pkgdir"/usr/share/applications ipython.desktop

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
