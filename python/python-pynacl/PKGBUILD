# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-pynacl
pkgver=1.6.0
pkgrel=1
pkgdesc='Python binding to the Networking and Cryptography (NaCl) library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/pyca/pynacl'
license=('Apache-2.0')
depends=(
  'glibc'
  'libsodium'
  'python'
  'python-cffi'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-pycparser'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-hypothesis'
  'python-pytest'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b068fb5877fd084d927471233c0678d04582f49cdcd1a298d38f39c2a775bbf83011a6279df894c6c92732dc947a467e4f628449f5f8419b254886c2b789e17e')

build() {
  cd ${pkgname#python-}-$pkgver
  export SODIUM_INSTALL=system
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd ${pkgname#python-}-$pkgver
  export SODIUM_INSTALL=system
  python -m installer --destdir="$pkgdir" dist/*.whl
}
