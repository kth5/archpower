# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=sphinx-inline-tabs
pkgname=python-$_pkgname
pkgver=2023.04.21
pkgrel=4
pkgdesc='Add inline tabbed content to your Sphinx documentation'
arch=(any)
url='https://github.com/pradyunsg/sphinx-inline-tabs'
license=('MIT')
depends=('python-sphinx')
makedepends=('git' 'python-build' 'python-installer' 'python-flit-core')
source=(git+$url#tag=$pkgver
        docutils-0.22.patch)
sha512sums=('71fd0451da69fa50756e6528862891f51948e46ca17d3314e200ced7aaf4a08f9f21a1c6ba2634bac3e6155fd4736de4ce2c4510062064fbdadf4d3d46bee608'
            '47d4924ef52e5fac94593b4bc5d1f749fc235d4a81c8b316bc23de743df12003a39559ac34225700f3935fb8214d6052309a8fa76691d83567e135d1b8998465')

prepare() {
  cd $_pkgname
  patch -p1 -i ../docutils-0.22.patch
}

build() {
  cd $_pkgname

  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
