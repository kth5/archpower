# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Nissar Chababy <funilrys at outlook dot com>
# Contributor: Thrasibule <guillaume dot horel at gmail dot com>
# Contributor: David Manouchehri <manouchehri@riseup.net>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>
# Contributor: Andrew Reed <reed.996@osu.edu>
# Contributor: "Score_Under" <seejay.11@gmail.com>

pkgname=python-lz4
_pkgname=lz4
pkgver=4.3.3
pkgrel=1
pkgdesc="LZ4 bindings for Python"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD')
url="https://github.com/python-lz4/python-lz4"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-pkgconfig' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'python-psutil')
source=("https://pypi.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-${pkgver}.tar.gz")
sha512sums=('9bdacf6766cca5d1c59f0a0f795910da104ce51d4c43057c4c07fed98765881a702604d60d22a10dea937c4d683e3fcf07c26a6c7a7b5a15b45759fcedb55900')

build() {
  cd "$srcdir"/$_pkgname-$pkgver
  PYLZ4_EXPERIMENTAL=1 \
  python -m build --wheel --no-isolation
}

check() {
  cd "$srcdir"/$_pkgname-$pkgver
  mv lz4{,.bak}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
