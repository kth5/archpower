# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>

_name=schema
pkgname="python-$_name"
pkgver=0.7.5
pkgrel=4
pkgdesc='Python module to validate and convert data structures.'
arch=(any)
url="https://github.com/keleshev/$_name"
depends=(python python-contextlib2)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
license=(MIT)
source=(
  $_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-0.7.5-mock.patch
)
sha256sums=('9af94571c6c2df01be002a8dcd5efda33a64b08ce2c7fba9646d2a8fe762c8e3'
            'fa8c47083e9531f6865dfbe61e7f33eddd09b1e40d42cc55b99e3e69a4ce256d')

prepare() {
  cd "$srcdir/$_name-$pkgver"
  # accept arbitrary versions
  sed 's|==|>=|g' -i requirements.txt

  # remove the use of python-mock
  patch -Np1 -i ../$pkgname-0.7.5-mock.patch
}

build() {
  cd "$srcdir/$_name-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$srcdir/$_name-$pkgver"
  pytest
}

package() {
  cd "$srcdir/$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
