# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org

pkgname=python-stevedore
pkgver=5.4.0
pkgrel=1
pkgdesc="Manage dynamic plugins for Python applications"
arch=(any)
url="https://github.com/openstack/stevedore"
license=('Apache-2.0')
depends=('python')
makedepends=('python-pbr' 'python-setuptools')
options=('!emptydirs')
source=(https://pypi.org/packages/source/s/stevedore/stevedore-$pkgver.tar.gz)
sha256sums=('79e92235ecb828fe952b6b8b0c6c87863248631922c8e8e0fa5b17b232c4514d')

prepare() {
  cd stevedore-$pkgver
  # Remove dependency on pbr; it's not a runtime dependency
  sed -i '/pbr/d' requirements.txt
}

package() {
  cd stevedore-$pkgver
  python3 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
