# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jiachen Yang <farseerfc@gmail.com>
# Contributor: val√®re monseur <valere dot monseur at ymail dot com>

pkgname=python-ghp-import
pkgver=2.1.0
pkgrel=5
pkgdesc='Easily import docs to your gh-pages branch'
arch=(any)
url="https://github.com/c-w/ghp-import"
license=('Apache-2.0')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
replaces=('ghp-import')
conflicts=('ghp-import')
source=("$url/archive/$pkgver/${pkgname#python-}-$pkgver.tar.gz")
sha512sums=('cc258745a972b883e6e5b2dc12601e74305237c1950f95c5a86093e0ebeb0c7eb2192470b2e85ab9ac274bfbc87abe2438750d4f59b3846032fb13f567b04f5b')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
