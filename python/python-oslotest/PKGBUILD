# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslotest
pkgver=5.0.1
pkgrel=3
pkgdesc="Oslo test framework"
arch=(any)
url="https://github.com/openstack/oslotest"
license=('Apache-2.0')
depends=('python-fixtures' 'python-subunit' 'python-testtools')
makedepends=('git' 'python-pbr')
checkdepends=('python-oslo-config' 'python-stestr')
source=("git+https://github.com/openstack/oslotest.git#tag=$pkgver")
sha512sums=('42c05950e4aded6df20939783422c3d2feb819182da27b77845d21471ef6f05783f4f4a5633663a7827672e337b1f7c9f170101982778b1cbb60c9c07dddf63e')

build() {
  cd oslotest
  python setup.py build
}

check() {
  cd oslotest
  stestr run
}

package() {
  cd oslotest
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
