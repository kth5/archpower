# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-config
pkgver=9.2.0
_commit=28187da6d7b8eae39208e7cefbbc7da32ec3d3be
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=(any)
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
depends=('python-debtcollector' 'python-netaddr' 'python-stevedore'
         'python-oslo-i18n' 'python-rfc3986' 'python-yaml' 'python-requests')
optdepends=('python-sphinx: for rst-generator')
makedepends=('git' 'python-pbr' 'python-setuptools')
checkdepends=('python-testscenarios' 'python-stestr' 'python-testtools' 'python-oslotest'
              'python-oslo-log' 'python-requests-mock' 'python-sphinx')
options=('!emptydirs')
source=("git+https://github.com/openstack/oslo.config.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd oslo.config
  python setup.py build
}

check() {
  cd oslo.config
  stestr run
}

package() {
  cd oslo.config
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
