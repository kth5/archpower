# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-bottle
pkgver=0.13.0
pkgrel=1
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="https://bottlepy.org"
license=('MIT')
options=(!emptydirs)
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-mako' 'python-jinja' 'python-cherrypy' 'python-twisted'
              'python-tornado' 'python-paste' 'python-gevent' 'python-eventlet'
              'gunicorn')
optdepends=('python-waitress: Waitress WSGI server'
            'python-cherrypy: Cherrypy WSGI server'
            'python-eventlet: Eventlet server support')
source=("git+https://github.com/defnull/bottle.git#tag=$pkgver")
sha512sums=('004ccca1eb7b9fcd4b5fda7ee1b0abb83ccf82263f9681b617522332089e6cd5ce66c7de73973b1c6c3773f5c7d77bd597f2161a7349ac0c697de3716c833885')

build() {
  cd bottle
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd bottle
  python -m unittest discover
}

package() {
  cd bottle
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/bottle-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
