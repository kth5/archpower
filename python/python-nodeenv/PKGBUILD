# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-nodeenv
pkgver=1.9.1
pkgrel=2
pkgdesc="Node.js virtual environment builder"
url="https://github.com/ekalinin/nodeenv"
license=('BSD-3-Clause')
arch=(any)
depends=('python-setuptools' 'make')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools-scm')
optdepends=('nodejs: for --node=system')
checkdepends=('nodejs' 'python-pytest' 'python-coverage')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ekalinin/nodeenv/archive/$pkgver.tar.gz")
sha512sums=('28fa814b57323dd996764d754f9c614c9dfd668c85ab21c23d0f9aeebb669ba3933f7b0b9ccbf0c808b5f15783eb3afdf3801f293952f3c4945729dee6ecb20b')

prepare() {
  cd nodeenv-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd nodeenv-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nodeenv-$pkgver
  pytest
}

package() {
  cd nodeenv-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
