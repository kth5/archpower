# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-concurrency
pkgver=5.0.1
pkgrel=1
pkgdesc="OpenStack library for all concurrency-related code"
arch=(any)
url="https://pypi.python.org/pypi/oslo.concurrency/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-i18n' 'python-oslo-utils'
         'python-fasteners')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-eventlet')
source=("https://github.com/openstack/oslo.concurrency/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e9f072b814c1e961110cb3da0c327ee9b1eb510780dcc0772b97cf8fc633577d7667e96b8f91b0741b47403dfbce50f12f76b97854e28f0d6c95a9c13a2a924d')

export PBR_VERSION=$pkgver

build() {
  cd oslo.concurrency-$pkgver
  python setup.py build
}

check() {
  cd oslo.concurrency-$pkgver
  stestr run || : # "1 failure about raising rlimit"
}

package() {
  cd oslo.concurrency-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
