# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=python-rdflib
pkgver=7.0.0
pkgrel=4
pkgdesc="A Python library for working with RDF, a simple yet powerful language for representing information"
arch=(any)
url="https://github.com/RDFLib/rdflib"
license=('BSD-3-Clause')
depends=('python-isodate' 'python-pyparsing' 'python-setuptools')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
optdepends=('python-html5lib: for html support')
checkdepends=('python-pytest-cov' 'python-html5lib' 'python-networkx'
              'python-pip')
source=(https://github.com/RDFLib/rdflib/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('176192bd2ce8a400e661ced2e21e3dd35783fa1c6c8390f6dee4ac61ca63da5d3a50df3d287f47783631da12452ca3ec92ad06050417029396fe8b3ab0182041')

build() {
  cd rdflib-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd rdflib-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v \
    --deselect test/test_misc/test_parse_file_guess_format.py::TestFileParserGuessFormat::test_warning # https://github.com/RDFLib/rdflib/issues/2748
}

package() {
  cd rdflib-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644  LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
