# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-elastic-transport
_pkgname=elastic-transport-python
pkgver=8.17.0
pkgrel=1
pkgdesc="Transport classes and utilities shared among Python Elastic client libraries"
arch=(any)
url="https://github.com/elastic/elastic-transport-python"
license=(Apache-2.0)
depends=(
  python
  python-aiohttp
  python-certifi
  python-httpx
  python-orjson
  python-requests
  python-urllib3
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-httpserver
  python-pytest-rerunfailures
  python-respx
  python-trustme
)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  # https://github.com/elastic/elastic-transport-python/pull/222
  "httpx-0.28-compatibility.patch::$url/commit/f2208774c9d1c887fde9b87e5a784772f9a8826b.patch"
)
sha256sums=('58dd66a35cf11577e017d2ffb2d85b53523a0f3faa7f89b4b54882987afcdcb4'
            'e8778b929f2de2e718be5c4d67d8f804d433750e55001fde242c4fd682265a99')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../httpx-0.28-compatibility.patch
}

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  # The calls to httpbin.org, and as such the tests, are flaky when running on
  # build.archlinux.org.
  # OpenTelemetry not in Arch repos (yet).
  pytest --override-ini="addopts=" \
    --reruns 1 \
    --reruns-delay 1 \
    --ignore tests/test_otel.py
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
