# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-elastic-transport
_pkgname=elastic-transport-python
pkgver=8.15.1
pkgrel=3
pkgdesc="Transport classes and utilities shared among Python Elastic client libraries"
arch=(any)
url="https://github.com/elastic/elastic-transport-python"
license=(Apache-2.0)
depends=(
  python
  python-aiohttp
  python-certifi
  python-httpx
  python-orjson
  python-requests
  python-urllib3
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-httpserver
  python-pytest-rerunfailures
  python-respx
  python-trustme
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        'remove_tests_incompatible_with_httpcore_1.0.6.patch')
sha256sums=('9bc0aa1b94cc0761efeceac5f0ba1d7dd19bf8b5571d863ca49c6b2f212db75c'
            'c6e0f55c5ce4bd412a7644e1ab54b1b29dd2315fecdd5a0ed1a410942ba91798')

prepare() {
  cd $_pkgname-$pkgver
  # Temporarily remove httpcore / httpx related tests due to incompatibilities with httpcore >= 1.0.6
  # See https://github.com/encode/httpx/discussions/3214#discussioncomment-10830925
  patch -Np1 -i $srcdir/remove_tests_incompatible_with_httpcore_1.0.6.patch
}

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  # The calls to httpbin.org, and as such the tests, are flaky when running on
  # build.archlinux.org.
  # OpenTelemetry not in Arch repos (yet).
  pytest --override-ini="addopts=" \
    --reruns 1 \
    --reruns-delay 1 \
    --ignore tests/test_otel.py
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
