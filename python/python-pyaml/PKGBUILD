# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyaml
pkgver=23.5.9
pkgrel=1
pkgdesc="PyYAML-based module to produce pretty and readable YAML-serialized data"
url="https://pypi.python.org/pypi/pyaml"
license=('BSD')
arch=(any)
depends=('python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-unidecode')
optdepends=('python-unidecode: Needed if same-id objects or recursion is used within serialized data')
source=("https://pypi.io/packages/source/p/pyaml/pyaml-$pkgver.tar.gz")
sha512sums=('ba6a7d51208b10a2265a52c684d43052fb57a7cd1fc4a4087a141a3adfc36f54b71e943056f346c26ae1b16800b8f2c3a2c0c20454322c9a24b760993c9dd49d')

build() {
  cd pyaml-$pkgver
  python setup.py build
}

check() {
  cd pyaml-$pkgver
  python setup.py test
}

package() {
  cd pyaml-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
