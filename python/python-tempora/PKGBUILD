# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgname='python-tempora'
_pkgbase="${pkgname//python-/}"
pkgver=5.8.0
pkgrel=1
pkgdesc="Objects and routines pertaining to date and time (tempora)."
arch=(any)
url="https://github.com/jaraco/tempora"
license=('MIT')
depends=(
  'python'
  'python-dateutil'
  'python-jaraco.functools'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-freezer'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ad3311aba123e6a9acbd5568ed9bf1aa2295fd799148605483158a69aeb5b838b9d1b3231979fea79827ceec1e767856398c7747bfbefcbfd66e3d7cf4dab4c9')

build() {
  cd "$_pkgbase-$pkgver"
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgbase-$pkgver"
  pytest
}

package() {
  cd "$_pkgbase-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
