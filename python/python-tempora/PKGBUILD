# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgname=python-tempora
pkgver=5.8.1
pkgrel=1
pkgdesc="Objects and routines pertaining to date and time (tempora)."
arch=(any)
url="https://github.com/jaraco/tempora"
license=('MIT')
depends=(
  'python'
  'python-dateutil'
  'python-jaraco.functools'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-freezer'
)
source=("git+$url#tag=v$pkgver")
sha512sums=('4c59894018a320764a1fed308d6b1e48669002727c8f686d35da6eae6e818717857fd31ea4d44bab1177fbfb717e0349e019e6dc726ee247d09af768e67e1e00')

prepare() {
  cd ${pkgname#python-}
  # Bring back license file
  git revert -n 9a81db3c77bc106017dcd4b0853a5a94f43ae33c
}

build() {
  cd ${pkgname#python-}
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd ${pkgname#python-}
  pytest
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
