# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-referencing
pkgver=0.36.2
pkgrel=1
pkgdesc='An implementation-agnostic implementation of JSON reference resolution'
arch=(any)
url='https://referencing.readthedocs.io'
license=(MIT)
depends=(python python-attrs python-rpds-py)
makedepends=(
  git
  python-build
  python-installer
  python-hatchling
  python-hatch-vcs
)
checkdepends=(
  python-pytest
  python-pytest-subtests
  python-jsonschema
)
source=(
  "$pkgname::git+https://github.com/python-jsonschema/referencing#tag=v$pkgver"
  'github.com-python-jsonschema-referencing-suite::git+https://github.com/python-jsonschema/referencing-suite'
)
sha512sums=('49cc0af3fb89bc8a411761c458852d7d600d4790162bf90c650fb7f19ff9d208c9a5a388e624593bf9712e4da3b5cd5782cc90260b714d3a53372a12da075a13'
            'SKIP')
b2sums=('e8a43e81499286e172be757fdcdcdf86e48cd7b82dd3281b0dea3139fa2a0a2054271d08117428e594d0f6c6f56bd0e01f8ab6579c129346566a35acd7f49706'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodules
  git submodule init
  git config submodule.suite.url "$srcdir/github.com-python-jsonschema-referencing-suite"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
