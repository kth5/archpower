# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>

pkgname=python-strictyaml
pkgver=1.7.3
pkgrel=4
pkgdesc="Type-safe YAML parser that parses a restricted subset of the YAML specificaton"
url="http://hitchdev.com/strictyaml"
arch=(any)
license=(MIT)
depends=(
  python
  python-dateutil
  python-ruamel.yaml.clib
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/crdoconnor/strictyaml#tag=$pkgver")
b2sums=('411d78f37d5cbc809c95544957904a282b1202c68a76619f00df7b8d9a1e3476b63dd5a7006d85d2a4384621323ca75b72402d439c3cbee5ddaa8a4a7418360c')

build() {
  cd strictyaml
  python -m build --wheel --no-isolation
}

package() {
  cd strictyaml
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
