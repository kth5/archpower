# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=4.2.0
pkgrel=1
epoch=1
pkgdesc='An XML Schema validator and decoder'
arch=(any)
url='https://github.com/sissaschool/xmlschema'
license=(MIT)
depends=(python python-elementpath)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(python-lxml python-jinja)
optdepends=('python-jinja: for XSD based code generators')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('757a3be17cf7d23ec4b78606e14fa7173b4c4bca4fa5f849d8ca0b06cc741745cfda5d5e71e65cf40af29e7428e37c91680d1fcab97cbda22f035df5af7a02bc')
b2sums=('412892d0abc3e9b41501be47906ab7caafdd1f0f371293d4babe75de16412c8edd8cdedbf810278e9e6a8267a5a3ae7c314cd39f87b21b1985a1732cddd951a4')


build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD" python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
