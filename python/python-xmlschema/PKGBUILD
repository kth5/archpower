# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=4.3.1
pkgrel=1
epoch=1
pkgdesc='An XML Schema validator and decoder'
arch=(any)
url='https://github.com/sissaschool/xmlschema'
license=(MIT)
depends=(python python-elementpath)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
checkdepends=(python-lxml python-jinja)
optdepends=('python-jinja: for XSD based code generators')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('416df8ed2e7c2c361e035d8925e70474d4809022a790eeccfbf5eebf7861d31fb90a1f9a99b46d30dec6dcac8f22c4f37633e89bc20e514a8433f08dd81db7a5')
b2sums=('8ba4bab9d1b4c00ebe59f6e910310ba19a8658ff06e3dea7284a1bc060b36fcf3fde531e5357da3555b98f7bf887ce55f34b980b3915793423e5afecaa6dbd61')


build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD" python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
