# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=4.0.1
pkgrel=1
epoch=1
pkgdesc='An XML Schema validator and decoder'
arch=(any)
url='https://github.com/sissaschool/xmlschema'
license=('MIT')
depends=('python' 'python-elementpath')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-lxml' 'python-jinja')
optdepends=('python-jinja: for XSD based code generators')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('c069a497fc3fe09608649b176349a6505fb7e903788ceed1e68b89a220f8095aa6b69f673b4291df078f771b587217d4f9073ec8a5f8e81c8dba43fe30cefde6')
b2sums=('66be11fce093d9fbca37097e85ca489c60a48ae1e66716b51689a91f6846c749448995d26222b90c787780581eb64513dc8818a0455768e429cf5f3ef4b9c56b')


build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD" python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
