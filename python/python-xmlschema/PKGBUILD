# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=3.4.3
pkgrel=1
epoch=1
pkgdesc='An XML Schema validator and decoder'
arch=(any)
url='https://github.com/sissaschool/xmlschema'
license=('MIT')
depends=('python' 'python-elementpath')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-lxml' 'python-jinja')
optdepends=('python-jinja: for XSD based code generators')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('1a3295577bf363b08a0923f3712b839be6f2b6959c35042a06bd4bff19339570b28ed82cc20fcea01eab492509f02d3204d7ed34395937a1cb5919129b5fe66d')
b2sums=('a005c28aae09c5cf9fcf5e499377bcdf2663e4a1987847035cd4077df5d7cf36c6cce9e360a1a5cdfb5ed3816cd4857f51c15bce3a894ff27d786348b20843c0')


build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD" python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
