# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
_pkgname="${pkgname#python-}"
pkgver=2.5.1
pkgrel=3
epoch=1
pkgdesc='An XML Schema validator and decoder'
arch=(any)
url='https://github.com/sissaschool/xmlschema'
license=('MIT')
depends=('python' 'python-elementpath')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-lxml' 'python-jinja')
optdepends=('python-jinja: for XSD based code generators')
_commit='faff4d8ca954d8722df89e1e77bc4246a36ed62c'  # refs/tags/2.5.1^{}
source=("$pkgname::git+$url#commit=$_commit" "adjust-tests-for-python3.patch")
b2sums=('985ef6f48ebab8a046265fbac55dd23416b3352612e0d0e065269964c72e4e87270d681df7e519d0570ff2a966b0b66003729fb11144978f76f42a8198fe1526'
        '08a2ad4c6e4dc5b176faee6c9504b34f123bffc866bc7f0627f3f9ac5827d09aa60c13cf23ddaad1c92bad543ae72be105c4d0f6faefd924f076e1fd5336eefa')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # Based on https://github.com/sissaschool/xmlschema/commit/059fd3bfb305809d5ebdede1bd68dff9dbaf3348
  patch -Np1 -i ${srcdir}/adjust-tests-for-python3.patch
}


build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD" python -m unittest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/${pkgname#python-}-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
