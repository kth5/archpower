# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-constantly
pkgver=23.10.0
pkgrel=1
pkgdesc='Symbolic constants in Python'
arch=(any)
license=('MIT')
url='https://github.com/twisted/constantly'
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel'
             'python-versioneer')
checkdepends=('python-pytest' 'python-twisted')
source=("https://github.com/twisted/constantly/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a9f729990bfcfa74e285c126ad8c8a9f6e8628edbd524400fe9ea293a7d80b8e06ab5eb975501c3d6042b00c322774b6e7bf44612536418cd51844d89a941332')

build() {
  cd constantly-$pkgver
  python -m build -nw
}

check() {
  cd constantly-$pkgver
  pytest
}

package() {
  cd constantly-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
