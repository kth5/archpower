# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-pyasyncore
_pkgname=${pkgname#python-}
pkgver=1.0.4
pkgrel=2
pkgdesc="Make asyncore available for Python 3.12 onwards"
arch=(any)
url="https://github.com/simonrob/pyasyncore"
license=(PSF-2.0)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-tests)
provides=(python-asyncore)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver/$pkgver.tar.gz")
sha256sums=('4d9d423ef3a3a601a9e6e638cb2951e1b49f7e94095818f25e439529fec34674')

build() {
  cd "$_pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$pkgver"
  python -m unittest discover -v
}

package() {
  cd "$_pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
