# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ecdsa
pkgver=0.18.0
pkgrel=4
pkgdesc="Implementation of ECDSA in Python"
arch=(any)
url="https://github.com/tlsfuzzer/python-ecdsa"
license=('MIT')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/e/ecdsa/ecdsa-$pkgver.tar.gz"
	"tigheter-bounds-hypothetis-tests.patch")
sha512sums=('f26dbeccd8e6ec832405d419b7244ca97b43a2811513493913a4f2f2fd1d2f453068fe0ba7b90b972f42a0d7b9834212fbd4d4559475cffeb1d80075fa954d78'
            'c907f0ee0b1eb836a1881b20aba5e2234313fa402aa04c7cf39b316ca0bc360717914a267fd6d11db878f254d4547a645c4388d2dc0678024bbecca6308c344f')

prepare() {
  cd ecdsa-$pkgver
  patch -Np1 -i ${srcdir}/tigheter-bounds-hypothetis-tests.patch
}

check() {
  cd ecdsa-$pkgver
  # https://github.com/tlsfuzzer/python-ecdsa/issues/336
  python -m pytest -k 'not test_implicit_unused_bits and not test_new_call_convention and not test_implicit_unexpected_unused and not test_new_call_convention'
}

package() {
  cd ecdsa-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
