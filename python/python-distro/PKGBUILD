# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Tomislav Ivek <tomislav.ivek@gmail.com>

pkgname=python-distro
pkgver=1.9.0
pkgrel=1
pkgdesc='Linux OS platform information API'
url='https://github.com/python-distro/distro'
arch=(any)
license=('Apache')
depends=('python')
makedepends=('python-sphinx' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
options=('!makeflags')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('9ed033e9fb0a5a531a93d85d8d3c0afb67ec009b851146c76f819617cc13b28a6e6dc412899690baec2aa57d57389a347968c649eb99fefa6f782340166f83c4')
b2sums=('e50d91a9eba711a8196a6a8bb078c5d8611955fd042dea398aa40db238bbabda55e09d142dd95c769d4e7032425412eb1bacede67053d3e5f1c3892d998c0a20')

build() {
  cd distro-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd distro-${pkgver}
  PYTHONPATH="build/lib" pytest
}

package() {
  cd distro-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
