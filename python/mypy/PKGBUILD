# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>
# Contributor: hexchain <i@hexchain.org>

pkgname=mypy
pkgver=1.19.1
pkgrel=1
pkgdesc='Optional static typing for Python (PEP484)'
arch=(any)
url="http://www.mypy-lang.org/"
license=('MIT')
depends=(
  'python'
  'python-librt'
  'python-mypy_extensions'
  'python-orjson'
  'python-pathspec'
  'python-typing_extensions'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-attrs'
  'python-filelock'
  'python-psutil'
  'python-pytest'
  'python-pytest-xdist'
  'python-tests'
)
optdepends=(
  'python-lxml: for reports'
  'python-pip: for installing missing types'
  'python-psutil: for dmypy'
  'python-setuptools: for mypyc'
)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/python/mypy/archive/v$pkgver.tar.gz"
  "$pkgname-exclude-tests.patch"
)
b2sums=('f1d1c511ede6e9e933a8e6236cfdef506778ad56acc1a72d89ee088f0c8dc25c9afc2b174c0a74138aebaee14666ce345e57c8a8ac71a85948c35ca8d6984a7b'
        '83b6d12dac919b917ba13c6a5b6da6e4cd6dc517f85b4f8e81d793d9c0e871af5a48708c4a5a54c45c7be89c5ed394b2f77007be89420fea4a1f3ca1efb04c0d')

prepare() {
  cd "$pkgname-$pkgver"
  # -Werror, not even once
  sed -e '/Werror/d' -i mypyc/build.py
  patch -Np1 < ../$pkgname-exclude-tests.patch
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd "$pkgname-$pkgver"
  # CFLAGS being set interferes with the following tests somehow, regardless
  # what it's set to:
  #   mypyc/test/test_run.py::TestRun::run-bools.test::testBoolOps
  #   mypyc/test/test_run.py::TestRun::run-i64.test::testI64GlueMethodsAndInheritance
  #   mypyc/test/test_run.py::TestRun::run-i64.test::testI64BasicOps
  #   mypyc/test/test_run.py::TestRun::run-i64.test::testI64DefaultArgValues
  #   mypyc/test/test_run.py::TestRun::run-i64.test::testI64ErrorValuesAndUndefined
  unset CFLAGS
  pytest -vv
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
