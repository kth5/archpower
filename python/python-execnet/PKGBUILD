# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-execnet
pkgver=2.0.1
_commit=2757f05c1a808463d731954fe6047781eecc45f7
pkgrel=1
pkgdesc="Rapid multi-Python deployment"
arch=(any)
license=('MIT')
url="https://codespeak.net/execnet"
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-hatchling'
  'python-hatch-vcs'
)
checkdepends=(
  'openssh'
  'procps-ng'
  'python-py'
  'python-pytest'
  'python-pytest-timeout'
)
source=("git+https://github.com/pytest-dev/execnet.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd execnet
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd execnet
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv testing
}

package() {
  cd execnet
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
