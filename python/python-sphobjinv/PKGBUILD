# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=python-sphobjinv
_name=${pkgname#python-}
pkgver=2.3.1
pkgrel=5
pkgdesc="Manipulate and inspect Sphinx objects.inv files"
arch=(any)
url="https://github.com/bskinn/sphobjinv"
license=('MIT')
depends=('python' 'python-attrs' 'python-certifi' 'python-jsonschema')
makedepends=('python-build' 'python-installer' 'python-wheel'
             'python-setuptools')
source=(https://github.com/bskinn/sphobjinv/archive/v$pkgver/$_name-$pkgver.tar.gz)
sha256sums=('dab4abb840c8d2a5b3532da4318243c70ab0d18f6a87596eae16c3ef178265b1')

build() {
  cd "$_name-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}

# vim:set ts=2 sw=2 et:
