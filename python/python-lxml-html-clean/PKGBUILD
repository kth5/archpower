# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-lxml-html-clean
pkgver=0.4.2
pkgrel=1
pkgdesc='HTML cleaner from lxml project'
arch=(any)
url='https://lxml-html-clean.readthedocs.io/'
license=(BSD-3-Clause)
depends=(
  python
  python-lxml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+https://github.com/fedora-python/lxml_html_clean#tag=$pkgver")
sha512sums=('d8cf542e0741346cdd08d6357148c4db6191b3a2c1a373b2345e185f6705966e7b399f7ad07aee8f5a13a042ddcbf35125837d87f69801a10c3a5f97b9fa2b98')
b2sums=('3057edaaf73a1b4a1162165e71c9ca592905ca8470e1082b98988b04996d095925bfb3dc86f4612a1a09802335ecb64aebcb923c978471e87da4a4e722d615e5')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  export PYTHONPATH=.

  # https://github.com/fedora-python/lxml_html_clean/issues/24
  local deselected=(
    tests/test_autolink.txt::test_autolink.txt
    tests/test_clean.py::CleanerTest::test_host_whitelist_invalid
    tests/test_clean.py::CleanerTest::test_host_whitelist_sneaky_userinfo
    tests/test_clean.py::CleanerTest::test_host_whitelist_valid
    tests/test_clean.txt::test_clean.txt
    tests/test_clean_embed.txt::test_clean_embed.txt
  )

  pytest -v ${deselected[@]/#/--deselect }
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
