# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-lxml-html-clean
pkgver=0.4.3
pkgrel=2
pkgdesc='HTML cleaner from lxml project'
arch=(any)
url='https://lxml-html-clean.readthedocs.io/'
license=(BSD-3-Clause)
depends=(
  python
  python-lxml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+https://github.com/fedora-python/lxml_html_clean#tag=$pkgver")
sha512sums=('b5141ae209996f734de805e10f8903f5aec6cb6662a8b7a33ee7fb26552386491046add279953f798afa79024403e9bdc85c1deb3501117ec0361199e0ccd12d')
b2sums=('e1cd69ba3c46e7c2dbd12d8397bc1a9c4b0e6d2c712ac13d36780e2dbb2da8df17676c335effcefe9bed4028df210f3e4700b2c7fd6ab943ec2e14a52e8eb6cf')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  export PYTHONPATH=.

  # https://github.com/fedora-python/lxml_html_clean/issues/24
  local deselected=(
    tests/test_autolink.txt::test_autolink.txt
    tests/test_clean.py::CleanerTest::test_host_whitelist_invalid
    tests/test_clean.py::CleanerTest::test_host_whitelist_sneaky_userinfo
    tests/test_clean.py::CleanerTest::test_host_whitelist_valid
    tests/test_clean.txt::test_clean.txt
    tests/test_clean_embed.txt::test_clean_embed.txt
  )

  pytest -v ${deselected[@]/#/--deselect }
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
