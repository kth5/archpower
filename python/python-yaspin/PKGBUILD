# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-yaspin
pkgver=3.1.0
pkgrel=2
pkgdesc="Yet Another Terminal Spinner"
url="https://github.com/pavdmyt/yaspin"
license=('MIT')
arch=(any)
depends=('python-termcolor')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest')
source=("git+https://github.com/pavdmyt/yaspin.git#tag=v$pkgver")
sha512sums=('be885c628c42951c5303572cc229b91d45df3214575bbd42ff921987093a6e927194550b8db45b75763a0bbc7d6a5366d8ee24baab474091c806678e03197c91')

build() {
  cd yaspin
  python -m build --no-isolation --wheel
}

check() {
  cd yaspin
  FORCE_COLOR=1 python -m pytest
}

package() {
  cd yaspin
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
