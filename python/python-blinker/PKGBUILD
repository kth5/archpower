# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Philipp Schmidt <philschmidt at gmx dot net>
# Contributor: Stefan Tatschner <stefan.tatschner at gmail dot com>

pkgname=python-blinker
pkgver=1.6.2
pkgrel=1
pkgdesc="Fast, simple object-to-object and broadcast signaling"
arch=(any)
url="https://blinker.readthedocs.io"
license=('MIT')
depends=('python' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest-asyncio')
source=("https://files.pythonhosted.org/packages/source/b/blinker/blinker-$pkgver.tar.gz")
sha512sums=('c84bb955e97e6a13e6fda5f070373736bd1361f40ad9202eeaa300055b163648db381060f6b45fdf405a0bd4ef07b694875735e6f220d51d1b7a5be7efb28f72')

build() {
  cd blinker-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd blinker-$pkgver
  PYTHONPATH="$PWD"/src \
  python -m pytest
}

package() {
  cd blinker-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
