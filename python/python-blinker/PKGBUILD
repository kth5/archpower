# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Philipp Schmidt <philschmidt at gmx dot net>
# Contributor: Stefan Tatschner <stefan.tatschner at gmail dot com>

pkgname=python-blinker
pkgver=1.7.0
pkgrel=2
pkgdesc="Fast, simple object-to-object and broadcast signaling"
arch=(any)
url="https://blinker.readthedocs.io"
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest-asyncio')
source=("https://files.pythonhosted.org/packages/source/b/blinker/blinker-$pkgver.tar.gz")
sha512sums=('b55dc104fd69bf9f682184202f9da7cb6925e7e863f38d5ad5b4b5988b2315aae02da6f4c9f0ec34238b8d5b9b6eec52b06258d93e14416cda3e2b718d9aa60c')

build() {
  cd blinker-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd blinker-$pkgver
  PYTHONPATH="$PWD"/src \
    python -m pytest
}

package() {
  cd blinker-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl 
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
