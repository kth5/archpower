# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgname=python-bottleneck
pkgver=1.6.0
pkgrel=1
pkgdesc="Fast NumPy array functions written in Cython"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/kwgoodman/bottleneck"
license=(BSD-2-Clause)
depends=(python-numpy)
makedepends=(python-setuptools python-build python-installer python-wheel python-versioneer)
checkdepends=(python-pytest)
#source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
source=($pkgname-$pkgver.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('70ae8d3c9d083f4c7a23088f9dad7b895bf533cfb1da9e605dacb84b9ef8a5da')

prepare() {
  cd bottleneck-${pkgver}
  # Drop build requirement on oldest-supported-numpy
  sed -i '/oldest-supported-numpy/d' pyproject.toml
}

build() {
  cd bottleneck-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd bottleneck-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --skip-build
  test-env/bin/python /usr/bin/pytest -vv --color=yes --pyargs bottleneck
}

package() {
  cd bottleneck-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
