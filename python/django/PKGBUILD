# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Shahar Weiss <sweiss4@gmx.net>

pkgbase=django
pkgname=('python-django')
pkgver=5.1.15
pkgrel=3
pkgdesc="A high-level Python Web framework that encourages rapid development and clean design"
arch=(any)
license=('BSD')
url="http://www.djangoproject.com/"
checkdepends=('python-pytest' 'python-tblib')
makedepends=('python' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
depends=('python' 'python-pytz' 'python-sqlparse' 'python-asgiref')
# TODO: package and add python-pymemcache https://docs.djangoproject.com/en/dev/topics/cache/
optdepends=('python-psycopg2: for PostgreSQL backend'
            'python-argon2-cffi: for Argon2 password hashing support')
source=("Django-$pkgver.tar.gz::https://www.djangoproject.com/download/$pkgver/tarball/"
         https://github.com/django/django/commit/8d7b1423.patch)
sha512sums=('050a3ded3566e378cb273cb95fa1720632f1920aa86993c998758236df5041628ada7a5fb6bd3056720a5becbb45e358c70a2ca7c230b28606a1fd954771c070'
            'ecce816e006b77788b1a7b63b08f1bc990b5852841567c5ff5b64d2ce48410715de79a95d2ed8696b6470c50d334399fb11f2f00399c7b7fa8236ed5e2c6ee16')

prepare() {
  cd "django-$pkgver"
  # Drop versioned setuptools requirement
  sed -i 's/>=61.0.0,<69.3.0//' pyproject.toml

  # Support Python 3.14
  patch -p1 -i ../8d7b1423.patch
}

build() {
  cd "django-$pkgver"
  python -m build --wheel --no-isolation
}

package_python-django() {
  cd "django-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  ln -s django-admin "$pkgdir"/usr/bin/django-admin3
  install -Dm644 extras/django_bash_completion \
    "$pkgdir"/usr/share/bash-completion/completions/django-admin.py
  ln -s django-admin.py \
    "$pkgdir"/usr/share/bash-completion/completions/django-admin
  ln -s django-admin.py \
    "$pkgdir"/usr/share/bash-completion/completions/manage.py

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

check() {
  cd "django-$pkgver"
  PYTHONPATH="$PWD" python tests/runtests.py || echo 'tests failed'
}
