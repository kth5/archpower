# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=python-pymongo
pkgver=4.6.1
pkgrel=1
pkgdesc='Python module for using MongoDB'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('Apache')
url='https://pypi.python.org/pypi/pymongo/'
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
optdepends=('python-pyopenssl: for [ocsp]'
            'python-requests: for [ocsp]'
            'python-service-identity: for [ocsp]'
            'python-snappy: for [snappy]'
            'python-zstandard: for [zstd]'
            'python-dnspython: for [srv]')
source=("https://pypi.io/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha512sums=('5bf535004f5dfb6b2541545ff204009d2a30a74e0ca80e02eedbce3019d3c60dcbc4f123c85febb73aeaa4b62e38bc3c84373e8a50f407cc396ef008e7b4d62b')

build() {
  cd pymongo-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd pymongo-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
