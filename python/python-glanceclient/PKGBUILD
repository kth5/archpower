# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-glanceclient
pkgver=4.5.0
pkgrel=2
pkgdesc="OpenStack Image API Client Library"
arch=(any)
url="https://docs.openstack.org/developer/python-glanceclient"
license=('Apache')
depends=('python-pbr' 'python-prettytable' 'python-keystoneauth1' 'python-requests' 'python-warlock'
         'python-oslo-utils' 'python-oslo-i18n' 'python-wrapt' 'python-pyopenssl')
makedepends=('python-setuptools')
checkdepends=('python-stestr' 'python-ddt' 'python-requests-mock' 'python-testscenarios')
options=('!emptydirs')
source=("https://github.com/openstack/python-glanceclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('377bbe7e7fd100c16992f3f25df780d268d71e9eeb8d68967a7d44566cd7926fc58825960529cd08a90ca52bc3b201f1029c257f67384b67fad39bdfc84e280b')

export PBR_VERSION=$pkgver

build() {
  cd python-glanceclient-$pkgver
  python setup.py build
}

check() {
  cd python-glanceclient-$pkgver
  # TODO: Failing ssl tests starting 3.12, needs further investigation
  stestr run --exclude-regex 'glanceclient.tests.unit.test_ssl'
}

package() {
  cd python-glanceclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
