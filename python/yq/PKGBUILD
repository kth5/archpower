# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yq
pkgver=3.4.3
pkgrel=1
pkgdesc="Command-line YAML, XML, TOML processor - jq wrapper for YAML/XML/TOML documents"
arch=(any)
url="https://github.com/kislyuk/yq"
license=(Apache-2.0)
depends=(jq
         python-argcomplete
         python-tomlkit
         python-xmltodict
         python-yaml)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
conflicts=(go-yq)
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('ba586a1a6f30cf705b2f92206712df2281cd320280210e7b7b80adcb8f256e3b')
b2sums=('a1b3d813d9761ac8a8429db3fe8efac64ade69e0d2fa9b137a1ca8c9e367a0f16b636482b6d9381e2de43392007f37388b6ab37def04739723488f271fd56d16')

build() {
  cd $pkgname-$pkgver
  python -m build -wn
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
