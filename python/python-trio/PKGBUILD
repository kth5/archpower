# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-trio
pkgver=0.25.1
pkgrel=1
pkgdesc='A friendly Python library for async concurrency and I/O'
arch=(any)
url=https://github.com/python-trio/trio
license=(MIT)
depends=(
  python
  python-attrs
  python-idna
  python-outcome
  python-sniffio
  python-sortedcontainers
  python-typing_extensions
)
makedepends=(
  git
  python-build
  python-setuptools
  python-installer
  python-wheel
)
checkdepends=(
  python-astor
  python-black
  python-isort
  python-jedi
  python-pylint
  python-pytest
  python-ruff
  python-trustme
)
provides=(python-multio-provider)
_tag=a4646040129eff570c3d7e28cc55770563a4d4bd
source=(git+https://github.com/python-trio/trio.git#tag=${_tag})
b2sums=('bef35ab29aa5a3e131d771c9943259bd407dd9c4c8555a7b78d73670a440b3967fafc4e02ffdb511a780c09000a4ac2af7593ae822ab67842e08a8e29e324c53')

pkgver() {
  cd trio
  git describe --tags | sed 's/^v//'
}

build() {
  cd trio
  python -m build --wheel --no-isolation
}

check() {
  cd trio
  PYTHONPATH=$PWD/src pytest -vv
}

package() {
  python -m installer --destdir="${pkgdir}" trio/dist/*.whl
  install -Dm 644 trio/LICENSE -t "${pkgdir}"/usr/share/licenses/python-trio/
}

# vim: ts=2 sw=2 et:
