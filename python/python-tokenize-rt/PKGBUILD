# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-tokenize-rt
pkgver=6.1.0
pkgrel=1
pkgdesc='Wrapper around the stdlib tokenize which roundtrips'
arch=(any)
url=https://github.com/asottile/tokenize-rt
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/${pkgname#python-}-$pkgver.tar.gz")
b2sums=('c83a669abf7291981f3f240483ecad50852c375bb232bb530d58a527af92f7f984b5f0860068a2852b3fdd8c748c43531d450cd2f9da9345548bf76c351f31e6')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
