# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>

pkgname=python-cssselect
pkgver=1.3.0
pkgrel=1
license=('BSD-3-Clause')
arch=(any)
url="https://pypi.python.org/pypi/cssselect"
pkgdesc="A Python3 library that parses CSS3 Selectors and translates them to XPath 1.0"
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-lxml' 'python-sybil')
source=("https://github.com/scrapy/cssselect/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d2711a1ed5e1fb21bd6f7422ab36c1d06b7b46b6cbdb3480182b72f4a877dd06c8b47a1b41b0a30adbd9fbd551d34b479e020d72cb1d46c3f483273e86e61ce9')

build() {
  cd cssselect-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd cssselect-$pkgver
  pytest
}

package() {
  cd cssselect-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
