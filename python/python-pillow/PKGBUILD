# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgname=python-pillow
pkgver=11.1.0
pkgrel=1
pkgdesc='Python Imaging Library (PIL) fork'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://pillow.readthedocs.io'
license=('MIT-CMU')
depends=(
  'glibc'
  'python'
  'python-packaging'
  'freetype2'
  'lcms2'
  'libraqm'
  'libtiff'
  'openjpeg2'
  'libjpeg-turbo'
  'libimagequant'
  'libxcb'
  'zlib'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'libwebp'
  'tk'
)
checkdepends=(
  'python-pytest'
  'python-pytest-timeout'
)
optdepends=(
  'libwebp: for webp images'
  'tk: for the ImageTK module'
  'python-olefile: OLE2 file support'
  'python-pyqt6: for the ImageQt module'
  'python-defusedxml: for reading XMP tags'
)
source=(
  "$pkgname::git+https://github.com/python-pillow/Pillow#tag=$pkgver"
  'ftbfs-trove-classifiers.patch'
)
sha512sums=('c921a65702e54344057b0c63592cde03d095f7eecab1e703e1f9f55a9f2618d83baadf772fd427a6cc651181a121af0a3267fde942f6139f5a7666b3fc040f65'
            '5ac423ac371b8303d6273382af3586943bd4c68cb9b03445754fde551c8bab1ce58670443c30c51698284e532996795d81414cb1d39b3a438b285228e2fbb047')
b2sums=('f9273ed4201bbc9771568a3c93c9ab0e95669a92f605f58025f99f69827eaea5bfcf44f9c0b8a1eb9104d5140e12a337b1cc8bde124404bda9295c8d867a2ca4'
        'c429863610bf2a0ee389addc99e60c8ec76ec4b703d8e822afdb2ea2277c6ef3ccc055ed84af720d651373942944feaa85048692b5daf044180abf5dcaf3215d')

prepare() {
  cd "$pkgname"

  # FTBFS: why are we validating pyproject.toml?
  patch -p1 -i "$srcdir/ftbfs-trove-classifiers.patch"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  [ ! "${CARCH}" == 'x86_64' ] && return 0
  cd "$pkgname"

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python selftest.py
  test-env/bin/python -m pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  install -vDm644 -t "$pkgdir/usr/include/python$python_version" src/libImaging/*.h

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
