# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgname=python-pillow
pkgver=10.4.0
pkgrel=1
pkgdesc='Python Imaging Library (PIL) fork'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://python-pillow.org'
license=('BSD-Attribution-HPND-disclaimer')
depends=(
  'glibc'
  'python'
  'python-packaging'
  'freetype2'
  'lcms2'
  'libraqm'
  'libtiff'
  'openjpeg2'
  'libjpeg-turbo'
  'libimagequant'
  'libxcb'
  'zlib'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'libwebp'
  'tk'
)
checkdepends=(
  'python-pytest'
  'python-pytest-timeout'
)
optdepends=(
  'libwebp: for webp images'
  'tk: for the ImageTK module'
  'python-olefile: OLE2 file support'
  'python-pyqt6: for the ImageQt module'
)
source=("$pkgname::git+https://github.com/python-pillow/Pillow#tag=$pkgver")
sha512sums=('3d8d9f5b32427f04b75d3a2ca4dc9247805c6918903f361dcc8fef32390fc59da0244539d8f85985aa33152b757e92462775f45b8f0aa9d2a94c47b3bddc1a6a')
b2sums=('5ca9614e6e76b2838f9c85c84a9b40e61cb672cf01707763195e31f9b7cf62dec2dc346e9e1a527e9e4ba089d399677fe68fac7f5fd8efd346ac763dec26c300')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python selftest.py
  test-env/bin/python -m pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  install -vDm644 -t "$pkgdir/usr/include/python$python_version" src/libImaging/*.h

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
