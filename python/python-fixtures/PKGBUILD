# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fixtures
pkgver=4.2.5
_commit=1edd6c2bc6cb48cc666eb7d9ffdd182659c2cae0
pkgrel=1
pkgdesc="Python fixtures for testing / resource management"
arch=(any)
url="https://github.com/testing-cabal/fixtures"
license=('Apache-2.0 OR BSD-3-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-hatch-vcs'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-testtools')
source=("$url/archive/$_commit/${pkgname#python-}-$_commit.tar.gz")
sha512sums=('8485f2e9e755fb99d1f575941e2d960ec05a2048229c651eba0933b4aba7e41bc504cda45cc233fcd024cb2268af94dd3aa949a30215cd85446dbdc0d7e124df')

pkgver() {
  cd ${pkgname#python-}-$_commit
  sed -n '/^CHANGES/{n;n;n;p;q;}' ChangeLog
}

prepare() {
  cd ${pkgname#python-}-$_commit
  echo '[tool.hatch.build.targets.wheel]' >> pyproject.toml
  echo 'exclude = ["fixtures/tests*"]' >> pyproject.toml
}

build() {
  cd ${pkgname#python-}-$_commit
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$_commit
  python -m testtools.run fixtures.test_suite
}

package() {
  cd ${pkgname#python-}-$_commit
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING BSD
}
