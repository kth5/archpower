# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fixtures
pkgver=4.2.3
pkgrel=1
pkgdesc="Python fixtures for testing / resource management"
arch=(any)
url="https://github.com/testing-cabal/fixtures"
license=('Apache-2.0 OR BSD-3-Clause')
depends=('python')
makedepends=(
  'python-build'
  'python-hatch-vcs'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
checkdepends=('python-testtools')
source=(
  "$url/archive/$pkgver/${pkgname#python-}-$pkgver.tar.gz"
  "$pkgname-dont-package-tests.patch"
)
sha512sums=('9275f2089813b79f8a831ab684524b8d512ff052783f9e4c1c5ccaec6ce078a72a9b714d161ef33f825eaae6c4ebe07e05988a0bc277c219df11f6fbab11e8a6'
            '65d1b1c5a1e5e15d2b6165c3935f75bf1350c123d30d28cb4f169d7a2d9c738f1de840c121dee5569925bab07a084387d45d48f3e6867d4f76c9324ed0a8ef07')

prepare() {
  cd ${pkgname#python-}-$pkgver
  patch -Np1 < ../$pkgname-dont-package-tests.patch
}

build() {
  cd ${pkgname#python-}-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m testtools.run fixtures.test_suite
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING BSD
}
