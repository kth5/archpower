# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>

_origname=reportlab
pkgname=python-reportlab
pkgver=4.1.0
pkgrel=1
pkgdesc="A proven industry-strength PDF generating solution"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.reportlab.com/"
depends=('python' 'freetype2' 'python-pillow')
makedepends=('freetype2' 'python-pillow' 'mercurial' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_origname::1}/${_origname}/${_origname}-${pkgver}.tar.gz)
sha256sums=('3a99faf412691159c068b3ff01c15307ce2fd2cf6b860199434874e002040a84')

build() {
    cd ${_origname}-${pkgver}
    python setup.py build
    python -m build --wheel --no-isolation
}

check() {
    cd ${_origname}-${pkgver}
    export PYTHONPATH="build:$PYTHONPATH"
    python -m unittest discover -vs tests || true
    rm tests/_i_am_actually_a_*
}

package() {
    cd ${_origname}-${pkgver}
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
