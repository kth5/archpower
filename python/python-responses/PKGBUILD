# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.25.5
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=(any)
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-urllib3' 'python-yaml')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpserver'
              'python-tomli-w')
source=("git+https://github.com/getsentry/responses.git#tag=$pkgver")
sha512sums=('2a6bf1cb62d3f99261ec945d89833ef3006e926782f6350c1757e3fe9fbe5a878876eb9b55b8eba5547bebc6997f9e5467efe92b1979b4ec88bd3923b7d89aad')

build() {
  cd responses
  python -m build -nw
}

check() {
  cd responses
  pytest
}

package() {
  cd responses
  python -m installer -d "$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
