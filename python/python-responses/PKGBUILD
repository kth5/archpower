# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.25.7
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=(any)
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-urllib3' 'python-yaml')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpserver'
              'python-tomli-w')
source=("git+https://github.com/getsentry/responses.git#tag=$pkgver")
sha512sums=('2bd67e7b95fb5982d4e52a3752665af5b52fa5cffe468becf8f0ae9f1a9ef8fec9c0d29546b948694201a8ccc769273b26438e1432321aa701ab70b3e4d42549')

build() {
  cd responses
  python -m build -nw
}

check() {
  cd responses
  pytest
}

package() {
  cd responses
  python -m installer -d "$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
