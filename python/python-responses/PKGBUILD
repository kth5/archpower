# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.25.8
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=(any)
license=('Apache-2.0')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-urllib3' 'python-yaml')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpserver'
              'python-tomli-w')
source=("git+https://github.com/getsentry/responses.git#tag=$pkgver")
sha512sums=('196919f76566ab21383a5b77068ff45cd5a0084cb027f010d5f884b68a37b4bc4e667c718f0a627e4e83e1e5647f1e81689dcae63d07322faa64d655ef449eed')

build() {
  cd responses
  python -m build -nw
}

check() {
  cd responses
  pytest
}

package() {
  cd responses
  python -m installer -d "$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
