# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.23.3
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=(any)
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-urllib3' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpserver' 'python-tomli'
              'python-tomli-w')
source=("https://github.com/getsentry/responses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('36ea5d02577983ebdebf5fca42c113368c6807aa928523471f958e9250ba170e8b90ab920d6e63315ccb6494905bbe454ca512c58b2f84cbbe530f50e163ad75')

prepare() {
  cd responses-$pkgver
  sed -i '/types-PyYAML/d' setup.py
}

build() {
  cd responses-$pkgver
  python setup.py build
}

check() {
  cd responses-$pkgver
  pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
