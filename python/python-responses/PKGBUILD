# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.25.2
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=(any)
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-urllib3' 'python-yaml')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpserver'
              'python-tomli-w')
source=("git+https://github.com/getsentry/responses.git#tag=$pkgver")
sha512sums=('d1d39a7a51534a9642aa6723aa2acfb10197cbdc05b53b6c65f3e2864fd93ceb98b3f82b1ead988aa0f8be8bca8e1af10e15751395615a11e9e4dccab6da4b45')

build() {
  cd responses
  python setup.py build
}

check() {
  cd responses
  pytest
}

package() {
  cd responses
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
