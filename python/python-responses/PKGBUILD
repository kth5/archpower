# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.25.0
pkgrel=3
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=(any)
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-urllib3' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpserver'
              'python-tomli-w')
source=("https://github.com/getsentry/responses/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "pytest-asyncio-failures.patch")
sha512sums=('a947641f8693a5687feeec8ded5b8d7791c18910f96d6c967e1d4576d1ded7d56933971690a3f3f6b3394b3d5abc6f3f78462545fcd572e7dd674d06340c7566'
            '23c800262ab92bf3a92ffcb3431ba082c4953480452760267b06d91d5a1aebc93b4b6d93149543e3f07f1da678186fdf7a057b80800fc75573618a1c30e4c685')

prepare() {
  cd responses-$pkgver
  patch -Np1 -i ${srcdir}/pytest-asyncio-failures.patch
}

build() {
  cd responses-$pkgver
  python setup.py build
}

check() {
  cd responses-$pkgver
  pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
