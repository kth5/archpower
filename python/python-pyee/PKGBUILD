# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josh Holbrook <josh.holbrook@gmail.com>

pkgname=python-pyee
_name=${pkgname#python-}
pkgver=11.1.0
pkgrel=2
pkgdesc="Port of node.js's EventEmitter to python"
arch=(any)
url=https://github.com/jfhbrook/pyee
license=(MIT)
depends=(python-typing_extensions)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest-asyncio
  python-pytest-trio
  python-twisted
)
optdepends=(
  python-trio
  python-twisted
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('b7e6cbfef3bc201174bb636a6c80b41f7fd31653e2b384ec22a49411525766ec7e60aafef5699a47705a8331a6b7b901c2007b2c24e621588d298e026876aa22')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
