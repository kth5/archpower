# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-randomly
pkgver=3.13.0
pkgrel=1
pkgdesc='Pytest plugin to randomly order tests and control random.seed'
arch=(any)
license=('MIT')
url='https://github.com/pytest-dev/pytest-randomly'
depends=('python-importlib-metadata' 'python-pytest')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-factory-boy' 'python-faker' 'python-numpy' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-randomly/archive/$pkgver.tar.gz")
sha512sums=('d9b6b965ad03fb504005f55cd1f4db9e733ea2412bb6456e846da8a0366123c4e537c6fea72e341e3f4bbe5fd3b78fce26599100623f26b634c97bf8519f1498')

build() {
  cd pytest-randomly-$pkgver
  python -m build -nw
}

check() {
  cd pytest-randomly-$pkgver
  # TODO: Understand why...
  PYTHONPATH="$PWD/src" pytest --deselect tests/test_pytest_randomly.py::test_entrypoint_injection
}

package() {
  cd pytest-randomly-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
