# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=public
pkgname=python-atpublic
pkgver=7.0.0
pkgrel=2
pkgdesc="@public decorator for Python"
arch=(any)
url="https://gitlab.com/warsaw/public"
license=(Apache-2.0)
depends=(python)
makedepends=(
  python-build
  python-hatchling
  python-installer
)
checkdepends=(
  python-pytest
  python-sybil
)
source=($url/-/archive/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('7f92967243716d493d046048034eac2f1ede8985af8a6752b1b258a252f43ff8c5e6f85cd3fd7cc52f91468547f47c2435f07cba99d7db5f28880156cfbdc4ab')
b2sums=('9e6e0b26fcaabc430534575da232807fa8f314de6762a60ae997aa982da16bdc237aa28da23e8ecb1ae35950292dcd10e840c59ef737b7bd0f4dd3f36c020c90')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
