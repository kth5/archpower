# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=public
pkgname=python-atpublic
pkgver=6.0.2
pkgrel=1
pkgdesc="@public decorator for Python"
arch=(any)
url="https://gitlab.com/warsaw/public"
license=(Apache-2.0)
depends=(python)
makedepends=(
  python-build
  python-hatchling
  python-installer
)
checkdepends=(
  python-pytest
  python-sybil
)
source=($url/-/archive/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('f4179821b0de4fd890b746f121350f00f692cd31e403686b64071ba1e7cbe106cd314e5caadee39ca335c556573d7b28695281f4b872e9f3101427a183756ef9')
b2sums=('77a9c20c0ed8f5209b8a065244ac197fc8d252e72c07eb1e7a10172da5087bb298216aa17b30f6c26b6b4819717c1f82d2ddc8e0b9e1596869a8be6e359aa1bd')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
