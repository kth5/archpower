# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-json-logger
pkgver=3.2.1
pkgrel=1
pkgdesc='A python library adding a json log formatter'
arch=(any)
license=('BSD')
url='https://github.com/nhairs/python-json-logger'
depends=('python')
makedepends=('python-build'
             'python-installer'
             'python-setuptools')
checkdepends=('python-freezegun'
              'python-pytest')
source=("https://github.com/nhairs/python-json-logger/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6ed550fb1be4cfa97c10c156d4cc3b1aabe7177264d74bfcb4c64df00b665753294e2c5de2e5c0439c2bd8c2bff855b97b275810216cf51cefe8833b3f5b4a77')

build() {
  cd python-json-logger-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd python-json-logger-$pkgver
  PYTHONPATH=src pytest
}

package() {
  cd python-json-logger-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
