# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-json-logger
pkgver=3.3.0
pkgrel=1
pkgdesc='A python library adding a json log formatter'
arch=(any)
license=('BSD')
url='https://github.com/nhairs/python-json-logger'
depends=('python')
makedepends=('git'
             'python-build'
             'python-installer'
             'python-setuptools')
checkdepends=('python-freezegun'
              'python-pytest')
source=("git+https://github.com/nhairs/python-json-logger.git#tag=v$pkgver")
sha512sums=('cdcf92436386ab9d2edac2709ff089363818b1a5980309e8b0e9fbf955e991b62148d7238b35f2f2d537d55bcf4d7670e5404610a11c56d65c304b14c9ec6e5f')

build() {
  cd python-json-logger
  python -m build --wheel --no-isolation
}

check() {
  cd python-json-logger
  PYTHONPATH=src pytest
}

package() {
  cd python-json-logger
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
