# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-xlsxwriter
pkgver=3.2.9
pkgrel=1
pkgdesc='A Python module for creating Excel XLSX files'
arch=(any)
url='https://github.com/jmcnamara/XlsxWriter'
license=(BSD-2-Clause)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
  python-setuptools
)
source=("$pkgname::git+$url#tag=RELEASE_$pkgver")
sha512sums=('49b6d8d7e6ade7048850461e258aefbf3fea432e9565fd16b9300cf334b85923d9c3c035b1aaee2364691a9127b0107edccd0ae66eb79c5dacf67731cc4f494b')
b2sums=('46ee66885ee34ca26eaa7d0e7af047877bde659ffcc59c36f5e2edc74e6195a8536443db5d61813c26cf531c07b811d20d50ead2a8811423652b252d6955b66a')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m unittest discover
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
