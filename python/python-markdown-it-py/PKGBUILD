# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_pkgname=markdown-it-py
pkgname=python-$_pkgname
pkgver=4.0.0
pkgrel=2
pkgdesc='Python port of markdown-it. Markdown parsing, done right!'
arch=(any)
url='https://github.com/executablebooks/markdown-it-py'
license=('MIT')
depends=('python-mdurl')
optdepends=('python-mdit_py_plugins: core plugins'
            'python-linkify-it-py: linkify extension')
makedepends=('python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-pytest' 'python-pytest-regressions' 'python-mdit_py_plugins' 'python-linkify-it-py')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('617927d7038e2918b395c720d4b8d8327342c266c75b6fd5957961944c2bed51a8e2f01a9c184e5d85dfc4fd8b28e07f3cc1d677aa29192e1a8e54f29d6a0829')

build() {
  cd $_pkgname-$pkgver

  python -m build
}

check() {
  return 0
  cd $_pkgname-$pkgver

  python -m pytest tests
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
