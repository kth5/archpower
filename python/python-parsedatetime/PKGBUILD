# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Daniel M. Capella <polycitizen@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgname=python-parsedatetime
pkgver=2.6
pkgrel=10
pkgdesc="Parse human-readable date/time strings."
arch=(any)
license=('Apache-2.0')
url='https://github.com/bear/parsedatetime'
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bear/parsedatetime/archive/v$pkgver.tar.gz")
sha512sums=('ed7649feb8773fdb33d609ef0abe4d20f555744bd70e9766aba6b9a79d8415c7e2efea1d7ef5c6b5906f3bc90bff124ef92992ce17d83fe70575da2db918e543')

build() {
  cd parsedatetime-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd parsedatetime-$pkgver
  pytest
}

package() {
  cd parsedatetime-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
