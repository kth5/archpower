# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgbase=python-deadlib
pkgname=(python-nntplib python-cgi python-mailcap)
pkgver=3.13.0
pkgrel=2
pkgdesc="Python dead batteries. See PEP 594."
arch=(any)
url="https://github.com/youknowone/python-deadlib"
license=("PSF-2.0")
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/youknowone/python-deadlib/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('bde43692d5a1de2a33777ff99f4c4344f7cca3a49362b2484a13870706194613')

build() {
  cd ${srcdir}/$pkgbase-$pkgver/nntplib
  python -m build --wheel --no-isolation

  cd ${srcdir}/$pkgbase-$pkgver/cgi
  python -m build --wheel --no-isolation

  cd ${srcdir}/$pkgbase-$pkgver/mailcap
  python -m build --wheel --no-isolation
}

# For mailman3
package_python-nntplib() {
  depends=('python')
  pkgdesc="Standard library nntplib redistribution. \"dead battery\"."
  cd $pkgbase-$pkgver/nntplib
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# Temporarily for falcon until it is updated to 4.x
package_python-cgi() {
  depends=('python')
  pkgdesc="Standard library cgi redistribution. \"dead battery\"."
  cd $pkgbase-$pkgver/cgi
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# For alot https://github.com/pazz/alot/issues/1632
package_python-mailcap() {
  depends=('python')
  pkgdesc="Standard library mailcap redistribution. \"dead battery\"."
  cd $pkgbase-$pkgver/mailcap
  python -m installer --destdir="$pkgdir" dist/*.whl
}
