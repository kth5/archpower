# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Rich Li <rich@dranek.com>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python-h5py
pkgver=3.15.1
pkgrel=3
pkgdesc="General-purpose Python bindings for the HDF5 library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.h5py.org/"
license=(BSD-3-Clause)
depends=(
  glibc
  hdf5
  liblzf
  python
  python-numpy
)
makedepends=(
  cython
  python-build
  python-installer
  python-pkgconfig
  python-setuptools
  python-wheel
)
conflicts=(hdf5-openmpi)
source=(
  "https://github.com/h5py/h5py/archive/$pkgver/${pkgname#python-}-$pkgver.tar.gz"
  remove-version-check.patch
  https://github.com/h5py/h5py/commit/812b65b1.patch
)
sha256sums=('55a03d8fc1db535b243cccda3e6237844da25a17a2df3bc0ea9db6fbbbd7bf2b'
            '621f108e5e95dc250047501447d0dc633fbf3f8bf19e4c4b4c4d5a286864664c'
            '16c732a4f4c27148f98d6ba1684c7d51e139732e1a70f66d521a512f7993617e')

prepare() {
  cd ${pkgname#python-}-$pkgver
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" setup_build.py
  # Drop annoying version check
  patch -p1 -i ../remove-version-check.patch
  # Fix tests with HDF5 2.0
  patch -p1 -i ../812b65b1.patch
}

build() {
  cd ${pkgname#python-}-$pkgver
  H5PY_SYSTEM_LZF=1 python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
