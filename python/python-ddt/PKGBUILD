# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ddt
pkgver=1.7.1
pkgrel=1
pkgdesc="Data-Driven/Decorated Tests"
arch=(any)
license=('MIT')
url="https://github.com/txels/ddt"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-aiounittest' 'python-six' 'python-yaml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txels/ddt/archive/$pkgver.tar.gz")
sha512sums=('7b1f30c08c74fce514380d68b0d44aa1bf8ff1b8cdbbd52bde83e0bb46439cc7ae2fc7f32e0acc257cb3636f6aadefcbfe6815ea5e372a70503131d7fc869511')

build() {
  cd ddt-$pkgver
  python setup.py build
}

check() {
  cd ddt-$pkgver
  pytest
}

package() {
  cd ddt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
