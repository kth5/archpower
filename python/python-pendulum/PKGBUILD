# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pendulum
pkgname=python-pendulum
pkgver=3.0.0
pkgrel=5
pkgdesc="Python datetimes made easy"
arch=(x86_64 powerpc64le powerpc64)
url="https://pendulum.eustace.io/"
_url="https://github.com/python-pendulum/pendulum"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
  python-dateutil
  python-tzdata
  python-time-machine
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-wheel
)
checkdepends=(
  python-babel
  python-freezegun
  python-pytest
  python-pytz
)
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/$pkgver.tar.gz
  $pkgname-3.0.0-fix-datetime-tests.patch::https://github.com/python-pendulum/pendulum/commit/9f7b8cfe642e2b7567388ccc8779e61fab772e74.patch
  $pkgname-3.0.0-use-std-zoneinfo.patch
)
sha512sums=('f4cab40bec00029ccda44f15c71cf07a4518b6d86570b2f3425db2e3bb96151b22aa5065b1e21cc4b2b0cfef216f6af1ae6a16eb4efdb811dbdb18a7eab6c24a'
            '107a146fd16067dacac0b6129535fd5ffaac137811569685e0d5edbe1db62764136fe4431ebf31949171b5d7eed336ccd3e827bc55336dc8a56132c13b18548d'
            'fa80d5209672a15b260aefe6972e0286007543da90e72daca4c9b22c4bfe1bbe67a7b72c0357b3a7d9ca49c2369c7d93d3cbce70af8e086ac95920eb59dda0dd')
b2sums=('5cbf0e2e504964e75c45ad41fcdab5a2ef0be91d9f4a8898305d15a5dc124abc3dc0986c7559d45091325bf4819f5e854ce87f92d534b24d1ac8c0852097df09'
        '76e220d95f41fd2a3ed706695f6cd67be1c5ff65286761f5bf2906d1054e3ea65353f9f42102d66c6a959b99d09a854e3aae4d1b8dc03ef2cfd82fa341216efb'
        '91e083dd19bc0706ada4660d30cb269a27f32bb2c77bfb7fd780e7b36c2a0ecb5df4c3a2009f1788951057f9e1823f6844727cf0c99c7ddb68010d1cf206af9d')

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-3.0.0-fix-datetime-tests.patch
  # backport of https://github.com/python-pendulum/pendulum/pull/805 but without removing the python-tzdata dependency (for now)
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-3.0.0-use-std-zoneinfo.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG.md,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
