# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Massimiliano Torromeo < massimiliano DOT torromeo AT gmail DOT com >

pkgname=python-asn1crypto
pkgver=1.5.1
pkgrel=6
pkgdesc='Python ASN.1 library with a focus on performance and a Pythonic API'
arch=(any)
url='https://github.com/wbond/asn1crypto'
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(
  "$pkgname::git+$url#tag=$pkgver"
	0001-Import-backflips-that-work-with-Python-3.12.patch
)
sha512sums=('93a4116c84b5d41d2c5bc9c3b02e024af3a5dc7e364d6e8806060d8300512a21219ef22b17cb84c6beb5f512037b3ac6f60b7180d23fc388d247cfbb202d7012'
            '1921c21e33c6469ab8a02be945902b43f52ebbc523b955e010a1f823cccfe28297812f4a1cd54e22ffa2fad8e6fae59dda4263fe68921943e55ed3dfb23d7034')
b2sums=('8eca111aeab3490fe9698fa06895ac26d2c8be12af70f05e4d4cf61d57ec1d7e8686d29c73ddbadc6540219fb251ebf86fce46eadbeef657b6cb577e84263420'
        '267cf647348309fb46672990d97e4f92d6a6a7ca074dd7d5a9f48e7e1c6cdd8336b12ef6c20dd640d68d688e54143e3c1ccb8250a57f720088df1ac0dfe09152')

prepare() {
  cd "$pkgname"

  # imp modules is removed in Python 3.12
  patch -p1 -i "$srcdir/0001-Import-backflips-that-work-with-Python-3.12.patch"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python run.py tests
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
