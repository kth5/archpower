# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-pallets-sphinx-themes
_pyname=${pkgname/python-/}
pkgver=2.3.0
pkgrel=1
pkgdesc='Sphinx themes for Pallets and related projects'
url='https://github.com/pallets/pallets-sphinx-themes'
arch=(any)
license=('BSD')
depends=('python' 'python-sphinx')
makedepends=('python-build' 'python-installer' 'python-flit-core')
source=(https://github.com/pallets/pallets-sphinx-themes/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('e3994bfbedea31ae04874cf5a9bea30be0e9cff04ae3913f8fcdc31843282e0b8adcef376662495a585ae2bcbe2889f5376364638691af8689ee028968cdb0c9')
b2sums=('946f389708730b7b5627cc0e338671465a5a252de5030ff84171528f86314961d90d73618e54795aae5e84fa173e184d1e2b7dd71d058a1a711524666146fed1')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
