# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-rsa
pkgver=4.9
pkgrel=3
_libname=rsa
pkgdesc="Pure-Python RSA implementation"
arch=(any)
url="https://github.com/sybrenstuvel/python-rsa/"
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz)
depends=(python-pyasn1)
makedepends=(python-poetry python-build python-installer python-wheel)
sha256sums=('e38464a49c6c85d7f1351b0126661487a7e0a14a50f1675ec50eb34d4f20ef21')

build() {
	cd "$srcdir/$_libname-$pkgver"
	python -m build --wheel --no-isolation
}

package() {
	cd "$srcdir/$_libname-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
