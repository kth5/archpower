# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-freezegun
pkgver=1.5.5
pkgrel=1
pkgdesc='Let your Python tests travel through time'
arch=(any)
url='https://github.com/spulec/freezegun'
license=(Apache-2.0)
depends=(python python-dateutil)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('97a570ace910a994550cf0ff5dad3429d410c177a1247466a3b1a9aa59335d6dc93bf943ae942c7ab77bc039035ac00e7b4ad522be415b645a145798a13eacf3')
b2sums=('a1f03fab4f384de154798c6524e4a229e7c411c7fc1bb33fe99b0ad42fb584942147b3603f6cc81105d8fc386c2b9c4694bbef2189911c0bb9ee7d934eeda5b8')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  TZ=C pytest -vvv
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
