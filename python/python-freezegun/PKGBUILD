# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-freezegun
pkgver=1.5.1
pkgrel=2
pkgdesc='Let your Python tests travel through time'
arch=(any)
url='https://github.com/spulec/freezegun'
license=('Apache-2.0')
depends=('python' 'python-dateutil')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$pkgname::git+$url#tag=$pkgver"
	"freezegun-support-python-3.13.patch")
sha512sums=('d826c24cbc7a5b622e3bc6b647b220ef99630e41c8c3b663d919c0e703e691289cca1c359417a5d5801dcb44c53a40b90b6a4e382409fbb20d3316a31c24a88e'
            'f1600d3112807a615edcfa415f31432aa74cafe8f02e9ebcb3310e3fcd96e57095a6e17892cad76cb0c340df52df71f0b7e48034029c2d1809c7e0005a0aed42')
b2sums=('42436f375fa49461338fff315602a86b3e69bf4fd5aa53432de6b111f90314faf72736a27e6060dbd4ad7bda19f38aa3357737b9afff444301607100d0b057a7'
        'dbd4bdff13b731b0946614942f59ae058c500f40aca7354ba28769192680dca39dd99562886ccf4426263843d48a080b5d94a19096487ef4a309ac053b9bd6ba')

prepare() {
  cd "$pkgname"

  patch -Np1 -i ${srcdir}/freezegun-support-python-3.13.patch
}


build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  TZ=C pytest -vvv
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
