# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tim Hatch <tim@timhatch.com>

pkgname=python-termcolor
pkgver=2.4.0
pkgrel=1
pkgdesc="ANSI Color formatting for output in terminal"
arch=(any)
license=('MIT')
url="https://github.com/termcolor/termcolor"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-hatch-vcs')
checkdepends=('python-pytest')
source=(https://pypi.python.org/packages/source/t/termcolor/termcolor-$pkgver.tar.gz)
sha256sums=('aab9e56047c8ac41ed798fa36d892a37aca6b3e9159f3e0c24bc64a9b3ac7b7a')

build() {
  cd termcolor-$pkgver
  python -m build --no-isolation --wheel
}

check() {
  cd termcolor-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd termcolor-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
