# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tim Hatch <tim@timhatch.com>

pkgname=python-termcolor
pkgver=3.3.0
pkgrel=1
pkgdesc="ANSI Color formatting for output in terminal"
arch=(any)
license=('MIT')
url="https://github.com/termcolor/termcolor"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-hatch-vcs')
checkdepends=('python-pytest' 'python-coverage')
source=(https://pypi.python.org/packages/source/t/termcolor/termcolor-$pkgver.tar.gz)
sha256sums=('348871ca648ec6a9a983a13ab626c0acce02f515b9e1983332b17af7979521c5')

build() {
  cd termcolor-$pkgver
  python -m build --no-isolation --wheel
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd termcolor-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd termcolor-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
