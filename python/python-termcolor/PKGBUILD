# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tim Hatch <tim@timhatch.com>

pkgname=python-termcolor
pkgver=3.1.0
pkgrel=1
pkgdesc="ANSI Color formatting for output in terminal"
arch=(any)
license=('MIT')
url="https://github.com/termcolor/termcolor"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-hatch-vcs')
checkdepends=('python-pytest' 'python-coverage')
source=(https://pypi.python.org/packages/source/t/termcolor/termcolor-$pkgver.tar.gz)
sha256sums=('6a6dd7fbee581909eeec6a756cff1d7f7c376063b14e4a298dc4980309e55970')

build() {
  cd termcolor-$pkgver
  python -m build --no-isolation --wheel
}

check() {
  cd termcolor-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd termcolor-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
