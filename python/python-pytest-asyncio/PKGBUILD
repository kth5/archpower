# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-pytest-asyncio
pkgver=0.25.2
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=(any)
url="https://github.com/pytest-dev/pytest-asyncio"
license=('Apache-2.0')
depends=('python' 'python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis')
source=("git+https://github.com/pytest-dev/pytest-asyncio.git#tag=v$pkgver?signed")
b2sums=('5b7ceeab8d5973c3e8fa86e1deb88c8f47769fa1ea2dc72d2d83577cdc23bfaf2025382249dd8f54a40d105c8d02aa874a21d949d137ebfc13c1921775e6e6e4')
validpgpkeys=('856F4E797CAB2AA5A0A967E1F0827C47163A10D6') # Michael Seifert <m.seifert@digitalernachschub.de>

build() {
  cd pytest-asyncio
  python -m build -wn
}

check() {
  cd pytest-asyncio
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-asyncio
  python -m installer --destdir="$pkgdir" dist/*.whl
}
