# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-pytest-asyncio
pkgver=0.26.0
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=(any)
url="https://github.com/pytest-dev/pytest-asyncio"
license=('Apache-2.0')
depends=('python' 'python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm')
checkdepends=('python-hypothesis')
source=("git+https://github.com/pytest-dev/pytest-asyncio.git#tag=v$pkgver?signed")
b2sums=('25176aa10cdf915e8bd8e85a68e81e5a3ab406a345644c82f232e7340e32a57690003f1182c32ca7ab41d6bad6fa61d28e69bdb534bc915397d0ab0958cbdf42')
validpgpkeys=('856F4E797CAB2AA5A0A967E1F0827C47163A10D6') # Michael Seifert <m.seifert@digitalernachschub.de>

build() {
  cd pytest-asyncio
  python -m build -wn
}

check() {
  cd pytest-asyncio
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-asyncio
  python -m installer --destdir="$pkgdir" dist/*.whl
}
