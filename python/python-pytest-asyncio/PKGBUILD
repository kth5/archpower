# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.23.6
pkgrel=2
pkgdesc="Pytest support for asyncio."
arch=(any)
url="https://github.com/pytest-dev/pytest-asyncio"
license=('Apache-2.0')
depends=('python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis')
source=("git+https://github.com/pytest-dev/pytest-asyncio.git#tag=v$pkgver?signed")
b2sums=('1a2bba9f7c76ec63166e8bad4f6daeb0069f7218ee277c78f01c4e12e92eab49a7966efd094dbeb563cd3b5230b1c1475ab18d9f06e446581919d9c3c376c8ea')
validpgpkeys=('856F4E797CAB2AA5A0A967E1F0827C47163A10D6') # Michael Seifert <m.seifert@digitalernachschub.de>

build() {
  cd pytest-asyncio
  python -m build -wn
}

check() {
  cd pytest-asyncio
  PYTHONPATH="$PWD:$PYTHONPATH" pytest
}

package() {
  cd pytest-asyncio
  python -m installer --destdir="$pkgdir" dist/*.whl
}
