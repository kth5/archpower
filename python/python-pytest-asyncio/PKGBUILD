# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.23.7
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=(any)
url="https://github.com/pytest-dev/pytest-asyncio"
license=('Apache-2.0')
depends=('python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis')
source=("git+https://github.com/pytest-dev/pytest-asyncio.git#tag=v$pkgver?signed")
b2sums=('8ec8e42f014fb4a9f0e9aab37f300656ab17c3f9f472660307c1846efb683c451a4bd1c8d1d8577b470aef7426621117f067c8a97e769b67c139d7bd5b67f0b0')
validpgpkeys=('856F4E797CAB2AA5A0A967E1F0827C47163A10D6') # Michael Seifert <m.seifert@digitalernachschub.de>

build() {
  cd pytest-asyncio
  python -m build -wn
}

check() {
  cd pytest-asyncio
  PYTHONPATH="$PWD:$PYTHONPATH" pytest
}

package() {
  cd pytest-asyncio
  python -m installer --destdir="$pkgdir" dist/*.whl
}
