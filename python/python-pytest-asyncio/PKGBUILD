# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.23.8
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=(any)
url="https://github.com/pytest-dev/pytest-asyncio"
license=('Apache-2.0')
depends=('python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis')
source=("git+https://github.com/pytest-dev/pytest-asyncio.git#tag=v$pkgver?signed")
b2sums=('f5c33c89bb0366b6a6926f44b2a366fdf1d22fc51defaab621ffbc8640e5e650ede4caf4c3581d736c1a9e8e2506f90183dd367769d39894f0e2622d40b95218')
validpgpkeys=('856F4E797CAB2AA5A0A967E1F0827C47163A10D6') # Michael Seifert <m.seifert@digitalernachschub.de>

build() {
  cd pytest-asyncio
  python -m build -wn
}

check() {
  cd pytest-asyncio
  PYTHONPATH="$PWD:$PYTHONPATH" pytest
}

package() {
  cd pytest-asyncio
  python -m installer --destdir="$pkgdir" dist/*.whl
}
