# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:

pkgname=python-hatch-fancy-pypi-readme
pkgver=23.1.0
pkgrel=1
pkgdesc='Fancy PyPI READMEs with Hatch'
arch=(any)
url='https://github.com/hynek/hatch-fancy-pypi-readme'
license=(MIT)
depends=(python-hatchling)
makedepends=(python-build python-installer)
checkdepends=(python-pytest python-wheel)
source=(https://github.com/hynek/hatch-fancy-pypi-readme/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('735b096c20d8b2ece32d9ab8e695cbfccb54d704770afb8f67f90a9236503811')

build() {
  cd hatch-fancy-pypi-readme-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd hatch-fancy-pypi-readme-$pkgver
  PYTHONPATH="$PWD"/src \
  pytest -v
}

package() {
  cd hatch-fancy-pypi-readme-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
