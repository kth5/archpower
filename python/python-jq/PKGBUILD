# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Mubashshir <ahmubashshir@gmail.com>

_name=jq.py
pkgname=python-jq
pkgver=1.9.1
pkgrel=1
pkgdesc='Python bindings for jq'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/mwilliamson/jq.py
license=(BSD-2-Clause)
depends=(
  jq
  python
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+$url.git#tag=$pkgver")
b2sums=('3fec451d3c10092345986acf52b7ece78e22c2363837059054b3f3b0b7b96bef0fee7e62e2cab507dad213b12c5a671ee8f8127df173c92feeb013bd4d889db9')

build() {
  cd $_name
  cython -3 jq.pyx
  JQPY_USE_SYSTEM_LIBS=1 python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

  cd $_name

  MYARCH=${CARCH}
  case "${CARCH}" in
    powerpc) MYARCH=ppc ;;
    powerpc64) MYARCH=ppc64 ;;
    powerpc64le) MYARCH=ppc64le ;;
  esac

  PYTHONPATH=build/lib.linux-$MYARCH-cpython-$python_version pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/jq-$pkgver.dist-info/licenses/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}
