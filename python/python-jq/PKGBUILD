# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Mubashshir <ahmubashshir@gmail.com>

_name=jq.py
pkgname=python-jq
pkgver=1.8.0
pkgrel=2
pkgdesc='Python bindings for jq'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/mwilliamson/jq.py
license=(BSD-2-Clause)
depends=(
  jq
  python
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("git+$url.git#tag=$pkgver")
b2sums=('ec76c281f026928e534dbcd2def044d4d6e205bf35b234e8d55ddc4212c5826c4f8a2fdf02aae279bbdbe807fd8636658075424bbb3e5a325274a542f498a974')

build() {
  cd $_name
  cython -3 jq.pyx
  JQPY_USE_SYSTEM_LIBS=1 python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  MYARCH=${CARCH}
  case "${CARCH}" in
    powerpc) MYARCH=ppc ;;
    powerpc64) MYARCH=ppc64 ;;
    powerpc64le) MYARCH=ppc64le ;;
    espresso) MYARCH=ppc ;;
  esac

  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

  cd $_name
  PYTHONPATH=build/lib.linux-$MYARCH-cpython-$python_version pytest
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/jq-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
