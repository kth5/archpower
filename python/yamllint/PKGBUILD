# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=yamllint
pkgver=1.35.1
pkgrel=1
pkgdesc='Linter for YAML files'
arch=('any')
url='https://github.com/adrienverge/yamllint'
license=('GPL-3.0-or-later')
depends=('python' 'python-pathspec' 'python-yaml')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('1add06304482fd0e4e8e8d4318f5a96ff5e3a7ceaa18961ce0bb4f7199681938c14feeeabf8ad38c470deaa8da1919dca9194f1c68027fa43ea3de2a9ae1b136')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m unittest discover tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
