# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=yamllint
pkgver=1.37.1
pkgrel=1
pkgdesc='Linter for YAML files'
arch=(any)
url='https://github.com/adrienverge/yamllint'
license=('GPL-3.0-or-later')
depends=('python' 'python-pathspec' 'python-yaml')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('7db7914a20888f3079bdaf97f4869baa54b3066c32c426c9edd841242570ad123424dbf18e401f736b352dfe354c955101739b1e57db3ec6f4245c2715a1e6c6')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m unittest discover tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
