# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=yamllint
pkgver=1.38.0
pkgrel=1
pkgdesc='Linter for YAML files'
arch=(any)
url='https://github.com/adrienverge/yamllint'
license=('GPL-3.0-or-later')
depends=('python' 'python-pathspec' 'python-yaml')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('5d722c0adfa03476a3d1de6adfe13caffa3e222dae981bc2b45037671d08d15bc02232484505a96372f2def0f72ebad8dd4ff9d17e458a12af1e5a907d1ecece')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m unittest discover tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
