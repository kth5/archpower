# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-isal
pkgver=1.7.2
pkgrel=3
pkgdesc='Faster zlib and gzip compatible compression and decompression by providing python bindings for the isa-l library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/pycompression/python-isal
license=(PSF-2.0)
depends=(
  glibc
  isa-l
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-versioningit
  python-wheel
)
makedepends_x86_64=(
  nasm
)
checkdepends=(
  python-pytest
  python-pytest-timeout
  python-tests
)
source=(
  "git+$url.git#tag=v$pkgver"
  "git+https://github.com/intel/isa-l.git"
)
sha256sums=('c4765892ecbfc2dafb4619c9fa5af7867515c8251227cb11802e2f710b50b555'
            'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.src/isal/isa-l.url "$srcdir/isa-l"
  git -c protocol.file.allow=always submodule update \
    src/isal/isa-l
}

build() {
  cd $pkgname
  export PYTHON_ISAL_LINK_DYNAMIC=1
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
