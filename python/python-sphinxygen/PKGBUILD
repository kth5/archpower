# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinxygen
pkgname=python-sphinxygen
pkgver=1.0.10
pkgrel=1
pkgdesc="A script to read Doxygen XML output and emit ReST for Sphinx"
arch=(any)
url="https://gitlab.com/drobilla/sphinxygen"
license=('0BSD OR ISC')
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  doxygen
  python-html5lib
  python-sphinx
)
optdepends=(
  'python-html5lib: for HTML5 support'
)
source=($url/-/archive/v$pkgver/$_name-v$pkgver.tar.gz)
sha512sums=('89a37f7165277d7189ed2109f7e330c97ba2dc6784e60a1b7b949138c0688fb706850551e4b1be18c8f1ed713a0e1d673cb2a94dfb381173ea7cec7a30b71ec2')
b2sums=('06cf250799b8fe537c552b87145f73da39a22dd87e0c31942116a76b0dd3368f0ea3b195986c3f5689170383521eb1d7beeaeb1681c3acddb5cfc09c64e97627')
validpgpkeys=()

build() {
  cd $_name-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-v$pkgver
  python -m unittest discover

}

package() {
  cd $_name-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
