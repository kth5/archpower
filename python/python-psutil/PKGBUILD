# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: SÃ©bastien Luttringer

pkgname=python-psutil
pkgver=6.1.1
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
pkgdesc='A cross-platform process and system utilities module for Python'
url='https://github.com/giampaolo/psutil'
license=('BSD-3-Clause')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'net-tools'
  'procps-ng'
  'python-pytest'
)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/release-$pkgver.tar.gz"
  "dont-package-tests.patch"
)
sha512sums=('b362446f7154831eba344e7c2a741d0d5b1159b20b58f4642e5cce9599d7dca77d36dac7d69fb6f55fee3c56e384c0c4a86b74f2591df463aeceb50ea6e7b616'
            'bc902748c8de706a2196f8a19801314f11035c17c8ad406330ba54f821c3ba9144c49d9b7e305d56500576993162c6d148f7db9cf0c61d4fd23b4d85adb3d461')

prepare() {
  cd psutil-release-$pkgver
  patch -Np1 -i ../dont-package-tests.patch
}

build() {
  cd psutil-release-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd psutil-release-$pkgver
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cp -vr psutil/tests "$PWD/tmp_install/$site_packages/psutil"
  cd "$PWD/tmp_install/$site_packages"
  # Deselect tests failing in a chroot - not sure why.
  pytest \
    --config-file=/dev/null \
    --rootdir="$PWD" \
    --deselect psutil/tests/test_connections.py::TestSystemWideConnections::test_multi_sockets_procs \
    --deselect psutil/tests/test_linux.py::TestRootFsDeviceFinder::test_against_findmnt \
    --deselect psutil/tests/test_linux.py::TestRootFsDeviceFinder::test_comparisons \
    --deselect psutil/tests/test_linux.py::TestRootFsDeviceFinder::test_disk_partitions_mocked \
    --deselect psutil/tests/test_linux.py::TestSystemNetIfAddrs::test_ips \
    --deselect psutil/tests/test_system.py::TestMiscAPIs::test_users
}

package() {
  cd psutil-release-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
