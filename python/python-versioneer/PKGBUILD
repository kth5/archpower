# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-versioneer
pkgver=0.29
pkgrel=1
pkgdesc='A tool for managing a recorded version number in setuptools-based python projects'
arch=('any')
url='https://github.com/python-versioneer/python-versioneer'
license=('custom:Unlicense')
depends=('python' 'python-setuptools')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
)
_commit='28c613dbef5fce09dc3ba6b1baa811c2d76b2245'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python setup.py make_versioneer
  python -m unittest discover test
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/${pkgname#python-}-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
