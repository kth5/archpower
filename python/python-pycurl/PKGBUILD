# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=python-pycurl
_name="${pkgname#python-}"
pkgver=7.45.3
_version="REL_${pkgver//./_}"
pkgrel=1
pkgdesc="A Python 3.x interface to libcurl"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://pycurl.sourceforge.net/"
license=('LGPL-2.1-only OR MIT')
depends=('curl' 'glibc' 'openssl' 'python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-bottle' 'python-pytest' 'vsftpd' 'python-flaky' 'python-pyflakes')
source=("https://github.com/pycurl/pycurl/archive/$_version/$pkgname-$pkgver.tar.gz")
sha512sums=('d57cb918cbf77cb8869c6cc158d464ca2386980e0c6b124d6de6edfb7ef085b9b25610949ee5206a8368e01f39eb5e6495a132d950b425df5b5dc6911c2331a3')

build() {
  cd $_name-$_version
  # the unified source release builds (see PYCURL_RELEASE=1 in Makefile) do not work with tests
  python -m build --wheel --no-isolation
  # needed for tests
  make -C tests/fake-curl/libcurl
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local pytest_options=(
    -vv
    --ignore tests/ftp_test.py  # downloads the internet
    # hanging tests (probably also due to downloading the internet)
    --ignore tests/multi_socket_test.py
    --ignore tests/multi_test.py
    --ignore tests/multi_timer_test.py
    --deselect tests/open_socket_cb_test.py::OpenSocketCbTest::test_socket_open
    --ignore tests/perform_test.py
    --ignore tests/post_test.py
    --ignore tests/read_cb_test.py
    --ignore tests/readdata_test.py
    --deselect tests/reset_test.py::ResetTest::test_reset
    --deselect tests/resolve_test.py::ResolveTest::test_resolve
    --deselect tests/seek_cb_test.py::SeekCbTest::test_seek_function
    --deselect tests/setopt_lifecycle_test.py::SetoptLifecycleTest::test_postfields_lifecycle
    --deselect tests/setopt_string_test.py::SetoptTest::test_setopt_string
    --deselect tests/relative_url_test.py::RelativeUrlTest::test_get_relative
    --deselect tests/pause_test.py::PauseTest::test_pause_via_call
    --deselect tests/pause_test.py::PauseTest::test_pause_via_return
    --deselect tests/setopt_test.py::SetoptTest::test_set_httpheader_none
    --deselect tests/setopt_test.py::SetoptTest::test_unset_httpheader
    --deselect tests/setopt_unicode_test.py::SetoptUnicodeTest::test_ascii_string
    --deselect tests/setopt_unicode_test.py::SetoptUnicodeTest::test_unicode_encoded
    --deselect tests/share_test.py::ShareTest::test_share
    --deselect tests/sockopt_cb_test.py::SockoptCbTest::test_sockoptfunction_ok
    --deselect tests/user_agent_string_test.py::UserAgentStringTest::test_pycurl_user_agent_string
    --ignore tests/write_test.py
    --ignore tests/write_to_stringio_test.py
    --deselect tests/xferinfo_cb_test.py::XferinfoCbTest::test_xferinfo_cb
    # unclear failures
    --deselect tests/multi_callback_test.py::MultiCallbackTest::test_multi_socket_action
    --deselect tests/multi_socket_select_test.py::MultiSocketSelectTest::test_multi_socket_select
  )

  cd $_name-$_version
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$_version
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING-MIT -t "$pkgdir"/usr/share/licenses/$pkgname/
}
