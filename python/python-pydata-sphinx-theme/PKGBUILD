# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=python-pydata-sphinx-theme
pkgver=0.16.1
pkgrel=2.1
pkgdesc="Clean, Bootstrap-based Sphinx theme by and for the PyData community."
url="https://pydata-sphinx-theme.readthedocs.io"
license=('BSD-3-Clause')
arch=('any')
depends=(
  python
  python-accessible-pygments
  python-babel
  python-beautifulsoup4
  python-docutils
  python-jinja
  python-pygments
  python-requests
  python-sphinx
  python-typing_extensions
)
makedepends=(
  git
  python-build
  python-installer
  python-sphinx-theme-builder
)
source=("git+https://github.com/pydata/pydata-sphinx-theme.git#tag=v$pkgver")
b2sums=('539321405ca0204e2dbb654fbc17cdefe51cd9209d080f7231db97fa9db4920b5d2a01c4c24ac2fbb1dfabf9f51213f49799e8260ec7cc923a94fb4ce4df0ca5')

build() {
  cd ${pkgname/python-/}
  python -m build --wheel --no-isolation
}

package() {
  install -D -m644 ${pkgname/python-/}/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  python -m installer --destdir="${pkgdir}" ${pkgname/python-/}/dist/pydata_sphinx_theme-${pkgver}-py3-none-any.whl
}
