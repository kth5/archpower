# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Ian Beringer <ian@ianberinger.com>

pkgname=python-kubernetes
pkgver=35.0.0
pkgrel=1
license=('Apache-2.0')
pkgdesc='Python client for the kubernetes API'
arch=(any)
url='https://github.com/kubernetes-client/python'
depends=(
  'python'
  'python-adal'
  'python-certifi'
  'python-dateutil'
  'python-durationpy'
  'python-google-auth'
  'python-requests'
  'python-requests-oauthlib'
  'python-six'
  'python-urllib3'
  'python-websocket-client'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-isort'
  'python-pluggy'
  'python-py'
  'python-pytest'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('009537e806dc43b9efaa628bf83a9a8e6287b6d718586c2f9fc79797bb771ad16159cf8479fb3c2754677936871c2294edfa5d4e2992b2bf424a2dbb2efa3a33')

build() {
  cd python-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd python-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd python-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
