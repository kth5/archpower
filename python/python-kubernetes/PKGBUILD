# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Ian Beringer <ian@ianberinger.com>

pkgname=python-kubernetes
_pkgbase="${pkgname#python-}"
pkgver=32.0.1
pkgrel=1
license=('Apache-2.0')
pkgdesc='Python client for the kubernetes API'
arch=(any)
url='https://github.com/kubernetes-client/python'
depends=(
  'python'
  'python-adal'
  'python-certifi'
  'python-dateutil'
  'python-durationpy'
  'python-google-auth'
  'python-requests'
  'python-requests-oauthlib'
  'python-six'
  'python-urllib3'
  'python-websocket-client'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-isort'
  'python-pluggy'
  'python-py'
  'python-pytest'
)
source=("$_pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('961656563884c033438689424d5e955ae8c98164f687c2ce2edbb122df6d26c6')

build() {
  cd python-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  [ ! "${CARCH}" == 'x86_64' ] && return 0
  cd python-$pkgver
  PYTHONPATH="$PWD" pytest -v
}

package() {
  cd python-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
