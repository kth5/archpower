# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-ndindex
_pkgname=${pkgname#python-}
pkgver=1.10.1
pkgrel=2
pkgdesc="Library for manipulating indices of ndarrays"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://quansight-labs.github.io/ndindex/"
license=(MIT)
depends=(
  glibc
  python
  python-numpy
)
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-versioneer
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-pytest
  python-sympy
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Quansight-Labs/ndindex/archive/$pkgver/$pkgver.tar.gz")
sha256sums=('eb879209b866ef68e8d51c31e02156666e3d706a93697775f5217b4b9690d375')

build() {
  cd "$_pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd "$_pkgname-$pkgver"
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$PWD/tmp_install/$site_packages"
  pytest --override-ini="addopts=" -W ignore::pytest.PytestUnknownMarkWarning
}

package() {
  cd "$_pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -r "$pkgdir/$site_packages/ndindex/tests"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
