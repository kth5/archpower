# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-packaging
pkgver=23.2
pkgrel=1
pkgdesc="Core utilities for Python packages"
arch=(any)
url="https://github.com/pypa/packaging"
license=('Apache')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest' 'python-pretend')
source=("https://github.com/pypa/packaging/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('77dfeb0dc6499c55eb5bc4a5bdcdaa146122b97e8f6190c0bf75baadb4e89e4cb5b62ac7d96175acc3d8b387507472b97f0bf18c70df2b6aa78ac54e6c0eb5a3')

build() {
  cd packaging-$pkgver
  python -m build -nw
}

check() {
  cd packaging-$pkgver
  PYTHONPATH=src pytest
}

package() {
  cd packaging-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
