# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-josepy
pkgver=2.0.0
pkgrel=1
pkgdesc='JOSE protocol implementation in Python'
arch=(any)
url='https://github.com/certbot/josepy'
license=('Apache-2.0')
depends=(
  'python'
  'python-cryptography'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-poetry-core'
)
checkdepends=('python-pytest')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'no-thanks-poetry.patch'
)
sha512sums=('38c6d8cf5e7503977cbf0c1b678b7696c76517c6ca8ba30ddf794274fa5b5f5f4a737536440d51770e97acf316583d726658410ef0ce4d89dfd7f1b740b9aa44'
            '5fc9682dbb93a3af76bb07653ec873c40a2acad24484ce64babde7d342390439efad018d5b5fda88df7338b562ba88d4254a32bda77bd1e981a0b312218e9bc8')
b2sums=('4fb1d225f16d76dc90cd797134579822e104cabff1926fcb2d5e18b44c5cba068159680038acc30028b1de9637621f5e8db15873115d87edc29a613f1acf7b9a'
        'b7bf362e953247424fadea853b3c7be9977021b7d109d240ced847b9271c739e6dfcf8025efbdd9bb746dc42e21905cb1c9c93982d4e39dab60354ab06c6e76a')

prepare() {
  cd "$pkgname"

  # poetry strikes again
  # https://github.com/certbot/josepy/issues/172
  patch -p1 -i "$srcdir/no-thanks-poetry.patch"
}
build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # install to temporary location
  python -m installer --destdir=test_dir dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="test_dir/$site_packages:$PYTHONPATH" pytest -v \
    -W ignore::DeprecationWarning
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
