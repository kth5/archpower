# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Donald Webster <fryfrog@gmail.com>
# Contributor: Justin Gottula <justin@jgottula.com>

pkgname=wsdd
pkgver=0.8
pkgrel=3
pkgdesc="Web Services Dynamic Discovery daemon for Windows discovery and discoverability"
url="https://github.com/christgau/wsdd"
arch=(any)
license=(MIT)
depends=(
  python
  python-defusedxml
)
makedepends=(git)
backup=(etc/conf.d/wsdd)
source=(
  "git+$url?signed#tag=v$pkgver"
  0001-Modify-systemd-service-for-Arch.patch
)
b2sums=('a7a970c9f23501cb48ced3d65c6b18fad672c78e3225d114d196e4c0e3499d32c58f848300514c77d135f852326625bc39c8dd4ea50e7f6f6c7bd1ef59e89c9f'
        '838f8f7a3eba30c0b565d79a64f6f3e642b7d7d68950f1ab279706fe3e4122f283ebefe4707bebe898b18e7bf750ca00221e0453f0c9b5740b36f9de2e16d323')
validpgpkeys=(
  55147D9E2AD7D45CC252F1C530462C9368BAB499 # Steffen Christgau <mail@s14u.de>
)

prepare() {
  cd wsdd
  git apply -3 ../0001-Modify-systemd-service-for-Arch.patch
}

package() {
  cd wsdd
  install -D src/wsdd.py "$pkgdir/usr/bin/wsdd"
  install -Dm644 AUTHORS README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 etc/firewalld/services/* -t "$pkgdir/usr/lib/firewalld/services"
  install -Dm644 etc/systemd/wsdd.conf "$pkgdir/etc/conf.d/wsdd"
  install -Dm644 etc/systemd/wsdd.service -t "$pkgdir/usr/lib/systemd/system"
  install -Dm644 man/wsdd.8 -t "$pkgdir/usr/share/man/man8"
}

# vim:set sw=2 sts=-1 et:
