# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=pydantic
pkgname=python-$_name
# WARNING: upstream pins pydantic-core down to the patch-level and using other versions breaks tests! only update in lock-step with python-pydantic-core!
pkgver=2.6.1
pkgrel=1
pkgdesc='Data parsing and validation using Python type hints'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/pydantic/pydantic"
license=(MIT)
depends=(
  python
  python-annotated-types
  python-pydantic-core
  python-typing-extensions
)
makedepends=(
  cython
  python-build
  python-installer
  python-importlib-metadata
  python-hatchling
  python-hatch-fancy-pypi-readme
  python-wheel
)
checkdepends=(
  python-ansi2html
  python-cloudpickle
  python-devtools
  python-dirty-equals
  python-email-validator
  python-faker
  python-hypothesis
  python-pygments
  python-pytest
  python-pytest-benchmark
  python-pytest-examples
  python-pytest-mock
  python-sqlalchemy
)
optdepends=(
  'mypy: for type validation with mypy'
  'python-dotenv: for .env file support'
  'python-email-validator: for email validation'
  'python-hypothesis: for hypothesis plugin when using legacy v1'
)
source=($url/archive/v$pkgver/$_name-v$pkgver.tar.gz)
sha512sums=('1180fcfdb3eb1f0e65449675fefa806abdfc75f2cab9580b8f2d36eca886c2a8f19d75f2c4655d4ea338e8cccaa48a0e9a3aebf229ff893ec89f3b20ab8d9285')
b2sums=('02048a98d3469e286e4945c16040ade103699ed804c0aa6a3f65d72596ff3840332f5c973851b90deaab278ca87e3a2ff49bf2aed1e839d0fada53699a310ee8')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    # disable broken tests: https://github.com/pydantic/pydantic/issues/8459
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_json
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_json_strict
    --deselect tests/benchmarks/test_north_star.py::test_north_star_dump_json
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_python
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_python_strict
    --deselect tests/benchmarks/test_north_star.py::test_north_star_dump_python
    --deselect tests/benchmarks/test_north_star.py::test_north_star_json_loads
    --deselect tests/benchmarks/test_north_star.py::test_north_star_json_dumps
    # disable broken doc test: https://github.com/pydantic/pydantic/issues/8460
    --deselect 'tests/test_docs.py::test_docs_examples[docs/concepts/serialization.md:354-391]'
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
