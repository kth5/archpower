# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=pydantic
pkgname=python-$_name
# WARNING: upstream pins pydantic-core down to the patch-level and using other versions breaks tests! only update in lock-step with python-pydantic-core!
pkgver=2.6.3
pkgrel=1
pkgdesc='Data parsing and validation using Python type hints'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/pydantic/pydantic"
license=(MIT)
depends=(
  python
  python-annotated-types
  python-pydantic-core
  python-typing-extensions
)
makedepends=(
  cython
  python-build
  python-installer
  python-importlib-metadata
  python-hatchling
  python-hatch-fancy-pypi-readme
  python-wheel
)
checkdepends=(
  python-ansi2html
  python-cloudpickle
  python-devtools
  python-dirty-equals
  python-email-validator
  python-faker
  python-hypothesis
  python-pygments
  python-pytest
  python-pytest-benchmark
  python-pytest-examples
  python-pytest-mock
  python-sqlalchemy
)
optdepends=(
  'mypy: for type validation with mypy'
  'python-dotenv: for .env file support'
  'python-email-validator: for email validation'
  'python-hypothesis: for hypothesis plugin when using legacy v1'
)
source=($url/archive/v$pkgver/$_name-v$pkgver.tar.gz)
sha512sums=('767cbe8b63905ff72cc826941fdfde7d98a6aa9868eed40a2fdea3df5608dd64d274ad44f47e323b167f01cc9250c72db8c339caf9ffe48fa8e12c2531d51c0f')
b2sums=('5eb1be9acb4f688f5aa2789a1f6248bbd6692f5004530eba6a84b8e587e83260f7cfd9b50e398e34d0edb12c6814f205b01ba29c1bb04208a9e25c65d3893113')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    # disable broken tests: https://github.com/pydantic/pydantic/issues/8459
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_json
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_json_strict
    --deselect tests/benchmarks/test_north_star.py::test_north_star_dump_json
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_python
    --deselect tests/benchmarks/test_north_star.py::test_north_star_validate_python_strict
    --deselect tests/benchmarks/test_north_star.py::test_north_star_dump_python
    --deselect tests/benchmarks/test_north_star.py::test_north_star_json_loads
    --deselect tests/benchmarks/test_north_star.py::test_north_star_json_dumps
    # disable broken doc test: https://github.com/pydantic/pydantic/issues/8460
    --deselect 'tests/test_docs.py::test_docs_examples[docs/concepts/serialization.md:354-391]'
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
