# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-rpds-py
pkgver=0.27.1
pkgrel=1
pkgdesc='Python bindings to the Rust rpds crate for persistent data structures'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/crate-py/rpds'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-maturin'
  'python-installer'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('8631ca1cc6dd4ce12a3426bf3665418f27abd6950dd657569367386e23dff1e8ccfa9efcd7f01202005cea8111f6359877241b1073bc9bdcf3fb489826cf169b')
b2sums=('24ada7e75aa1a12de44bbcb99f9a317bb90aab6d4c2ca036780ddcb4aa44d7f7935ec4ed7549f047fd206ceb4e95a326d61dd8d921dc772c29ff56085d4c35ea')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
