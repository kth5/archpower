# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-rpds-py
pkgver=0.28.0
pkgrel=1
pkgdesc='Python bindings to the Rust rpds crate for persistent data structures'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/crate-py/rpds'
license=(MIT)
depends=(
  glibc
  gcc-libs
  python
)
makedepends=(
  git
  python-build
  python-maturin
  python-installer
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('4bb186928937d657bae25eaa8fb6fc23d685d4bf1cc1a134ac4139dd8e0546fb52ba486d2935fe9cad088b39a14f4891c32a4ef83e94dd280d176ad313edad53')
b2sums=('6f2954a52552d68de6da8de27f81f039461a86e9e38de74a410bff2d00cfcb24bd217e884ce1a037695bba503726c6d27ca594700fc69d86283ee1c78909f41b')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
