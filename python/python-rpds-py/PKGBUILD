# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-rpds-py
pkgver=0.22.3
pkgrel=1
pkgdesc='Python bindings to the Rust rpds crate for persistent data structures'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/crate-py/rpds'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-maturin'
  'python-installer'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('b1656556609cd5a1214283c8282ad8d1088c74f360cf318824c5cceba3fbe6ddf7d45c7c8df206d09946b7625697326b422161f222ae9eef25c1979e31fdd87c')
b2sums=('19a1b71560b386fd506e30466c6c9c8d4bed36da3b4607ed749820d3335becf32636ed31658e2a43511ea6e34d76f55449decdc08f777d26c88e60c6c9ec9270')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
