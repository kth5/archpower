# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=cerberus
pkgname=python-cerberus
pkgver=1.3.8
pkgrel=2
pkgdesc="Lightweight, extensible schema and data validation tool for Python dictionaries"
url="https://github.com/pyeve/cerberus"
license=(ISC)
arch=(any)
depends=(
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-benchmark
)
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('4305b4dd8065f94f68498b2264b3730e7972ce65c248fd50255bb04419bc376b3ff863dfd141a1a7cebffc46852189c439ac42b6482ac8262afa1b163d308088')
b2sums=('dd6d9311ae4b745adc9cad6e3aa7aa0a25525d9314430eadc8e0d895854542b9ea2b0d48efa9ad8996911074460ff5eded1f1af0c4a57bbe5fa49fbd8bc448ab')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
  # NOTE: remove build dir as pytest otherwise chokes on duplicate imports
  rm -frv build
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
