# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=cerberus
pkgname=python-cerberus
pkgver=1.3.7
pkgrel=1
pkgdesc="Lightweight, extensible schema and data validation tool for Python dictionaries"
url="https://github.com/pyeve/cerberus"
license=(ISC)
arch=(any)
depends=(
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-benchmark
)
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('69ed22089f3fb8c1869585abff39510f9a0bcf8dfbf82fa995177999e634e1a93bc535a2c7d330ec2491b01a0fe807d803144d3f08cdd6c9589c39e2aaec9235')
b2sums=('0cf9cb6fbb156007f7c44dfc427579649b8ed1e70cdf7f89f728b4d6071fda7073d5e5328be6e398744fa715dbc53ff8ea79195ef87e42f9b3bc4c838532c531')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
  # NOTE: remove build dir as pytest otherwise chokes on duplicate imports
  rm -frv build
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
