# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-filelock
pkgver=3.17.0
pkgrel=1
pkgdesc="A platform independent file lock"
url="https://github.com/benediktschmitt/py-filelock"
license=('Unlicense')
arch=(any)
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-hatch-vcs'
  'python-hatchling'
  'python-installer'
)
checkdepends=(
  'python-pytest'
  'python-pytest-asyncio'
  'python-pytest-mock'
  'python-pytest-timeout'
  'python-virtualenv'
)
source=("git+https://github.com/benediktschmitt/py-filelock.git#tag=$pkgver")
sha512sums=('852c56156890e5e29df4f265743befd98179c9ef9467cb7082476a3af033140376dad6459fcd3fad492787a5ae8f8abf0958fe22694fdb5fe6ec10c077e850c5')

build() {
  cd py-filelock
  python -m build --wheel --no-isolation
}

check() {
  cd py-filelock
  PYTHONPATH=src pytest tests
}

package() {
  cd py-filelock
  python -m installer --destdir="$pkgdir" dist/*.whl
}
