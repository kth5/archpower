# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-filelock
pkgver=3.20.3
pkgrel=1
pkgdesc="A platform independent file lock"
url="https://github.com/benediktschmitt/py-filelock"
license=('Unlicense')
arch=(any)
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-hatch-vcs'
  'python-hatchling'
  'python-installer'
)
checkdepends=(
  'python-pytest'
  'python-pytest-asyncio'
  'python-pytest-mock'
  'python-pytest-timeout'
  'python-virtualenv'
)
source=("git+$url.git#tag=$pkgver")
b2sums=('50cf3f9a8e0f8154182001f7b23f17babb4c81f36d22f9d5be18d71ed3cb84bf388a224a17d9c36fd4bd391cf9b8aec0cace26f34e28c6c6e42ab5a292bf5e3d')

build() {
  cd py-filelock
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  return 0
  cd py-filelock
  PYTHONPATH=src pytest tests
}

package() {
  cd py-filelock
  python -m installer --destdir="$pkgdir" dist/*.whl
}
