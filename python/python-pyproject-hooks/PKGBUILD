# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_bootstrap=1
_bootstrap_version=3.14.0
pkgname=python-pyproject-hooks
_name=${pkgname#python-}
pkgver=1.2.0
pkgrel=6
pkgdesc="A low-level library for calling build-backends in pyproject.toml-based project"
arch=(any)
url="https://github.com/pypa/pyproject-hooks"
license=(MIT)
depends=(python)
if (( _bootstrap == 0 )); then
  makedepends=(
    python-build
    python-installer
    python-flit-core
    python-wheel
  )
else
  makedepends=(
    git
  )
fi
checkdepends=(
  python-pytest
  python-setuptools
  python-testpath
)
if (( _bootstrap == 0 )); then
  source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
else
  source=(
    python-bootstrap::git+https://gitlab.archlinux.org/archlinux/python-bootstrap.git#tag=$_bootstrap_version
    python-build::git+https://github.com/pypa/build.git
    python-flit::git+https://github.com/pypa/flit.git
    python-installer::git+https://github.com/pypa/installer.git
    python-wheel::git+https://github.com/pypa/wheel.git
    python-packaging::git+https://github.com/pypa/packaging
    python-pyproject-hooks::git+https://github.com/pypa/pyproject-hooks
    python-setuptools::git+https://github.com/pypa/setuptools.git
  )
fi
sha512sums=('a71bffda21bf944dd0d62dc801ae6b719ba4a54538b7a03cc84c3ff930ca1b91eaecf226d8089a09a707eaf9892f7cce31e7e167008af64ac918870079f3f65f'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('77a00054d250153d5e73a9ecd9ea4350cea5eee1bbb9d2ef086527df3f0a5adb4558f8e4a8fcbae6591786bb80803f87013266dcf08112e519eff9305a5fd2b2'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  if (( _bootstrap == 0 )); then
    cd $_name-$pkgver
  else
    cd python-bootstrap
    git submodule init

    git config submodule."external/build".url ../python-build
    git config submodule."external/flit".url ../python-flit
    git config submodule."external/installer".url ../python-installer
    git config submodule."external/wheel".url ../python-wheel
    git config submodule."external/packaging".url ../python-packaging
    git config submodule."external/pyproject-hooks".url ../python-pyproject-hooks
    git config submodule."external/setuptools".url ../python-setuptools

    git -c protocol.file.allow=always submodule update
    git submodule update --init --recursive
  fi
}

build() {
  if (( _bootstrap == 0 )); then
    cd $_name-$pkgver
    python -m build --wheel --no-isolation
  else
    cd python-bootstrap
    python -m bootstrap.build
  fi
}

check() {
  if (( _bootstrap == 1 )); then
    return 0
  fi

  cd $_name-$pkgver
  export PYTHONPATH="$PWD/src:$PYTHONPATH"
  pytest -vv
}

package() {
  if (( _bootstrap == 0 )); then
    cd $_name-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  else
    cd python-bootstrap
    python -m bootstrap.install dist/${_name/-/_}-*-py3-none-any.whl -d "$pkgdir"
    install -vDm 644 external/$_name/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  fi
}
