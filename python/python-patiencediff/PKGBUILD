# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-patiencediff
pkgver=0.2.18
pkgrel=2
pkgdesc='Patiencediff Python and C implementations'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://www.breezy-vcs.org/
license=(GPL-2.0-only)
depends=(
  glibc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-rust
  python-wheel
)
_tag=a57fe4929c13a57e6153fc03e249bb6e8d796020
source=(git+https://github.com/breezy-team/patiencediff.git#tag=${_tag})
sha256sums=('495409c3048a7557d539ea382fb57409401eac2d14e34d78f1a2aef36128357d')

pkgver() {
  cd patiencediff
  git describe --tags | sed 's/^v//'
}

build() {
  cd patiencediff
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" patiencediff/dist/*.whl
}

# vim: ts=2 sw=2 et:
