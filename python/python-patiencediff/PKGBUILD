# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-patiencediff
pkgver=0.2.15
pkgrel=1
pkgdesc='Patiencediff Python and C implementations'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://www.breezy-vcs.org/
license=(GPL2)
depends=(
  glibc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=2879bd4a0fde0701e3ff3abab1cdd04832d80b43
source=(git+https://github.com/breezy-team/patiencediff.git#tag=${_tag})
sha256sums=('bed7a61d6803327958f34be863d42e09e23a9ab3ed5654be3ed03d88df287e33')

pkgver() {
  cd patiencediff
  git describe --tags | sed 's/^v//'
}

build() {
  cd patiencediff
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" patiencediff/dist/*.whl
}

# vim: ts=2 sw=2 et:
