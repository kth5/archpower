# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=3.2.5.1
_commit=a03a69f9a81f154d9c10964005a61877bf3ed5fc
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=(any)
license=('Apache')
url='https://github.com/Mergifyio/daiquiri'
depends=('python-json-logger')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-setuptools-scm'
             'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/Mergifyio/daiquiri.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd daiquiri
  python -m build -nw
}

check() {
  cd daiquiri
  python -m pytest
}

package() {
  cd daiquiri
  python -m installer -d "$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
