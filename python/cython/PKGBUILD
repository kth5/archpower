# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Igor Scabini <furester @ gmail.com>

pkgname=cython
pkgver=3.1.3
pkgrel=1
pkgdesc='C-Extensions for Python'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://cython.org'
license=(Apache-2.0)
depends=(glibc
         python)
replaces=(cython-dev)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(gdb
              python-interpreters-pep-734
              python-numpy
              python-pytest
              python-tests)
source=(git+https://github.com/cython/cython#tag=$pkgver)
sha256sums=('e6b9a93310b24be882b57c3619a0a3875c20948704bf84376f9a551f5f94f8a7')

build() {
  cd cython
  python -m build --wheel --no-isolation
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd cython
  python runtests.py -vv -j 64 --no-pyregr
}

package() {
  cd cython
  python -m installer --destdir="$pkgdir" dist/*.whl

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}3
    ln -s ${f}3 "$pkgdir"/usr/bin/$f
  done
}
