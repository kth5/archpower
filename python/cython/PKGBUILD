# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Igor Scabini <furester @ gmail.com>

pkgname=cython
pkgver=0.29.35
pkgrel=2
pkgdesc='C-Extensions for Python'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://cython.org'
license=(APACHE)
depends=(python)
makedepends=(python-setuptools)
source=(https://github.com/cython/cython/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a2da56cc22be823acf49741b9aa3aa116d4f07fa8e8b35a3cb08b8447b37c607')

build() {
  cd cython-$pkgver
  python setup.py build
}

package() {
  cd cython-$pkgver
  python setup.py install --root="$pkgdir" --skip-build

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}3
    ln -s ${f}3 "$pkgdir"/usr/bin/$f
  done
}
