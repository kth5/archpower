# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: @RubenKelevra <cyrond@gmail.com>

pkgname=python-watchfiles
pkgver=1.0.5
pkgrel=1
pkgdesc='Simple, modern and high performance file watching and code reload in Python'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/samuelcolvin/watchfiles'
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
  python-anyio
)
makedepends=(
  python-build
  python-installer
  python-maturin
)
checkdepends=(
  python-dirty-equals
  python-pytest
  python-pytest-mock
  python-pytest-timeout
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('caa9accbea4b1501a9dcaec78213694fd69cdb04cc859085bf89bf55de471422507c7b8d499ee04c40c74a6f64e1b505ff98ffb78a0bec4246dcc6ac7f4ff6e5')

prepare() {
  cd ${pkgname#python-}-$pkgver
  # This prevents tests from detecting the watchfiles module.
  rm -v tests/__init__.py
}

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # Don't add CWD to PYTHONPATH, the watchfiles package in CWD will take
  # precedence over installed one.
  test-env/bin/python -Im pytest
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
