# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rapidjson
pkgver=1.23
pkgrel=2
pkgdesc="Python wrapper around rapidjson"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://pypi.python.org/pypi/python-rapidjson"
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'rapidjson')
checkdepends=('python-pytest' 'python-pytest-benchmark' 'python-pytz')
source=("https://files.pythonhosted.org/packages/26/3a/c32aee1dc385e50c1d6e78e56abdbc6aca283127f06f6ec0be1a86b2e3c1/python_rapidjson-1.23.tar.gz")
sha512sums=('bab2f89b3e586dd5d88b9c457bd8ecf3ea14c2f6e637d980ed89cf83e23f5702ae30f0dc1ce5a1ea92e9b3e78a9dbc0faf43d6698a84f34a889d2fc4a61802f5')

prepare() {
  #rm -r python-rapidjson-$pkgver/rapidjson
  #sed -i 's/if not os.path.isdir/if os.path.isdir/' python-rapidjson-$pkgver/setup.py
  # Shared rapidjson fails tests.
  :
}

build() {
  cd python_rapidjson-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  MYARCH=${CARCH}
  case "${CARCH}" in
    powerpc) MYARCH=ppc ;;
    powerpc64) MYARCH=ppc64 ;;
    powerpc64le) MYARCH=ppc64le ;;
    espresso) MYARCH=ppc ;;
  esac

  cd python_rapidjson-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$MYARCH-cpython-$python_version" pytest --ignore benchmarks/
}

package() {
  cd python_rapidjson-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
