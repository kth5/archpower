# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-transaction
pkgver=5.0
pkgrel=2
pkgdesc='Transaction management for Python'
arch=(any)
license=('ZPL')
url='https://github.com/zopefoundation/transaction'
depends=('python-zope-interface')
makedepends=('git' 'python-setuptools')
checkdepends=('python-zope-testrunner')
source=("git+https://github.com/zopefoundation/transaction.git#tag=$pkgver")
sha512sums=('929577f6ec5f912979a13f7b5ef071c7bb837d95102ec215a4792e06196214c9a89146d1cc12a20794c0bf1398d2220aa53fe680a6ca96f41d3892d9b62f57e5')

build() {
  cd transaction
  python setup.py build
}

check() {
  cd transaction
  PYTHONPATH="$PWD"/build/lib zope-testrunner --test-path=src
}

package() {
  cd transaction
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
