# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-uritemplate
pkgver=4.1.1
pkgrel=7
pkgdesc='A Python implementation of RFC6570'
arch=(any)
url='https://github.com/python-hyper/uritemplate'
license=('BSD-3-Clause OR Apache-2.0')
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=("$pkgname::git+$url#tag=$pkgver?signed")
sha512sums=('613ab2922b85a766272496cd1bc23596d87740e5475eaebc2d4b8b138a9ad6a385e6c54a3674b23facb07df7b1ab91a4fdb67eac02157dd4ba6ae2cf3b3548e5')
b2sums=('f93f51cefdbce2d4f32ab0357a3b33d6dc13b210742a5ac101fa25309f71d05803b01854fbac01d4a48220edd5c92799012e00617318e74415c1b8c889ef5749')
validpgpkeys=('0161BB7EB208B5E04FDC9F81D9DA0A049113F853') # Ian Stapleton Cordasco <graffatcolmingov@gmail.com>

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
