# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=python-prometheus_client
pkgver=0.20.0
pkgrel=1

pkgdesc="Prometheus instrumentation library for Python applications"
url="https://github.com/prometheus/client_python"
arch=(any)
license=('APACHE')

depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-twisted' 'python-pytest')

source=(client_python-$pkgver.tar.gz::https://github.com/prometheus/client_python/archive/v${pkgver}.tar.gz)

sha512sums=('b93de8fe7813e38b848d5053e5e9817090532912337b8eefe12b5ee456f33f96fc284e52a0fb426a846b2ef38b83614d834ee2c54ab54f27712b587eab5ae9f8')

build() {
  cd client_python-$pkgver
  python setup.py build
}

check() {
  cd client_python-$pkgver
  # Make reproducible by not writing pyc files for test files. (rebuilderd does not run tests)
  PYTHONDONTWRITEBYTECODE=1 python -m unittest discover -vs .
}

package() {
  cd client_python-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
