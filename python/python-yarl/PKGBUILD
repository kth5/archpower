# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-yarl
pkgver=1.23.0
pkgrel=1
pkgdesc='Yet another URL library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aio-libs/yarl/'
license=('Apache-2.0')
depends=(
  'glibc'
  'python'
  'python-idna'
  'python-multidict'
  'python-propcache'
)
makedepends=(
  'cython'
  'python-setuptools'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-expandvars'
)
checkdepends=(
  'python-hypothesis'
  'python-pytest'
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8aaa02e5cc06a9eaa73c2f3218b5860dce9bc53faf571901cd74483b2221c7f567c49cc70e5bc2e394b01ee3d6726c730534c8494e79d6cfb2a2d5a1b3ac7e3d')
b2sums=('70c07cef7b4512f7b73d5d727a3c8155488c4912dcf35c2929beb68383f2a986b78a000b34de29cf862f6da0bc38059e65cc0b03a7e9a1d858546c62c1ada367')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -Pm pytest -v --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
