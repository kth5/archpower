# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=yarl
pkgname=python-yarl
pkgver=1.13.1
pkgrel=1
pkgdesc='Yet another URL library'
url='https://github.com/aio-libs/yarl/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('Apache-2.0')
depends=(
  'glibc'
  'python'
  'python-multidict'
  'python-idna'
)
makedepends=(
  'cython'
  'python-setuptools'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-expandvars'
)
checkdepends=(
  'python-pytest'
  'python-pytest-xdist'
)
source=(https://github.com/aio-libs/yarl/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1609e60b34b4e02c187761e1d24d6a5873873b3ad94683b8e6107be5314676e98ed972eb3aa864ef0cb8312e0d526790da1cba995a4d3996df84c81be62564b7')
b2sums=('cc1e1ea527c718a7395e9acc52b8bcc2855eaa52a75bcfb160986773d1d616d938a0fcf16c6f89ec7374ffbdf54956c5b294a613481e0327e94e9444719b3cd7')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed 's| .install-cython ||g' -i Makefile
}

build() {
  export LANG=en_US.UTF-8

  cd ${_pkgname}-${pkgver}
  make cythonize
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  cd tests
  ../test-env/bin/python -m pytest -v --override-ini="addopts="
}

package() {
  export LANG=en_US.UTF-8

  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
