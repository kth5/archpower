# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-setuptools-gettext
pkgver=0.1.16
pkgrel=2
pkgdesc='Setuptools plugin for building .mo files'
arch=(any)
url=https://github.com/breezy-team/setuptools-gettext
license=(GPL2)
depends=(
  python
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
_tag=a37094c89a586ddbaca879b68c43735092d1db9e
#source=(git+https://github.com/breezy-team/setuptools-gettext.git?signed#tag=${_tag})
source=(git+https://github.com/breezy-team/setuptools-gettext.git#tag=${_tag})
b2sums=('a48dd99ddf076706e126404de38e2fac0f3c445adb6fbd5655e1e33c034fbeb2a04abd90f3db3dacd38d7007957e20e225cc564ea120f0141d652d9578af3eea')
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>

pkgver() {
  cd setuptools-gettext
  git describe --tags | sed 's/^v//'
}

build() {
  cd setuptools-gettext
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" setuptools-gettext/dist/*.whl
}

# vim: ts=2 sw=2 et:
