# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-setuptools-gettext
pkgver=0.1.11
pkgrel=1
pkgdesc='Setuptools plugin for building .mo files'
arch=(any)
url=https://github.com/breezy-team/setuptools-gettext
license=(GPL2)
depends=(
  python
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
_tag=372b711fb63b1eb46cb297c34c98e2b81acc69dd
#source=(git+https://github.com/breezy-team/setuptools-gettext.git?signed#tag=${_tag})
source=(git+https://github.com/breezy-team/setuptools-gettext.git#tag=${_tag})
b2sums=('3bd069f3db0dc022417886ac88f2322559e7ea64db418ef3678522a60e85561c2418a64deba9824533d90bf197a35a2a826819a61c5ed79104cc56cf2c5326db')
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>

pkgver() {
  cd setuptools-gettext
  git describe --tags | sed 's/^v//'
}

build() {
  cd setuptools-gettext
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" setuptools-gettext/dist/*.whl
}

# vim: ts=2 sw=2 et:
