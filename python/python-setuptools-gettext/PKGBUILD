# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-setuptools-gettext
pkgver=0.1.14
pkgrel=1
pkgdesc='Setuptools plugin for building .mo files'
arch=(any)
url=https://github.com/breezy-team/setuptools-gettext
license=(GPL2)
depends=(
  python
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
_tag=0e9965aa50eeacd104f08124527b69e827048da6
#source=(git+https://github.com/breezy-team/setuptools-gettext.git?signed#tag=${_tag})
source=(git+https://github.com/breezy-team/setuptools-gettext.git#tag=${_tag})
b2sums=('9d074eb402fb431d9191bda2d55223de69cdb2d5123b32e35155c2ee837ee8efcf0c5fdf85caacdc731b233bdd16b50f7a8c07aa6c1d4d88046ad72f82b1b054')
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>

pkgver() {
  cd setuptools-gettext
  git describe --tags | sed 's/^v//'
}

build() {
  cd setuptools-gettext
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" setuptools-gettext/dist/*.whl
}

# vim: ts=2 sw=2 et:
