# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=hypercorn
pkgver=0.16.0
pkgrel=3
pkgdesc='An ASGI Server based on Hyper libraries and inspired by Gunicorn'
url=https://github.com/pgjones/hypercorn
arch=(any)
license=(MIT)
depends=(
  python
  python-h11
  python-h2
  python-priority
  python-wsproto
)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
)
checkdepends=(
  python-distlib
  python-hypothesis
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
  python-pytest-sugar
  python-pytest-trio
  python-trio
)
optdepends=('python-trio: trio support')
source=(git+$url.git#tag=$pkgver)
sha256sums=('799865554b028af3d483c932b1cfa7ad8b2a8ec4f3bbd0e7abbe78110b3c41f7')

build() {
  cd hypercorn
  python -m build --wheel --no-isolation
}

check() {
  cd hypercorn
  PYTHONPATH="src:$PYTHONPATH" python -m pytest \
    --ignore=tests/trio/test_lifespan.py
}

package() {
  python -m installer --destdir="${pkgdir}" hypercorn/dist/*.whl
  install -Dm 644 hypercorn/LICENSE -t "${pkgdir}"/usr/share/licenses/hypercorn/
}

# vim: ts=2 sw=2 et:
