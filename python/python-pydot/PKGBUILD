# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contrinutor: shining <shiningxc@gmail.com>
# Contributor: phillid <dbphillipsnz _at_thingy_that_swirly_a_symbol gmaildott comm>

pkgname=python-pydot
pkgver=3.0.4
pkgrel=2
pkgdesc="Python interface to Graphviz's Dot"
arch=(any)
url="https://github.com/erocarrera/pydot"
license=('MIT')
depends=('python-pyparsing' 'graphviz')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-chardet' 'python-parameterized')
source=("$pkgname-$pkgver.tar.gz::https://github.com/erocarrera/pydot/archive/v$pkgver.tar.gz")
sha512sums=('9e8a3f7cce095cd50bb665e0f798c22589ea1caa783012afb1b9542924c39fb20968b8c123d33ee7ce86e307f5389d8c08cc491d7efcd62f8e34621a3a7e4abe')

build() {
  cd pydot-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd pydot-$pkgver/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python -m unittest -v
}

package() {
  cd pydot-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSES/MIT.txt
}
