# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-repeat
pkgver=0.9.3
pkgrel=2
pkgdesc='pytest plugin for repeating test execution'
arch=('any')
license=('MPL')
url='https://github.com/pytest-dev/pytest-repeat'
depends=('python-pytest')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling' 'python-hatch-vcs')
source=("git+https://github.com/pytest-dev/pytest-repeat.git#tag=v$pkgver")
sha512sums=('781f9d8775346294ae17cce9c4bd2490756ef108c71c9ff42ddaf8c74e5375768689dd4cf0c417d1a36df266732c930c811f539306138d9c0e8b4dad1f5712ad')

build() {
  cd pytest-repeat
  python -m build --wheel --no-isolation
}

check() {
  # Hack entry points by installing it

  cd pytest-repeat
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl
  PYTHONPATH="$PWD/tmp_install$site_packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-repeat
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
