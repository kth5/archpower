# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-importlib-metadata
pkgver=8.7.0
pkgrel=1
pkgdesc="Read metadata from Python packages"
url="https://importlib-metadata.readthedocs.io"
license=('Apache-2.0')
arch=(any)
depends=('python-zipp')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-jaraco.test' 'python-pytest' 'python-pyfakefs' 'python-pip' 'python-tests')
source=("git+https://github.com/python/importlib_metadata.git#tag=v$pkgver?signed")
sha512sums=('2577eabc2920d2e0d7658f2eee695a172540d2a9bcfbfa1d975f2b38af99cccd74ed87b2faa8ec37be1c48ff2d8c169b622b5463ccff1de52a82355a717766ea')
validpgpkeys=('CE380CF3044959B8F377DA03708E6CB181B4C47E') # Jason R. Coombs <jaraco@jaraco.com>

build() {
  cd importlib_metadata
  python -m build -nw
}

check() {
  cd importlib_metadata
  python -m pytest --ignore exercises.py
}

package() {
  cd importlib_metadata
  python -m installer -d "$pkgdir" dist/*.whl
}
