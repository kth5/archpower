# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-importlib-metadata
pkgver=7.2.1
pkgrel=1
pkgdesc="Read metadata from Python packages"
url="https://importlib-metadata.readthedocs.io"
license=('Apache-2.0')
arch=(any)
depends=('python-zipp')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-jaraco.test' 'python-pytest' 'python-pyfakefs' 'python-pip' 'python-tests')
source=("git+https://github.com/python/importlib_metadata.git#tag=v$pkgver?signed")
sha512sums=('af2f8c5e3fbadd3c7c6a00a32b7811dcf9b4e79e7579e0aa08327566b381f9ca3ab0fcf93f73a6c3622ffcbd4a87d4e365537374687e419423568a3044d06d87')
validpgpkeys=('CE380CF3044959B8F377DA03708E6CB181B4C47E') # Jason R. Coombs <jaraco@jaraco.com>

build() {
  cd importlib_metadata
  python -m build -nw
}

check() {
  cd importlib_metadata
  python -m pytest --ignore exercises.py
}

package() {
  cd importlib_metadata
  python -m installer -d "$pkgdir" dist/*.whl
}
