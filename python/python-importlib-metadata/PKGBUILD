# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-importlib-metadata
pkgver=8.6.1
pkgrel=1
pkgdesc="Read metadata from Python packages"
url="https://importlib-metadata.readthedocs.io"
license=('Apache-2.0')
arch=(any)
depends=('python-zipp')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-jaraco.test' 'python-pytest' 'python-pyfakefs' 'python-pip' 'python-tests')
source=("git+https://github.com/python/importlib_metadata.git#tag=v$pkgver")
sha512sums=('c569fad7cca85d5a97023f9c940ae74a2f69acdaec8ca05c2eb578181b40b5ab7dc157afbcd2015fbafdd4735505fd58c03ac588badded98ddc2d396087abfaa')
validpgpkeys=('CE380CF3044959B8F377DA03708E6CB181B4C47E') # Jason R. Coombs <jaraco@jaraco.com>

build() {
  cd importlib_metadata
  python -m build -nw
}

check() {
  cd importlib_metadata
  python -m pytest --ignore exercises.py
}

package() {
  cd importlib_metadata
  python -m installer -d "$pkgdir" dist/*.whl
}
