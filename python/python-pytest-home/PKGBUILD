# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pytest-home
_name=${pkgname#python-}
pkgver=0.6.0
pkgrel=2
pkgdesc='Home directory fixtures'
arch=(any)
url=https://github.com/jaraco/pytest-home
license=(MIT)
depends=(python-pytest)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('6a39bf84b409a1b871522b7ac4cb49d37458b9b6f95befe51845b16e7fd37b28ba910fbdb1797287b7831acae3ef2012b05b1e955c40408a4a8fdc50a66be0af')
validpgpkeys=('CE380CF3044959B8F377DA03708E6CB181B4C47E') # Jason R. Coombs <jaraco@jaraco.com>

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  PYTHONPATH="$PWD/${_name/-/_}:$PYTHONPATH" pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"${_name/-/_}"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
