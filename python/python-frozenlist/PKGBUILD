# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=python-frozenlist
pkgver=1.7.0
pkgrel=1
pkgdesc='FrozenList is a list-like structure which can be made immutable'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aio-libs/frozenlist'
license=('Apache-2.0')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'cython'
  'python-build'
  'python-expandvars'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('007118195032ebeeba172d2113b8e0af330d6a1ed67ad3ab4313c01b4948a3d5890e526fb3258e930e3ea8a221f239fd061abe3eead0c6b6196a95e78fb29422')
b2sums=('8ce7444524bcc1d3bc0c4288a21a06830fb461dbb2c6226995dddca5574f90a88aac03bf21caa0401ffacf5d069096c2f7430325c74ae26105d03c6ff7348c06')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" CHANGES.rst README.rst
}

# vim: ts=2 sw=2 et:
