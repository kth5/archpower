# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=python-frozenlist
pkgver=1.8.0
pkgrel=2
pkgdesc='A list-like structure which can be made immutable'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aio-libs/frozenlist'
license=('Apache-2.0')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'cython'
  'python-build'
  'python-expandvars'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('dc1ae0bdec79a02de81adfbf280c26126dc5c8cdae22eb80b28d1ca65445f945cd71ce9bfdbf03d5aef8f36af13862c57ce5d1f159d0177889f403b871744118')
b2sums=('e7f3948385cea658ec0fd3a348092aa17b74f01c0ab3bb712a90353032ae011636fa5089da57495640f8b9ab8668f49b227576aba973e6521d7224bcf9914825')

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest --override-ini="addopts="
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 -t "$pkgdir/usr/share/doc/$pkgname" CHANGES.rst README.rst
}
