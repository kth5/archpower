# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-log
pkgver=7.2.0
pkgrel=3
pkgdesc="Oslo Logging Library"
arch=(any)
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
         'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
         'python-dateutil')
makedepends=('git' 'python-build' 'python-installer')
checkdepends=('python-stestr' 'python-testtools' 'python-oslotest' 'python-eventlet')
source=("git+https://github.com/openstack/oslo.log.git#tag=$pkgver")
sha512sums=('d1d18d1fcf595b3138d4a103f0586b5b6df987c0595e9e663651224c022c7326436f2814b6f8f0d8e3f220331fb89a383fe26d8be81c6c128cf4394cdeedf6db')

build() {
  cd oslo.log
  python -m build --wheel --no-isolation
}

check() {
  cd oslo.log
  stestr run
}

package() {
  cd oslo.log
  python -m installer --destdir "$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
