# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-log
pkgver=5.5.1
_commit=a6c4f8a9c62ae264f819716177f3da899eaf1a54
pkgrel=2
pkgdesc="Oslo Logging Library"
arch=(any)
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
         'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
         'python-dateutil')
makedepends=('git' 'python-setuptools')
checkdepends=('python-stestr' 'python-testtools' 'python-oslotest' 'python-eventlet')
source=("git+https://github.com/openstack/oslo.log.git#commit=$_commit")
sha512sums=('b9d0c9e177d37df3279e50bd110ee388b7af3e72a47cd3729ae3a86a69d4186ad94d3262efb6aaa34b868b3b0c99077bc35ee2cd7afbe4792d8c70e2ad2a8407')

build() {
  cd oslo.log
  python setup.py build
}

check() {
  cd oslo.log
  stestr run
}

package() {
  cd oslo.log
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
