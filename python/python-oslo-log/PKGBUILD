# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-log
pkgver=7.1.0
pkgrel=1
pkgdesc="Oslo Logging Library"
arch=(any)
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
         'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
         'python-dateutil')
makedepends=('git' 'python-setuptools')
checkdepends=('python-stestr' 'python-testtools' 'python-oslotest' 'python-eventlet')
source=("git+https://github.com/openstack/oslo.log.git#tag=$pkgver")
sha512sums=('541526f0ccc3f4f4a8bf45449b6737956a01cfe4f15fa04980fb60532fab6a06fcb440b8f1be8971b7b68e36c8ad25f53a75b4797d6a3ae5bcd22ed912ad94b8')

build() {
  cd oslo.log
  python setup.py build
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd oslo.log
  stestr run
}

package() {
  cd oslo.log
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
