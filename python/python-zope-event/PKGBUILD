# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgname=python-zope-event
_pkgname=zope.event
pkgver=5.1.1
pkgrel=1
pkgdesc="Provides a simple event system"
arch=(any)
url="https://github.com/zopefoundation/zope.event"
license=('ZPL-2.1')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-zope-testrunner')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1eef1aa743ab30b3b7e4582a1aafdfe275644485dd6b39c3638a05d478a90cb07e35e5319e48a9316809aeeda39f07eff61c246d56692ba853f790bdd83de618')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
