# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgname=python-zope-event
_pkgname=zope.event
pkgver=6.0
pkgrel=1
pkgdesc="Provides a simple event system"
arch=(any)
url="https://github.com/zopefoundation/zope.event"
license=('ZPL-2.1')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-zope-testrunner')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('53e1590da4f0e141ba2615db8889841ee7f28e238a8e9902d66ee59bb32bdde5f89b679a05da1b0d83002156f119d11143f995799ed7cd515ac782c1a27ba6fd')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m zope.testrunner -vc --test-path src/
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
