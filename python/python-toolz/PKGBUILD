# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>

_name=toolz
pkgname=python-toolz
pkgver=1.0.0
pkgrel=2
pkgdesc="A functional standard library for Python"
arch=(any)
url="https://github.com/pytoolz/toolz/"
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=(
  "$url/archive/$pkgver/$_name-$pkgver.tar.gz"
  "$pkgname-$pkgver-shakespeare.txt::http://www.gutenberg.org/files/100/100-0.txt"
)
sha512sums=('750e153fe3dc072b9ab6e541660c4a8ba8b51b2ef0ebd78cb2adf75fa915ac80f82fe3fa973aa8873df7b37ff1147594253dea9d19b012f807f18e9038f76f05'
            '405b316309156bbd516340285e17c3186e82d0db0d936222a811f1aac010ea342416dff3acb4d14ca929269b91c177456455a6804be11ea12444626301d1a3ed')
b2sums=('29a8870c8f9b1a3bbdaecd2db14f3cdaeaed04c5385deef57c259bd0c6641f8647be0dd2b73711c829fe62dc0aa2ab6b1f683f040d01ee0aed100eb0f20a3416'
        'db4fc0ea2f373642c415d58eefd5c556476651c2b1d795ee111747753e450e02be55fb30cf67ca6ce9aedd9d5b375f13fefc9a191d2fe6fb1f963eff07f74329')

prepare() {
  cp "$pkgname-$pkgver-shakespeare.txt" "$_name-$pkgver/bench/shakespeare.txt"
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
