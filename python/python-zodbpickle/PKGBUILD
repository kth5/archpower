# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zodbpickle
_pkgname=${pkgname#python-}
pkgver=4.3
pkgrel=2
pkgdesc="Fork of Python's pickle module to work with ZODB"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/zopefoundation/zodbpickle"
license=('PSF-2.0 AND ZPL-2.1')
depends=(
  'glibc'
  'python'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-tests'
  'python-zope-testrunner'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('7ccc1523bcc56ab4745a4f73c16cec80c155586978e2ab479c30a88b79148c8a')

build() {
  cd "$_pkgname-$pkgver"
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd "$_pkgname-$pkgver"
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$PWD/tmp_install/$site_packages"
  zope-testrunner --test-path=src -vc
}

package() {
  cd "$_pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
