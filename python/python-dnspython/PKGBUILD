# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Mathijs Kadijk <maccain13@gmail.com>

_name=dnspython
pkgname=python-dnspython
pkgver=2.7.0
pkgrel=1
epoch=1
pkgdesc="A DNS toolkit for Python"
arch=(any)
url="http://www.dnspython.org"
license=('ISC')
depends=('python')
makedepends=(
  'python-build'
  'python-hatchling'
  'python-installer'
  'python-wheel'
)
checkdepends=(
  'python-cryptography'
  'python-idna'
  'python-pytest'
  'python-trio'
)
optdepends=(
  'python-cryptography: DNSSEC support'
  'python-requests-toolbelt: DoH support'
  'python-idna: support for updated IDNA 2008'
  'python-curio: async support'
  'python-trio: async support'
  'python-sniffio: async support'
)
source=("https://github.com/rthalley/dnspython/archive/v$pkgver/$_name-$pkgver.tar.gz")
sha256sums=('a18503bcd166c9ecb6b9ed91f38ed03d0d7ca282c4ac57282c9fd6b85726fc4b')

build() {
  cd dnspython-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd dnspython-${pkgver}
  pytest
}

package() {
  cd dnspython-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
