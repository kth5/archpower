# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pyproj
pkgver=3.7.2
pkgrel=1
pkgdesc="Python interfaces to PROJ.4 library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/pyproj4/pyproj"
license=(custom)
depends=(proj python-certifi)
makedepends=(python-setuptools python-wheel python-build python-installer cython)
checkdepends=(python-pytest python-numpy python-pandas python-shapely python-xarray)
source=(https://github.com/pyproj4/pyproj/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a13e65d9c3de0b3a7a510746165ed55163924298f2222523d4f381a8ee58fbcf')

prepare() {
  # Remove RPATH
  sed -i 's/library_dirs if os.name.*/None/' pyproj-$pkgver/setup.py
}

build() {
  export PROJ_DIR=/usr
  cd pyproj-$pkgver
  python -m build --wheel --no-isolation
}

# Import issues, no time to investigate
#check() {
#  cd pyproj-$pkgver
#  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
#  PYTHONPATH="${PWD}"/build/lib.linux-$CARCH-${python_version} pytest -vv --color=yes
#}

package() {
  cd pyproj-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE_proj "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
