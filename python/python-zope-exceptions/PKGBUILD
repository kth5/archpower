# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-exceptions
pkgver=5.0.1
pkgrel=1
pkgdesc="Generic exceptions and implementations for Zope"
arch=(any)
url="https://github.com/zopefoundation/zope.exceptions"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
makedepends=('python-setuptools' 'python-zope-interface')
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.exceptions/archive/$pkgver.tar.gz")
sha512sums=('1b599b4e4cd3d3e3a9a1dd793d912d2ab860be4167f16cdbef092319fd46105401db8b16e04aacb18299da5d795fcbad488ce644f184b3336e8ffcf7f2306bc4')

build() {
  cd zope.exceptions-$pkgver
  python setup.py build
}

check() {
  cd zope.exceptions-$pkgver
  PYTHONPATH="$PWD/build/lib" zope-testrunner --test-path=src
}

package() {
  cd zope.exceptions-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
