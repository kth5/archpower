# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-exceptions
pkgver=5.1
pkgrel=1
pkgdesc="Generic exceptions and implementations for Zope"
arch=(any)
url="https://github.com/zopefoundation/zope.exceptions"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
makedepends=('git' 'python-setuptools' 'python-zope-interface')
checkdepends=('python-zope-testrunner')
source=("git+https://github.com/zopefoundation/zope.exceptions.git#tag=$pkgver")
sha512sums=('a4416eb47db0ec1e9d6a921bdf2aaa53b4ea7e764292c495fb3b7ad0cb6181b5ca14a81f7a08a8d47dd29e9c40280805956642bb9b54f4eb33e3c4c5a4c77b1c')

build() {
  cd zope.exceptions
  python setup.py build
}

check() {
  cd zope.exceptions
  PYTHONPATH="$PWD/build/lib" zope-testrunner --test-path=src
}

package() {
  cd zope.exceptions
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
