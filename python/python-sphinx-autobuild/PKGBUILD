# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=sphinx-autobuild
pkgname=python-$_pkgname
pkgver=2024.04.13
pkgrel=2
pkgdesc='Rebuild Sphinx documentation on changes, with live-reload in the browser'
arch=('any')
url='https://github.com/executablebooks/sphinx-autobuild'
license=('BSD')
depends=('python' 'python-sphinx' 'python-colorama' 'python-watchfiles' 'uvicorn' 'python-starlette')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('3d444e7e90cd5dfba7fbc252793627c7fef16281888363fb0b881ebdfdb1e0e6fcd6e326d4a396851725180c82e5d9ffb83ab592d21243bb2fd0fdd2015e8e33')

build() {
  cd $_pkgname-$pkgver

  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH="$PWD" pytest
}

package() {
  cd $_pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
