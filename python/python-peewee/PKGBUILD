# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Blackleg <blacklg@openaliasbox.org>
# Contributor: Nidhogg
# Contributor: juantascon

pkgname=python-peewee
pkgver=3.19.0
pkgrel=1
pkgdesc="A small, expressive orm"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/coleifer/peewee"
license=('MIT')
depends=(
  'glibc'
  'python'
  'sqlite'
)
makedepends=(
  'cython'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-apsw'
  'python-flask'
  'python-gevent'
  'python-psycopg2'
)
optdepends=(
  'python-psycopg2: for PostgreSQL database support'
  'python-pymysql: for MySQL database support'
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2924cbb8009f7523bf78aab2f7cc17ce5abfb5f45fbc25ce074e597934e2bd269e1806672b95c570fb199169fefc8e23551254934d964deb69596b8f3c27e6d3')

prepare() {
  cd ${pkgname#python-}-$pkgver
  # Python 3.13 compatibility
  sed -i 's/isinstance(value, (int, long))/isinstance(value, int)/' \
    playhouse/_sqlite_ext.pyx
}

build() {
  cd ${pkgname#python-}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  python -m unittest discover -v
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
