# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: cclin <cclinet@outlook.com>

pkgname=python-aiofiles
pkgver=25.1.0
pkgrel=2
pkgdesc='File support for asyncio'
arch=(any)
license=(Apache-2.0)
url=https://github.com/Tinche/aiofiles
depends=(python)
makedepends=(
  git
  python-build
  python-hatch-vcs
  python-hatchling
  python-installer
)
checkdepends=(
  python-pytest-asyncio
  python-pytest
)
_tag=348f5ef6561c2b2c8a7497bd10487eab4102332f
source=(git+https://github.com/Tinche/aiofiles.git#tag=${_tag})
b2sums=('b9cf863194e5244d8570707720201c06338dc3dfe2a0042970fba1839ab702cd216f12b50b8bd5e5c02f14c8c5860692f97cbacc15e04548236fd92424dcd051')

pkgver() {
  cd aiofiles
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiofiles
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd aiofiles
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  python -m installer --destdir="${pkgdir}" aiofiles/dist/*.whl
  install -Dm 644 aiofiles/LICENSE -t "${pkgdir}"/usr/share/licenses/python-aiofiles/
}

# vim: ts=2 sw=2 et:
