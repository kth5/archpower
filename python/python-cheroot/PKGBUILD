# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgname=python-cheroot
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=11.1.2
pkgrel=2
arch=(any)
url="https://github.com/cherrypy/cheroot"
license=(BSD-3-Clause)
depends=(
  python
  python-jaraco.functools
  python-more-itertools
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-chardet
  python-colorama
  python-jaraco.context
  python-jaraco.text
  python-portend
  python-pyopenssl
  python-pytest
  python-pytest-mock
  python-pytest-rerunfailures
  python-pytest-sugar
  python-pytest-xdist
  python-requests-toolbelt
  python-requests-unixsocket
  python-trustme
  python-urllib3
  python-watchdog
)
optdepends=('python-pyopenssl: for SSL and certificate handling within cheroot')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d5dc548c756f660cb0d7383c10b0650158f11a1bf2303aba9dfe1cbaee7ce683c68760036e8921305ad64bcc326e60ec2eead7e33898673304fb2d622ffb42e2')
b2sums=('cb3ee2ec15db1b36c08b4829693bebe68077a1a96e1052eb6638376e62a3b469a1c6f0a89a126479bd55ae572eda936be13f673204c4a70b076fc3d9edea3c29')

build() {
  cd ${pkgname#python-}-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}-$pkgver
  pytest --override-ini="addopts=" -n auto \
    --ignore=cheroot/test/test_server.py \
    --deselect=cheroot/test/test_ssl.py::test_http_over_https_error \
    -W=ignore::DeprecationWarning \
    -W=ignore::pytest.PytestUnhandledThreadExceptionWarning
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
