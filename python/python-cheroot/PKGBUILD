# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgname=python-cheroot
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=11.1.1
pkgrel=1
arch=(any)
url="https://github.com/cherrypy/cheroot"
license=(BSD-3-Clause)
depends=(
  python
  python-jaraco.functools
  python-more-itertools
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-chardet
  python-colorama
  python-jaraco.context
  python-portend
  python-pyopenssl
  python-pytest
  python-pytest-mock
  python-pytest-rerunfailures
  python-pytest-sugar
  python-pytest-xdist
  python-requests-toolbelt
  python-requests-unixsocket
  python-trustme
  python-urllib3
  python-watchdog
)
optdepends=('python-pyopenssl: for SSL and certificate handling within cheroot')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('59599d2f260397875d4ae077991c5c490014cd6e9cfde95e93c1c110492fae988b53497009313d46beb609f14e5297d33865bd07666cde05c43fa44ca57e8daf')
b2sums=('f5ac4679ce38114c656a442ffbb544638dca7852aaa76ebe1b213a6029c26c449d38e8ca9a094778b10b83a1e80cc08b0153d57abb703b028e0e07d95d52e491')

build() {
  cd ${pkgname#python-}-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  return 0
  cd ${pkgname#python-}-$pkgver
  pytest --override-ini="addopts=" -n auto \
    --ignore=cheroot/test/test_server.py \
    -W=ignore::DeprecationWarning \
    -W=ignore::pytest.PytestUnhandledThreadExceptionWarning
}

package() {
  cd ${pkgname#python-}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
