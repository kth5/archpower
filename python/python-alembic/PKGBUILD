# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: googol <googol@posteo.de>
# Contributor: Oliver Mangold <o.mangold at gmail dot com>

pkgname=python-alembic
pkgver=1.16.1
pkgrel=1
pkgdesc='Lightweight database migration tool for usage with SQLAlchemy'
url='https://github.com/sqlalchemy/alembic'
arch=(any)
license=(MIT)
depends=(
  python
  python-mako
  python-sqlalchemy
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/sqlalchemy/alembic/archive/refs/tags/rel_${pkgver//./_}.tar.gz
)
sha512sums=('94fa390de17a4c1fa6fc5b514498fc43aa1b59d208efc32c8efbe52f6d5b571cd89c9bdfdba7971f4f160ea67c00bbcc41b5f5f704a2c53e2bbb8e12162d5189')
b2sums=('e384bdb31d735763fdff59b19c3926babf8db913e3edb0d57e07830633cb35966eb9dfbedcef35907726fc4fad84bced8296303afb80a2e0442ab8162ef84a3e')

prepare() {
  cd alembic-rel_${pkgver//./_}
  # do not treat warnings as errors
  sed 's|-p no:warnings||g' -i setup.cfg

  sed -i '/tag_build=dev/d' setup.cfg
}

build() {
  cd alembic-rel_${pkgver//./_}
  python -m build --wheel --no-isolation
}

check() {
  cd alembic-rel_${pkgver//./_}
  pytest -vv
}

package() {
  cd alembic-rel_${pkgver//./_}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
