# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-stestr
pkgver=4.2.0
pkgrel=2
pkgdesc="A test runner runner similar to testrepository"
url="https://stestr.readthedocs.io/en/latest/"
license=('Apache-2.0')
arch=(any)
depends=('python-cliff' 'python-subunit' 'python-fixtures' 'python-testtools'
         'python-yaml' 'python-voluptuous' 'python-tomlkit')
makedepends=('git' 'python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-ddt')
source=("git+https://github.com/mtreinish/stestr.git#tag=$pkgver")
sha512sums=('232aba1b37f42c13695e586023207259292fc546712d539857095a5819da6091059ec8c1d1262287e2d7f8c7ef6771c5533a730ef5539efdd7f30207b205853e')

build() {
  cd stestr
  python -m build --wheel --no-isolation
}

check() {
  cd stestr
  python -m venv --system-site-packages testenv
  testenv/bin/python -m installer dist/*.whl
  PATH="$PWD/testenv/bin:$PATH" testenv/bin/stestr run
}

package() {
  cd stestr
  python -m installer --destdir="$pkgdir" dist/*.whl
}
