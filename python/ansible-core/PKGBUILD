# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible-core
pkgver=2.15.2
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=(any)
url='https://pypi.org/project/ansible-core'
license=('GPL3')
depends=(
  'python'
  'python-cryptography'
  'python-jinja'
  'python-packaging'
  'python-pyyaml'
  'python-resolvelib'
  # not directly required, but either convenient or indirectly required
  'python-paramiko'
  'python-typing-extensions'
)
makedepends=(
  'python-build'
  'python-docutils'
  'python-installer'
  'python-setuptools'
  'python-straight.plugin'
  'python-wheel'
)
checkdepends=(
  'git'
  'openssh'
  'python-bcrypt'
  'python-botocore'
  'python-passlib'
  'python-pexpect'
  'python-py'  # missing as dependency of python-pytest-forked
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-xdist'
  'python-pytest-forked'
  'python-pywinrm'
  'python-voluptuous'
)
optdepends=(
  'python-dnspython: for dig lookup'
  'python-jmespath: json_query support'
  'python-netaddr: for the ipaddr filter'
  'python-passlib: crypt values for vars_prompt'
  'python-pip: for module to manage Python libarary dependencies'
  'python-pywinrm: connect to Windows machines'
  'python-setuptools: for module to manage Python libarary dependencies'
  'python-systemd: for journald support'
  'sshpass: for ssh connections with password'
)
provides=('python-ansible' 'ansible-base')
replaces=('ansible-base')
backup=('etc/ansible/ansible.cfg')
source=("https://pypi.python.org/packages/source/a/ansible-core/ansible-core-${pkgver}.tar.gz"
	"update_urls_crypto_unit_test.patch")
sha512sums=('75cfc9a5f104d15a83ea464e34b9b2d351fcddc23b7bc4e86a1975d2aef22a336c865a4bd25cee58ba64b67eebfc2553ca55ea581ffcfc4e5261af46fbdf7e55'
            '3637e46c5af2ae2b1a5c5a7304caaa60a7a0eefff87ffa5ac3477600c1b5afa69bc33baf8045eda8da696cad8094b31a4ee84773eebb8193c5777ff2262951f4')
b2sums=('0be12f45e2dffc9a57974d64092b53b1f74509263d4a68da0485bc691340ca9c1336714a33dd8099f3a11fda1e9fea98d1d88b4af9611e26b0e7c0823eb86ddc'
        'c07ab96d1b8d9e75b96164fddd0350972ff5bfca794477fdc980276e9aec00c03b78109a21a79e0bd57adc9c39d19437bb5e5006523be1b4d87fed6414b9ad2f')

prepare() {
  cd $pkgname-$pkgver

  # Update urls unit test to support latest cryptography release (needed for unit test to pass)
  # https://github.com/ansible/ansible/pull/81296
  patch -Np1 <$srcdir/update_urls_crypto_unit_test.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation

  make docs
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  # tests require upstream wrapper to find ansible-core internals: https://github.com/ansible/ansible/issues/80472
  cd $pkgname-$pkgver
  # we do not have libselinux packaged
  rm -v test/units/module_utils/basic/test_selinux.py
  bin/ansible-test units --python $python_version --truncate 0
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 COPYING -t "${pkgdir}"/usr/share/doc/${pkgname}/
  install -Dm644 examples/ansible.cfg -t "${pkgdir}"/etc/ansible/
  install -Dm644 examples/{ansible.cfg,hosts} -t "${pkgdir}"/usr/share/ansible/doc/examples/
  install -Dm644 docs/man/man1/*.1 -t "${pkgdir}"/usr/share/man/man1/
}
