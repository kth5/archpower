# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible-core
_pkgname=ansible
pkgver=2.18.6
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=(any)
url='https://www.ansible.com'
license=('GPL-3.0-or-later AND BSD-2-Clause AND PSF-2.0 AND MIT AND Apache-2.0')
depends=(
  'python'
  'python-cryptography'
  'python-jinja'
  'python-packaging'
  'python-pyyaml'
  'python-resolvelib'
  # not directly required, but either convenient or indirectly required
  'python-paramiko'
  'python-typing_extensions'
)
makedepends=(
  'python-build'
  'python-docutils'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'git'
  'openssh'
  'python-bcrypt'
  'python-botocore'
  'python-passlib'
  'python-pexpect'
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-xdist'
  'python-pytest-forked'
  'python-pywinrm'
  'python-voluptuous'
)
optdepends=(
  'python-argcomplete: shell completions'
  'python-dnspython: for dig lookup'
  'python-jmespath: json_query support'
  'python-netaddr: for the ipaddr filter'
  'python-passlib: crypt values for vars_prompt'
  'python-pip: for module to manage Python libarary dependencies'
  'python-pywinrm: connect to Windows machines'
  'python-setuptools: for module to manage Python libarary dependencies'
  'python-systemd: for journald support'
  'sshpass: for ssh connections with password'
)
provides=('python-ansible' 'ansible-base')
replaces=('ansible-base')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ansible/ansible/archive/refs/tags/v${pkgver}.tar.gz"
        'bump_resolvelib_upper_version_bound.patch'
        'relax_setuptools_version_requirements.patch')
sha512sums=('1b23cd170ecaf79ce0b7e5260e743912d1a9e7a6ab185527dd65dcb16a2788add2054c8b6ec1a6197190c9910f81d6287493ae17860898de9a70251dff1a77a0'
            '420d03e64a189043c7fbaa6a5799eed04ea28c4c2729197b4a51467ec672ab73325f6ea46d02a0ad84bc89aa1a8ffc4d23c716003215029eb60fc68577154e5c'
            'c8258287560e76ec31f3ddddca2f248d7efdc94c640c450f9edf866b6c9068d9eddbb4723c70b3a55b4139ee24f01c7458159c64070e1ce2237b81e7681db42e')
b2sums=('72bd27b2d30522657cbe175772831697a9c3859fe8b941038584e0d52f8da2d769998ece2b186554903f55ed175b11d89d89bb6d6c388936a3c30530427584d5'
        'f2885491361673f067716b0d130a9043843715513f55bcb6fefc86159169056b567922f642816a2cfbbb7b6d31efc94c1f204f72865dc33881cf13886aa3967b'
        '20063c5153c0d7b4a0f93ded55de09f9e396ac6902f551da20b2fdf066e629498343a3731c942ea3625879cc95774120259e9516c5a34c2cac51b5877bee6e79')

prepare() {
  cd "${_pkgname}-${pkgver}"

  # Temporary patch to allow build / checks to pass with resolvelib 1.1.0
  # For now, upstream has set the upper version bound for it at `< 1.1.0`
  # See https://github.com/ansible/ansible/blob/devel/requirements.txt#L10-L15
  # And https://github.com/ansible/ansible/blob/devel/lib/ansible/galaxy/dependency_resolution/providers.py#L40-L43
  #
  # EDIT: resolvelib version requirement has been bumped but not yet tagged
  # See https://github.com/ansible/ansible/commit/771f7ad29ca4d259761eaa88673c2e32f6412bbe
  patch -Np1 < "${srcdir}/bump_resolvelib_upper_version_bound.patch"

  # Temporary patch to allow building with latest setuptools
  # Currently, upstream has set the upper version version bound for it at `< 72.1.0`
  # See https://github.com/ansible/ansible/blob/devel/pyproject.toml#L2
  patch -Np1 < "${srcdir}/relax_setuptools_version_requirements.patch"
}

build() {
  cd "${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
  python packaging/cli-doc/build.py man --output-dir man
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  # tests require upstream wrapper to find ansible-core internals: https://github.com/ansible/ansible/issues/80472
  cd "${_pkgname}-${pkgver}"
  # we do not have libselinux packaged
  rm -v test/units/module_utils/basic/test_selinux.py
  bin/ansible-test units --python "${python_version}" --truncate 0
}

package() {
  cd "${_pkgname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 man/ansible*1 -t "${pkgdir}/usr/share/man/man1/"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm 644 licenses/MIT-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/MIT-license.txt"
  install -Dm 644 licenses/simplified_bsd.txt "${pkgdir}/usr/share/licenses/${pkgname}/simplified_bsd.txt"
}
