# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marcin "avalan" Falkiewicz <avalatron@gmail.com>
# Contributor: C Anthony Risinger <anthony@xtfx.me>

pkgname=python-ply
pkgver=3.11
pkgrel=13
pkgdesc='Implementation of lex and yacc parsing tools'
arch=(any)
url='https://www.dabeaz.com/ply/'
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("https://pypi.io/packages/source/p/ply/ply-$pkgver.tar.gz"
        python-3.11.patch
        LICENSE)
sha512sums=('37e39a4f930874933223be58a3da7f259e155b75135f1edd47069b3b40e5e96af883ebf1c8a1bbd32f914a9e92cfc12e29fec05cf61b518f46c1d37421b20008'
            '7f55edd7b78abdd786e4a7b46deb912878d272cf63029ceff285d9387f14c364aa0c9ff1b53d8d60df377e848d5f9272c814948478d6a39f543dac4cca3bc270'
            '5cea085a23ed260afc9084380c1d59e3942c2385089a8eb2391a89c85cde4df2ceb7eed21bb8c4d0cad5b78e59742da69e4c5c78fd0e223f5c99c56e9296a075')

prepare() {
  patch -d ply-$pkgver -p1 < python-3.11.patch # Fix tests with python 3.11 (Fedora)
}

check() {
  cd "ply-$pkgver/test"
  python testlex.py
  python testyacc.py
}

package() {
  cd "ply-$pkgver"
  python setup.py install --root="$pkgdir"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
