# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=clikit
pkgname=python-clikit
pkgver=0.6.2
pkgrel=1
pkgdesc="clikit allows you to create beautiful and testable command-line interfaces"
arch=('any')
url="https://github.com/sdispater/${_pkgname}"
license=('MIT')
depends=('python-crashtest' 'python-pastel' 'python-pylev')
makedepends=('python-dephell')
checkdepends=('python-pytest-mock')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('4bb1d934e0c0a93f25429919242d11d60dcd1d9c65ac096033092d7973cbfc6e')
b2sums=('b72f245128c698375f97139cf245bdc8c017e3e2d142f6e3183a42620b23a024531e5b14f05163c6defa9ee54c52c19a64932c62484f2ec8da5c75ea1b7d3d27')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # poetry-generated setup.py are fatally broken, see:
    # https://github.com/sdispater/poetry/issues/866
    dephell deps convert --from pyproject.toml --to setup.py
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    PYTHONPATH=$PWD/src python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
