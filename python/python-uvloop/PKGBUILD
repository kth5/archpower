# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-uvloop
pkgver=0.20.0
pkgrel=1
pkgdesc='Ultra fast asyncio event loop'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://github.com/MagicStack/uvloop
license=(
  APACHE
  MIT
)
depends=(
  glibc
  python
  libuv
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=2d35f106d3ccc51d648b4ab90c964c0748b5873f
source=(git+https://github.com/MagicStack/uvloop.git#tag=${_tag}
        cython3.patch)
b2sums=('c863fbe169e5c968a5bc312f2c3c8b320e903c9fc7676e7bd6eef4d3a38f78a6f1b899244773ed9a9ffe0f83370622be4b24b76a020a75dc2502c379de3a5a20'
        '8fca88bd34eaa0233340e1aa05a27ec9b98c52a8dd6fcf741b19d5d5057dfa5ec9d3758d120e2a3f643a269dee4330568c288f63f0065b8a8b10aac5e2a752f5')

prepare() {
  sed 's/self.use_system_libuv = False/self.use_system_libuv = True/' -i uvloop/setup.py
  sed -e 's|>=0.29.36,<0.30.0|>=0.29.36|g' -i uvloop/pyproject.toml
  patch -d uvloop -p1 < cython3.patch
}

pkgver() {
  cd uvloop
  git describe --tags | sed 's/^v//'
}

build() {
  cd uvloop
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" uvloop/dist/*.whl
  install -Dm 644 uvloop/LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm 644 uvloop/LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
