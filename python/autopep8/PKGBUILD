# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=autopep8
pkgver=2.1.0
pkgrel=2
epoch=1
pkgdesc='A tool that automatically formats Python code to conform to the PEP 8 style guide'
arch=(any)
url='https://github.com/hhatto/autopep8'
license=('MIT')
depends=(
  'python'
  'python-pycodestyle'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-pytest')
_commit='8f12c0ab83c3dcc9eaf5078393dd2c53e1d253d2'
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('bd0b5b7dd3d680f06135f3d8a7af1c48059fdee5caa73f115137e62e25d0889206988a87ed86c2bd55bff6653c462e1bdfe2a8e9f8042b2f32fbef2e95b4eb07')
b2sums=('5595d00c8b915b4e57e44606d0bd62238461a44db2fa91f0bb2205bb801a4de2a6ba372e33958d0c88b5f6dbe6b3c91cb518cd36cae9a077ba4ac9fc91a5b4fa')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/$pkgname-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
