# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pyftpdlib
_pkgname=${pkgname#python-}
pkgver=2.0.1
pkgrel=3
pkgdesc="Extremely fast and scalable Python FTP server library"
arch=(any)
url="https://github.com/giampaolo/pyftpdlib"
license=(MIT)
depends=(
  python
  python-pyasynchat
  python-pyasyncore
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pyopenssl
  python-pytest
)
optdepends=(
  'python-pyopenssl: FTPS support'
  'python-psutil: to keep track of FTP server memory usage'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/release-$pkgver.tar.gz")
sha256sums=('962b58f5affa7fca06f121c8922311f7b7fb71e651e859d3de97995d0bec00d9')

build() {
  cd "$_pkgname-release-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-release-$pkgver"
  pytest \
    --deselect pyftpdlib/test/test_functional_ssl.py::TestFtpRetrieveDataTLSMixin::test_restore_on_retr
}

package() {
  cd "$_pkgname-release-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
