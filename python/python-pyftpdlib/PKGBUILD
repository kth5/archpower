# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pyftpdlib
pkgver=2.1.0
pkgrel=2
pkgdesc="Extremely fast and scalable Python FTP server library"
arch=(any)
url="https://github.com/giampaolo/pyftpdlib"
license=(MIT)
depends=(
  python
  python-pyasynchat
  python-pyasyncore
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pyopenssl
  python-pytest
)
optdepends=(
  'python-pyopenssl: FTPS support'
  'python-psutil: to keep track of FTP server memory usage'
)
source=("$url/archive/release-$pkgver/$pkgname-release-$pkgver.tar.gz")
sha256sums=('22ef6dc8fa4a8ca66c4de413721239ed0dfb4d7b7a8ee26f40ebf084bbfd1532')

build() {
  cd "${pkgname#python-}-release-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname#python-}-release-$pkgver"
  pytest --override-ini="addopts=" \
    --deselect=tests/test_functional_ssl.py::TestFtpListingCmdsTLSMixin::test_nlst \
    --deselect=tests/test_functional_ssl.py::TestFtpListingCmdsTLSMixin::test_nlst \
    --deselect=tests/test_functional_ssl.py::TestFtpListingCmdsTLSMixin::test_stat
}

package() {
  cd "${pkgname#python-}-release-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
