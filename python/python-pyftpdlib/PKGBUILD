# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pyftpdlib
_pkgname=${pkgname#python-}
pkgver=1.5.10
pkgrel=1
pkgdesc="Extremely fast and scalable Python FTP server library"
arch=(any)
url="https://github.com/giampaolo/pyftpdlib"
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pyopenssl
  python-pytest
)
optdepends=(
  'python-pyopenssl: FTPS support'
  'python-psutil: to keep track of FTP server memory usage'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/release-$pkgver/release-$pkgver.tar.gz")
sha256sums=('993a48b652c0a2ab9af898fd3acbe422e8558ba91d1451eec20db2584a0178de')

_archive="$_pkgname-release-$pkgver"

prepare() {
  cd "$_archive"

  # Don't package tests.
  sed -i "s/packages=\['pyftpdlib', 'pyftpdlib.test'\]/packages=['pyftpdlib']/" setup.py
}

build() {
  cd "$_archive"

  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd "$_archive"

  pytest
}

package() {
  cd "$_archive"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
