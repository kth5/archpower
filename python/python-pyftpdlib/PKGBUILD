# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pyftpdlib
_pkgname=${pkgname#python-}
pkgver=2.0.0
pkgrel=2
pkgdesc="Extremely fast and scalable Python FTP server library"
arch=(any)
url="https://github.com/giampaolo/pyftpdlib"
license=(MIT)
depends=(
  python
  python-pyasynchat
  python-pyasyncore
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-psutil
  python-pyopenssl
  python-pytest
)
optdepends=(
  'python-pyopenssl: FTPS support'
  'python-psutil: to keep track of FTP server memory usage'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/release-$pkgver.tar.gz")
sha256sums=('272b33844bb22118826368b3974f1460a58426c5325f96cc1d6aaf7550d14fa6')

prepare() {
  cd "$_pkgname-release-$pkgver"
  # Don't package tests.
  sed -i "s/packages=\['pyftpdlib', 'pyftpdlib.test'\]/packages=['pyftpdlib']/" setup.py
}

build() {
  cd "$_pkgname-release-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-release-$pkgver"
  pytest
}

package() {
  cd "$_pkgname-release-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
