# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-aiodns
_name=${pkgname#python-}
pkgver=4.0.0
pkgrel=1
pkgdesc='Simple DNS resolver for asyncio'
arch=(any)
url=https://github.com/saghul/aiodns
license=(MIT)
depends=(python-pycares)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-uvloop
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('521626b7368ce7de1f8e4f5df0a891d96702be3e3b886d7e4590062da55945691e9c7e0c66f66d4a6ccbb2656e72dd7f0c29d96d9ff0ba89823c2fb915b9af53')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  # Override addopts as they invoke coverage testing
  PYTHONPATH=aiodns:$PYTHONPATH pytest --override-ini="addopts=" -v -k 'not test_gethostbyaddr'
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/licenses/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname

  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
