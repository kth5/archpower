# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: AlphaJack <alphajack@tuta.io>
# Contributor: D. Can Celasun <can@dcc.im>
# Contributor: Carl George <arch@cgtx.us >

pkgname=mkdocs
pkgver=1.6.1
pkgrel=1
pkgdesc='Project documentation with Markdown'
url=https://www.mkdocs.org
license=(BSD-2-Clause)
arch=(any)
depends=(
  mkdocs-get-deps
  python
  python-babel
  python-click
  python-ghp-import
  python-importlib-metadata
  python-jinja
  python-markdown
  python-markupsafe
  python-mergedeep
  python-packaging
  python-pathspec
  python-pyyaml-env-tag
  python-typing_extensions
  python-yaml
  python-watchdog
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
  python-setuptools
  python-wheel
)
optdepends=("python-lunr: to prebuild search index")
_tag=bb7e8b62185b11d9f59bb7f50b13c15134f62f8a
source=(git+https://github.com/mkdocs/mkdocs.git#tag=${_tag})
b2sums=('89bb03ed4d4c101886029f417e5f9d747c2a3c7d134e189f8fa669b01ef74a28f385a632f3b009e5eafd5f3448d7c9b393f1f9daf5a4ff0eef62c8cd7a863af7')

pkgver() {
  cd mkdocs
  git describe --tags
}

build() {
  cd mkdocs
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" mkdocs/dist/*.whl
  install -Dm 644 mkdocs/LICENSE -t "${pkgdir}"/usr/share/licenses/mkdocs/
}

# vim: ts=2 sw=2 et:
