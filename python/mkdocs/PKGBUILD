# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: AlphaJack <alphajack@tuta.io>
# Contributor: D. Can Celasun <can@dcc.im>
# Contributor: Carl George <arch@cgtx.us >

pkgname=mkdocs
pkgver=1.6.0
pkgrel=1
pkgdesc='Project documentation with Markdown'
url=https://www.mkdocs.org
license=(BSD-2-Clause)
arch=(any)
depends=(
  mkdocs-get-deps
  python
  python-babel
  python-click
  python-ghp-import
  python-importlib-metadata
  python-jinja
  python-markdown
  python-markupsafe
  python-mergedeep
  python-packaging
  python-pathspec
  python-pyyaml-env-tag
  python-typing_extensions
  python-yaml
  python-watchdog
)
makedepends=(
  git
  python-build
  python-hatchling
  python-installer
  python-setuptools
  python-wheel
)
optdepends=("python-lunr: to prebuild search index")
_tag=0998fec7eb32d269f02c6d87071d6163c251db30
source=(git+https://github.com/mkdocs/mkdocs.git#tag=${_tag})
b2sums=('71435da7a11fad8f3a0e43908e4e949521b7484fee4f4050cf02be6eb3f6bf6c1e85941004a8cb7e1293dd0262afc9c981d9a9abb3c0943c420a98e0a684097e')

pkgver() {
  cd mkdocs
  git describe --tags
}

build() {
  cd mkdocs
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" mkdocs/dist/*.whl
  install -Dm 644 mkdocs/LICENSE -t "${pkgdir}"/usr/share/licenses/mkdocs/
}

# vim: ts=2 sw=2 et:
