# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : RÃ©my Oudompheng <remy@archlinux.org>

pkgname=pyalpm
pkgver=0.11.1
pkgrel=1
pkgdesc="Python 3 bindings for libalpm"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://gitlab.archlinux.org/archlinux/pyalpm"
license=('GPL-3.0-or-later')
depends=('python' 'pacman')
makedepends=('git' 'meson-python' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-pytest-pacman')
source=("git+https://gitlab.archlinux.org/archlinux/pyalpm.git#tag=$pkgver")
validpgpkeys=('E499C79F53C96A54E572FEE1C06086337C50773E')
sha512sums=('e99e71f43554a87935621cfdbbe04a4a8c501723716a48c97a84dabd32e16b5ec88bc9202fafa7cc53bfc572d36ea35af8413945d8643bdc3a6362907daccfc7')

prepare() {
  cd "${pkgname}"
}

build() {
  cd "${pkgname}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname}"
  meson setup builddir
  meson compile -C builddir
  meson test -C builddir -v
}

package() {
  cd "${pkgname}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
