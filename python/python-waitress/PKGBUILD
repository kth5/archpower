# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-waitress
pkgver=3.0.0
pkgrel=2
pkgdesc="Waitress WSGI server"
arch=(any)
license=('ZPL-2.1')
url="https://github.com/Pylons/waitress"
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest-cov' 'python-pytest')
source=("https://github.com/Pylons/waitress/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4ec7d002a0c23eb7d0f3fb5091ab101e36609938f5f65ac2c7f444a910e6e33a762fb25aef4c038489d2e4fe89c0a52d8769dfc6d55c79f7851a29473d311ae5')

build() {
  cd waitress-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd waitress-$pkgver
  PYTHONPATH="$PWD"/src pytest
}

package_python-waitress() {
  cd waitress-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
