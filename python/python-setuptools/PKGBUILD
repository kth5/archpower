# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-setuptools
pkgver=69.5.1
pkgrel=1
epoch=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=(any)
url="https://pypi.org/project/setuptools/"
license=('PSF')
groups=(python-build-backend)
depends=(
  'python-jaraco.text'
  'python-more-itertools'
  'python-ordered-set'
  'python-packaging'
  'python-platformdirs'
  'python-tomli'
  'python-validate-pyproject'
)
makedepends=(
  'git'
  'python-setuptools'
)
checkdepends=(
  'python-build'
  'python-ini2toml'
  'python-jaraco.envs'
  'python-jaraco.path'
  'python-pip'
  'python-pytest-enabler'
  'python-pytest-home'
  'python-pytest-timeout'
  'python-sphinx'
  'python-tomli-w'
  'python-wheel'
)
provides=('python-distribute')
replaces=('python-distribute')
source=(
  "git+https://github.com/pypa/setuptools.git#tag=v$pkgver?signed"
  system-validate-pyproject.patch
  add-dependency.patch
  build-no-isolation.patch
)
sha512sums=('206fd64c775c0e9723c59dd512741a98006a835dd1846068af0da23922ccb181a9feb77bce43f19e3744183739a251f333b58070c958e8cd79c7cfce60fb3577'
            '91bcd7ff2cadc83e9ba9582a31954998f23d6d124f84bd746dc518f01870da7e7f7e7fc69a423dc95165499c2d6dfd3669909660e6a59fcdaca57805362af709'
            '9c5d80c753e78bf613572fb789a234984087d0ce96d0bad22b5ed731d83c77bf6d8acfa65c78f6c78f9063be7819c2b58988fdf8e7fc89b55339f94a87b3b21f'
            '9f334d6fceda637058257e425b26b2dc1ab276a62a0c12618c62c43183f32e7c0c36a88639fcad7ecbf2f12261f2f43875042795bc49cb2daf4e093b77479509')
validpgpkeys=('CE380CF3044959B8F377DA03708E6CB181B4C47E') # Jason R. Coombs <jaraco@jaraco.com>

prepare() {
  cd setuptools

  # https://github.com/pypa/setuptools/issues/4334
  git cherry-pick -n 4a0a9ce587515edce83ab97aa5c7943c045ac180

  patch -p1 -i ../system-validate-pyproject.patch

  rm -r {pkg_resources,setuptools}/{extern,_vendor} setuptools/config/_validate_pyproject

  # Upstream devendoring logic is badly broken, see:
  # https://bugs.archlinux.org/task/58670
  # https://github.com/pypa/pip/issues/5429
  # https://github.com/pypa/setuptools/issues/1383
  # The simplest fix is to simply rewrite import paths to use the canonical
  # location in the first place
  for _module in setuptools pkg_resources '' ; do
      find . -name \*.py -exec sed -i \
          -e 's/from '$_module.extern' import/import/' \
          -e 's/from '$_module.extern'\./from /' \
          -e 's/import '$_module.extern'\./import /' \
          -e "s/__import__('$_module.extern./__import__('/" \
          -e 's/from \.\.extern\./from /' \
          {} +
  done

  # Add the devendored dependencies into metadata of setuptools
  patch -p1 -i ../add-dependency.patch

  # Fix tests invoking python-build
  patch -p1 -i ../build-no-isolation.patch

  # Remove post-release tag since we are using stable tags
  sed -e '/tag_build = .post/d' \
      -e '/tag_date = 1/d' \
      -i setup.cfg
}

build() {
  cd setuptools
  export SETUPTOOLS_INSTALL_WINDOWS_SPECIFIC_FILES=0
  python setup.py build
}

check() { (
  # Workaround UTF-8 tests by setting LC_CTYPE
  export LC_CTYPE=en_US.UTF-8

  # https://github.com/pypa/setuptools/pull/810
  export PYTHONDONTWRITEBYTECODE=1

  cd setuptools
  # 1,4: subtle difference introduced by devendoring
  # 2: pip failures related to devendoring,
  # 3,5: TODO
  # 6: jaraco.develop is not packaged
  # 7,8: failing already prior to Python 3.12 rebuild
  PYTHONPATH="$PWD"/build/lib python -m pytest \
    --deselect setuptools/tests/config/test_apply_pyprojecttoml.py::test_apply_pyproject_equivalent_to_setupcfg \
    --deselect setuptools/tests/test_virtualenv.py \
    --deselect setuptools/tests/test_editable_install.py::test_editable_with_prefix \
    --deselect setuptools/_normalization.py::setuptools._normalization.safe_version \
    --deselect setuptools/tests/test_easy_install.py::TestSetupRequires::test_setup_requires_honors_fetch_params \
    --ignore tools/finalize.py \
    --deselect 'setuptools/tests/config/test_apply_pyprojecttoml.py::TestPresetField::test_not_listed_in_dynamic[install_requires-dependencies-value0]' \
    --deselect setuptools/tests/test_editable_install.py::test_debugging_tips
)}

package() {
  cd setuptools
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
}
