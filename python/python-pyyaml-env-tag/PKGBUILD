# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: AlphaJack <alphajack@tuta.io>

pkgname=python-pyyaml-env-tag
pkgver=0.1
pkgrel=1
pkgdesc='A custom YAML tag for referencing environment variables in YAML files'
url=https://github.com/waylan/pyyaml-env-tag
license=(MIT)
arch=(any)
depends=(
  python
  python-yaml
)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
  python-wheel
)
_commit=2bd726793dfbd8b4f6e28e8ca4f676a23a50dc2f
source=(git+https://github.com/waylan/pyyaml-env-tag.git#commit=${_commit})
b2sums=('a05efda51704031635ba35659796d6650fa7c3eb370b7da36e9f8be0108af927a64aa4dc44c3b160b437895fe7f1349f1da9096709777073a32a7706c97c346d')

build(){
 cd pyyaml-env-tag
 python -m build --wheel --no-isolation
}

package(){
 python -m installer --destdir="${pkgdir}" pyyaml-env-tag/dist/*.whl
 install -Dm 644 pyyaml-env-tag/LICENSE -t "${pkgdir}"/usr/share/licenses/python-pyyaml-env-tag/
}

# vim: ts=2 sw=2 et:
