# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-jsonschema-specifications
pkgver=2024.10.1
pkgrel=1
pkgdesc='The JSON Schema meta-schemas and vocabularies, exposed as a Registry'
arch=(any)
url='https://github.com/python-jsonschema/jsonschema-specifications'
license=('MIT')
depends=('python' 'python-referencing')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-hatchling'
  'python-hatch-vcs'
)
checkdepends=('python-pytest')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('8d4b621360561355be3f359c303727ffe479fadfede1e41be8a64727c57898ed62470a39ccd341abd92e3e6d7506b7b7ca4c2df665dde4f423c989956adfc9f8')
b2sums=('7a424a92e7ecaa1e20c161745584fee2f2d4206ef48ac4f210c19ebafd906809d8b1802cb9ced453ae6b935bce4d39502294d9e4623e5bd5913f62b8a779a3e6')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/jsonschema_specifications-$pkgver.dist-info/licenses/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
