# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-jsonschema-specifications
pkgver=2025.4.1
pkgrel=1
pkgdesc='The JSON Schema meta-schemas and vocabularies, exposed as a Registry'
arch=(any)
url='https://github.com/python-jsonschema/jsonschema-specifications'
license=('MIT')
depends=('python' 'python-referencing')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-hatchling'
  'python-hatch-vcs'
)
checkdepends=('python-pytest')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('492fa595d7d713b77dcb62282233c2f0f0c99aa783245bae8cfa340fe04f0512ab132d134b5648e5cb8784925f2f36a3461da2c4f2f6ce101cbbbc5a473f0aa9')
b2sums=('37b4d93c1554ee3ac813df8bf2e33645f92f7484232f4f12dc41c2a1436fcb9bdabf6e7f8576893bb36a3f15138421b9241ed36b9404032ba5727aa5ed669db7')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/jsonschema_specifications-$pkgver.dist-info/licenses/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
