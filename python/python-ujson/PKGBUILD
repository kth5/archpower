# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-ujson
_pkgname=ultrajson
pkgver=5.11.0
pkgrel=2
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/ultrajson/ultrajson"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('BSD')
depends=('python')
makedepends=(python-setuptools-scm python-wheel python-build python-installer)
checkdepends=('python-pytest')
source=(https://github.com/ultrajson/ultrajson/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('908e72720ba3692a7bbc852c21bfd8b6013eb9047f3412beca3ac13656350eb995bd6355f9be288f30d1b878bf4378362b4a0d0500f655d7d4e9698966666b35')
b2sums=('66ad0cfc08288ea9bcfc5cae9903139af4be9d8d5b7fc37ec1965e653b9e820055a79456e34b1c2fed36690cee3a952819a1afb0170a3559eba4303044fc1a04')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

MYARCH=${CARCH}
case "${CARCH}" in
	powerpc*) MYARCH=${CARCH/powerpc/ppc} ;;
esac

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="build/lib.linux-${MYARCH}-cpython-${PYTHONVERSION}" pytest
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
