# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Donald Webster <fryfrog@gmail.com>
pkgname='python-logutils'
_pkgbase="${pkgname#python-}"
pkgver=0.3.5
pkgrel=14
pkgdesc='Python module to parse metadata in video files.'
arch=(any)
url='https://bitbucket.org/vinay.sajip/logutils/'
license=('LGPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/l/logutils/logutils-${pkgver}.tar.gz"
	"unittest-deprecation.patch"
	"logutils-python3-13.patch")
sha512sums=('0194bf4f01e7a29d4211495dc5fead9d4bd5bc1b791d25ff38ad28960a229a9bcdbaa7e6d3b5a4105eef6c9aa159459d6529c2615df113e7ec1ae93aac5cce6c'
            '3e55d37bc6091a29c26d05fd0ce5976c8c0e79ea6a1e98fded1b01763da0a067100b6fe5b2a4f8e6f95ddf7b9e1b0b0f847360da76bf8a8354db0ce7d90d646a'
            '22b1840993d1a7c0f0d049adcc08ebcc14fc16c34faec4cf256c8cb5b4279c9080f98f25ab37ce292ed0756fd188c67e1de34182e8182c29032a0fcfb64421e2')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -Np1 -i ${srcdir}/unittest-deprecation.patch
  patch -Np1 -i ${srcdir}/logutils-python3-13.patch
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  # redis tests want to spin up a redis-server, undesired for downstream packaging
  rm -v ./tests/test_redis.py
  python -m unittest discover -vs ./tests
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py install --root="$pkgdir/" --optimize=1
}
