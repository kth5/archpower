# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=8.2.3
_commit=41ed2420cda8ab7650a39900451099f4730266c3
pkgrel=2
pkgdesc='Retry code until it succeeeds'
arch=(any)
license=('Apache-2.0')
url='https://github.com/jd/tenacity'
depends=('python')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('git' 'python-setuptools-scm' 'python-setuptools' 'python-wheel' 'python-build' 'python-installer')
checkdepends=('python-pytest' 'python-tornado' 'python-typeguard')
source=("git+https://github.com/jd/tenacity.git#commit=$_commit")
sha512sums=('e8d6d0bdd98b4265064463bfdcf6bed53388c9beebe215251f7fa334b15c45e2670f949fbf42246d19ba1053c95055431ec7b5b920400f454ac21f003502742e')

build() {
  cd tenacity
  python -m build --wheel --no-isolation
}

check() {
  cd tenacity
  pytest
}

package() {
  cd tenacity
  python -m installer --destdir="$pkgdir" dist/*.whl
}
