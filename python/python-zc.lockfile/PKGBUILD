# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Hu <integral@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-zc.lockfile
_pkgname=${pkgname#python-}
pkgver=4.0
pkgrel=1
arch=(any)
pkgdesc="Zope testing frameworks"
url="https://github.com/zopefoundation/${_pkgname}"
license=('ZPL-2.1')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-zope-testing' 'python-zope-testrunner')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('412fa836258cfe1c8b4492403d1d917410336fbbdf98c37a6b4a22a6ff7ffe4463ce9a77ac3c8df7bcc11acab2ccbe4435647b993bc02d941ee1967f5d00c978')

build() {
	cd "${_pkgname}-${pkgver}/"
	python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
	cd "${_pkgname}-${pkgver}/"
	python -m venv --system-site-packages test-env
	test-env/bin/python -m installer dist/*.whl
	test-env/bin/python -m zope.testrunner -vc --test-path src/
}

package() {
	cd "${_pkgname}-${pkgver}/"
	python -m installer --destdir="${pkgdir}" dist/*.whl
}
