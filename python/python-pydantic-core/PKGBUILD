# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pydantic-core
pkgname=python-pydantic-core
# WARNING: this package is pinned down to the patch-level version in python-pydantic and should only be updated in lock-step with it
pkgver=2.16.3
pkgrel=1
epoch=1
pkgdesc="Core validation logic for pydantic written in rust "
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/pydantic/pydantic-core"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
  python-typing-extensions
)
makedepends=(
  python-build
  python-installer
  python-maturin
)
checkdepends=(
  python-dirty-equals
  python-hypothesis
  python-pytest
  python-pytest-benchmark
  python-pytest-examples
  python-pytest-mock
  python-pytest-timeout
)
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
        pyo3-0.20.3.patch)
sha512sums=('bcc1c8cc53f72934af6d5d570b6c8ade1dbb96e475f4899aa82436babee68fb9e7c805c373075207402bf0ad9b1c9d756b068ab05aa088ec876abf2a6d34d0aa'
            '5f7b8d40c60260785c84bfcc2c7f6ff01190f7601536dfe8714b8933eec3e081bdda6406b7c5b07f6c4d7910f8a8e4ab703c0a095681d16c0bea42374bfb3cde')
b2sums=('372796a3f864717a87bea4c09c2ae411b8a400110912f45fdcc5b1b03e89bb88d84ba69e2b732dc88c54396a4d984a41003bf2dfbeacf39b082f9b2626152f10'
        'a3b28e042290dd11dad52a0907395e5403097d3b826033a0741928bf492a6d05b50537cf444c70f13b08bb7673f69c3f4e15dde3304c26dfa7516781bf6dd405')

prepare() {
  # we don't support version pinning
  sed -e 's/,!=4.7.0//g' -i $_name-$pkgver/pyproject.toml

  cd $_name-$pkgver
  patch -Np1 -i $srcdir/pyo3-0.20.3.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  HYPOTHESIS_PROFILE=slow pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
