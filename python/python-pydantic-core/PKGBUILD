# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

_name=pydantic-core
pkgname=python-pydantic-core
# WARNING: this package is pinned down to the patch-level version in python-pydantic and should only be updated in lock-step with it
pkgver=2.41.5
pkgrel=3
epoch=3
pkgdesc="Core validation logic for pydantic written in rust "
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/pydantic/pydantic-core"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
  python-typing-inspection
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-maturin
)
options=(!lto)
source=(
    $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)

sha512sums=('dc24a219b8fced95dc0caed2c5bfe4b9b3b264c18097cc26593604603ce36bc3d19ad78fe62a62fc2033a31c604c6e6e18ce5aa451bf6b06556941273a85dafc')
b2sums=('6a852a710509f8db92cabfb48d6b7b1b959c873f65f4cec4a35946dac943720f1e80f55021264847574e877a5f19353e95bc338deff432447d40ad836631e947')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
