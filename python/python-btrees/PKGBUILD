# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-btrees
pkgver=6.2
pkgrel=2
pkgdesc='A fancy and practical functional tools'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('ZPL-2.1')
url='https://github.com/zopefoundation/btrees'
depends=('python-persistent' 'python-zope-interface')
makedepends=('python-setuptools' 'python-build' 'python-wheel' 'python-installer')
checkdepends=('python-transaction' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('41268dabdb872d0a99aa337fa96d9e5d9c3281374ea430e2eba7c4f7066c93bd774cc55a2a36c6bd99d980f8b2d3dcf97fb4641ec6e7386a99a62b7cbbbe6bbe')

MYARCH=${CARCH}
case "${CARCH}" in
	powerpc*) MYARCH=${CARCH/powerpc/ppc} ;;
esac

build() {
  cd BTrees-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd BTrees-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$MYARCH-cpython-${python_version}" python -m zope.testrunner --test-path=src
}

package() {
  cd BTrees-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
