# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-btrees
pkgver=6.1
pkgrel=2
pkgdesc='A fancy and practical functional tools'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('ZPL')
url='https://github.com/zopefoundation/btrees'
depends=('python-persistent' 'python-zope-interface')
makedepends=('python-setuptools' 'python-build' 'python-wheel' 'python-installer'
             'python-transaction' 'python-zope-testrunner' 'python-persistent'
             'python-zope-deferredimport')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('ec45359eb18a30cf838bc5e1c4276e59d1672f19da17d06aed9877b8661c926a87ff408317401615d267dcb6be9de1772491de19182d79ad5f1510324f4203bb')

prepare() {
  cd BTrees-$pkgver
  # HACK: allow building with our setuptools version
  sed -i 's/setuptools<74/setuptools/' pyproject.toml
}

build() {
  cd BTrees-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd BTrees-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
