# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson
# Contributor: Pekka Ristola <pekkarr [at] protonmail [dot] com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>

pkgname=python-rapidfuzz
pkgver=3.12.2
pkgrel=1
pkgdesc='Rapid fuzzy string matching in Python using various string metrics'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/rapidfuzz/RapidFuzz'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'python'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'cython'
  'python-scikit-build-core'
  'rapidfuzz-cpp'
)
checkdepends=('python-hypothesis' 'python-pandas' 'python-pytest')
optdepends=('python-numpy')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-taskflow-taskflow::git+https://github.com/taskflow/taskflow'
)
sha512sums=('f25c0e8a01bb251b930993888eed777d8a8d97cbbc0ca73d99f9010561d94990a724dab6d951ee99a66091cafdc3f9378b7a24401d7677988775e2ee28933682'
            'SKIP')
b2sums=('fcaae6b1ff2879c7875bb4ee29a339f60265f4e72fc342f79cbbb49719154079caff1b5c37d246c719a3c917a29e34e634ffefcf1ed4adde6d26ef5ca46bf190'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodules
  git submodule init extern/taskflow
  git config submodule.extern/taskflow.url "$srcdir/github.com-taskflow-taskflow"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"

  case "${CARCH}" in
    powerpc) ;;
    *) RAPIDFUZZ_BUILD_EXTENSION=1 ;;
  esac

  python -m build --wheel --no-isolation
}

check() {
  MYARCH=${CARCH}
  case "${CARCH}" in
    powerpc) MYARCH=ppc ;;
    powerpc64) MYARCH=ppc64; return 0 ;;
    powerpc64le) MYARCH=ppc64le; return 0 ;;
    espresso) MYARCH=ppc ;;
  esac

  cd "$pkgname"

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  # install to temporary directory
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl

  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$MYARCH-cpython-$python_version" pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
