# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
_name=${pkgname#python-}
pkgver=8.2.1
pkgrel=1
epoch=1
pkgdesc="Simple powerful testing with Python"
arch=(any)
url="https://pytest.org/"
license=('MIT')
depends=('python-iniconfig' 'python-packaging' 'python-pluggy')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-argcomplete'
  'python-attrs'
  'python-hypothesis'
  'python-pygments'
  'python-requests'
  'python-xmlschema'
  # Unlisted, but actually used. Check again after a while:
  'lsof'
  'python-decorator'
  'python-docutils'
  'python-jinja'
  'python-numpy'
  'python-pexpect'
  'python-pytest-xdist'
  'python-twisted'
)
source=("git+https://github.com/$_name-dev/$_name.git#tag=$pkgver")
b2sums=('15e2a2c215616ccc142d1faf03cd07027bd6f79b4b75c6439842749429a94e4a175498b9364a25e390dc63ebad00e05c0eafe09acf9e25410d7d940856fc02b1')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  # https://github.com/pytest-dev/pytest/issues/10042
  PYTHONPATH="$PWD/src:$PYTHONPATH" pytest -o xfail_strict=False
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
