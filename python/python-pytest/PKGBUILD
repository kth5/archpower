# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=7.4.0
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=(any)
license=('MIT')
url="https://pytest.org/"
depends=('python-iniconfig' 'python-packaging' 'python-pluggy')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-argcomplete' 'python-attrs' 'python-hypothesis'
              'python-nose' 'python-pygments' 'python-requests'
              'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'lsof' 'python-decorator' 'python-docutils'
              'python-exceptiongroup' 'python-jinja' 'python-numpy'
              'python-pexpect' 'python-pytest-xdist' 'python-twisted')
source=("https://files.pythonhosted.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")
sha256sums=('b4bf8c45bd59934ed84001ad51e11b4ee40d40a1229d2c79f9c592b0a3f6bd8a')
b2sums=('59b9e90ab226dd317a5beeb0b189d0e323379e781dcca3fc7aa37ab7ef6d623a25f6f50e4b648cd274bdbc202e914fd393749d093a4c9d8c2f19124403d6b4fe')

# Remove dep on setuptools_scm
prepare() {
  cd pytest-$pkgver
  sed -i "/^\[metadata\]/a version = $pkgver
  /setuptools-scm/d" setup.cfg
  sed -i '/setuptools-scm\|wheel/d
  /\[tool.setuptools_scm\]/,/^$/d' pyproject.toml
}

build() {
  cd pytest-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd pytest-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # https://github.com/pytest-dev/pytest/issues/10042
  test-env/bin/python -m pytest -o xfail_strict=False
}

package() {
  cd pytest-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pytest-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
