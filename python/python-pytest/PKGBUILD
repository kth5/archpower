# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
_name=${pkgname#python-}
pkgver=8.4.1
pkgrel=1
epoch=1
pkgdesc="Simple powerful testing with Python"
arch=(any)
url=https://docs.pytest.org
license=(MIT)
depends=(
  python-iniconfig
  python-packaging
  python-pluggy
  python-pygments
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(
  python-argcomplete
  python-attrs
  python-hypothesis
  python-requests
  python-xmlschema
  # Unlisted, but actually used. Check again after a while:
  lsof
  python-decorator
  python-docutils
  python-jinja
  python-numpy
  python-pexpect
  python-pytest-xdist
  python-twisted
)
source=("git+https://github.com/$_name-dev/$_name.git#tag=$pkgver")
b2sums=('3a5535661b240ba7f5c6ffd9b5ea3b4946bc6451c58f9f872118d147dd73482905f27d0aaca9c4bef96d0a7777717b99e7a715dc9745706f0a355a36c69386bc')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  # https://github.com/pytest-dev/pytest/issues/10042
  PYTHONPATH="$PWD/src:$PYTHONPATH" pytest -o xfail_strict=False
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/licenses/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
