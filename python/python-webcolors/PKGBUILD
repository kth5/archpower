# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-webcolors
pkgver=24.11.0
pkgrel=1
pkgdesc="A library for working with color names and color value formats defined by the HTML and CSS specifications for use in documents on the Web"
url="https://github.com/ubernostrum/webcolors"
license=('BSD-3')
arch=(any)
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-pdm-backend')
source=("git+https://github.com/ubernostrum/webcolors.git#tag=$pkgver")
sha512sums=('5d30e2c6a9db42f0d771e9b5dcbbe8a14614f1eca2cfd6e85c07a09d5f2c336da1234884cea4dfc78bcda64db463461cf310137cd1b474f15260af0d2db3a257')

build() {
  cd webcolors
  python -m build -nw
}

check() {
  cd webcolors
  PYTHONPATH="$PWD"/src python -m unittest discover -vs .
}

package() {
  cd webcolors
  python -m installer -d "$pkgdir" dist/*.whl
}
