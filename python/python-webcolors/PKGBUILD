# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-webcolors
pkgver=24.8.0
pkgrel=1
pkgdesc="A library for working with color names and color value formats defined by the HTML and CSS specifications for use in documents on the Web"
url="https://github.com/ubernostrum/webcolors"
license=('BSD-3')
arch=(any)
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://pypi.io/packages/source/w/webcolors/webcolors-$pkgver.tar.gz")
sha512sums=('9a008261fe5283a90d4986a77463ac2614585c421d4ba7c19d99d98f7d13be6cf53f9ef3916a7b5b4af5ebadbbb0ba2fee266b419706f181acbd6a4c012cc5cb')

build() {
  cd webcolors-$pkgver
  python -m build -nw
}

check() {
  cd webcolors-$pkgver
  PYTHONPATH="$PWD/build/lib" python -m unittest discover -vs .
}

package() {
  cd webcolors-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
