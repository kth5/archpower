# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-greenlet
pkgver=3.3.1
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://pypi.org/project/greenlet/"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-objgraph' 'python-psutil')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz")
sha512sums=('890e9a56f15c39101b45aabb006545640777a987d8f962c63859c2b4e26adfb5ae33310905982475809d3fcfb3b0ee5a9b11817c380708bba2d3c25cd5ad1822')

build() {
    cd greenlet-$pkgver
    case "${CARCH}" in
      powerpc|powerpc64le)
        export CFLAGS="${CFLAGS/-fno-omit-frame-pointer/}"
        export CXXFLAGS="${CXXFLAGS/-fno-omit-frame-pointer/}"
      ;;
      x86_64)
        # see https://github.com/python-greenlet/greenlet/issues/454
        export CFLAGS="$CFLAGS -fcf-protection=none"
        export CXXFLAGS="$CXXFLAGS -fcf-protection=none"
      ;;
    esac

    python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
    MYARCH=${CARCH}
    case "${CARCH}" in
	powerpc*) MYARCH=${CARCH/powerpc/ppc} ;;
    esac

    cd greenlet-$pkgver
    local _python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
    echo PYTHONPATH="$PWD/build/lib.linux-$MYARCH-cpython-$_python_version" python -m unittest discover -v greenlet.tests
    PYTHONPATH="$PWD/build/lib.linux-$MYARCH-cpython-$_python_version" python -m unittest discover -v greenlet.tests
}

package() {
    cd greenlet-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    rm -rf "$pkgdir"/usr/lib/python*/site-packages/greenlet/tests
    install -Dm644 LICENSE.PSF -t "$pkgdir"/usr/share/licenses/$pkgname/
}
