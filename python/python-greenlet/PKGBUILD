# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-greenlet
pkgver=3.2.3
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://pypi.org/project/greenlet/"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz")
sha512sums=('3153123855b4900387454fa1d4223b321980e3462e1e139b9f90b4eb2d0772102e06375055c6eb612530d7037169ff6e28166f95efe09073ea331db76284d39d')

build() {
    case "${CARCH}" in
      powerpc64)
        export CFLAGS="-O2 -pipe"
        export CXXFLAGS="-O2 -pipe"
      ;;
      powerpc64le)
        export CFLAGS="${CFLAGS/-fno-omit-frame-pointer/}"
        export CXXFLAGS="${CXXFLAGS/-fno-omit-frame-pointer/}"
      ;;
    esac

    cd greenlet-$pkgver
    python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
    cd greenlet-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    rm -rf "$pkgdir"/usr/lib/python*/site-packages/greenlet/tests
    install -Dm644 LICENSE.PSF -t "$pkgdir"/usr/share/licenses/$pkgname/
}
