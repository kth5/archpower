# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgname=python-paramiko
pkgver=4.0.0
pkgrel=1
pkgdesc="Python module that implements the SSH2 protocol"
url="https://github.com/paramiko/paramiko/"
license=('LGPL-2.1-only')
arch=(any)
depends=('python-bcrypt' 'python-cryptography' 'python-pynacl' 'python-invoke')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest-relaxed' 'python-pyasn1' 'python-gssapi' 'python-icecream')
optdepends=('python-gssapi: GSS-API/SSPI support'
            'python-pyasn1: GSS-API/SSPI support')
source=(
  "https://github.com/paramiko/paramiko/archive/$pkgver/$pkgname-$pkgver.tar.gz"
)
sha512sums=('5a6d4cf000f13e1a9ee2558f18a942e3b7f334807b007bed77cfb9d50f15d423b3490a0deca08f82749391092d2514c44cd7d8dda2dc66436db3816c2501c72a')

prepare() {
  cd paramiko-$pkgver
  # Devendor test setup
  sed -i "s/from invoke.vendor.lexicon import Lexicon/from lexicon import Lexicon/" tests/conftest.py
}

build() {
  cd paramiko-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd paramiko-$pkgver
  LANG=en_US.UTF-8 pytest
}

package() {
  cd paramiko-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/demos
  install -m644 demos/* "$pkgdir"/usr/share/doc/$pkgname/demos
  chmod 755 "$pkgdir"/usr/share/doc/$pkgname/demos/*.py
}
