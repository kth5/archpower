# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aiounittest
pkgver=1.4.3
pkgrel=1
pkgdesc="Test asyncio code more easily"
url="https://github.com/kwarunek/aiounittest"
license=('MIT')
arch=('any')
depends=('python-wrapt')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
source=("git+https://github.com/kwarunek/aiounittest.git#tag=$pkgver")
sha512sums=('a3e9b2c14cd2f859cca4952ee40a3e4479a1177b0553199f6ece387c909bc97889557c7d54f921b21c282f67aafcc39831d906d6d90db93542da4910d8037604')

build() {
  cd aiounittest
  python setup.py build
}

check() {
  cd aiounittest
  PYTHONPATH=. pytest
}

package() {
  cd aiounittest
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
