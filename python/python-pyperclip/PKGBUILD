# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com>

pkgname=python-pyperclip
pkgver=1.9.0
_commit=5aef21c14cda3a9c858adf108581fd2849c6321f
pkgrel=1
pkgdesc="A cross-platform clipboard module for Python"
arch=(any)
url="https://github.com/asweigart/pyperclip"
license=('BSD-3-Clause')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
# GTK/Qt tests crash xvfb, and Klipper tests require a running klipper
#checkdepends=('xorg-server-xvfb' 'python-pytest' 'xsel' 'xclip')
depends=('python')
optdepends=('xclip: xclip backend'
            'xsel: xsel backend'
            'wl-clipboard: wl-clipboard backend'
            'plasma-workspace: klipper backend')
source=("$pkgname-$_commit.tar.gz::https://github.com/asweigart/pyperclip/archive/$_commit.tar.gz")
sha512sums=('ab5e024f54f70bc715406b27827c2a1dff141cd6b7bedb400e53377d0058632056c4f2bc69ef2e2c54ad89405af6d1caef3267a089433cad0a8a843a904c861f')

build() {
  cd pyperclip-$_commit
  python -m build --wheel --skip-dependency-check --no-isolation
}

# https://github.com/asweigart/pyperclip/issues/263
#check() {
#  cd pyperclip-$_commit
#  PYTHONPATH="$PWD"/build/lib xvfb-run py.test
#}

package() {
  cd pyperclip-$_commit
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pyperclip-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
