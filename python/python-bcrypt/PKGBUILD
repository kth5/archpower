# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Contributor: Alexander Diana <alexander@rouk.org>

pkgname=python-bcrypt
pkgver=4.2.1
pkgrel=2
pkgdesc="Modern password hashing for your software and your servers"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/pyca/bcrypt"
license=('Apache')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools-rust' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyca/bcrypt/archive/$pkgver.tar.gz")
sha512sums=('1a8af20bffa321c4e88c60b9e22ac1139de85033f11014cf1cbfcd261069bf62f7830432715561f3919c14408e408b05b0774a48d1ea954b600adc635fe7cf57')

build() {
  cd bcrypt-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd bcrypt-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" pytest
}

package() {
  cd bcrypt-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
