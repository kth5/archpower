# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Contributor: Alexander Diana <alexander@rouk.org>

pkgname=python-bcrypt
pkgver=4.3.0
pkgrel=1
pkgdesc="Modern password hashing for your software and your servers"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/pyca/bcrypt"
license=('Apache-2.0')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-rust'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('da142a8cac0aefcb94b513740d5846c7ca47b93bac9b28ce33f5960b43fc311819de1820b41e010f10721f155c6fe0f5d30a01b6f0cb359c733a2228a401a7e0')
b2sums=('1caf433bf1b3c66df077f94018772c41df1c99cc3f3f2dfb8b01b287c348ed7ef022b90f0422b5bf8d05eebbbee2afefc7421bb1a859b9dbcb648294d78a99ed')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m installer --destdir=test_dir dist/*.whl

  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" pytest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
