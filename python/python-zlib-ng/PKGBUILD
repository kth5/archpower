# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-zlib-ng
pkgver=0.5.1
pkgrel=2
pkgdesc="A drop-in replacement for Python's zlib and gzip modules using zlib-ng"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/pycompression/python-zlib-ng
license=(PSF-2.0)
depends=(
  glibc
  python
  zlib-ng
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-versioningit
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-timeout
  python-tests
)
source=("git+$url.git#tag=v$pkgver")
sha256sums=('9ad66f69a2e107e18c47b42968ad1860b15aa7faaa0d5fb36d74b583b5ac98c8')

build() {
  cd $pkgname
  export PYTHON_ZLIB_NG_LINK_DYNAMIC=1
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
