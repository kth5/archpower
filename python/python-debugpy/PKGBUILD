# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: mark.blakeney at bullet-systems dot net

_pipname=debugpy
pkgname=python-$_pipname
pkgver=1.6.5
pkgrel=1
pkgdesc='An implementation of the Debug Adapter Protocol for Python'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://aka.ms/debugpy'
license=(EPL MIT)
depends=(python)
makedepends=(python-setuptools)
source=(https://github.com/microsoft/debugpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('1487d37ab6b53a6d8c87ff2468396f5da6b6f341c06a139eedaa3efb94f0819f')

prepare() {
  cd $_pipname-$pkgver
# remove precompiled libs
  rm src/debugpy/_vendored/pydevd/pydevd_attach_to_process/*.{so,dll,dylib,exe,pdb}
}

build() {
  cd $_pipname-$pkgver
  python setup.py build
# Compile attach libraries
  cd build/lib*/debugpy/_vendored/pydevd/pydevd_attach_to_process
  case "${CARCH}" in
    powerpc)     arch=ppc     ;;
    powerpc64)   arch=ppc64   ;;
    powerpc64le) arch=ppc64le ;;
    riscv64)     arch=riscv64 ;;
    x86_64)      arch=amd64; CXX="g++ -m64" ;;
  esac
  ${CXX} ${CXXFLAGS} -shared -o attach_linux_${arch}.so -fPIC -nostartfiles linux_and_mac/attach.cpp ${LDFLAGS}
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
