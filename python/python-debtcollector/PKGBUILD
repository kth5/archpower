# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-debtcollector
pkgver=3.0.0
pkgrel=2
arch=(any)
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='https://docs.openstack.org/developer/debtcollector'
license=('Apache')
depends=('python-wrapt')
makedepends=('python-pbr' 'python-setuptools')
checkdepends=('python-stestr')
source=("https://github.com/openstack/debtcollector/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f3e189b952db59e69dce79459b2a9cf0e795503b53bac9688a442dfcdecdd920477481e26bf552f88f0983d348f2605c176acefba5341066648e066f22f249f9')

export PBR_VERSION=$pkgver

build() {
  cd debtcollector-$pkgver
  python setup.py build
}

check() {
  cd debtcollector-$pkgver
  stestr run
}

package() {
  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
