# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.10.16
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/ijl/orjson"
license=(
  Apache-2.0
  MIT
)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  maturin
  python-installer
  rust
)
checkdepends_powerpc64=(
  python-arrow
  python-pendulum
  python-psutil
  python-pytest
  python-pytz
  python-xxhash
)
checkdepends_powerpc64le=(
  python-arrow
  python-pendulum
  python-psutil
  python-pytest
  python-pytz
  python-xxhash
)
checkdepends_x86_64=(
  python-arrow
  python-pendulum
  python-psutil
  python-pytest
  python-pytz
  python-xxhash
)

source=($url/archive/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('90ee01d328f8e17f1f76ecf5bf72ab61ffd9c6671a2ad10fdae0042bfde18d105663d0784c6f756ef8fa64e535f16e2233f0b544b402b79850d69c17c748940e')
b2sums=('613a806cbaa918abf7769760d55345c2c73fa6f112258391d84047de683182ea78cc4672fd0831a6a4eb6d66caaad882efa604686cbb69fa9b90b5835dfd2f8b')

build() {
  cd $_name-$pkgver
  maturin build --release --strip
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE-* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
