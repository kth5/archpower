# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>

pkgname=python-urwid
pkgver=3.0.4
pkgrel=4
pkgdesc='Curses-based user interface library'
arch=(x86_64 powerpc powerpc64 powerpc64le espresso)
url='https://urwid.org/'
license=('LGPL-2.1-only')
depends=(
  'glib2'
  'python'
  'python-wcwidth'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=(
  'python-gobject'
  'python-pytest'
  'python-pyzmq'
  'python-tornado'
  'python-trio'
  'python-twisted'
)
optdepends=(
  'python-gobject: for gobject integration'
  'python-pyserial: for LCD and serial integration'
  'python-pyzmq: for zmq integration'
  'python-tornado: for tornado integration'
  'python-trio: for trio integration'
  'python-twisted: for twisted integration'
)
source=("git+https://github.com/urwid/urwid.git#tag=$pkgver")
sha512sums=('1b6df1946fb69d6c2aaff8117ca32b03687aca63e068d732953c87931cf2388b80638b27eb6c9b528bbfe5df1822df534e27101f58b46585b476c60dca22eea1')
b2sums=('1e4e7557a6beb5a7f9b1b0cb5bf9a2268a453e53f6962b19f4f9ed51b9be5c9aec88a70c0c0d75fe14cbbe7c5d1bb7ef70007715fbb742cbcc48cc8bec9d25c1')

prepare() {
  cd ${pkgname#python-}
  # Revert backwards-incompatible change that broke downstream packages, e.g.
  # todoman and pudb. See:
  # https://github.com/urwid/urwid/issues/1086
  git revert -n eaddcd279f59a0f68f00ad06019aea69f91a809b
}

build() {
  cd ${pkgname#python-}
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname#python-}
  # Override addopts as they invoke coverage testing
  pytest --override-ini="addopts=-vv --doctest-modules -s" tests
}

package() {
  cd ${pkgname#python-}
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
