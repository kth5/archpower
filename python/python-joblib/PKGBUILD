# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Torsten Ke√üler <tpkessler@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-joblib
pkgver=1.4.2
pkgrel=2
pkgdesc="Set of tools to provide lightweight pipelining in Python"
arch=(any)
url="https://joblib.readthedocs.io/"
license=('BSD-3-Clause')
depends=('python')
optdepends=('python-numpy: for array manipulation'
            'python-lz4: for compressed serialization')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/joblib/joblib/archive/${pkgver}.tar.gz")
sha256sums=('1d95f5f3ab303be89aa4666956bf05a589f56e52c29b836267c8e3885223ff90')

build() {
  cd joblib-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd joblib-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
