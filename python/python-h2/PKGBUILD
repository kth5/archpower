# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-h2
pkgver=4.2.0
pkgrel=1
pkgdesc="HTTP/2 State-Machine based protocol implementation"
arch=(any)
license=('MIT')
url="https://hyper.rtfd.org"
depends=('python-hpack' 'python-hyperframe')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-hypothesis')
source=("git+https://github.com/python-hyper/h2.git#tag=v$pkgver")
sha512sums=('7b3407653b0bf51b462080a425697e65c06fdc39e8b0da418a7a136bf1b6e77f11fdce219236d2eaaf8374e80645352cee7ff3d82477fe6eee08388e19cd8a78')

build() {
  cd h2
  python -m build --wheel --no-isolation
}

check() {
  cd h2
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd h2
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
