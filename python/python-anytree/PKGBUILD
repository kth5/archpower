# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=python-anytree
pkgver=2.12.1
pkgrel=2
pkgdesc="Simple, lightweight and extensible Tree data structure."
arch=(any)
url="https://anytree.readthedocs.io/"
license=('Apache')
depends=('python-six')
makedepends=('python-build' 'python-installer' 'python-poetry-core' 'python-wheel' 'git')
checkdepends=('python-pytest' 'graphviz')
source=("$pkgname::git+https://github.com/c0fec0de/anytree#tag=$pkgver")
b2sums=('997f4611bf6e9b9bce01393d887743e395fb30a8a0fe5c8666847dd04edda8a2480c2d773b5508fa8539ad690d2cf2dd21852d6983d2939825f3d0ca9227c13e')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"
  mkdir -p tests/dotexport
  python -m pytest
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.rst "$pkgdir/usr/share/doc/$pkgname/README.rst"
}
