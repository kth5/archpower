# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=slirp4netns
pkgdesc='User-mode networking for unprivileged network namespaces'
pkgver=1.3.3
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/rootless-containers/slirp4netns"
license=(GPL-2.0-or-later)
depends=(glibc glib2 libcap libseccomp libslirp)
makedepends=(git)
source=("git+$url#tag=v$pkgver?signed")
sha256sums=('4b8092c594218a74132cd0d3c45cfa8c6a4346c98d5e331b72cf4ccaf1445cff')
validpgpkeys=('C020EA876CE4E06C7AB95AEF49524C6F9F638F1A') # Akihiro Suda <akihiro.suda.cz@hco.ntt.co.jp>

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
