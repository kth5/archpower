# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libpanel
pkgname=(
  libpanel
  libpanel-docs
)
pkgver=1.10.1
pkgrel=1
pkgdesc="Dock/panel library for GTK 4"
url="https://gitlab.gnome.org/GNOME/libpanel"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-3.0-or-later)
depends=(
  cairo
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+$url.git#tag=$pkgver")
b2sums=('92d94ba2a1de14df81c0092398ea9c173401c4081f68345962a46ce1890bae59ca1ba2b2b0c7f9e16264aa7abc34fce8fe75d0f46f7ac486eaa203a4e3a0e862')

build() {
  local meson_options=(
    -D install-examples=true
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libpanel() {
  provides=(libpanel-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libpanel-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
