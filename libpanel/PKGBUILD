# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libpanel
pkgname=(
  libpanel
  libpanel-docs
)
pkgver=1.10.3
pkgrel=1
pkgdesc="Dock/panel library for GTK 4"
url="https://gitlab.gnome.org/GNOME/libpanel"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-3.0-or-later)
depends=(
  cairo
  glib2
  glibc
  graphene
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+$url.git#tag=$pkgver")
b2sums=('36ec65afe96f2378e3c5498ebcf505eeca7e06f292869964101f99671aee1f3518dc29c3bfdec2491920717ba52c2a5cdc43f39b1cddd42da4c0be95f7197190')

build() {
  local meson_options=(
    -D install-examples=true
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libpanel() {
  provides=(libpanel-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libpanel-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
