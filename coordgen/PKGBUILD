# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coordgen
pkgver=3.0.2
pkgrel=3
pkgdesc='Schrodinger-developed 2D Coordinate Generation'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/schrodinger/coordgenlibs'
license=(BSD-3-Clause)
depends=(gcc-libs
         glibc
         maeparser)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/schrodinger/coordgenlibs#tag=v$pkgver
        fix-cmake.patch)
sha256sums=('4fae2a90370dfa4ad62792bbbb7a01169d3b6f96a929fe3af53ada0b389e6962'
            'abb9313af5a52e1a892fb65f7dd6cb17b078d2cd926565e94df45338d2c13e09')

prepare() {
  cd coordgenlibs
  # fix installation of cmake targets and config
  # https://github.com/schrodinger/coordgenlibs/pull/135
  patch -p1 < ../fix-cmake.patch
}

build() {
  cmake -B build -S coordgenlibs \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOORDGEN_USE_MAEPARSER=ON \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 coordgenlibs/templates.mae -t "$pkgdir"/usr/share/coordgen
  install -Dm644 coordgenlibs/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
