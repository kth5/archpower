# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nlohmann-json
pkgver=3.11.3
pkgrel=1.1
# tests are tracked in a separate repo
# https://github.com/nlohmann/json_test_data
_test_pkgver=3.1.0
pkgdesc="JSON for Modern C++"
arch=(any)
url="https://github.com/nlohmann/json"
license=(MIT)
makedepends=(
  cmake
  git
)
source=(
  $pkgname::git+$url.git#tag=v$pkgver
  json_test_data-$_test_pkgver.tar.gz::https://github.com/nlohmann/json_test_data/archive/v$_test_pkgver.tar.gz
)
sha512sums=('cd1abc605a40c4ec300c0c455e504988e8ce318765e25c7bee4942aa03f0a04c9c8f52e8cf5e464442b87e58a8aace4b81371e7e6848c527f1ddd3a51dac2357'
            'db6c411b37f2154f5dd1ed90f4e8fa0907f4a736cd0ff79943bcacf9da422285ff142bb6a7dc6022b236090083166ac1ab197be3f480d8dc50b26a91a9477821')
b2sums=('8840eb8346065a766c5fff736b36d042ae746696183612c0fb5f6b10b873e29e8f3cc3594c20f0c2a76c59b4a25cbe6ca9825947cf751798b5b0ac60c4dccbd7'
        '809be0728a0b9d007fcc752911bdf6f7e548d6e3ec59871ea2b16d87d8248ca4dd2f681a1d0f82c618463294188ad41d6d965b8bdc39c70fdcf4b939d4121e9c')

prepare() {
  mkdir -vp build-test/
  mv -v json_test_data-${_test_pkgver}/ build-test/json_test_data/
}

build() {
  local cmake_options=(
    -B build
    -D BUILD_TESTING=ON
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_LIBDIR=/usr/lib
    -D JSON_BuildTests=ON
    -D JSON_MultipleHeaders=ON
    -S $pkgname
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname/LICENSE.MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
}
