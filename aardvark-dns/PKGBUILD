# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=aardvark-dns
pkgver=1.16.0
pkgrel=1
pkgdesc="Authoritative dns server for A/AAAA container records"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/containers/aardvark-dns"
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
  git
)
source=(git+$url#tag=v$pkgver)
sha256sums=('4913f48d17baa4a97bb9ba2d63da57a883cf4554cc7b1ee75b6accffd59eb89f')
# NOTE: pinning commit until upstream clarifies commitment to chain of trust:
# https://github.com/containers/aardvark-dns/issues/83
# validpgpkeys=('74FE091D25519980B2D84447160386BECB6F0643')  # Brent Baude <bbaude@redhat.com>

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
