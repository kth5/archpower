# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pmix
pkgbase=openpmix
pkgname=(
  openpmix
  openpmix-docs
)
# NOTE: keep in sync with openmpi major versions
pkgver=4.2.5
pkgrel=1
pkgdesc="Extended version of the PMI standard"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/openpmix/openpmix"
license=(BSD)
makedepends=(
  glibc
  hwloc
  perl
  libevent
  libevent
  python
  zlib
)
source=($url/releases/download/v$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('495d1537d16e17e47467f154204ceb7f048fff869f37fd9fe9847519c70f8a1a8c966c35a87096e9fd8cf1638d9e48956498311f8eced33401094a8b167f61a1')
b2sums=('8f209293cfa10d116c6a5a4bc1dacc08add7edbccbf4020be56bb93645dfaffc23fc88d63a4c82c2196e39927e87a4258cb894101f7d7f0d5269056b56751c46')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  cd $_name-$pkgver
  ./autogen.pl
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc/$pkgname
  )

  cd $_name-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make V=1
}

check() {
  make -k check -C $_name-$pkgver
}

package_openpmix() {
  depends=(
    glibc
    hwloc
    libevent libevent_{core,pthreads}-2.1.so
    zlib
  )
  optdepends=(
    'openpmix-docs: for documentation'
  )
  provides=(libpmix.so pmix)
  backup=(etc/$pkgname/pmix-mca-params.conf)

  make DESTDIR="$pkgdir" install -C $_name-$pkgver

  (
    cd "$pkgdir"
    _pick $pkgbase-docs usr/share/doc/
  )

  install -vDm 644 $_name-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_openpmix-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir/"
}
