# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=libaec
pkgver=1.1.4
pkgrel=2
pkgdesc="Adaptive Entropy Coding library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://gitlab.dkrz.de/k202009/libaec"
license=(BSD-2-Clause)
depends=(glibc)
makedepends=(cmake)
source=("${url}/-/archive/v${pkgver}/libaec-${pkgver}.tar.gz")
sha256sums=('95439e861968cb0638a10b0bbdb37c9a10df1b22c5ee0293902acdbc68140f53')

prepare() {
  mv ${srcdir}/${pkgname}-v${pkgver}* ${srcdir}/${pkgname}-v${pkgver}
}

build() {
  cmake -B build -S ${pkgname}-v${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    ${pkgname}-v${pkgver}/LICENSE.txt
}
