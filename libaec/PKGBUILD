# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libaec
pkgver=1.1.3
pkgrel=2
pkgdesc="Adaptive Entropy Coding library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://gitlab.dkrz.de/k202009/libaec"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=(${url}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2
        fix-cmake-install-dir.patch)
sha256sums=('46216f9d2f2d3ffea4c61c9198fe0236f7f316d702f49065c811447186d18222'
            'dbe438d44aa4b8c5991ad70e9e9b170a02466e003c134f123f6bc82ae1be22ea')

prepare() {
  patch -d $pkgname-v$pkgver -p1 < fix-cmake-install-dir.patch # Fix cmake install dir
}

build() {
  cmake -B build -S ${pkgname}-v${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-v${pkgver}/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  mv "${pkgdir}"/usr/{,lib}/cmake
}
