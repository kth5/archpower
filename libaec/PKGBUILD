# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=libaec
pkgver=1.1.5
pkgrel=1
pkgdesc="Adaptive Entropy Coding library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://gitlab.dkrz.de/k202009/libaec"
license=(BSD-2-Clause)
depends=(glibc)
makedepends=(cmake)
source=("$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.bz2")
b2sums=('43764239b5d7cce08bced884a5fa49aeb12df1778ccd613774cfeab5e7f8bad2f9674bfaf52815a8533e522a03768c947a41de698ade4ed358e60e897d13cc17')

build() {
  cmake -B build -S $pkgname-v$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    $pkgname-v$pkgver/LICENSE.txt
}
