# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=bats-assert
pkgver=2.1.0
pkgrel=2
pkgdesc="Common assertions for Bats"
url='https://github.com/bats-core/bats-assert'
arch=(any)
license=("custom:CC0")
depends=(
  bash
  bats
  bats-support
)
checkdepends=(
  coreutils
)
replaces=(bash-bats-assert)
source=(${url}/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('797689b91df5018b717c09578e1c0f6fea11f344c2e5985ec5eaca89bab39aafa7d88b31043a34576bcdf0ab5cf160c9624ee3167bd0854d7b16aa947b3928e4')
b2sums=('936c1dc1b594c2792e5fcc85b90c3bfe270157efb4a1bc25c91de1f4ef721f0a6734b40b53725eb84a225701366f6d75140df348832bca0bffaf565ea5b9e75e')

check() {
  cd "${pkgname}-${pkgver}"
  bats --jobs "$(nproc)" --tap test
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 644 *.bash -t "${pkgdir}/usr/lib/bats/${pkgname}"
  install -Dm 644 src/*.bash -t "${pkgdir}/usr/lib/bats/${pkgname}/src"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
