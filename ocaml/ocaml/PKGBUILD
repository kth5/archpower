# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jürgen Hötzel <juergen@archlinux.org>

pkgbase='ocaml'
pkgname=('ocaml' 'ocaml-compiler-libs')
pkgver=5.1.0
pkgrel=1
pkgdesc="A functional language with OO extensions"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('LGPL2.1' 'custom: QPL-1.0')
url="https://caml.inria.fr/"
makedepends=('ncurses>=5.6-7')
optdepends=('ncurses: advanced ncurses features' 'tk: advanced tk features')
source=(https://caml.inria.fr/distrib/ocaml-${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        ocaml-5.0.0-elfv2-ppc64.patch)
sha512sums=('23579b76592e225f2ddec58d78084dfd11befede18b61be71d3896fd72a90cc0fe4fb1f64a7dcbc83239ed69ec4254e13ab86fd810671851044c2a353da3adc5'
            'a7063fd3ce8f0622fafa9619b986abb11e2811c7e8e79973317c9c64f357a6d293b4057a258cf2de5446ed2014c41593cdbb65b874b2bbf17a9d4fe181c505d8')
options=('!makeflags' '!emptydirs' 'staticlibs')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/ocaml-5.0.0-elfv2-ppc64.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=' -ffat-lto-objects'
  CXXFLAGS+=' -ffat-lto-objects'

  case "${CARCH}" in
    powerpc*)
      _configure_flags=(--disable-native-compiler)
      _make_target="world"
    ;;
    *)
      _make_target="world-opt"
    ;;
  esac

  ./configure --prefix /usr --mandir /usr/share/man ${_configure_flags[@]}
  make --debug=v ${_make_target}
}

package_ocaml() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Save >10MB with this one, makepkg only strips debug symbols.
  #find "${pkgdir}/usr/lib" -type f -name '*.so.*' -exec strip --strip-unneeded {} \;

  # install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"

  # remove compiler libs
  rm -rf "${pkgdir}/usr/lib/ocaml/compiler-libs"
}

package_ocaml-compiler-libs() {
pkgdesc="Several modules used internally by the OCaml compiler"
license=('custom: QPL-1.0')
depends=('ocaml')
optdepends=()

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Remove non-compiler-libs
  find "${pkgdir}/usr/lib/ocaml/" -mindepth 1 -maxdepth 1 -not -name "compiler-libs" -execdir rm -rf "{}" "+"
  rm -rf   "${pkgdir}/usr/bin" "${pkgdir}/usr/share"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
