# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Armin K. <krejzi at email dot com>

pkgname=ocaml-ctypes
pkgver=0.22.0
pkgrel=1
pkgdesc="Library for binding to C libraries using pure OCaml"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/ocamllabs/ocaml-ctypes"
license=('MIT')
depends=('ocaml' 'libffi' 'ocaml-integers' 'ocaml-bigarray-compat')
makedepends=('ocaml-findlib' 'ocaml-compiler-libs' 'dune')
options=('!strip' '!makeflags')
source=($pkgname-$pkgver.tar.gz::https://github.com/ocamllabs/ocaml-ctypes/archive/$pkgver.tar.gz)
sha512sums=('98925bddfba81fd91383c5ee87a47f8f3424958e0affc299585b83029ce3cc52ac4c059f94d548cb15f37379ba3a8742a04b94f76787177b26c233b7037faa09')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  dune build -p ctypes,ctypes-foreign
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir "/usr/lib/ocaml" --docdir "/usr/share/doc"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
