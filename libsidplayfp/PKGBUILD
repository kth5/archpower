# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=2.15.0
pkgrel=1
pkgdesc='Library to play Commodore 64 music'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/libsidplayfp/libsidplayfp'
license=('GPL-2.0-only')
depends=('glibc' 'gcc-libs' 'libgcrypt')
makedepends=('git' 'vice' 'xa')
optdepends=('vice: better SID support')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'github.com-libsidplayfp-exsid-driver::git+https://github.com/libsidplayfp/exsid-driver'
  'github.com-libsidplayfp-resid::git+https://github.com/libsidplayfp/resid'
)
sha512sums=('fe3582eacf6644ebafe8de70a43d44c6e3923cf1fbb9a1c66f6c0d559bd771f90209c9efe4cc8cfebe64c10658e45e5b249628fc29d34c63f18b7eda290110aa'
            'SKIP'
            'SKIP')
b2sums=('01d09b6f50b4dbdf54e6b8325855c8cf1aff88a2efef80b1ab74ab7309b29c634f5b718c76900f4fcbb8fa381cd150786d40dd7e1276f845f5807a7d81868cda'
        'SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  git submodule init
  git config submodule.src/builders/exsid-builder/driver.url "$srcdir/github.com-libsidplayfp-exsid-driver"
  git config submodule.src/builders/resid-builder/resid.url "$srcdir/github.com-libsidplayfp-resid"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$pkgname"

  autoreconf -vfi

  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  DESTDIR="$pkgdir" make -C "$pkgname" install
}
