# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=10.0.2.0
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
url="https://www.jruby.org/"
arch=(any)
license=(
  '(EPL-2.0 OR GPL-2.0-only OR LGPL-2.1-only) AND BSD-3-Clause AND MIT AND MIT-0 AND Apache-2.0 AND Ruby'
)
depends=(
  bash
  gcc-libs
  glibc
  java-runtime-headless
)
options=(
  !debug
  !strip
)
source=(
  "https://repo1.maven.org/maven2/org/jruby/jruby-dist/$pkgver/jruby-dist-$pkgver-bin.tar.gz"{,.asc}
)
b2sums=('3ce233f49114f8fed5d81ea58e2352dbd68bb0428aa77992dd1982bf4426fbb5731fae6bbc4f1025d141a6e46122cb3ee06e111e6c32d43960140d82c079234d'
        'SKIP')
validpgpkeys=(
  416952B945B78A34C6C7678562EBFC78FE4156D1  # Thomas E. Enebo <tom.enebo@gmail.com>
)

# https://www.jruby.org/download
md5sums=('e0eb01fd82c9464cbbf2fcd5a557779b'
         'SKIP')
sha1sums=('6da46adfdb021a1efb906c779446f47b59eff8a2'
          'SKIP')
sha256sums=('b8a026f38aa98461a04ed0aa0b20891ce257ecbe53e124719ce9ee5b804525f1'
            'SKIP')

prepare() {
  cd jruby-$pkgver
  rm -rv bin/*.{bat,dll,exe} lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-*BSD}
  chmod -cR g-w .
}

package() {
  mkdir -p "$pkgdir"/{opt,usr/bin,usr/share/licenses/$pkgname}
  cp -a jruby-$pkgver "$pkgdir/opt/jruby"
  ln -srt "$pkgdir/usr/bin" \
    "$pkgdir"/opt/jruby/bin/j{irb,irb_swing,gem,ruby,rubyc}
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 jruby-$pkgver/COPYING
}

# vim:set sw=2 sts=-1 et:
