# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=maven
pkgver=3.9.8
pkgrel=1
pkgdesc='Java project management and project comprehension tool'
url='https://maven.apache.org'
arch=(any)
license=('Apache-2.0')
depends=('java-environment>=8' 'bash' 'procps-ng')
makedepends=('maven' 'java-environment>=22')
backup=('usr/share/java/maven/conf/settings.xml')
source=(https://downloads.apache.org/maven/maven-3/${pkgver}/source/apache-maven-${pkgver}-src.tar.gz{,.asc}
        # both bin artifacts are only used for reproducible builds from source
        https://downloads.apache.org/maven/maven-3/${pkgver}/binaries/apache-maven-${pkgver}-bin.tar.gz{,.asc,.sha512}
        maven.sh)
noextract=(apache-maven-${pkgver}-bin.tar.gz)
# https://www.apache.org/dist/maven/KEYS
validpgpkeys=(
  '042B29E928995B9DB963C636C7CA19B7B620D787' # StephenConnolly<stephenc@apache.org>
  'AE9E53FC28FF2AB1012273D0BF1518E0160788A2' # Karl Heinz Marbaise (ASF Key) <khmarbaise@apache.org>
  'BBE7232D7991050B54C8EA0ADC08637CA615D22C' # Enrico Olivelli <eolivelli@apache.org>
  'B02137D875D833D9B23392ECAE5A7FB608A0221C' # Robert Scholte <rfscholte@apache.org>
  '6A814B1F869C2BBEAB7CB7271A2A1C94BDE89688' # Michael Osipov (Java developer) <1983-01-06@gmx.net>
  '29BEA2A645F2D6CED7FB12E02B172E3E156466E8' # Tamas Cservenak (ASF) (Release key) <cstamas@apache.org>
  '88BE34F94BDB2B5357044E2E3A387D43964143E3' # Tamas Cservenak <tamas@cservenak.net>
)
sha256sums=('c035591b9238d6832c19ad6e56506631f6330ad5c53868a80fdd5eaea365a467'
            'SKIP'
            '067672629075b740e3d0a928e21021dd615a53287af36d4ccca44e87e081d102'
            'SKIP'
            '535dd1e378ad0fc6bdd449963362472082d150da68cba58ead3f7e44ef9b87e2'
            '15557fc169d83396238df69b5c1d5551cb9c570629decedd6b484d3b4e422546')

_buildnumber() {
  # use same build number as the binary dist but prove that our source build is
  # identical and therefor canonical in the build() step
  bsdtar xOf <(bsdtar xOf "${srcdir}/apache-maven-${pkgver}-bin.tar.gz" apache-maven-${pkgver}/lib/maven-core-${pkgver}.jar) \
    org/apache/maven/messages/build.properties | grep buildNumber | cut -d= -f2 | tr -cd '[:print:]'
}

build() {
  cd apache-maven-${pkgver}

  export JAVA_HOME="/usr/lib/jvm/java-22-openjdk"
  export PATH="/usr/lib/jvm/java-22-openjdk/bin:$PATH"
  # Consult the link for JDK and line separator of buildpec for reproducible
  # https://github.com/jvm-repo-rebuild/reproducible-central/blob/master/content/org/apache/maven/maven/README.md
  mvn package \
    -DbuildNumber="$(_buildnumber)" \
    -Dline.separator=$'\n' \
    -Dproject.build.sourceEncoding=UTF-8 -e \
    -Dmaven.repo.local="${srcdir}/repo" \
    -DskipTests

  # check reproducible builds result against upstream hashes. This way we can
  # prove that our source build is identical and therefor canonical so we are
  # technically free to use the static build number in our build env. On top we
  # ensure bit by bit identical upstream signed binary dist against our variant
  # via diff exiting non-successful on mismatch. (only works with JDK21)
  #sha512sum -c <(printf "$(cat ${srcdir}/apache-maven-${pkgver}-bin.tar.gz.sha512) apache-maven/target/apache-maven-${pkgver}-bin.tar.gz")
  #diff "${srcdir}/apache-maven-${pkgver}-bin.tar.gz" apache-maven/target/apache-maven-${pkgver}-bin.tar.gz
}

check() {
  cd apache-maven-${pkgver}

  export JAVA_HOME="/usr/lib/jvm/java-22-openjdk"
  export PATH="/usr/lib/jvm/java-22-openjdk/bin:$PATH"
  mvn test \
    -Dmaven.repo.local="${srcdir}/repo"
}

package() {
  cd apache-maven-${pkgver}

  install -Dm 755 ../maven.sh -t "${pkgdir}/etc/profile.d"

  install -d "${pkgdir}/usr/share/java/${pkgname}"
  bsdtar xf apache-maven/target/apache-maven-${pkgver}-bin.tar.gz \
    --strip-components=1 \
    -C "${pkgdir}/usr/share/java/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/share/java/${pkgname}/bin/mvn" "${pkgdir}/usr/bin/mvn"
  ln -s "/usr/share/java/${pkgname}/bin/mvnDebug" "${pkgdir}/usr/bin/mvnDebug"
}

# vim: ts=2 sw=2 et:
