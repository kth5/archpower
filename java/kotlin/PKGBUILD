# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Romain Gautier <romain.gautier@nimamoh.com>

pkgname=kotlin
pkgver=1.9.21
pkgrel=1
pkgdesc='Statically typed programming language that can interoperate with Java'
arch=(any)
url='https://kotlinlang.org/'
license=(APACHE custom)
depends=('java-environment>=8')
makedepends=(setconf)
source=("https://github.com/JetBrains/kotlin/releases/download/v${pkgver/_/-}/kotlin-compiler-${pkgver/_/-}.zip")
sha512sums=('94b1e2707c03ac88066bb8d4b4b2040842a7d0968ccb5797e66bfd648ecebdec9e36f8120bea8c7d1d0b7ee8c2d0d64f9c17931bbed0444b3e81bac8e84558a4')
b2sums=('749f3a806087d64c7e6b2443d1dfc7cf30fa075f58453b21f1173b1640dc927b074420e8f29c22793717553e8be947fc0f2866d2af02d0e34a202b176260c132')

prepare() {
  cd ${pkgname}c/bin

  setconf kotlin      DIR         /usr/bin
  setconf kotlinc-js  DIR         /usr/bin
  setconf kotlinc-jvm DIR         /usr/bin
  setconf kotlinc     KOTLIN_HOME /usr/share/$pkgname
}

package() {
  cd ${pkgname}c

  # executables
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlin
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlinc
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlinc-js
  install -Dm755 -t "$pkgdir/usr/bin" bin/kotlinc-jvm

  # jar files
  cd lib
  install -d "$pkgdir/usr/share/$pkgname/lib"
  for jar in *.jar; do
    install -Dm644 "$jar" "$pkgdir/usr/share/$pkgname/lib"
  done

  # licenses
  cd "$srcdir/${pkgname}c/license"
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname"
  for txt in third_party/*.txt; do
    install -Dm644 "$txt" "$pkgdir/usr/share/licenses/$pkgname"
  done

  # build.txt must be installed for for "-version" to work
  cd $srcdir/${pkgname}c
  install -Dm644 build.txt "$pkgdir/usr/share/$pkgname"
}

# getver: github.com/JetBrains/kotlin/releases/latest
