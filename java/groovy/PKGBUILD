# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Musikolo <musikolo@hotmail.com>
# Contributor: Ruben Fonseca <fonseka@gmail.com>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>

pkgname=groovy
pkgver=4.0.15
pkgrel=1
pkgdesc='Programming language for the Java platform, inspired by Python, Ruby and Smalltalk'
arch=(any)
url='https://groovy-lang.org/'
license=(Apache)
depends=(bash java-environment)
makedepends=(gendesk)
optdepends=('groovy-docs: html and pdf documentation for Groovy')
source=("https://groovy.jfrog.io/artifactory/dist-release-local/groovy-zips/apache-groovy-binary-$pkgver.zip")
sha256sums=('31d96c1e1cf75c7e8173cdcef9bed1e3edd4e87e6400400584220e0bb42892e5')
b2sums=('926d8a6ff8c08dd025a0ad388eb08bc9a1b96e5e748a1eee482ab5c4f898e55ae0e4567318ffd465f1632b794dd82dfe4d4f24183dc84c7089026682ce503b90')

prepare() {
  gendesk -f -n \
    --pkgname $pkgname \
    --pkgdesc 'Groovy programming language' \
    --exec groovyConsole \
    --name 'Groovy Console'

  for f in $pkgname-$pkgver/bin/*; do
    sed 's:bin/env\ sh:bin/env\ sh\nGROOVY_HOME=/usr/share/groovy\nexport _JAVA_OPTIONS="-Dawt.useSystemAAFontSettings=gasp $_JAVA_OPTIONS":' -i "$f"
  done
}

package() {
  cd $pkgname-$pkgver

  install -d "$pkgdir/usr/share/groovy" "$pkgdir/usr/bin"
  cp -r lib conf "$pkgdir/usr/share/groovy"
  cp bin/* "$pkgdir/usr/bin"
  rm "$pkgdir"/usr/bin/*completion

  install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir"/usr/share/applications
  install -Dm644 bin/*completion -t "$pkgdir"/usr/share/bash-completion/completions
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

  # Remove DOS/Windows batch files
  find "$pkgdir" -name '*.bat' -type f -exec rm -v {} \;
}
