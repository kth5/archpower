# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=java-hamcrest
pkgver=3.0
pkgrel=1
pkgdesc='Java matcher objects library'
arch=(any)
url='https://github.com/hamcrest/JavaHamcrest'
license=(BSD-3-Clause)
makedepends=(git gradle java-environment)
depends=(java-runtime-headless)
source=("git+$url#commit=68984b85e869df6a888fffcad87e4b676a8fc0ac")
b2sums=('758e2c85426a6b31aef52da1656deca219383c07e0beb612279c23d33bcf0b0ffa1d76c37a1230854dbfd827e1ae8449af77db16221595993ed83ea8898b848a')

build() {
  cd JavaHamcrest

  gradle clean build javadoc jar
}

package() {
  cd JavaHamcrest

  install -Dm644 hamcrest-core/build/libs/hamcrest-core-$pkgver.jar \
    "$pkgdir/usr/share/java/hamcrest/core-1.3.jar"
  install -Dm644 hamcrest-integration/build/libs/hamcrest-integration-1.3.1-SNAPSHOT.jar \
    "$pkgdir/usr/share/java/hamcrest/integration-1.3.jar"
  install -Dm644 hamcrest-library/build/libs/hamcrest-library-$pkgver.jar \
    "$pkgdir/usr/share/java/hamcrest/library-1.3.jar"
  install -Dm644 hamcrest/build/libs/hamcrest-$pkgver.jar \
    "$pkgdir/usr/share/java/hamcrest/generator-1.3.jar"

  ln -s core-1.3.jar "$pkgdir/usr/share/java/hamcrest/core.jar"
  ln -s integration-1.3.jar "$pkgdir/usr/share/java/hamcrest/integration.jar"
  ln -s library-1.3.jar "$pkgdir/usr/share/java/hamcrest/library.jar"
  ln -s generator-1.3.jar "$pkgdir/usr/share/java/hamcrest/generator.jar"
  ln -s hamcrest/core-1.3.jar "$pkgdir/usr/share/java/hamcrest-core.jar"
  ln -s hamcrest/integration-1.3.jar "$pkgdir/usr/share/java/hamcrest-integration.jar"
  ln -s hamcrest/library-1.3.jar "$pkgdir/usr/share/java/hamcrest-library.jar"
  ln -s hamcrest/generator-1.3.jar "$pkgdir/usr/share/java/hamcrest-generator.jar"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
