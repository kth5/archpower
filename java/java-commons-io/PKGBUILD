# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe Laíns (FFY00) <lains@archlçinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=java-commons-io
pkgver=2.15.1
pkgrel=1
pkgdesc='IO related classes for Java.'
arch=(any)
url='https://commons.apache.org/io/'
license=('APACHE')
depends=('java-runtime')
source=("https://www.apache.org/dist/commons/io/binaries/commons-io-$pkgver-bin.tar.gz"{,.asc})
sha512sums=('c00b9cbd8e213311137e10e3425cf19af60c737103b3ed968c33fdc5f803892f364f4e754a7d3a222a579ee6a58a1e8731ef51835d516d2a6aab8fd2f0845e35'
            'SKIP')
validpgpkeys=('6BDACA2C0493CCA133B372D09C4F7E9D98B1CC53'  # Benson Margulies
              'CD5464315F0B98C77E6E8ECD9DAADC1C9FCC82D0'  # Benedikt Ritter
              '2DB4F1EF0FA761ECC4EA935C86FDC7E2A11262CB') # Gary David Gregory (Code signing key) <ggregory@apache.org>

package() {
  install -Dm 644 "$srcdir"/commons-io-$pkgver/commons-io-$pkgver.jar "$pkgdir"/usr/share/java/commons-io/commons-io.jar
}
