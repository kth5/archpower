# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lilv
pkgver=0.24.14
pkgrel=2
pkgdesc="A C library interface to the LV2 plug-in standard"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://drobilla.net/software/lilv.html"
license=(ISC)
depends=(glibc lv2)
makedepends=(libsndfile python serd sord sratom swig waf)
optdepends=(
  'bash-completion: completion for bash'
  'libsndfile: for lv2apply'
  'python: for Python bindings'
)
provides=(liblilv-0.so)
options=(debug)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('f266e91f3cbc325c25dd7d08bde5033091cb3072c2dcb1490e9474f562b798dbc71c45ca7d971ed4dfd6bb16f5f6725ae242a58c4486684b71350e73f1469f47'
            'SKIP')
b2sums=('bc84fe5a4bf34f88ea7f9c09cb3168186f7f0fd2f3f23b08f55100502d959fd74fe7e1c6d9307772b1983fbeedde100f75d6751e9bf92a9663aaaa115ca0770c'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

prepare() {
  cd $pkgname-$pkgver
  # let wscript(s) find the custom waf scripts
  mkdir -pv tools
  touch __init__.py
  cp -v waflib/extras/{autoship,autowaf,lv2}.py tools/
  mkdir -pv plugins/tools/
  cp -v waflib/extras/{autoship,autowaf,lv2}.py plugins/tools/
  rm -rv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/g" \
      -i wscript
}

build() {
  export LINKFLAGS="$LDFLAGS"

  cd $pkgname-$pkgver
  waf configure --prefix=/usr \
                --no-bash-completion \
                --dyn-manifest \
                --test
  waf -v build
}

check() {
  cd $pkgname-$pkgver
  waf test
}

package() {
  depends+=(libserd-0.so libsord-0.so libsratom-0.so)

  cd $pkgname-$pkgver
  waf install --destdir="$pkgdir"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 utils/lilv.bash_completion "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
# vim:set ts=2 sw=2 et:
