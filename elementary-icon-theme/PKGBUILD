# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego Principe <cdprincipe@gmail.com>
# Contributor: paterbrown <paterbrown@silberhelme.de>
# Contributor: Josip <josipponjavic@gmail.com>

pkgname=elementary-icon-theme
pkgver=8.0.0
pkgrel=1
pkgdesc='Named, vector icons for elementary OS'
arch=(any)
url=https://github.com/elementary/icons
license=(GPL3)
groups=(pantheon)
depends=(hicolor-icon-theme)
makedepends=(
  git
  librsvg
  meson
  xorg-xcursorgen
)
_tag=3d5dd03c505aaee68a4b2904ae0d00cbcf141e91
source=(elementary-icon-theme::git+https://github.com/elementary/icons.git#tag=${_tag})
sha256sums=('7cbc6f63b7459bf2d5b44447a97dc0485ea017e025fc3fcba600941f153e84a3')

pkgver() {
  cd elementary-icon-theme

  git describe --tags
}

build() {
  arch-meson elementary-icon-theme build \
    -Dvolume_icons=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
