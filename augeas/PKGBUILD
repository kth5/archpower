# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=augeas
pkgver=1.14.1
pkgrel=3
pkgdesc="A configuration editing tool that parses config files and transforms them into a tree"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://augeas.net"
license=('LGPL-2.1-or-later')
depends=(
  gcc-libs
  glibc
  libxml2
  readline
)
source=("https://github.com/hercules-team/augeas/releases/download/release-$pkgver/augeas-$pkgver.tar.gz")
sha256sums=('368bfdd782e4b9c7163baadd621359c82b162734864b667051ff6bcb57b9edff')
validpgpkeys=('AED6E2A185EEB379F17476D2E012D07AD0E3CC30') # David Lutterkort <lutter@watzmann.net>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
