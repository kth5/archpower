# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=libnvme
pkgver=1.16.1
pkgrel=2
pkgdesc="C Library for NVM Express on Linux"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/linux-nvme/libnvme"
license=('LGPL-2.1-only')
makedepends=(
    'asciidoc'
    'meson'
    'python'
    'swig'
    'systemd'
)
depends=(
    'dbus'
    'gcc-libs'
    'glibc'
    'json-c'
    'keyutils'
    'liburing'
    'openssl'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/linux-nvme/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('ce1d9d393feb84c4e82ca096db2bdb7dd4a5fd1997d711cc1904796944f2c579')

build() {
    local meson_options=(
        -Dlibdbus=auto
        -Ddocs-build=true
        build
        "${pkgname}-${pkgver}"
    )
    arch-meson "${meson_options[@]}"
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
