# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
pkgver=0.1.116
pkgrel=1
pkgdesc='Dynamic, any to any, pixel format conversion library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://gegl.org/babl/'
license=('LGPL-3.0-or-later')
depends=('glibc' 'lcms2')
makedepends=(
    'git'
    'meson'
    'gobject-introspection'
    'vala'
    'gi-docgen'
)
source=("git+https://gitlab.gnome.org/GNOME/babl.git#tag=BABL_${pkgver//./_}")
sha256sums=('18f435742abe400a246d4932d900194b2130af796d95e0ca874d05b86c58c660')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^BABL_//;s/_$//;s/_/./g;s/-/+/g'
}

build() {
  arch-meson "${pkgname}" build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
