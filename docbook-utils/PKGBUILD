# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Contributor: Suat SARIALP <muhendis.suat@gmail.com>

pkgname=docbook-utils
pkgver=0.6.15
pkgrel=1
pkgdesc='Shell scripts to manage DocBook documents'
arch=(any)
# url="https://www.sourceware.org/docbook-tools/"
url="https://github.com/devexp-db/docbook-utils"
license=('GPL-2.0-or-later')
depends=('openjade' 'docbook-dsssl' 'docbook-sgml31' 'sh')
makedepends=('perl-sgmls')
optdepends=('perl-sgmls: for conversion to man and texinfo'
            'lynx: for conversion to txt'
            'texlive-htmlxml: for conversion to pdf')
source=(#https://sourceware.org/ftp/docbook-tools/new-trials/SOURCES/${pkgname}-${pkgver}.tar.gz
        #ftp://sources.redhat.com/pub/docbook-tools/new-trials/SOURCES/${pkgname}-${pkgver}.tar.gz
        https://github.com/devexp-db/docbook-utils/releases/download/v${pkgver}/docbook-utils-${pkgver}.tar.xz
        db2html # script taken from Fedora
        bug_214982.patch
        support_source_date_epoch.patch)
sha256sums=('154b120dd897d6411a55e5a2833044807eaf31a61412df601384be6fd101ed8f'
            '10b9c29ad659cce5036871a6e85598fd33cc52c0c38cf059eeb485382a5d90d7'
            '749099d57e2693856030c8f2036dd25c55720f2d836da3ec0e49105c13cc6625'
            '185d076375d6c69bc2df5853097da9f536bed355e0bf8eee134e613b3571ccdf')

prepare() {
  cd "${pkgname}-${pkgver}"

  # Support SOURCE_DATE_EPOCH; patches from Debian
  patch -Np1 -i "${srcdir}"/bug_214982.patch
  patch -Np1 -i "${srcdir}"/support_source_date_epoch.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man
  make  
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}" htmldir="/usr/share/doc/${pkgname}/html"

  #common alternative names
  for doctype in 'html' 'ps' 'dvi' 'man' 'pdf' 'rtf' 'tex' 'texi' 'txt'; do
    ln -sv "docbook2${doctype}" "${pkgdir}/usr/bin/db2${doctype}"
    ln -sv "jw.1" "${pkgdir}/usr/share/man/man1/db2${doctype}"
  done

  # db2html is not just a symlink, as it has to create the output directory - FS#67758
  rm "${pkgdir}/usr/bin/db2html"
  install -p -m 755 ../db2html "${pkgdir}/usr/bin/db2html"
}
