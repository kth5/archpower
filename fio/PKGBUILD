# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>

pkgname=fio
pkgver=3.41
pkgrel=1
pkgdesc='Scriptable I/O tool for storage benchmarks and drive testing'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/axboe/fio'
license=(GPL2)
# TODO: enable rdma, need to move the libraries from AUR
depends=(bash curl libaio python numactl glusterfs gperftools)
optdepends=('gnuplot: generating plots using fio_generate_plots')
source=(https://github.com/axboe/fio/archive/fio-$pkgver.zip
        atomics.patch)
sha256sums=('4d01043e35ed4f6c8e98073630c1b7e9d1567b2f693056a075f593080dccaff0'
            '2ffed3415d275640f4df8b2ed1608ca6da19e3fe891c596d6c52a32ccae165f6')

prepare() {
  cd fio-fio-$pkgver
  patch -Np1 -i ${srcdir}/atomics.patch

  sed -e 's|#!/usr/bin/python2.7$|#!/usr/bin/python|' -i tools/{fio_jsonplus_clat2csv,fiologparser.py,hist/fiologparser_hist.py,hist/half-bins.py,plot/fio2gnuplot}
}

build() {
  cd fio-fio-$pkgver
  ./configure --disable-native --extra-cflags="$CFLAGS" ${configure_flags[@]}
  make
}

package() {
  cd fio-fio-$pkgver
  make DESTDIR="$pkgdir" prefix=/usr mandir=/usr/share/man install

  # documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 HOWTO.rst README.rst REPORTING-BUGS SERVER-TODO "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 MORAL-LICENSE "$pkgdir/usr/share/licenses/$pkgname/MORAL-LICENSE"

  # examples
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
  install -m644 examples/* "$pkgdir/usr/share/doc/$pkgname/examples"
}
