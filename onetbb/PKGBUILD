# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

pkgname=onetbb
pkgver=2022.3.0
pkgrel=3
pkgdesc='oneAPI Threading Building Blocks - a high level abstract threading library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://uxlfoundation.github.io/oneTBB/'
license=('Apache-2.0')
depends=(
    'gcc-libs'
    'glibc'
    'hwloc')
optdepends=(
    'python: for Python module')
makedepends=(
    'cmake'
    'python'
    'python-setuptools'
    'swig')
conflicts=('intel-tbb' 'tbb')
provides=("intel-tbb=${pkgver}" "tbb=${pkgver}")
replaces=('intel-tbb' 'tbb')
source=("https://github.com/uxlfoundation/oneTBB/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-onetbb-fix-linkage-of-test-malloc-pure-c.patch')
sha512sums=('fdc50589785b1949ca1dd4429bbcedb180be4b8966da5243ddd1f8e9f97310dd603681e0bb83c1d6c2d3e27932f577ef6739e4e82f3c54af147f4d6d906b39f1'
            'e94bcdfb6fd6d9d4ee9a16f4dc21c57bd24d78143899239afcf708aea0c8daf94a34490ccea1e9b2308036bdeb143eea2d3d8d02274f0806e47d83a7e9696ba5')

prepare() {
    # https://github.com/uxlfoundation/oneTBB/issues/1735
    # https://gitlab.archlinux.org/archlinux/packaging/packages/onetbb/-/merge_requests/2
    patch -d "oneTBB-${pkgver}" -Np1 -i "${srcdir}/010-onetbb-fix-linkage-of-test-malloc-pure-c.patch"
}

build() {
    case "${CARCH}" in
      powerpc)
        export CXXFLAGS+=' -latomic'
      ;;
    esac

    cmake -B build -S "oneTBB-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DTBB_STRICT:BOOL='OFF' \
        -DTBB4PY_BUILD:BOOL='ON' \
        -Wno-dev
    cmake --build build
}

check() {
    case "${CARCH}" in
      powerpc) return 0 ;;
    esac

    ctest --test-dir build --output-on-failure -E test_partitioner # hangs on build server
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    rm -r "${pkgdir}/usr/lib"/python*
    
    cd "oneTBB-${pkgver}/python"
    TBBROOT="${pkgdir}/usr" python setup.py install --root="$pkgdir"
}
