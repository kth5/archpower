# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

pkgname=onetbb
pkgver=2022.1.0
pkgrel=1
pkgdesc='oneAPI Threading Building Blocks - a high level abstract threading library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://uxlfoundation.github.io/oneTBB/'
license=('Apache-2.0')
depends=(
    'gcc-libs'
    'glibc'
    'hwloc')
optdepends=(
    'python: for Python module')
makedepends=(
    'cmake'
    'python'
    'python-setuptools'
    'swig')
conflicts=('intel-tbb' 'tbb')
provides=("intel-tbb=${pkgver}" "tbb=${pkgver}")
replaces=('intel-tbb' 'tbb')
source=("https://github.com/uxlfoundation/oneTBB/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7582748f7d0e0ab46ea6ee7771dfaf7fc08ca7ab7f274fb3373eae0e3411aaafbac192ece15008d9a3d9e8566f8737f96f3f4b5ccf11449ac089d5cd9ebb9eab')

build() {
    case "${CARCH}" in
      powerpc|riscv64)
        export CXXFLAGS+=' -latomic'
      ;;
    esac

    cmake -B build -S "oneTBB-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DTBB_STRICT:BOOL='OFF' \
        -DTBB4PY_BUILD:BOOL='ON' \
        -Wno-dev
    cmake --build build
}

check() {
    case "${CARCH}" in
      powerpc|riscv64) return 0 ;;
    esac

    ctest --test-dir build --output-on-failure -E test_partitioner # hangs on build server
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    rm -r "${pkgdir}/usr/lib"/python*
    
    cd "oneTBB-${pkgver}/python"
    TBBROOT="${pkgdir}/usr" python setup.py install --root="$pkgdir"
}
