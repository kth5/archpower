# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=attr
pkgver=2.5.2
pkgrel=1
pkgdesc='Extended attribute support library for ACL support'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://savannah.nongnu.org/projects/attr'
license=('LGPL')
depends=('glibc')
makedepends=('gettext')
replaces=('xfsattr')
provides=('xfsattr' 'libattr.so')
conflicts=('xfsattr')
backup=('etc/xattr.conf')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34'  # Brandon Philips <brandon@ifup.co>
              'B902B5271325F892AC251AD441633B9FE837F581'  # Frysinger <vapier@gentoo.org>
              '259B3792B3D6D319212CC4DCD5BF9FEB0313653A') # Andreas Gruenbacher <andreas.gruenbacher@gmail.com>
source=("https://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('f2e97b0ab7ce293681ab701915766190d607a1dba7fae8a718138150b700a70b'
            'SKIP')

prepare() {
  # remove the nfs entries from /etc/xattr.conf
  # http://lists.gnu.org/archive/html/bug-coreutils/2019-03/msg00008.html
  sed -i '/nfs/d' $pkgname-$pkgver/xattr.conf
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
