# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: ajs124 < aur AT ajs124 DOT de >

pkgname=firejail
pkgver=0.9.74
pkgrel=1
pkgdesc='Linux namespaces sandbox program'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/netblue30/firejail
license=('GPL-2.0-only')
depends=('apparmor')
makedepends=('git')
optdepends=('xdg-dbus-proxy: for D-Bus filtering')
backup=('etc/apparmor.d/local/firejail-default'
        'etc/firejail/firecfg.config'
        'etc/firejail/firejail.config'
        'etc/firejail/login.users')
install=firejail.install
source=(git+https://github.com/netblue30/firejail.git#tag=${pkgver})
sha256sums=('5f0c747749d36189973926372220fef2e96b2a496da2f519ba1ad1537157724d')
b2sums=('c868d381e40637b5794fbead2d504b395c44efd74bf0bd098be25517a406ca5dc2a22a5727a6aee95953aada6140e29d52f9bb918d2a0d1d345064447c08f29a')
#validpgpkeys=('F951164995F5C4006A73411E2CCB36ADFC5849A7')

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-apparmor
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
