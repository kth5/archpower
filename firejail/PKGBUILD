# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: ajs124 < aur AT ajs124 DOT de >

pkgname=firejail
pkgver=0.9.76
pkgrel=1
pkgdesc='Linux namespaces sandbox program'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/netblue30/firejail
license=('GPL-2.0-only')
depends=('apparmor')
makedepends=('git')
optdepends=('xdg-dbus-proxy: for D-Bus filtering')
backup=('etc/apparmor.d/local/firejail-default'
        'etc/firejail/firecfg.config'
        'etc/firejail/firejail.config'
        'etc/firejail/login.users')
install=firejail.install
source=("git+https://github.com/netblue30/firejail.git#tag=$pkgver")
sha256sums=('2480946d5fc2cda77482cf2ea557a99392e5bcf1616fcb1b1855fb18492dbdf0')
b2sums=('7cbace0bf6c81399524f569b98c00fdf16cbaf60230bad1a776e2c4f85390a4483b899a8ae1974db9da13426b92f73d41e2ef5e1fc7025c4dc1846af6d7f8887')
#validpgpkeys=('F951164995F5C4006A73411E2CCB36ADFC5849A7')

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-apparmor
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
