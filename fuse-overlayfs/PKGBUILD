# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='FUSE implementation of overlayfs'
pkgname=fuse-overlayfs
pkgver=1.15
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/containers/fuse-overlayfs
license=(GPL3)
depends=(fuse3)
makedepends=(git)
source=("git+https://github.com/containers/fuse-overlayfs.git#tag=v$pkgver?signed")
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha512sums=('45a620410e7fb1283e2bb9e2774b28e25a2a715cd629c9161f8e6d6d49da3abff1fbe4844c44099e0b9a8927a74bc461a6a26c8db59b7de2cbce978d3915174a')

build () {
	cd "${pkgname}"
	autoreconf -fis
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin
	make
}

check(){
	cd "${pkgname}"
	make check
}

package (){
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
}
