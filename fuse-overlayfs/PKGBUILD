# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='FUSE implementation of overlayfs'
pkgname=fuse-overlayfs
pkgver=1.13
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://github.com/containers/fuse-overlayfs
license=(GPL3)
depends=(fuse3)
makedepends=(git)
_commit=6931a9c088d0b110f41021e89073d6db83871a05	#refs/tags/v1.13^{}
source=("git+https://github.com/containers/fuse-overlayfs.git#commit=$_commit?signed")
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha512sums=('SKIP')

pkgver() {
	cd "${pkgname}"
	git describe --tags | sed 's/^v//;s/-/+/g'
}

build () {
	cd "${pkgname}"
	autoreconf -fis
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin
	make
}

check(){
	cd "${pkgname}"
	make check
}

package (){
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
}
