# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: hexchian <i at hexchain dot org>

pkgname=crun
pkgver=1.19.1
pkgrel=2
pkgdesc="A fast and lightweight fully featured OCI runtime and C library for running containers"
url="https://github.com/containers/crun"
license=('LGPL')
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
provides=('oci-runtime')
depends=('yajl' 'systemd-libs' 'libcap' 'libseccomp')
depends_powerpc64le=('criu')
depends_riscv64=('criu')
depends_x86_64=('criu')
makedepends=('libtool' 'python' 'go-md2man' 'systemd' 'git')
source=("git+https://github.com/containers/crun.git#tag=$pkgver?signed")
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA')
sha256sums=('61ec3eb34cf423eb572c84d5d4144f9c26af0e1c8211139914d923747995cdb2')

build() {
    cd "$pkgname"
    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --enable-shared \
        --enable-dynamic \
        --with-python-bindings
    make
}

package() {
    cd "$pkgname"
    make DESTDIR="$pkgdir" install
}
