# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Julien Nicoulaud <julien.nicoulaud at gmail dot com>

pkgname=zsh-syntax-highlighting
pkgver=0.8.0
pkgrel=1
pkgdesc="Fish shell like syntax highlighting for Zsh"
arch=(any)
url="https://github.com/zsh-users/zsh-syntax-highlighting"
license=(BSD-3-Clause)
depends=(zsh)
checkdepends=(procps-ng)
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('58593c8bf3fa1476ecf0fceb1fc4eab986a778de657d359a28fe48798e787896fbf2588441c69b017a08227ab44dd46473afb16e0f125aae59cc58004bfa8c2e')
b2sums=('bdb95dc825514e2c0ced2a33133967089fdbd851e00874f68497cd5daae2010f5335fb173569f5af6aea65f4e23744076c420ebc957b7bdba325fcec573d3805')

build() {
  make -C $pkgname-$pkgver
}

check() {
  make quiet-test -C $pkgname-$pkgver
}

package() {
  make PREFIX="/usr" SHARE_DIR="$pkgdir/usr/share/zsh/plugins/$pkgname" DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  # create symlink for using with oh-my-zsh
  ln -sv zsh-syntax-highlighting.zsh "$pkgdir/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh"

  # licence
  install -vdm 755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -sv /usr/share/doc/$pkgname/COPYING.md "$pkgdir/usr/share/licenses/$pkgname/"
}
