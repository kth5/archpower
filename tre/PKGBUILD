# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de
# Contributor: Igor Galic<i.galic@gmail.com>

pkgname=tre
pkgver=0.9.0
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
pkgdesc="POSIX compliant regexp matching library. Includes agrep for approximate grepping."
url="https://github.com/laurikari/tre/"
depends=('glibc')
license=('custom:BSD')
source=("https://github.com/laurikari/tre/releases/download/v${pkgver}/tre-${pkgver}.tar.gz")
sha256sums=('f57f5698cafdfe516d11fb0b71705916fe1162f14b08cf69d7cf86923b5a2477')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --enable-static 
	make 
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install 

	install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -d "$pkgdir/usr/share/doc/$pkgname"

	cd doc
	for f in default.css tre-api.html tre-syntax.html; do
		install -Dm644 "$f" "$pkgdir/usr/share/doc/$pkgname/$f"
	done 
}
