# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=libjxl
pkgname=('libjxl' 'libjxl-doc')
pkgver=0.7.0
pkgrel=3
pkgdesc='JPEG XL image format reference implementation'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://jpeg.org/jpegxl/'
license=('BSD')
makedepends=('git' 'cmake' 'brotli' 'gdk-pixbuf2' 'giflib'
             'gperftools' 'highway' 'libjpeg-turbo' 'libpng' 'openexr'
             'gtest' 'java-environment' 'python' 'asciidoc' 'doxygen'
             'graphviz' 'xdg-utils')
makedepends_powerpc64le=('java-runtime')
makedepends_x86_64=('gimp' 'java-runtime')
options=('debug')
source=("git+https://github.com/libjxl/libjxl.git#tag=v${pkgver}"
        'git+https://github.com/google/brotli.git'
        'git+https://github.com/mm2/Little-CMS.git'
        'git+https://github.com/google/googletest.git'
        'git+https://github.com/webmproject/sjpeg.git'
        'git+https://skia.googlesource.com/skcms.git'
        'git+https://github.com/google/highway.git'
        'git+https://github.com/glennrp/libpng.git'
        'git+https://github.com/madler/zlib.git'
        'libjxl-testdata'::'git+https://github.com/libjxl/testdata.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
    git -C libjxl submodule init
    local _submodule
    for _submodule in brotli googletest sjpeg skcms highway libpng zlib
    do
        git -C libjxl config --local "submodule.third_party/${_submodule}.url" "${srcdir}/${_submodule}"
    done
    git -C libjxl config --local submodule.third_party/lcms.url "${srcdir}/Little-CMS"
    git -C libjxl config --local submodule.third_party/testdata.url "${srcdir}/libjxl-testdata"
    git -C libjxl -c protocol.file.allow=always submodule update
}

build() {
    case "${CARCH}" in
      powerpc) EXTRA_CXX_FLAGS='-latomic' ;;
      *) _cmake_options=(-DJPEGXL_INSTALL_JARDIR='/usr/share/java') ;;
    esac

    export CFLAGS+=' -ffat-lto-objects'
    export CXXFLAGS+=" -ffat-lto-objects ${EXTRA_CXX_FLAGS}"
    cmake -B build -S libjxl \
        -DCMAKE_BUILD_TYPE:STRING='Release' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DJPEGXL_ENABLE_BENCHMARK:BOOL='false' \
        -DJPEGXL_ENABLE_EXAMPLES:BOOL='false' \
        -DJPEGXL_ENABLE_FUZZERS:BOOL='false' \
        -DJPEGXL_ENABLE_PLUGINS:BOOL='true' \
        -DJPEGXL_ENABLE_VIEWERS:BOOL='false' \
        -DJPEGXL_FORCE_SYSTEM_BROTLI:BOOL='true' \
        -DJPEGXL_FORCE_SYSTEM_GTEST:BOOL='true' \
        -DJPEGXL_FORCE_SYSTEM_HWY:BOOL='true' \
        -DJPEGXL_BUNDLE_LIBPNG:BOOL='NO' \
        -DBUILD_TESTING:BOOL='OFF' \
        -DASCIIDOC:BOOL='OFF' \
        -Wno-dev ${_cmake_options}
    make -C build all doc
}

check() {
    make -C build test
}

package_libjxl() {
    depends=('brotli' 'giflib' 'gperftools' 'highway' 'libjpeg-turbo' 'libpng' 'openexr')
    optdepends=('gdk-pixbuf2: for gdk-pixbuf loader'
                'gimp: for gimp plugin')
    optdepends_powerpc64le=('java-runtime: for JNI bindings')
    optdepends_x86_64=('java-runtime: for JNI bindings')
    provides=('libjxl.so' 'libjxl_threads.so')
    
    make -C build DESTDIR="$pkgdir" install
    install -D -m644 libjxl/{LICENSE,PATENTS} -t "${pkgdir}/usr/share/licenses/${pkgname}"
    case "${CARCH}" in
      powerpc64le|x86_64) mv "${pkgdir}/usr/share/java"/{org.jpeg.jpegxl,jpegxl}.jar ;;
    esac
}

package_libjxl-doc() {
    pkgdesc+=' (documentation)'
    
    install -d -m755 "${pkgdir}/usr/share/doc"
    install -D -m644 libjxl/{LICENSE,PATENTS} -t "${pkgdir}/usr/share/licenses/${pkgname}"
    cp -dr --no-preserve='ownership' build/html "${pkgdir}/usr/share/doc/libjxl"
}
