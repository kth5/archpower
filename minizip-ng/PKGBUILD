# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=minizip-ng
pkgver=4.0.0
pkgrel=2
pkgdesc='Fork of the popular zip manipulation library found in the zlib distribution'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/zlib-ng/minizip-ng'
license=(custom)
depends=(openssl bzip2 zstd)
makedepends=(cmake)
source=(https://github.com/zlib-ng/minizip-ng/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f9062e576de026fd5026d65597de3b05263cd4d91400cacdbbe36dfa8a642fff')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMZ_LIB_SUFFIX="-ng" \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
