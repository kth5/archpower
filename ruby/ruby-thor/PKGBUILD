# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='thor'
pkgname="ruby-${_gemname}"
pkgver=1.4.0
pkgrel=1
pkgdesc='Toolkit for building powerful command-line interfaces'
url='https://github.com/erikhuda/thor'
arch=(any)
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
  ruby-webmock
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6f1fe42212583be1fc963c852524795967a725eac65d27a8597b81d52ba22ea35ab0ea69e37df2a81c0280957444851be45861ba86f90cd0c794c0151ea84fd4')
b2sums=('94a8973f62d683be6c7089798be3ad3f3b90c2fe6aa391a28624f3672d83bb1b95865aa7c0719aec7f01481c5c0826ae94b92c146131f8f7c2f5227e1376fc1f')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec to allow newer version of the deps
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place --regexp-extended \
    --expression '/coveralls/Id' \
    --expression '/SimpleCov.start do/,/end/d' \
    --expression '/simplecov/Id' \
    Gemfile \
    spec/helper.rb

  rm --verbose spec/quality_spec.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"

  rspec spec
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install --install-dir "${pkgdir}${_gemdir}" --bindir "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -D --mode=644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install -D --mode=644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
