# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

_gemname='concurrent-ruby'
pkgname='ruby-concurrent'
pkgver=1.3.5
pkgrel=1
pkgdesc='Modern concurrency tools including agents, futures, promises, thread pools, actors, supervisors, and more.'
arch=(any)
url="https://github.com/ruby-concurrency/${_gemname}"
license=('MIT')
depends=(
  ruby
)
makedepends=(
  git
  jruby
  ruby-rake
  ruby-rake-compiler
  ruby-rake-compiler-dock
  ruby-rdoc
)
optdepends=('java-runtime>=8: jruby support')
options=('!emptydirs')
source=("git+${url}#tag=v${pkgver}")
sha512sums=('c228d949589680d78fbe706fe0b67dd1a0ce70fd20072e68529c28c990c597bbcd3c47a9efe4eb72dc8ff9a28502ac70c2953375840b19049f8290563bb36c84')
b2sums=('02b92b96b5ecfe94f590b6f6ee68d714739b4459c4a4ba23273cebbed5667aa7662c2d047e844333aedd42a388b19167b9b401eb0a289c8cc6cbff499efb893e')

prepare() {
  cd "${_gemname}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}"

  local _gemdir="$(gem env gemdir)"

  export JRUBY_HOME=/opt/jruby
  rake repackage
  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${_gemname}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
