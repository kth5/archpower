# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-http1
pkgver=0.35.2
pkgrel=1
pkgdesc='A low level implementation of the HTTP/1 protocol'
arch=(any)
url='https://github.com/socketry/protocol-http1'
license=(MIT)
depends=(
  ruby
  ruby-protocol-http
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-traces
)
options=(!emptydirs)
source=(https://github.com/socketry/protocol-http1/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('4545ee7beb45c69b48b5f1d5f80f19f5400accd7e9f9db82016d73d94f27a7f50988a215f9a289392802a066856b6867e7ac00681146411096cad0e9942966c2')
b2sums=('e47bbc76ad68488778f1860a1de03f9d3e428c32a2e8003632830248f7716ef2870656bd08c7a47fcde1893b975a6981b5aef9aab6c75dff151f9e6ed5219400')

prepare() {
  cd protocol-http1-$pkgver

  sed -i -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    protocol-http1.gemspec \
    gems.rb

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http1-$pkgver
  gem build protocol-http1.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-http1-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http1-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-http1-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
