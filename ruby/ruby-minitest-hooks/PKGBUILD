# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-minitest-hooks
pkgver=1.5.2
pkgrel=2
pkgdesc='Around and before_all/after_all/around_all hooks for Minitest'
arch=(any)
url='https://github.com/jeremyevans/minitest-hooks'
license=(MIT)
depends=(
  ruby
  ruby-minitest
)
makedepends=(
  git
  ruby-minitest-global_expectations
  ruby-rake
  ruby-rdoc
  ruby-sequel
  ruby-sqlite3
)
options=(!emptydirs)
source=(git+https://github.com/jeremyevans/minitest-hooks.git#tag=$pkgver)
sha256sums=('25907325542707bb490f462926d6d12bf2881b7b0726fa8d2240b77a3c362070')

build() {
  local _gemdir="$(gem env gemdir)"
  cd minitest-hooks
  gem build minitest-hooks.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    minitest-hooks-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd minitest-hooks
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd minitest-hooks
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
