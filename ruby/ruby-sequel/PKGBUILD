# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sequel
pkgver=5.96.0
pkgrel=1
pkgdesc='Sequel: The Database Toolkit for Ruby'
arch=(any)
url='https://github.com/jeremyevans/sequel'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-csv
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  ruby-minitest-global_expectations
  ruby-minitest-hooks
  ruby-rake
)
options=(!emptydirs)
source=(git+https://github.com/jeremyevans/sequel.git#tag=$pkgver)
sha512sums=('d70da4fa09da561890efb0f4954b6d575ca0e1371ce97b6f5199ae49bc0db3a850a56b2b29565cd254a661b0a428726bc6d58ca344c28ba9d192ef5e9a22ba15')
b2sums=('0bf0611dd3df3768cec2a9261807913a1dfef5d45644a0419c8917d2408ffe42e0f2287b7390f8cab2d12fd37d3988bf147329af28067b0f3eaa624e66d404e0')

prepare() {
  cd sequel
  sed '/activemodel/d' -i sequel.gemspec
  rm spec/extensions/active_model_spec.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  gem build sequel.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sequel-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sequel
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd sequel
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
