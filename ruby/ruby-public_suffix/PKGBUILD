# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: farawayer <farwayer@gmail.com>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='public_suffix'
pkgname="ruby-${_gemname}"
pkgver=6.0.2
pkgrel=1
pkgdesc='Domain name parser based on the Public Suffix List'
arch=(any)
url='https://simonecarletti.com/code/publicsuffix-ruby'
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-mocha
  ruby-rake
  ruby-yard
)
source=("https://github.com/weppos/publicsuffix-ruby/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
options=("!emptydirs")
sha512sums=('01f1ffb85f8e0e52b6dc84fc82da0b248c6d85d5c5ef92cff724fc2ec5d7757329129e7c0c5e012be396dc9fa3d52037ba6b10f7d2cf1c934364113e7f526c48')
b2sums=('2fbc5ff8dd47626fd15f463e8f376d869367fbb95b2a0917f784f3ec37650890caaa488b1484ec31597aa3ae15edaea1275f26922dba6963ab2b6849a3329e99')

prepare() {
  cd "publicsuffix-ruby-${pkgver}"

  sed --in-place \
    --expression "s,git ls-files -z,find . | sed -e 's|^./||'," \
    --expression 's/x0/n/' \
    "${_gemname}.gemspec"
  sed --in-place '/rubocop/Id' Rakefile
  sed --in-place '/reporters/Id' test/test_helper.rb
}

build() {
  cd "publicsuffix-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "publicsuffix-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake test
}

package() {
  cd "publicsuffix-ruby-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
