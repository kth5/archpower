# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: farawayer <farwayer@gmail.com>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='public_suffix'
pkgname="ruby-${_gemname}"
pkgver=7.0.2
pkgrel=1
pkgdesc='Domain name parser based on the Public Suffix List'
arch=(any)
url='https://simonecarletti.com/code/publicsuffix-ruby'
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-mocha
  ruby-rake
  ruby-yard
)
source=("https://github.com/weppos/publicsuffix-ruby/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
options=("!emptydirs")
sha512sums=('e7202feb21b41ddc0de5bf3cb5470ffb5762c889648ae134f2b03ee05fa37817163deb19569fbdfc6332115545da2557bc17cb6cef18a85a5344ff7f37f34d2c')
b2sums=('46a7697242f1def1ecdc9b70e5ac5ad9ef91ca4b3605b677d675da941415013e641ee7c5d4b317e2c6435b424bdc2a927a0acf2e10ef3a207f460a1f5b8dd6db')

prepare() {
  cd "publicsuffix-ruby-${pkgver}"

  sed --in-place \
    --expression "s,git ls-files -z,find . | sed -e 's|^./||'," \
    --expression 's/x0/n/' \
    "${_gemname}.gemspec"
  sed --in-place '/rubocop/Id' Rakefile
  sed --in-place '/reporters/Id' test/test_helper.rb
}

build() {
  cd "publicsuffix-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "publicsuffix-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake test
}

package() {
  cd "publicsuffix-ruby-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
