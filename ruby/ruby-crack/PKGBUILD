# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Simon Kohlmeyer <simon.kohlmeyer@gmail.com>

_gemname='crack'
pkgname="ruby-${_gemname}"
pkgver=1.0.0
pkgrel=5
pkgdesc='Really simple JSON and XML parsing, ripped from Merb and Rails.'
arch=('any')
url='https://github.com/jnunemaker/crack/'
license=('MIT')
depends=(
  ruby
  ruby-rexml
  ruby-bigdecimal
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-rake
)
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('01f2231033a64aa3a107de3c0185559800be32c679671835962bd4e8f74cb644b9cb1e4895880ab03bc15e82aac465875d1ce187b977be65f514282718b7837c')
b2sums=('265948336f3a0567d08766ddd179502e9dac0b0ef647a636d74b2bf7b6d8a2bb08385df566f118151cf4b72c700d17c3f69bfe8d61dc7035102319c2513190c1')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # we build with a tar ball, so `git` won't work
  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"

  # `rake` isn't an actual dependency, so we can remove it
  sed --in-place '/rake/d' Gemfile
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  rake test
}
package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 README.md History -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
