# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

_gemname=rexml
pkgname=ruby-$_gemname
pkgver=3.4.4
pkgrel=1
pkgdesc='An XML toolkit for Ruby'
arch=(any)
url='https://github.com/ruby/rexml'
license=(BSD-2)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-test-unit
  ruby-test-unit-ruby-core
)
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3bb0d4a2cf4d8f567f6a7c1867506eed052fadf15b556f249a7d7dff533242817b90671450e7cc5144d7f09bb9f85c59e01eb9c703da2a479a1ea740f6e055ec')
b2sums=('d92c88bc8f632c752cd353419818cf452f80824484e5411a2c26a0f13b7d8820f5e985bcbe27f9387725e37a9a54d8e1d2a16966ba12d6f82d286ce8cf12ea97')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
