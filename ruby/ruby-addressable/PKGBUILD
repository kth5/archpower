# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Artem Vorotnikov <artem at vorotnikov dot me>

_gemname=addressable
pkgname="ruby-${_gemname}"
pkgver=2.8.7
pkgrel=2
pkgdesc='URI Implementation'
arch=(any)
url='https://github.com/sporkmonger/addressable'
license=('Apache')
depends=(
  ruby
  ruby-public_suffix
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("${url}/archive/${_gemname}-${pkgver}/${pkgname}-${_gemname}-${pkgver}.tar.gz")
sha512sums=('df75ec756f504183e08c259ecaf1432ee76b5bf1008a05299e3933aace0f08346762ca086aa6c3227cf0518d600c6d419c763cc1b9bf19897bee009fabacca99')
b2sums=('8ce28224387fdb5500fcff25afd2be4bef4410f819caa1a0eb25e9cc376dbbdfafe1fdb509d7faf30f7f61535fe856552ccb239346449b8ed358051bc0703722')

prepare() {
  cd ${_gemname}-${_gemname}-${pkgver}

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec" Gemfile

  # ignore coverage
  sed --in-place --regexp-extended \
    --expression '/group :coverage/,/end/d' \
    Gemfile
  sed --in-place --regexp-extended \
    --expression '/begin/,/end if/d' \
    spec/spec_helper.rb
}

build() {
  cd "${_gemname}-${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${_gemname}-${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
