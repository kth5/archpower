# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

_gemname=test-unit
pkgname="ruby-${_gemname}"
pkgver=3.5.9
pkgrel=1
pkgdesc='An xUnit family unit testing framework for Ruby'
arch=(any)
url="https://github.com/${_gemname}/${_gemname}"
license=('GPL')
depends=('ruby' 'ruby-power_assert')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3af89f786bfa7b155d55609762dc84da4039f8953a8235694aff2e846a663a2372f2c2b1e46ed57ca9785de1d241337145743d7af0f348df6e3b5025edd9ce2b')

# fix wrong license identifier to match the official ones from https://spdx.org/licenses/
prepare() {
  cd "${_gemname}-${pkgver}"
  sed --in-place 's/spec.licenses.*/spec.licenses = ["BSD-2-Clause", "PSF-2.0", "Ruby"]/' "${_gemname}.gemspec"
}

build() {
 cd "${_gemname}-${pkgver}"
 gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"
  ruby test/run-test.rb
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  rm -rf "${pkgdir}/${_gemdir}/cache"

  install -Dm 644 README.md doc/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 BSDL COPYING PSFL -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
