# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-childprocess
pkgver=5.1.0
pkgrel=3
pkgdesc='Cross-platform Ruby library for managing child processes'
arch=(any)
url='https://github.com/enkessler/childprocess'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-rspec
)
options=(!emptydirs)
source=(https://github.com/enkessler/childprocess/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('144b9cd1de515694a73d332682dfc975c6c4d8e38d7c475a0e9a714f6671808e')

prepare() {
  cd childprocess-$pkgver
  sed -r -e '/[cC]overalls/d' -e 's|git ls-files -- spec/\*|echo|' -i childprocess.gemspec spec/spec_helper.rb
  sed 's|git ls-files|find -type f|' -i childprocess.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd childprocess-$pkgver
  gem build childprocess.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    childprocess-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd childprocess-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd childprocess-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
