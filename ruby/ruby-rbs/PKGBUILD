# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='rbs'
pkgname="ruby-${_gemname}"
pkgver=3.8.0
pkgrel=1
pkgdesc='RBS is the language for type signatures for Ruby and standard library definitions'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/ruby/${_gemname}"
license=('custom:BSDL')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-abbrev
  ruby-base64
  ruby-bigdecimal
  ruby-bundler
  ruby-csv
  ruby-dbm
  ruby-json-schema
  ruby-minitest
  ruby-mutex_m
  ruby-net-smtp
  ruby-nkf
  ruby-rake
  ruby-rake-compiler
  ruby-rspec
  ruby-test-unit
)
options=('!emptydirs')
source=("git+${url}#tag=v$pkgver")
sha512sums=('94de5d47ba6a1f22c4c939f4e50669f3aa484962908d866c27faa9c53b69eaa6e0c0c66670a2ef741c80474b997f1774024d89e31896deb5bbf8c95f8aa98f78')
b2sums=('0b34e8a2ae0ca129e705ab446b86472f6d3d2be6207685b591b19810afe3dae06d3190922362df715c93abd70022c01aa2e15a4fc92bf7e6941868cf3144e717')

prepare() {
  ln -s "${_gemname}" "${_gemname}-${pkgver}"

  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place --regexp-extended \
    --expression 's/:rubocop, //g' \
    --expression '/task :rubocop do/,+8d' \
    Rakefile

  sed --in-place --regexp-extended \
    --expression '/activesupport/d' \
    --expression '/goodcheck/d' \
    --expression '/raap/d' \
    --expression '/rubocop/d' \
    --expression '/group :profilers do/,/end/d' \
    --expression '/group :typecheck_test do/,/end/d' \
    Gemfile

  rm Gemfile.lock
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test stdlib_test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 BSDL COPYING --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
