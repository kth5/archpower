# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='rbs'
pkgname="ruby-${_gemname}"
pkgver=3.4.4
pkgrel=2
pkgdesc='RBS is the language for type signatures for Ruby and standard library definitions'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/ruby/${_gemname}"
license=('custom:BSDL')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-ast
  ruby-bundler
  ruby-json-schema
  ruby-minitest
  ruby-net-smtp
  ruby-rake
  ruby-rake-compiler
  ruby-test-unit
)
options=('!emptydirs')
source=("git+${url}#tag=v$pkgver")
sha512sums=('6477ae1443278e981561937a764f00f25e09fc7ecfed25486777b1e9575e270d894fc10e91b681594a7392a1fb672709ec2fcb70829c5deb642f293487ea4402')
b2sums=('052990cd353bc42b65d43ee37e9f630a5a37987b74a3c9f18ee2a70f6fdea174727a67c963f806c71e1c114462fb23304f713c00bd08fb827e2b3b6a1fc28b27')

prepare() {
  ln -s "${_gemname}" "${_gemname}-${pkgver}"

  cd "${_gemname}-${pkgver}"

  git cherry-pick -n f9e1c01df11ef32539d477132b75a6dd08558988

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files -z|find . -type f -not -path "*/\.git*" -print0|' "${_gemname}.gemspec"

  sed --in-place '6,22d' Gemfile
  rm Gemfile.lock
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 BSDL COPYING --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
