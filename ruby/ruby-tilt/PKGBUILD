# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=tilt
pkgname=ruby-tilt
pkgver=2.3.0
pkgrel=5
pkgdesc='Generic interface to multiple Ruby template engines'
url='https://github.com/rtomayko/tilt'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jeremyevans/tilt/archive/v${pkgver}.tar.gz)
sha256sums=('344882278fc0a50a760a562e3c25f0997fc9b8816b4c5c259b4188604bf8d5c9')
sha512sums=('3438c196c2e0936f2c3551fe3786d71b1336118352b97c95d3504b900082ce4248bbaf715f3f812eb7fe6e959767c75e316bf6cd5b659c216bbfd630dbf678bc')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
