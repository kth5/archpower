# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-http2
pkgver=0.23.0
pkgrel=1
pkgdesc='A low level implementation of the HTTP/2 protocol'
arch=(any)
url='https://github.com/socketry/protocol-http2'
license=(MIT)
depends=(
  ruby
  ruby-protocol-hpack
  ruby-protocol-http
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-metrics
  ruby-sus
  ruby-traces
)
options=(!emptydirs)
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('27fb3feccd5c2f2b8971cfb0a18beb548baa7e4b940db4214a2de1e66b959c8e')

prepare() {
  cd protocol-http2

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i protocol-http2.gemspec

  sed -i \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http2
  gem build protocol-http2.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-http2-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http2
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-http2
  cp -a tmp_install/* "$pkgdir"/
}
