# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='nokogiri'
pkgname="ruby-${_gemname}"
pkgver=1.18.9
pkgrel=1
pkgdesc='Nokogiri (é‹¸) is an HTML, XML, SAX, and Reader parser'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://nokogiri.org'
license=('MIT')
depends=(
  libxslt
  ruby
  ruby-mini_portile2
  ruby-racc
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-minitest
  ruby-rake
  ruby-rake-compiler
  ruby-rake-compiler-dock
  ruby-rubyzip
  ruby-simplecov
)
options=('!emptydirs')
source=(
  "https://github.com/sparklemotion/${_gemname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "https://github.com/sparklemotion/${_gemname}/commit/af5b1b0590e2f365763d1801a4a5106198b47a53.patch"
)
sha512sums=('55b7d6f4b88cdb770c51e21ddc97b373343b25d3bd1250a69c4bb0dfcf34bf5a93624c15279b567be5fd6d04bb4e47a68ce5d8ead779493ad498286deeeb71e0'
            '3ca191db16e283e485fa1f8541834229b97a956b77ea300fcef1c0149f11174e4f9e34424ec67a8eb3f7818d8fe6e52adfb23c8fcf072f417d269ae03a5211c0')
b2sums=('c4eb387d0bd0883997b6d911e70c2474a750f70e0fafe7a87fe2c564d7d063b9f7c55c9a6bce9aeef52c7facc19f0b0ae38a6bce1d71d82f151527029861af8c'
        '268936f19c3bbcc672e408e0488cee14f10609824fd879b7adf0f93ffcf7588c8201820b0913d056d80cfd83a006c54faa2e4a1fba34795555a66afa97ec1e64')

prepare() {
  cd "${_gemname}-${pkgver}"

  patch --verbose --strip=1 --input="../af5b1b0590e2f365763d1801a4a5106198b47a53.patch"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we don't do jruby
  sed --in-place \
    --expression '/\.java/d' \
    --expression '/\.jar/d' \
    "${_gemname}.gemspec"

  # we don't have rubocop in the repos
  rm --verbose rakelib/rubocop.rake
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  export CFLAGS+=' -ffat-lto-objects'

  gem build "${_gemname}.gemspec" -- --use-system-libraries

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem" \
    -- \
    --use-system-libraries

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
