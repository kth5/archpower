# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_name=json
pkgname="ruby-json"
pkgver=2.7.1
pkgrel=1
pkgdesc="JSON implementation for Ruby"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/flori/json"
license=(Ruby)
depends=(
  glibc
  ruby
)
makedepends=(
  ragel
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-test-unit
  ruby-test-unit-ruby-core
)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('81e7cf51363095088142a7ac15428765f296e9b8c205946abf9ab98ddf7c57aa732c8111e6b0e239606645dfbc24dd40fbe981a52ab2d9fac97c0e3fb08133a0')
b2sums=('9921b8dd26123d27b752d3dd633f788437a1aa2003b09ad6d0f43e946362f48a8e5b8dcc70d08f61cc9affa2578750e40c5a78d3bf88e0417af8890100987c79')

prepare() {
  cd $_name-$pkgver

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed -i -e 's|~>|>=|g' $_name.gemspec
}

build() {
  local gemdir="$(gem env gemdir)"
  local gem_install_options=(
    --local
    --verbose
    --ignore-dependencies
    --no-user-install
    --install-dir tmp_install/$gemdir
    --bindir tmp_install/usr/bin
    $_name-$pkgver.gem
  )
  local unrepro_files=(
    tmp_install/$gemdir/cache/
    tmp_install/$gemdir/gems/$_name-$pkgver/vendor/
    tmp_install/$gemdir/doc/$_name-$pkgver/ri/ext/
  )

  cd $_name-$pkgver

  gem build $_name.gemspec
  gem install "${gem_install_options[@]}"

  # remove unrepreducible files
  rm -frv "${unrepro_files[@]}"

  find tmp_install/$gemdir/gems/ \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find tmp_install/$gemdir/extensions/ \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local gemdir="$(gem env gemdir)"

  cd $_name-$pkgver

  GEM_HOME="tmp_install/$gemdir" rake test
}

package() {
  cd $_name-$pkgver

  mv -v tmp_install/* "$pkgdir/"

  install -vDm 0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 0644 ./*.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
