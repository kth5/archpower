# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rack-test
pkgname=ruby-${_gemname}
pkgver=2.2.0
pkgrel=2
pkgdesc='Small, simple testing API for Rack apps'
url='https://github.com/rack-test/rack-test'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rack')
makedepends=('ruby-rdoc')
checkdepends=('ruby-rake' 'ruby-rspec' 'ruby-minitest' 'ruby-minitest-global_expectations')
options=('!emptydirs')
source=(https://github.com/rack-test/rack-test/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha256sums=('727719e34ca4c9056ede02abe5efc8ca57d02a6e68a3c5a646e70b63d330bf50')
sha512sums=('149dd5a1209c2a5a86c83a2b632d1ac046053cc60fbb0ac10dbf44db71e25cce24d4a3f1f43a22a28d91f259d0e599652529ea892fae6fa01ea2711f1d84de96')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

check() {
  cd ${_gemname}-${pkgver}
  rake spec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README*.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 MIT-LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
