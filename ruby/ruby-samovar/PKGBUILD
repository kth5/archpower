# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-samovar
pkgver=2.4.1
pkgrel=1
pkgdesc='A flexible option parser excellent support for sub-commands and help documentation'
arch=(any)
url='https://github.com/ioquatix/samovar'
license=(MIT)
depends=(
  ruby
  ruby-console
  ruby-mapping
)
makedepends=(
  git
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-covered
  ruby-decode
  ruby-sus
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/samovar.git#tag=v$pkgver)
sha512sums=('cce6cd82bded5d281ce270d621e5ac0497765b4e57de7a3eb82072f9dcdc7c89afcfcf9eeb5e81cc234306de8806e5c408946a5e1bff8b141eaa7d29e2af9901')
b2sums=('edd33e3fd9d158733f408796faca6918364af877a3fa80685ca1af9f1c3d8ea8df5ca3e6c0aecf39f93709cd4c7c49f194db6a8e78b1a5506721f1a603e3744f')

prepare() {
  cd samovar

  sed -i '/release\.pem/d' samovar.gemspec
  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/agent-context/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd samovar
  gem build --verbose samovar.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    samovar-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd samovar
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd samovar
  cp -a tmp_install/* "$pkgdir"/
}
