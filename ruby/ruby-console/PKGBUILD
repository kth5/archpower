# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-console
pkgver=1.33.0
pkgrel=1
pkgdesc='Beautiful logging for Ruby'
arch=(any)
url='https://github.com/socketry/console'
license=(MIT)
depends=(
  ruby
  ruby-fiber-annotation
  ruby-fiber-local
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=("git+${url}.git#tag=v$pkgver")
sha512sums=('ce57b3bbe20b8e782de7e8f88a9dc3800846273b60d31fa572d4d60eb47e99cfd11be97db2789c0b616ae33e36acf4317fbe6c28de529792d51acc97abc78db2')
b2sums=('9041047ff13413e2c6d99ab3ba0819d761acb74a0923c326bf8351dd1e72757e5ae918e6b28dfc533a6d814f1d1823f88f5004a608ee8bfd68fee50a82fd2296')

prepare() {
  cd console

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i console.gemspec
  sed -i \
    -e '/group :maintenance/,/end/d' \
    -e '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd console
  gem build console.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    console-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd console
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd console
  cp -a tmp_install/* "$pkgdir"/
}
