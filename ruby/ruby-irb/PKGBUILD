# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='irb'
pkgname="ruby-${_gemname}"
pkgver=1.13.1
pkgrel=2
pkgdesc='Interactive Ruby command-line tool for REPL (Read Eval Print Loop).'
arch=(any)
url="https://github.com/ruby/${_gemname}"
license=('RUBY' 'BSD-2')
depends=(
  ruby
  ruby-reline
)
makedepends=(
  ruby-bundler
  ruby-rdoc
)
checkdepends=(
  ruby-debug
  ruby-rake
  ruby-test-unit
  ruby-test-unit-ruby-core
  ruby-tracer
)
options=('!emptydirs')
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "${url}/commit/bad7492ab07f053b714b92d00e6caa0492870bb0.patch"
)
sha512sums=('63f71bac1a51bb8c022c0e32f396b6a28e53be2652e27cfd0f025cd02a6878d934ddcbc6ccd7760a06f24d8811f6c24fc2404b4a89d67e6dbd3342b2300d28ad'
            '16e0868031759a7820a6b20b203678823709ea2daed92853118b6d9079e1a5d243dac2f0c989733cc877960406ead8bddefd638594a8a48c2e9e151709fd1637')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  patch --verbose --strip=1 --input="../bad7492ab07f053b714b92d00e6caa0492870bb0.patch"
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
  rake test_yamatanooroti
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"
  local _platform="$(gem env platform | cut -d':' -f2)"
  local _extension_api_version="$(ruby -e 'puts Gem.extension_api_version')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  install --verbose --directory --mode=0755 \
    "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_platform}" \
    "${pkgdir}${_gemdir}/specifications/default"

  mv --verbose "${pkgdir}${_gemdir}/specifications/${_gemname}-${pkgver}.gemspec" "${pkgdir}${_gemdir}/specifications/default/${_gemname}-${pkgver}.gemspec"
  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/lib/${_gemname}.rb" "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_gemname}.rb"
  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/lib/${_gemname}" "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_gemname}"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}${_gemdir}/cache/" \
    "${pkgdir}${_gemdir}/build_info/" \
    "${pkgdir}${_gemdir}/extensions/" \
    "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/"{.git*,.document,bin,doc,man,*.*,Gemfile,Rakefile} \
    "${pkgdir}${_gemdir}/plugins/" \
    "${pkgdir}${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  install --verbose -D --mode=0644 man/irb.1 --target-directory "${pkgdir}/usr/share/man/man1"
  install --verbose -D --mode=0644 LICENSE.txt --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
