# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='net-imap'
pkgname="ruby-${_gemname}"
pkgver=0.5.8
pkgrel=1
pkgdesc='Ruby client api for Internet Message Access Protocol'
arch=(any)
url="https://github.com/ruby/${_gemname}"
license=('RUBY' 'BSD-2-CLAUSE')
depends=(
  ruby
  ruby-net-protocol
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-base64
  ruby-bundler
  ruby-rake
  ruby-test-unit
  ruby-test-unit-ruby-core
)
options=('!emptydirs')
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "${pkgname}_disable_simplecov.patch"
)
sha512sums=('bd87d59f996b2fede494ca21f39921b62bb3801a8e09dada95170e7a5ad6306dd20c12f8880d5962cf75c46c29ae134c49a4f23e86c5b5143fed9548436a3555'
            '59d7e1420ffc554bfeec155fbe1f6c4534fe8b2c92f4a1cb68361f337ddf186c9018fa952ee5b255acd0ce0c5862452c06ab2785ed6cd98b58857c2ab1a9d393')
b2sums=('b3cd46655fee9d4e4880b8e50ea164cc8b1154631ccedd86aff7a613df73a849cfb8576f1cdf90c4a352501933fd967d525e1f5d31187715793bcde9e2f9e38f'
        '7c8a437ab751c48afb441209634bc695e050d7fb7c28038e87a57c1acc73c66136f215c17a1fc6b51d6a54174894e1537eddc5ca80930428e5c62d0ae7548273')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|`git ls-files -z 2>/dev/null`\.split\("\\x0"\)|`find . -type f -not -path "*/\.git*" -printf "%P\n"`\.split\("\n"\)|' "${_gemname}.gemspec"

  patch --verbose --strip=1 --input="../${pkgname}_disable_simplecov.patch"
  sed --in-place --regexp-extended \
    --expression '/group :test/,/end/d' \
    Gemfile
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
