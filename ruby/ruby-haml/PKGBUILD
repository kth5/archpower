# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Aoibhinn di Tori <email@aoibh.in>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

_gemname='haml'
pkgname="ruby-${_gemname}"
pkgver=7.2.0
pkgrel=1
pkgdesc='HTML Abstraction Markup Language - A Markup Haiku'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://haml.info'
license=('MIT')
depends=(
  ruby
  ruby-temple
  ruby-thor
  ruby-tilt
)
makedepends=(
  git
  ruby-rdoc
)
options=('!emptydirs')
source=("git+https://github.com/haml/${_gemname}.git#tag=v${pkgver}")
sha512sums=('53c7f5ddf3766c0221f1edc26d25c6ebd44c089fd0f7a7de48caf9bd797781631b90776b84c6ba934db38b6ac59694636280acccd5a59c339ee0bd8e39ecc2b6')
b2sums=('73c5c8e4efaec147aa51185b5b2017658957d483e9cc0afe2a94c9d486b7e5167bff0db9b2693d9267625e4bd2d8cc893fb4744ae5fc5ae189aec9414786bf83')

prepare() {
  cd "${_gemname}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

# needs rails
#check() {
#  cd "${_gemname}"
#
#  local _gemdir="$(gem env gemdir)"
#
#  GEM_HOME="tmp_install/${_gemdir}" rake test
#}

package() {
  cd "${_gemname}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 MIT-LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
