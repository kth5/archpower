# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Aoibhinn di Tori <email@aoibh.in>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

_gemname='haml'
pkgname="ruby-${_gemname}"
pkgver=6.3.0
pkgrel=4
pkgdesc='HTML Abstraction Markup Language - A Markup Haiku'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://haml.info'
license=('MIT')
depends=(
  ruby
  ruby-temple
  ruby-thor
  ruby-tilt
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("https://github.com/haml/${_gemname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('64ee2eb7fad2e0b77307dd18a268b53a7f21b78b99a8bf80831cda9f8b6d8be983e7a23ba62942d251e7baf2c9732e38c18b842052690e5bbeb27961ca139c73')
b2sums=('0672d39857fa0dac466bc29468a76e71d583da6637ce8534eb4d84aa4388bdc0f42d747d74cc5389bd24faf428076c13cf1897edc93317b2da7b770830d53b37')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files -z|find -print0|' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

# needs rails
#check() {
#  cd "${_gemname}-${pkgver}"
#
#  local _gemdir="$(gem env gemdir)"
#
#  GEM_HOME="tmp_install/${_gemdir}" rake test
#}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 MIT-LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
