# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-http
pkgver=0.58.1
pkgrel=1
pkgdesc='Provides abstractions to handle HTTP protocols'
arch=(any)
url='https://github.com/socketry/protocol-http'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-benchmark
)
options=(!emptydirs)
source=("git+${url}.git#tag=v${pkgver}")
sha512sums=('23b352d4296c5437a58dee0306e0b93df1108b0247c480acbc8db774d63a578d236e59c384a532f6ab1aca1bfcd370403f931b48188bdb9833f8181d3ac0c5cf')
b2sums=('4af3d9dac1724bcdb8e6c886dca1c2014b4fe562b9de321239e0f4f940a8ba4a109f72272a7338e6816650fce523dc5aaaf24925befe73d7f1a38d688e498386')

prepare() {
  cd protocol-http

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i protocol-http.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http
  gem build protocol-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-http
  cp -a tmp_install/* "$pkgdir"/
}
