# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-traces
pkgver=0.18.2
pkgrel=1
pkgdesc='Application instrumentation and tracing'
arch=(any)
url='https://github.com/socketry/traces'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-console
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=(git+https://github.com/socketry/traces.git#tag=v$pkgver)
sha256sums=('ae5d65b1c568866a6613174ff9d8f452df1e7bd3ffa64a7f9da5cb50fc9ee73e')

prepare() {
  cd traces
  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i traces.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd traces
  gem build traces.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    traces-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd traces
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd traces
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
