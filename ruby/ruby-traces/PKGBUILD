# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-traces
pkgver=0.18.1
pkgrel=1
pkgdesc='Application instrumentation and tracing'
arch=(any)
url='https://github.com/socketry/traces'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-console
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=(https://github.com/socketry/traces/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('5cd4fad140e0f9a9b2514be4520f7022b9648152981e0ad7ce133122f7307cd6')

prepare() {
  cd traces-$pkgver
  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i traces.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd traces-$pkgver
  gem build traces.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    traces-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd traces-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd traces-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
