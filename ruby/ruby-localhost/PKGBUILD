# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-localhost
pkgver=1.5.0
pkgrel=1
pkgdesc='Manage a local certificate authority for self-signed localhost development servers'
arch=(any)
url='https://github.com/socketry/localhost'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-io-endpoint
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-async-http
)
options=(!emptydirs)
source=(git+https://github.com/socketry/localhost.git#tag=v$pkgver)
sha512sums=('c20f6caa401ccb7b69bcf51241124e2537c291d9e34db6998ea15a722368851615ebe18a2ebf694d351839bee0f0f4edd8614eb3b6b82b3470f3b6787b311913')
b2sums=('254372c748dde22001fac3aae64de86d613471a72b6e0a9e9e123e07435fa140cae58e3ab2f182c68b31d9b3749aead39290328fed4bf7c098242859728dee8b')

prepare() {
  cd localhost
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i localhost.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd localhost
  gem build localhost.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    localhost-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd localhost
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd localhost
  cp -a tmp_install/* "$pkgdir"/
}
