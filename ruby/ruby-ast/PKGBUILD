# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-ast
pkgver=2.4.3
pkgrel=1
pkgdesc='A library for working with Abstract Syntax Trees'
arch=(any)
url='https://whitequark.github.io/ast/'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
)
checkdepends=(
  ruby-bundler
  ruby-kramdown
  ruby-rake
  ruby-rspec
  ruby-simplecov
  ruby-yard
)
options=(!emptydirs)
source=(git+https://github.com/whitequark/ast.git#tag=v$pkgver)
sha256sums=('cb433cc76baf2fa8c4d1c40006f9a2dc109861bfbee12dd51e3c4395f9af2833')

prepare() {
  cd ast

  sed -e '4,12d' -i spec/helper.rb
  sed -r \
    -e '/[cC]overalls/d' \
    -e '/simplecov/d' \
    -e 's|~>|>=|g' \
    -i ast.gemspec spec/helper.rb

  cat spec/helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd ast
  gem build ast.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    ast-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd ast
  GEM_HOME="tmp_install/$_gemdir" rake spec
}

package() {
  cd ast
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.MIT -t "$pkgdir"/usr/share/licenses/$pkgname/
}
