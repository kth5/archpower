# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=i18n
pkgname=ruby-i18n
pkgver=1.14.8
pkgrel=1
pkgdesc="New wave internationalisation support for Ruby"
arch=(any)
url="https://github.com/ruby-i18n/i18n"
license=(MIT)
depends=(
  ruby
  ruby-concurrent
)
makedepends=(ruby-rdoc)
checkdepends=(
  ruby-activesupport
  ruby-bundler
  ruby-minitest
  ruby-mocha
  ruby-racc
  ruby-rake
  ruby-test_declarative
)
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0be488e2350f9ef6c707986de7dccc62888ef2b555633c61d16c2ecaac598b28758cfbc7fce8e357490a849a8865ec2472c1e71c1706000fe735648aa6fff8ab')
b2sums=('b0838c185159f2906d5ddc2b294d146eaa145bf69917a0a5b84cddd176c4ef67f9bfbde6db9844b3323615521fa71a81db13d4ddfd53bf7afa42b511dfa62374')

prepare() {
  cd $_name-$pkgver

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed -i -e 's|~>|>=|g' $_name.gemspec Gemfile
}

build() {
  local gemdir="$(gem env gemdir)"
  local gem_install_options=(
    --local
    --verbose
    --ignore-dependencies
    --no-user-install
    --install-dir tmp_install/$gemdir
    --bindir tmp_install/usr/bin
    $_name-$pkgver.gem
  )
  local unrepro_files=(
    tmp_install/$gemdir/cache/
    tmp_install/$gemdir/gems/$_name-$pkgver/vendor/
    tmp_install/$gemdir/doc/$_name-$pkgver/ri/ext/
  )

  cd $_name-$pkgver

  gem build $_name.gemspec
  gem install "${gem_install_options[@]}"

  # remove unrepreducible files
  rm -frv "${unrepro_files[@]}"

  find tmp_install/$gemdir/gems/ \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find tmp_install/$gemdir/extensions/ \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local gemdir="$(gem env gemdir)"

  cd $_name-$pkgver

  GEM_HOME="tmp_install/$gemdir" rake test
}

package() {
  cd $_name-$pkgver

  mv -v tmp_install/* "$pkgdir/"

  install -vDm 0644 MIT-LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 0644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
