# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

_gemname=rspec-mocks
pkgname=ruby-${_gemname}
pkgver=3.13.7
pkgrel=1
pkgdesc="RSpecs 'test double' framework, with support for stubbing and mocking"
url='https://github.com/rspec/rspec'
arch=(any)
license=('MIT')
depends=(
  ruby
  ruby-diff-lcs
  ruby-ffi
  ruby-rspec-support
)
makedepends=(
  git
  ruby-rdoc
)
options=('!emptydirs')
source=(
  "${pkgname}::git+${url}#tag=${_gemname}-v${pkgver}"
  "${pkgname}_fix_tests.patch"
  "${pkgname}_no_version_constraints.patch"
)
sha512sums=('bda108ef67b99ec863a34ff31379035ce2048de5a53d2c0dace03587946a9d85d7bfb8abfeb1ce8ab4d2ad4e6b8e375c6b4ddd30d144c031b305185178f3526e'
            '2929551a75872ad7bc55317b162a2b9a6145f428362a4f484eb357baac402a3d2bc346fffd249c33f1978b1a4def44884a51f3b5be228798181defd330b05973'
            '146889d0136cb07d3f3c48e0abd71e8bd6d4d8fedc3cae3b6baa5258429ce2b0db2868ee9e579a7b9b98fcb02a302fc5865dad73df138d8056add57063169624')
b2sums=('a7ce78c3d4a017ea0d555070a2eb3f299e235f809f03c2fd3a22b6889a842e9bdb4c19f76e9555fde87126db6a6b066551a8e3a1651a2cba0d07b11a5ce3df30'
        'f27f3a55c1c0546fa18933d352a7d168c91e3f6fea4762aec715ac5600e32694eec5fe4b90a267d95ed45f02eb39e262f49bde36263baefeedd3c271ad819168'
        'a76fdcc662727a4ee8702718983f0056861f5815cdb5365d5780e0c18532ffab3f09c01ced8457db992833919676ba56b71716342b4129de20840c17a6520cc9')

prepare() {
  cd "${pkgname}"

  patch --verbose --strip=1 --input="../${pkgname}_no_version_constraints.patch"
  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"
}

build() {
  cd "${pkgname}/rspec-mocks"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${pkgname}/rspec-mocks"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
