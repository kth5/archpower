# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer:  Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Stefan Tatschner <rumpelsepp@sevenbyte.org>
# Contributor: Nick Ã˜stergaard <oe.nick at gmail dot com>
# Contributor: Nicolas Bigaouette <nbigaouette@chemcomp.com>

pkgname='asciidoctor'
pkgver=2.0.26
pkgrel=1
pkgdesc='An implementation of AsciiDoc in Ruby'
arch=(any)
url='https://asciidoctor.org'
license=('MIT')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
options=('!emptydirs')
source=("git+https://github.com/asciidoctor/asciidoctor#tag=v${pkgver}")
sha512sums=('073de38f7a1238b23632c6da8da2a6b0960a0c92bfe5f1258082661f7a1d4ebd0c3a6f94a67fc68311fcef032cb4670ec0739fcfb75d4472b76605a434a3aaa4')
b2sums=('3f499286b8427957758c73e00b976f22749f4c673f8d8476a2d1a75d3c08cab1f6621fcee7207123fe2486762c5280a4ec1c2f130d4c5d04b3f0add26e7d4536')

prepare() {
  cd "${pkgname}"

  # resolves #4634 normalize output from inspect when comparing to fixture
  git cherry-pick -n e5442fc2848680eada862841b0e92dc9265e1a29

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    "${pkgname}.gemspec"
}

build() {
  cd "${pkgname}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${pkgname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${pkgname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${pkgname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${pkgname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${pkgname}"

  local _gemdir="$(gem env gemdir)"

  install --verbose -D --mode=0644 "tmp_install${_gemdir}/gems/${pkgname}-${pkgver}/man/asciidoctor.1" "${pkgdir}/usr/share/man/man1/asciidoctor.1"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.adoc --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
