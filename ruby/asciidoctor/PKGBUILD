# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer:  Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Stefan Tatschner <rumpelsepp@sevenbyte.org>
# Contributor: Nick Ã˜stergaard <oe.nick at gmail dot com>
# Contributor: Nicolas Bigaouette <nbigaouette@chemcomp.com>

pkgname='asciidoctor'
pkgver=2.0.23
pkgrel=2
pkgdesc='An implementation of AsciiDoc in Ruby'
arch=(any)
url='https://asciidoctor.org'
license=('MIT')
makedepends=('ruby-rdoc')
depends=('ruby')
makedepends=('ruby-erb')
options=('!emptydirs')
source=("https://rubygems.org/downloads/${pkgname}-${pkgver}.gem")
noextract=("$pkgname-$pkgver.gem")
sha512sums=('61aeb0c78438b61cdd7caf31840c82fc68d94ed296e381927c1e3a0b36be5bc29a025a7d9a076c19293f1c8b69056120f0493456106eeeb4a083afb59a414328')

package() {
    local _gemdir="$(ruby -e 'puts Gem.default_dir')"

    gem install --ignore-dependencies --no-user-install --verbose \
        -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" \
        "$pkgname-$pkgver.gem"

    install -Dm644 "$pkgdir/$_gemdir/gems/$pkgname-$pkgver/LICENSE" \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 "$pkgdir/$_gemdir/gems/$pkgname-$pkgver/man/asciidoctor.1" \
        "$pkgdir/usr/share/man/man1/asciidoctor.1"

    rm "$pkgdir/$_gemdir/cache/$pkgname-$pkgver.gem"
    rm -rf "$pkgdir/$_gemdir/gems/$pkgname-$pkgver/man"
}
