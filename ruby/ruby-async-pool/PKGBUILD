# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-pool
pkgver=0.11.0
pkgrel=1
pkgdesc='A singleplex and multiplex resource pool for implementing robust clients'
arch=(any)
url='https://github.com/socketry/async-pool'
license=(MIT)
depends=(
  ruby
  ruby-async
  ruby-metrics
  ruby-traces
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-io-event
  ruby-sus
  ruby-sus-fixtures-async
  ruby-sus-fixtures-benchmark
)
options=(!emptydirs)
source=(https://github.com/socketry/async-pool/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('8686c117fe13b089be25a9c8c22be8ce500948c4c966db015f6a7a343c488f49714bbd9661b930a1f282d35351cdd030d6e863cb82cbd603ac6d8ddb806a15da')
b2sums=('f2f2905907e4b1c8e4e9ae0f5316826c43010fd272b1228032b54972f92615d12dc575e6e7b8443583396a66557a0e0abc4ff492cc9469793c7183ae59173f9e')

prepare() {
  cd async-pool-$pkgver

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i async-pool.gemspec gems.rb

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-pool-$pkgver
  gem build async-pool.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-pool-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-pool-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-pool-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
