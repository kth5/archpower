# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-pool
pkgver=0.10.3
pkgrel=1
pkgdesc='A singleplex and multiplex resource pool for implementing robust clients'
arch=(any)
url='https://github.com/socketry/async-pool'
license=(MIT)
depends=(
  ruby
  ruby-async
  ruby-metrics
  ruby-traces
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-io-event
  ruby-sus
  ruby-sus-fixtures-async
)
options=(!emptydirs)
source=(https://github.com/socketry/async-pool/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('68ba131e2f2cf487c94545bfcd8b9079c13fd177dee4dea997ed338248437a52d9e363c09d84a729fb93bbc24aba104f0bc707a89b7b7b9aef79d9cfa45235ec')
b2sums=('99ff19a24bd991e9c3bf6ee8f0c5517384017c6e8cc05d32aedf308cd4a8379dbae3f68ff067d20181a175767ce308839a25fb92b11dcb4ba29b8f0f0ec6075f')

prepare() {
  cd async-pool-$pkgver

  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i async-pool.gemspec gems.rb

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-pool-$pkgver
  gem build async-pool.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-pool-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-pool-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-pool-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
