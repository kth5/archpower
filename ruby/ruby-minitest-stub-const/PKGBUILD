# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='minitest-stub-const'
pkgname="ruby-${_gemname}"
pkgver=0.6
pkgrel=6
pkgdesc='Stub constants for the duration of a block in MiniTest.Similar to RSpecs stub_const'
arch=('any')
url='https://github.com/adammck/minitest-stub-const'
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rake
)
checkdepends=(
  ruby-minitest
)
options=('!emptydirs')
source=(
  "${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz"
  "${pkgname}_fix_tests.patch::${url}/pull/16/commits/d974c99e98f9f826b6a5c3c9a3ec4ab40255c798.patch"
)
sha512sums=('1363d85a75d84e2643eddd676d47f0969700854433697731f9896298ec53158f046e3e89ca3d1cbbe85f2cac037c51b5982c0f2f75c405b190d2501afb598a71'
            '0bdab91d89858cc601fd9723674f825c442447d86c9c7a4927233a5cd112990e5ae2baaaccca602c4866e70e05f4496bf354ffed615964573f03ef295d38ee27')
b2sums=('ee0084d348ffb7ce644474f3c47e6943d4a5748928a0d5f1ce602735e8781157a269ee916821df01c9c1b5fcf0cfe0e8d96bb04f4c933195db2f5c73d981c49d'
        'da28dc309498bc80d1c0ef61787f40e69b6ef51a0f7a1cf703df045fceb451e746a333efc05b11d042ea3f05294feb9cc54a3106b130482bb9f6b1b540173f09')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
 }
