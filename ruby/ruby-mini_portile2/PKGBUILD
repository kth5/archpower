# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Dmitry Kharitonov <darksab0r@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: eagletmt <eagletmt@gmail.com>

_gemname='mini_portile2'
pkgname="ruby-${_gemname}"
pkgver=2.8.7
pkgrel=1
pkgdesc='Simple autoconf builder for developers'
url='https://github.com/flavorjones/mini_portile'
arch=(any)
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://github.com/flavorjones/mini_portile/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a7a8635e08878c24e026751279e3578b3136a8415d047b299ff9227820d48070b235a78e6d1a86ec334f599a87bb16727f39dbce0bad931051a4bfd2fe01cdf7')
b2sums=('f4ccc8040df0299779b6e83e6fb4352a31295729aad6387588616cde7c7e24c69e72284b44bdb265d86ad7976ba1a2fa094dca0cdd3a9d85c3ad9a5be5852e38')

prepare() {
  cd "mini_portile-${pkgver}"

  sed -r 's|~>|>=|g' -i "${_gemname}.gemspec"
  sed 's|git ls-files -z|find -type f -print0|' -i "${_gemname}.gemspec"
}

build() {
  cd "mini_portile-${pkgver}"

  gem build "${_gemname}.gemspec"
}

package() {
  cd "mini_portile-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
