# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Dmitry Kharitonov <darksab0r@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: eagletmt <eagletmt@gmail.com>

_gemname='mini_portile2'
pkgname="ruby-${_gemname}"
pkgver=2.8.8
pkgrel=4
pkgdesc='Simple autoconf builder for developers'
url='https://github.com/flavorjones/mini_portile'
arch=(any)
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  cmake
  ruby-bundler
  ruby-minitar
  ruby-minitest
  ruby-minitest-hooks
  ruby-net-ftp
  ruby-rake
  ruby-webrick
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('14d4cab6e5fc12a6ad9da38dfeddf568bfc14c96b6aee24e665195aa332d10c83c2d34986f6693b7381dee9a5eab5ca3ec3584124d92d956c7bc04bb9d1aface')
b2sums=('83f05311f7df82a6581e8af46a3d5638b1b4266e9e612489d73f8755bd608942c4f12a0a72700f52e031505522c03e4f21e93fef01f40fdcca0758d5fff150f0')

source+=('https://raw.githubusercontent.com/llvm/llvm-project/refs/tags/llvmorg-21-init/llvm/cmake/config.guess')
sha512sums+=('a167d8fd201fdb9162726631110b23b61a69d0fe6a85d60c0553c936b6f9ff83b22283f0e9654d438a494ee9cbda19aeaf3616cd2c26ac537fad338f59086dcd')
b2sums+=('40fb3e56a2f56b368a02494001f731a4ede2681d0676658e2cfe72db16fccb3f5b211f7dfdcd1f94bab58e8ae1e4ed8e0fc324fd7a753e10c5803c5dd997a3e1')

_caresver='c-ares-1.7.5'

prepare() {
  cd "mini_portile-${pkgver}"

  # patch the config.guess
  cd examples/
  TEMP_DIR=$(mktemp -d)
  _sum_old=$(md5sum $_caresver.tar.gz | awk '{print $1}')
  tar -xf $_caresver.tar.gz -C "$TEMP_DIR"
  cp $srcdir/config.guess "$TEMP_DIR/$_caresver/"
  tar -cf $_caresver.tar.gz -C "$TEMP_DIR" $_caresver
  _sum_new=$(md5sum $_caresver.tar.gz | awk '{print $1}')
  sed -i "s/$_sum_old/$_sum_new/g" Rakefile
  rm -r "$TEMP_DIR"
  cd ..

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|`git ls-files -z`\.split\("\\x0"\)|`find . -type f -not -path "*/\.git*" -printf "%P\\n"`\.split\("\\n"\)|' "${_gemname}.gemspec"

  sed --in-place --regexp-extended 's/2\.8\.7/3\.5/g' test/assets/test-cmake-1.0/CMakeLists.txt
}

build() {
  cd "mini_portile-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  case "${CARCH}" in
    powerpc*|riscv64) return 0 ;;
  esac

  cd "mini_portile-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "mini_portile-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
