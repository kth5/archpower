# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

_gemname=rspec-support
pkgname=ruby-${_gemname}
pkgver=3.13.2
pkgrel=2
pkgdesc='Common code needed by the other RSpec gems. Not intended for direct use'
url='https://github.com/rspec/rspec'
arch=(any)
license=('MIT')
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bigdecimal
  ruby-bundler
  ruby-childprocess
  ruby-diff-lcs
  ruby-drb
  ruby-ffi
  ruby-minitest
  ruby-rake
  ruby-rspec
  ruby-rspec-core
  ruby-rspec-expectations
  ruby-rspec-mocks
  ruby-test-unit
  ruby-thread_order
)
options=('!emptydirs')
source=(
  "${pkgname}::git+${url}#tag=${_gemname}-v${pkgver}"
  "${pkgname}_no_version_constraints.patch"
  "${pkgname}_fix-diff-lcs-errors.patch"
)
sha512sums=('06c89f2be3d61be71059476cb15af3a80774cc50e52ce8a0708171a480f80f3eba7c82836b9555cfb643f95c4ba052ca7155bf0400292abfea03b773e0213b4b'
            '39ada3f754b90a4895d87a8005f91fc3e3bcd1d4ed534e0210c97a2fb1173a361f354cb2608f8ebe0130bae0d6bb4c39929c1e20e17aef2ed92cf8613f79ea62'
            '40b1a23ead8ad59cbfc7914069a46bc14ec6876c445f23f02ec7ed4bdd5fc07053beac93f5dc96e1d32c9f67ebeb3af8846716f8f842cecbadd59cd3717c747f')
b2sums=('c36d62a8085d1c8af43a245997267133e2a510a857e8abdbe6d5a976066552ec31a40b07eded60da78f3dd851344d1fe4e27d36a0eaaf9d99d8e65218b1a6bea'
        'ebafb00a756958c2b8664dcdedfc2cd753b9c258c8aa2ae798e88cfeb17b3a6bcac18c26389b89e4e2766dd9d44c3b90cd5811e1f07fe368264ed1640d71143e'
        '0735ff5b85f17c5eef2a510b852c1003696405a480a09099bfb91ef5f3dbcadc0a7b888e2781b1c1e35e05679b5c340936548a3f6e7b8931c760c16e556bd2e5')

prepare() {
  cd "${pkgname}"

  git cherry-pick -n 9310b4b72e205c6352c4b30015a3b8f3d2fb02e8 # Format hashes in new {a: :b} format on Ruby 3.4

  patch --verbose --strip=1 --input="../${pkgname}_fix-diff-lcs-errors.patch"
  patch --verbose --strip=1 --input="../${pkgname}_no_version_constraints.patch"

  sed --in-place --regexp-extended \
    --expression '/%w\[ core mocks expectations support \]/,+6d' \
    rspec-support/spec/rspec/support/caller_filter_spec.rb
}

build() {
  cd "${pkgname}/rspec-support"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unreproducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${pkgname}/rspec-support"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${pkgname}/rspec-support"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
