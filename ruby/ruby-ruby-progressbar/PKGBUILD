# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>

_gemname='ruby-progressbar'
pkgname="ruby-${_gemname}"
pkgver=1.13.0
pkgrel=4
pkgdesc='Flexible text progress bar library for Ruby'
arch=('any')
url="https://github.com/jfelchner/${_gemname}"
license=('MIT')
depends=('ruby')
#checkdepends=('ruby-rspec')
options=('!emptydirs')
source=("${url}/archive/releases/v${pkgver}/${_gemname}-${pkgver}.tar.gz")
sha512sums=('64723422d6216f2640bc60c8068c8d0437fbc5f315343b13c3167130d868a0aa978f098554ace3ea71406688c0b3c0ed94296adc6b9229ea14df5dbcb56322a3')

prepare() {
  cd "${_gemname}-releases-v${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place '/cert_chain/d' "${_gemname}.gemspec"
  sed --in-place '/signing_key/d' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-releases-v${pkgver}"

  gem build "${_gemname}.gemspec"
}

# needs https://rubygems.org/gems/rspectacular to work
#check() {
#  cd "${_gemname}-releases-v${pkgver}"
#
#  rspec
#}

package() {
  cd "${_gemname}-releases-v${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install --verbose -D --mode=0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: set ts=2 sw=2 et:
