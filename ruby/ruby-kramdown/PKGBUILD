# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=kramdown
pkgname=ruby-kramdown
pkgver=2.4.0
pkgrel=1
pkgdesc='Fast, pure Ruby Markdown superset converter, using a strict syntax definition'
url='https://kramdown.gettalong.org/'
arch=(any)
license=('MIT')
depends=('ruby' 'ruby-rexml')
makedepends=('ruby-rdoc' 'ruby-rake')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gettalong/kramdown/archive/REL_${pkgver//./_}.tar.gz)
sha256sums=('39f5b9f4cc2259cbe7a14536b09d064b0403a7a901751811b48753e9d347c855')
sha512sums=('38e411cb14fa823095be410aa62793072d98bc81b84b1642c431bf094da54137d5ee8ae59bccd6b68cda34a7506ef175706dfaf08052ad703e835855cf59f7e5')

prepare() {
  cd ${_gemname}-REL_${pkgver//./_}
  rake gemspec
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-REL_${pkgver//./_}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-REL_${pkgver//./_}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/man/man1"
  mv "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/man/man1/kramdown.1" "${pkgdir}/usr/share/man/man1"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  rm -r "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/test"
}

# vim: ts=2 sw=2 et:
