# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-rack-session
pkgver=2.1.0
pkgrel=2
pkgdesc='Session management implementation for Rack.'
url="https://github.com/rack/rack-session"
arch=('any')
license=('MIT')
depends=(
  ruby
  ruby-rack
  ruby-base64
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-minitest
  ruby-minitest-global_expectations
  ruby-minitest-sprint
)
options=('!emptydirs')
source=("${pkgname}-v${pkgver}.tar.gz::$url/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('0350a45e47d87a872411eb583e37fc9948059a089cc8622f3e98e14b6106306f')
sha512sums=('e9f9fa6d5fe0e0f39ecb130c96d98010b03f9d89301424dfda671beaf14e9f3e07f480fa8e638501a53861033f1cce060ecc79548d90bde8ad7d734d1f008a9e')

prepare() {
  cd rack-session-${pkgver}
  sed -r 's|~>|>=|g' -i rack-session.gemspec
}

build() {
  cd rack-session-${pkgver}

  local _gemdir="$(gem env gemdir)"

  gem build rack-session.gemspec

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "rack-session-${pkgver}.gem"

  rm -rf "tmp_install/${_gemdir}/cache/"
}

check() {
  cd rack-session-${pkgver}

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" rake test
}

package() {
  cd rack-session-${pkgver}

  cp -a tmp_install/* "${pkgdir}"

  install -Dm 0644 license.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 0644 {readme,security}.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
