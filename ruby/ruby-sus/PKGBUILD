# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-sus
pkgver=0.32.0
pkgrel=2
pkgdesc='A fast and scalable test runner'
arch=(any)
url='https://github.com/socketry/sus'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  git
  ruby-rdoc
)
checkdepends=(
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
)
options=(!emptydirs)
source=(git+https://github.com/socketry/sus.git#tag=v$pkgver)
sha512sums=('969e49824fc6e71c81171e7be372eb161dcf4c4a91066b42e62da51c30f6876810a6ddf1c2037689c1f98b47a4574081fb4d527fa6fa54e389a485cbc9e833ec')
b2sums=('7f126e74c73a86c56bb6670852d9f0471ecb5a6d2661e6008194dbe400a37ee8d3aa7f8aaba931cc75439799a44b882b13c2e06153fcf6afcf17bec1b1054c65')

prepare() {
  cd sus

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i sus.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  gem build sus.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    sus-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd sus
  PATH="$PWD/tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd sus
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
