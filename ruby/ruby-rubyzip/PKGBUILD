# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='rubyzip'
pkgname="ruby-${_gemname}"
pkgver=2.4.1
pkgrel=1
pkgdesc='a ruby module for reading and writing zip files'
arch=(any)
url="https://github.com/rubyzip/${_gemname}"
license=('BSD-2-Clause')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "${url}/commit/e3eb62491bdede40f89e55e1de3adeeb5490aa7c.patch"
  "${url}/commit/98881e23d1e5f43b104533f752a9e177509714c2.patch"
)
sha512sums=('10799311ce960a909ae5cca20e6bd0db810d701e30cbfd557d7845ca1934b572561f913b49f6879db12fc98722b8b3b12f3c548305106838922bfa08a4bed122'
            '12925d5bbc92ec3ed04fe23e2bf71ef57b21dfe40b0b89e822270141109f7806d0e8decfaf11b36341dd39caeda48ebf2705af5ba038846af8b6853570877e34'
            '681539360da2b1ef2b7ed9514cfd34aa2becd44d45e4d92f03b9524dad9dd05715c8071e80ccb544a0d520ed7b707b6305262311e4ac115e6d5a2c8de5004e75')
b2sums=('506ed57bfa2bc97787c5d18295f024045f418857b221cf7270af2ce4eacde9e92ad9c1c165f3c697980000f87951028bf839771566088380c4d56d3171a25c05'
        'f45a9e818424f8ee1a5a0b8efc6d3311e75e1ee0a8fe465276a39350a6696471f6d13e782ef8190dfa7885cade9df6656b69ab9d4c3a45537818cca0b5eec62b'
        '24f61c8a4e20f6d545f34771da2fa3fe5997db71b10b6276e7c1e36e3a19ab46c9521b2ffa7f561f20762af0ce5a2ff729b8e926e727479fd1e5f2ddd8fe74f1')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # remove rubocop
  rm --verbose .rubocop*
  sed --in-place '/[rR]ubo[cC]op/d' Rakefile

  # fix version check
  filterdiff -p1 -i test/constants_test.rb ../e3eb62491bdede40f89e55e1de3adeeb5490aa7c.patch | patch -Rp1
  patch -Np1 -i ../98881e23d1e5f43b104533f752a9e177509714c2.patch
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 README.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
