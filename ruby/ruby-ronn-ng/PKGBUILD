# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=ronn-ng
pkgname=ruby-$_gemname
pkgver=0.10.1
pkgrel=3
pkgdesc='Manual page formatter that generates man pages from markdown'
arch=(any)
url='https://github.com/apjanke/ronn-ng'
license=(MIT)
depends=(
  ruby
  ruby-rdiscount
  ruby-mustache
  ruby-kramdown
  ruby-kramdown-parser-gfm
  ruby-nokogiri
  ruby-erb
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-rack
  ruby-rake
  ruby-sinatra
  ruby-test-unit
)
conflicts=('ruby-ronn')
provides=('ruby-ronn')
replaces=('ruby-ronn')
options=(!emptydirs)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('3f6ee5355c7a19d5cb04c1b677028dd13ad2ff620acdb2e83d9b6f154a7b1e7b5a77ea0bcfbcef4bbb46a60b1c0fbc52fda079ff860c83a71f89b8fed0b83665')
b2sums=('1e56b90c5919e0a1af317da78161b34124268521fbc379fbba09ff9cb344073665460abc53fd961e4773ee444f33fba7e72ae3f6af84f8a170cfb1e89e557922')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE.txt --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"

  install --verbose -D --mode=0644 man/ronn.1 --target-directory "$pkgdir/usr/share/man/man1"
  install --verbose -D --mode=0644 man/ronn-format.7 --target-directory "$pkgdir/usr/share/man/man7"

  install --verbose -D --mode=0644 completion/bash/ronn --target-directory "$pkgdir/usr/share/bash-completion/completions"
  install --verbose -D --mode=0644 completion/zsh/_ronn --target-directory "$pkgdir/usr/share/zsh/site-functions"
}
