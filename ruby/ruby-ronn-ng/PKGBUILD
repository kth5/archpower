# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=ronn-ng
pkgname=ruby-$_gemname
pkgver=0.10.1
pkgrel=1
pkgdesc='Manual page formatter that generates man pages from markdown'
arch=(any)
url='https://github.com/apjanke/ronn-ng'
license=(MIT)
depends=(
  ruby
  ruby-rdiscount
  ruby-mustache
  ruby-kramdown
  ruby-nokogiri
)
makedepends=(
  ruby-rdoc
)
conflicts=('ruby-ronn')
provides=('ruby-ronn')
replaces=('ruby-ronn')
options=(!emptydirs)
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=($_gemname-$pkgver.gem)
sha512sums=('cda3689bbd5e997fcc49fe0c7b1732f1af13bc27245e1e008f5305339964273128f7cb6a12422c5e46cfe718ac8853bafc2796cd4740e4b03911a748807092c6')
b2sums=('075a0954264dafc60cc6f402402ed318576fc776ab1d085b86994663891c6d38cb69071d46f3a057b5d85703bff9e83cc9e55eb421cc1cd37289a9291d996283')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/man/ronn.1" "$pkgdir/usr/share/man/man1/ronn.1"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/man/ronn-format.7" "$pkgdir/usr/share/man/man7/ronn-format.7"

  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/completion/bash/ronn" "$pkgdir/usr/share/bash-completion/completions/ronn"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/completion/zsh/_ronn" "$pkgdir/usr/share/zsh/site-functions/_ronn"
}
