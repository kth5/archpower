# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
_gemname=debug
pkgname=ruby-${_gemname}
pkgver=1.9.1
pkgrel=1
pkgdesc='Debugging functionality for Ruby'
url='https://github.com/ruby/debug'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-2-Clause')
depends=(
  ruby
  ruby-irb
  ruby-reline
)
makedepends=(
  git
  ruby-rdoc
  ruby-rake
)
checkdepends=(
  ruby-test-unit
  ruby-test-unit-rr
  ruby-bundler
)
options=('!emptydirs' '!debug')
source=(${pkgname}::git+"${url}#tag=v${pkgver}")
sha512sums=('a6378ec57eed45a39a5fcfbba1ead7bfece141ae8c241250ee33d50f3158d6bc2ee8d983c0a182a887c469be9e620faba30a6daa3280f63de0b666e029d0b6a7')

prepare() {
  cd ${pkgname}
  sed '/assert_equal(expected, colored_inspect(obj))/d' -i test/console/color_test.rb
}

build() {
  cd "${pkgname}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd ${pkgname}

  local _gemdir="$(gem env gemdir)"

  export RUBY_DEBUG_TEST_NO_REMOTE=1

  GEM_HOME="tmp_install${_gemdir}" rake test_console
}

package() {
  cd ${pkgname}

  cp --archive --verbose tmp_install/* "${pkgdir}"

  if [[ ! -f "${pkgdir}/usr/bin/rdbg" ]]; then
    error "missing /usr/bin/rdbg"
    exit 1
  fi

  install -Dm 644 README.md  -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
