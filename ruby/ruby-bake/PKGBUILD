# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-bake
pkgver=0.24.1
pkgrel=1
pkgdesc='A replacement for rake with a simpler syntax'
arch=(any)
url='https://github.com/ioquatix/bake'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-samovar
)
makedepends=(
  git
)
checkdepends=(
  ruby-bundler
  ruby-covered
  ruby-rspec
)
options=(!emptydirs)
source=(git+https://github.com/ioquatix/bake.git#tag=v$pkgver)
sha512sums=('0962e1520792bd03dd26cea3219754e8a9b29ec2143ff571620da8a269e084365561375ead6d6ea3ad7579d9275c44ddd6a074d90161a70a746177ed77e5d074')
b2sums=('52c72076766322e0abb04c1d52313f591aa1473915307a9e57efd2acc27d1ceaecdc460a4f745f06bd688c66ca59885277091f538cb09b87b96cd0eb93a56c0a')

prepare() {
  cd bake
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i bake.gemspec
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd bake
  gem build bake.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    bake-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd bake
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd bake
  cp -a tmp_install/* "$pkgdir"/
}
