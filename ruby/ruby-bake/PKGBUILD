# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-bake
pkgver=0.23.1
pkgrel=3
pkgdesc='A replacement for rake with a simpler syntax'
arch=(any)
url='https://github.com/ioquatix/bake'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-samovar
)
checkdepends=(
  ruby-bundler
  ruby-covered
  ruby-rspec
)
options=(!emptydirs)
source=(https://github.com/ioquatix/bake/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('0abbec2f2d6c9f1bbd1c04e51bf5094fb239fa466c50af95959b0d483f5d80f9a0e6b050595230dfe6e6aac2a231e3597bc63c66c39033dd6a770d4aca7329f5')
b2sums=('71419d1da57e87ed40d28df3ff07eaf834a11bec3cfe6b9adcba5f873059b18eddb1772b6f7661fa875ecb63477aa9c01317e52879a5a3f9341bc01dc0727d95')

prepare() {
  cd bake-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i bake.gemspec
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$pkgver
  gem build bake.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    bake-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd bake-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
