# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-bake
pkgver=0.24.0
pkgrel=1
pkgdesc='A replacement for rake with a simpler syntax'
arch=(any)
url='https://github.com/ioquatix/bake'
license=(MIT)
depends=(
  ruby
  ruby-bigdecimal
  ruby-samovar
)
checkdepends=(
  ruby-bundler
  ruby-covered
  ruby-rspec
)
options=(!emptydirs)
source=(https://github.com/ioquatix/bake/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('6c7e650569cfb7435ef06fb552360c8583fd001a9ef814e6672c5cc5b656ebdf871f0646a01c0ab1726e47ffd15678c28bb050254c5093502d3519f9dc649025')
b2sums=('7c23ddb070bfc82e10db495ddd4ccc80df716145e4579c95f15d9321339778a466874d4afe528c17ba04efe2bc22ce3762a69bec169c817d92d7ec50ad779ce1')

prepare() {
  cd bake-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i bake.gemspec
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$pkgver
  gem build bake.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    bake-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd bake-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
