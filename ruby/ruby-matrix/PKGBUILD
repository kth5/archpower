# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

# This is a bundled gem and should not be dropped. see https://stdgems.org/

_gemname='matrix'
pkgname="ruby-${_gemname}"
pkgver=0.4.2
pkgrel=1
pkgdesc='An implementation of Matrix and Vector classes'
arch=('any')
url="https://github.com/ruby/${_gemname}"
license=('RUBY' 'BSD-2-CLAUSE')
depends=(ruby)
makedepends=(
  ruby-rake
)
checkdepends=(
  ruby-bundler
  ruby-test-unit
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b47d1d662acdca703afd323fe9293726e051b0ef87162ff45c26c62fad2f3158f71d943f36f7c6706702885e2fdea8483b37ee45f25295225e86dec5f88590a4')
b2sums=('03b7f3482d6d5a48c626fee824039c182a35242a2c25875e7cada4d781d1939ed780b5cbf8c45645d07c81e4f5d5c5559a1ec236a5152e095a60228e4ed3c59e')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
