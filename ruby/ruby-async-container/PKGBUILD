# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-container
pkgver=0.26.0
pkgrel=1
pkgdesc='Abstract container-based parallelism using threads and processes where appropriate'
arch=(any)
url='https://github.com/socketry/async-container'
license=(MIT)
depends=(
  ruby
  ruby-async
)
checkdepends=(
  ruby-bake
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
)
options=(!emptydirs)
source=(https://github.com/socketry/async-container/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f2837f841193e3f7b84f5880a83d79291a61cbdcf07f79ec42758111504f6156')

prepare() {
  cd async-container-$pkgver

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i async-container.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-container-$pkgver
  gem build async-container.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-container-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  local _gemdir="$(gem env gemdir)"
  cd async-container-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-container-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
