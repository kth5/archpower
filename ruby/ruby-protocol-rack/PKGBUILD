# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-rack
pkgver=0.21.0
pkgrel=1
pkgdesc='An implementation of the Rack protocol/specification'
arch=(any)
url='https://github.com/socketry/protocol-rack'
license=(MIT)
depends=(
  ruby
  ruby-protocol-http
  ruby-rack
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-async-http
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-decode
  ruby-sus
  ruby-sus-fixtures-async-http
  ruby-sus-fixtures-console
)
options=(!emptydirs)
source=(https://github.com/socketry/protocol-rack/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('1d3142e3b315c598f9b4eecfdc58b267d7dfffaa5c49917f880be61bcfda441d9901538251ab6a0720c9319cffca0b3910bb9a06c034d9e252bde1623cd5322f')
b2sums=('06093b170a81ed1fb60f4b1208fcb8621a8f662ce9eb19c935b2d78269575c5cfe8c937522d8aedb4637653784326bc7704777b576770983d61a958966977728')

prepare() {
  cd protocol-rack-$pkgver

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i protocol-rack.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-rack-$pkgver
  gem build protocol-rack.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-rack-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-rack-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-rack-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
