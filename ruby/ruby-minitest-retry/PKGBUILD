# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='minitest-retry'
pkgname="ruby-${_gemname}"
pkgver=0.2.5
pkgrel=2
pkgdesc='re-run the test when the test fails'
arch=('any')
url='https://github.com/y-yagi/minitest-retry'
license=('MIT')
depends=(
  ruby
  ruby-minitest
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-debug
  ruby-rake
)
options=('!emptydirs')
source=(
  "${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz"
)
sha512sums=('9777d33e93b629af6f9a77c3c18d15875e80f2a1419936067d7c7c7d36fe61496e756d9c5e249508f57f12073ae5e8c1c1c7c0b4f7bd39dfec844a82366d8330')
b2sums=('1c752b6bc64674a450434cc87160e473084ac1ddce5a053c8f9b3d77075665bfbb5837e5ab5efe057a19d0d2e331a960be01fd72ac4751834d13e1e737091905')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # allow latest dependencies
  sed --in-place 's|`git ls-files -z`|`find . -print0`|' "${_gemname}.gemspec"

  # drop useless dependency
  sed --in-place '/bundler/d' Rakefile "${_gemname}.gemspec"
  sed --in-place '/pry/d' Gemfile test/test_helper.rb
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
 }
