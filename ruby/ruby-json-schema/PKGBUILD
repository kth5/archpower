# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Roberto Valentini <valantin89 [at] gmail [dot] com>

_gemname='json-schema'
pkgname="ruby-${_gemname}"
pkgver=5.1.1
pkgrel=2
pkgdesc='Interface for validating JSON objects against a JSON schema conforming to JSON Schema Draft 4.'
arch=('any')
url='https://github.com/voxpupuli/json-schema'
license=('MIT')
depends=(
  ruby
  ruby-addressable
  ruby-bigdecimal
)
makedepends=(
  ruby-bundler
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  ruby-rake
  ruby-test-unit
  ruby-webmock
)
options=(!emptydirs)
source=(
  "${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz"
)
sha512sums=('4ffee1388c65f4730b39cb28384c1dc20e142a3d5fe66474447dd819934315e6b96431f539fdb006a4ab250c28ecd1faabf82a63e24881de0c967a83279a73a4')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # replace upper version boundaries for ruby gems
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"

  sed --in-place '/development/g' "${_gemname}.gemspec"
  rm -f Gemfile
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 CHANGELOG.md CONTRIBUTING.md CONTRIBUTORS.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
