# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>
# Contributor: Mario Finelli <mario at finel dot li>

_gemname='docile'
pkgname="ruby-${_gemname}"
pkgver=1.4.1
pkgrel=2
pkgdesc="keeps your Ruby DSLs tame and well-behaved"
arch=('any')
url='https://ms-ati.github.io/docile'
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
options=(!emptydirs)
source=("https://github.com/ms-ati/docile/archive/v$pkgver/$_gemname-$pkgver.tar.gz")
sha512sums=('4c87155b062de8863e153d58725d8f84ac0a58bc11a88dd1f49e61f6bb7d89f177a9df96c5310a6a83c82b0af3f43a7c49fa33670cfde3925b86f4d77e1e2c95')

prepare() {
  cd "${_gemname}-${pkgver}"

  # we use an archive not a git checkout
  sed --in-place --regexp-extended 's|git ls-files -z|find -print0|' "${_gemname}.gemspec"

  # remove dependency on simplecov to break dependency cycle
  sed --in-place '/require "simplecov"/,+5d' spec/spec_helper.rb
  sed --in-place '/Object.send/d' spec/spec_helper.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install --verbose -D --mode=0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: set ts=2 sw=2 et:
