# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-parser
pkgver=3.3.7.4
pkgrel=1
pkgdesc='A production-ready Ruby parser written in pure Ruby'
arch=(any)
url='https://github.com/whitequark/parser'
license=(MIT)
depends=(
  ruby
  ruby-ast
  ruby-racc
)
makedepends=(
  git
  ragel
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-erb
  ruby-minitest
  ruby-simplecov
)
options=(!emptydirs)
source=(git+https://github.com/whitequark/parser.git#tag=v$pkgver)
sha512sums=('041110735a802c5f1f82886c795bb12239adebf97057683c69615210301cd4443d639b6f7933926d1d86b114f2a0921a694cdab413ff87a9d366ed0a8a6f0e51')
b2sums=('70e7ba6d69c1bd8406062c7d0e826150dd511fb2056a674540e12a6db19412d4267bb3ae399c21971fbfba2076d3151b784bd9bd4c1b1d7fe079665446b365cf')

prepare() {
  cd parser
  sed 's/~>/>=/' -i parser.gemspec
  sed -i '/[cC]liver/d' Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd parser
  rake generate
  gem build parser.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    parser-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd parser
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd parser
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
