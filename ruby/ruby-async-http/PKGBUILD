# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-http
pkgver=0.91.0
pkgrel=1
pkgdesc='A HTTP client and server library'
arch=(any)
url='https://github.com/socketry/async-http'
license=(MIT)
depends=(
  ruby
  ruby-async
  ruby-async-pool
  ruby-io-endpoint
  ruby-io-stream
  ruby-metrics
  ruby-protocol-http
  ruby-protocol-http1
  ruby-protocol-http2
  ruby-traces
)
makedepends=(
  ruby-rdoc
)
options=(!emptydirs)
source=(https://github.com/socketry/async-http/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('e08a32ecedb98cffe1ed9502ff2284ce2364c628f070f2295abcd0358e56aa8e3c9e4b50973814e0cbee0429add2830215399fb4009e33674942c28e09a080a4')
b2sums=('76e7e8ee25177c0bcbab7018f89f0050931eb67196d329fc267c56566c8e992c2e1c23883dd9b7505e2502c0c8cf19f523cdf2e83aa109f2a2e876aae2774942')

prepare() {
  cd async-http-$pkgver

  sed -r \
    -e 's|~>|>=|g' \
    -e '/signing_key/d' \
    -i async-http.gemspec

  sed --in-place \
    --expression '/group :maintenance/,/end/d' \
    --expression '/rubocop/d' \
    gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-http-$pkgver
  gem build async-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

package() {
  cd async-http-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
