# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=test-unit-ruby-core
pkgname=ruby-test-unit-ruby-core
pkgver=1.0.6
pkgrel=6
pkgdesc="Additional test assertions for Ruby standard libraries"
arch=(any)
url="https://github.com/ruby/test-unit-ruby-core"
license=(BSD-2-Clause Ruby)
depends=(ruby)
makedepends=(
  ruby-rake
  ruby-rdoc
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('7b71546112e19ad453d76098c47bc1e471d1c9fda4962a3217c06cad405a773ff080b94aa650ffed94df0067ac1842ed22b438c43b143e717de64392337be446')
b2sums=('ad1fbab21cf4972451d44c6e31cdae5b6c7209b8f4b8f54cfbfdb3d04d456c551e7f038a18e6d465a0cc9f1b965d913138a23d6e0c6e00551b655682340d1c44')

prepare() {
  cd $_name-$pkgver

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed -i -e 's|~>|>=|g' $_name.gemspec
}

build() {
  local gemdir="$(gem env gemdir)"
  local gem_install_options=(
    --local
    --verbose
    --ignore-dependencies
    --no-user-install
    --install-dir tmp_install/$gemdir
    --bindir tmp_install/usr/bin
    $_name-$pkgver.gem
  )
  local unrepro_files=(
    tmp_install/$gemdir/cache/
    tmp_install/$gemdir/gems/$_name-$pkgver/vendor/
    tmp_install/$gemdir/doc/$_name-$pkgver/ri/ext/
  )

  cd $_name-$pkgver

  gem build $_name.gemspec
  gem install "${gem_install_options[@]}"

  # remove unrepreducible files
  rm -frv "${unrepro_files[@]}"

  find tmp_install/$gemdir/gems/ \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find tmp_install/$gemdir/extensions/ \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

# NOTE: there is not test target

package() {
  cd $_name-$pkgver

  mv -v tmp_install/* "$pkgdir/"

  install -vDm 644 BSDL COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
