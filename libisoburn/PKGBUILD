# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Gour <Gour <gour@mail.inet.hr>

pkgname=libisoburn
pkgver=1.5.6
pkgrel=1.1
pkgdesc="frontend for libraries libburn and libisofs"
url="https://dev.lovelyhq.com/libburnia"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL2')
depends=('libburn' 'libisofs' 'readline')
optdepends=('tk:      for xorriso-tcltk frontend'
            'sudo:    for use with xorriso-dd-target')
            #'bwidget: for xorriso-tcltk frontend') # AUR
provides=('xorriso' 'xorriso-tcltk')
source=(https://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('2b80a6f73dd633a5d243facbe97a15e5c9a07644a5e1a242c219b9375a45f71b'
            'SKIP')
validpgpkeys=('44BC9FD0D688EB007C4DD029E9CBDFC0ABC0A854') # Thomas Schmitt <scdbackup@gmx.net>

build() {
  cd "${pkgname}"-${pkgver/.pl01/}
  ./configure --prefix=/usr --build=${CHOST}
  make
}

package() {
  cd "${pkgname}"-${pkgver/.pl01/}
  make DESTDIR="${pkgdir}" install
}
