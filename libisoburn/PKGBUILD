# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Gour <Gour <gour@mail.inet.hr>

pkgname=libisoburn
pkgver=1.5.6
pkgrel=2
pkgdesc='Frontend for libraries libburn & libisofs'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://dev.lovelyhq.com/libburnia/libisoburn'
license=(GPL-2.0-or-later)
depends=(
  glibc
  sh
  readline
  libburn
  libisofs
)
makedepends=(git)
optdepends=(
  'tk: for xorriso-tcltk frontend'
  'sudo: for use with xorriso-dd-target'
)
provides=(xorriso xorriso-tcltk)
source=("$pkgname::git+$url#tag=release-$pkgver")
sha512sums=('bc8e32bb17ba6ee2ddc7dc57d89910b6a0c559f779e9b380a38cdc016d2850bada1b0c60c52675a3a9d7ee1287af731555f2ba69912512fc70284f022b9ff1e6')
b2sums=('5a7f10dd5bcc74edcd77a467f0449e2126c41b5163b87ef622b7db2f50acc6019a1c246829fb7d389523b8ba9852aa16495396b13c63d7f21aad62157241b202')

prepare() {
  cd "$pkgname"

  autoreconf -vfi
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --enable-year2038 \
    --disable-static

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}
