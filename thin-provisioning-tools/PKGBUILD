# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jason Hall <cakersq@gmail.com>

pkgname=thin-provisioning-tools
pkgver=1.0.6
pkgrel=1
pkgdesc='Suite of tools for manipulating the metadata of the dm-thin device-mapper target'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/jthornber/thin-provisioning-tools'
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jthornber/${pkgname}/archive/v${pkgver}.tar.gz"
        hotfix-no-atomic64.diff)
sha256sums=('bb64481c2bef330004b12cdae7cdc68a4790e55e4c0b7a3061dfb718e8396962'
            '6eb1ecd180a3184374ac930b5a69414dc664cde14ba7945b008d5c3357e5784d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  case "${CARCH}" in
    powerpc)
     # T2 is also awesome
     patch -Np1 -i ${srcdir}/hotfix-no-atomic64.diff
    ;;
  esac

  cargo build --release
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make \
    DESTDIR="${pkgdir}"/usr \
    BINDIR="${pkgdir}"/usr/bin \
    DATADIR="${pkgdir}"/usr/share \
    install
}

