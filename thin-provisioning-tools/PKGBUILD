# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jason Hall <cakersq@gmail.com>

pkgname=thin-provisioning-tools
pkgver=1.2.1
pkgrel=1
pkgdesc='Suite of tools for manipulating the metadata of the dm-thin device-mapper target'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/jthornber/thin-provisioning-tools'
license=('GPL3')
depends=('gcc-libs' 'systemd-libs')
makedepends=('git' 'cargo' 'clang' 'device-mapper')
source=("git+https://github.com/jthornber/thin-provisioning-tools#tag=v${pkgver}"
        hotfix-no-atomic64.diff)
sha256sums=('01b5c632b270ab40800b8483079541f0cb4bcffbca234436a20b070ec11dfa58'
            '6eb1ecd180a3184374ac930b5a69414dc664cde14ba7945b008d5c3357e5784d')

build() {
  cd "${srcdir}/${pkgname}"

  case "${CARCH}" in
    powerpc)
      # T2 is awesome
      patch -Np1 -i ${srcdir}/hotfix-no-atomic64.diff
    ;;
  esac

  cargo build --release
}

package() {
  cd "${srcdir}/${pkgname}"

  make \
    DESTDIR="${pkgdir}"/usr \
    BINDIR="${pkgdir}"/usr/bin \
    DATADIR="${pkgdir}"/usr/share \
    install
}

