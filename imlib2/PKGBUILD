# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

pkgname=imlib2
pkgver=1.12.5
pkgrel=1
pkgdesc='Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support'
url='https://sourceforge.net/projects/enlightenment/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('BSD')
makedepends=(# Currently highway does provide a static library only, that libjxl links to.
             # This introduces a build dependency for now...
             'highway'
             'libheif' 'libid3tag' 'libjxl' 'librsvg' 'libspectre' 'libwebp' 'openjpeg2')
depends=('bzip2' 'freetype2' 'giflib' 'libjpeg-turbo' 'libpng' 'libtiff' 'libxext' 'xz')
optdepends=('libheif: HEIF loader (for AVIF)'
            'libid3tag: ID3 loader'
            'libjxl: JXL loader'
            'librsvg: SVG loader'
            'libspectre: PS loader'
            'libwebp: WEBP loader'
            'openjpeg2: J2K loader')
source=("https://downloads.sourceforge.net/project/enlightenment/imlib2-src/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('fa2315f28379b430a6e6605b4284b07be06a3ef422d4f5e1c9bb24714c4cf6dd')
sha512sums=('ac83840506b3c104936c57fab370b11e460854857218f91975bf8b589f3037dd628e5ff274caf67af4a64c0930ef3ad4884bef048b889fc926410cc068e3144c')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/imlib2 \
    --x-libraries=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}


check() {
  cd "${pkgname}-${pkgver}"
  make check
}


package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
