# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aleksandar TrifunoviÄ‡ <akstrfn at gmail dot com>

pkgname=abseil-cpp
pkgver=20210324.2
pkgrel=1
pkgdesc="Collection of C++ library code designed to augment the C++ standard library"
arch=(x86_64 powerpc64le powerpc)
url='https://abseil.io'
license=('Apache')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/abseil/abseil-cpp/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('59b862f50e710277f8ede96f083a5bb8d7c9595376146838b9580be90374ee1f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -Bbuild \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_CXX_FLAGS="-DNDEBUG" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_STANDARD=17 \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"
  cmake --build .
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  DESTDIR="$pkgdir" cmake --install .
}
