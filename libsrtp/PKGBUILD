# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Yejun Yang <yejunx AT gmail DOT com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgbase=libsrtp
pkgname=(
  libsrtp
  libsrtp-docs
)
pkgver=2.7.0
pkgrel=1
epoch=1
pkgdesc="Library for SRTP (Secure Realtime Transport Protocol)"
url="https://github.com/cisco/libsrtp"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(BSD-3-Clause)
depends=(
  glibc
  nspr
  nss
)
makedepends=(
  doxygen
  git
  libpcap
  meson
)
checkdepends=(procps-ng)
source=("git+https://github.com/cisco/libsrtp#tag=v$pkgver")
b2sums=('4e72d8ddb6ff1056331377001b575e59973acf27ac8fcdfe5d08e4f44f3702012e8af7da9970de3a0e4643aa1139fcce4af3df3c31258ac8c5b35420b67aa9dd')

prepare() {
  cd libsrtp
}

build() {
  local meson_options=(
    -D crypto-library=nss
    -D crypto-library-kdf=disabled
  )

  arch-meson libsrtp build "${meson_options[@]}"
  meson compile -C build
  meson compile -C build doc
}

check() {
  case "${CARCH}" in
    riscv64) return 0 ;;
  esac

  meson test -C build --print-errorlogs
}

package_libsrtp() {
  provides=("libsrtp${pkgver%%.*}.so")

  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libsrtp/LICENSE
}

package_libsrtp-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "$pkgdir/usr/share/doc"
  cp -a build/html "$pkgdir/usr/share/doc/libsrtp"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libsrtp/LICENSE
}

# vim:set sw=2 sts=-1 et:
