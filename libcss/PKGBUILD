# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libcss
pkgver=0.9.2
pkgrel=1
pkgdesc='CSS parser and selection engine'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.netsurf-browser.org/projects/libcss/'
license=(MIT)
makedepends=(netsurf-buildsystem)
depends=('libparserutils>=0.2.5' 'libwapcaplet>=0.4.3')
source=("https://download.netsurf-browser.org/libs/releases/libcss-$pkgver-src.tar.gz")
b2sums=('4d5f2554e4ffa5cf8fef3046a244e27dba954e48770c28a2ea79b63826dc056e8fa47d006e7ab4ae2048cf706e7adc2a81053eea2cfbe8b0f0bd683610af9632')

prepare() {
  sed -i 's,_BSD_SOURCE,_DEFAULT_SOURCE,' $pkgname-$pkgver/Makefile
}

build() {
  export CFLAGS="$CFLAGS -shared -fPIC -w"
  export LDFLAGS="$LDFLAGS -shared -z,now"
  make -C $pkgname-$pkgver COMPONENT_TYPE=lib-shared INCLUDEDIR=include LIBDIR=lib PREFIX=/usr
}

package() {
  make -C $pkgname-$pkgver COMPONENT_TYPE=lib-shared DESTDIR="$pkgdir" INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr install
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
