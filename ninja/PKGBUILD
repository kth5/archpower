# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Filip Brcic <brcha@gna.org>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=ninja
pkgver=1.13.2
pkgrel=2
pkgdesc='Small build system with a focus on speed'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://ninja-build.org/'
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cmake
  gtest
  python
  re2c
)
source=("https://github.com/ninja-build/ninja/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('974d6b2f4eeefa25625d34da3cb36bdcebe7fbce40f4c16ac0835fd1c0cbae17')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac

  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd $pkgname-$pkgver
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" doc/manual.asciidoc
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING

  install -vDm644 -t "$pkgdir/usr/share/vim/vimfiles/syntax" misc/ninja.vim
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -vDm644 -t "$pkgdir/$site_packages" misc/ninja_syntax.py

  install -vDm644 misc/bash-completion "$pkgdir/usr/share/bash-completion/completions/ninja"
  install -vDm644 misc/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_ninja"
}
