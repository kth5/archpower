# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=mgard
pkgver=1.5.2
pkgrel=6
pkgdesc="MultiGrid Adaptive Reduction of Data"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/CODARcode/MGARD"
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  protobuf libprotobuf.so
  zlib libz.so
  zstd libzstd.so
)
makedepends=(
  catch2
  cmake
  ninja
  python
  #tclap # 1.4 required but not packaged
)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-pr-186.patch::https://patch-diff.githubusercontent.com/raw/CODARcode/MGARD/pull/186.patch)
b2sums=('fbebe814483f7ebbfd1f3bc33ca387fe77f7b0f0de369e5c4d2525d01172146197458e1a070824a8697fd0b9acef267f5813562aa9848db32d9175f879ce13f7'
        '627d63a928cb0a2abe828f6cead63063ed03aef96675dd0aceb8d7bfb6fe5452f2d202478e495a4aac05292ead726181f6d3b1bc0275892b7380a095ed0988d1')

prepare() {
  cd ${pkgname^^}-${pkgver}
  patch -Np1 -i ${srcdir}/${pkgname}-pr-186.patch
}

build() {
  local cmake_options=(
    -B build
    -S ${pkgname^^}-${pkgver}
    -G Ninja
    -W no-dev
    -DCMAKE_INSTALL_PREFIX=/usr
    -DMGARD_ENABLE_SERIAL=ON
    -DMGARD_ENABLE_OPENMP=ON
    -DMGARD_ENABLE_MDR=ON
    #-DMGARD_ENABLE_CLI=ON  requires tclap 1.4
    -DBUILD_TESTING=ON
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
