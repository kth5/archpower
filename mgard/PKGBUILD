# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=mgard
pkgver=1.5.2
pkgrel=2
pkgdesc="MultiGrid Adaptive Reduction of Data"
arch=(riscv64 powerpc64le riscv64)
url="https://github.com/CODARcode/MGARD"
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  protobuf libprotobuf.so
  zlib libz.so
  zstd libzstd.so
)
makedepends=(
  catch2
  cmake
  ninja
  python
  #tclap # 1.4 required but not packaged
)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('fbebe814483f7ebbfd1f3bc33ca387fe77f7b0f0de369e5c4d2525d01172146197458e1a070824a8697fd0b9acef267f5813562aa9848db32d9175f879ce13f7')

build() {
  local cmake_flags=(
    -B build -S ${pkgname^^}-${pkgver} -G Ninja
    -DCMAKE_INSTALL_PREFIX=/usr
    -DMGARD_ENABLE_SERIAL=ON
    -DMGARD_ENABLE_OPENMP=ON
    -DMGARD_ENABLE_MDR=ON
    #-DMGARD_ENABLE_CLI=ON  requires tclap 1.4
    -DBUILD_TESTING=ON
    -Wno-dev
  )
  cmake "${cmake_flags[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
