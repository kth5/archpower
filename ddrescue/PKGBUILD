# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ddrescue
pkgver=1.29.1
pkgrel=1
pkgdesc="GNU data recovery tool"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://www.gnu.org/software/ddrescue/ddrescue.html"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz{,.sig})
sha512sums=('622de9f7739458142ae5a12f08557e1e5180534a42640e99e3db5ac535e6007616b534d79ce8f864944b1a5dc3385748aa2c216901522200f4f61969a9ff16aa'
            'SKIP')
b2sums=('c1aaf24e645683e2f7a980f8eb237dc9673613229a953885d05c582c0344619311e67046f0cb5d914402341d11656454c1791a83b6f4ea9cb3fd5bd0bb6af369'
        'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742') # Antonio Diaz Diaz

build() {
  # fake configure script reinvents autotools and requires custom parameters to
  # set CPPFLAGS, CXXFLAGS and LDFLAGS
  local configure_options=(
    --prefix=/usr
    CPPFLAGS="$CPPFLAGS"
    CXXFLAGS="$CXXFLAGS"
    LDFLAGS="$LDFLAGS"
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

package() {
  make DESTDIR="$pkgdir" install install-man -C "$pkgname-$pkgver"
  install -vDm 644  $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
