# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=dmidecode
pkgver=3.6
pkgrel=1
pkgdesc="Desktop Management Interface table related utilities"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.nongnu.org/dmidecode"
license=('GPL-2.0-or-later')
depends=('glibc')
# source=("https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
source=("https://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('e40c65f3ec3dafe31ad8349a4ef1a97122d38f65004ed66575e1a8d575dd8bae'
            'SKIP')
validpgpkeys=('7CA69F4460F1BDC41FD2C858A5526B9BB3CD4E6A') # Jean Delvare (kernel.org) <jdelvare@suse.de>

build() {
  cd ${pkgname}-${pkgver}

  sed -i "s:sbin:bin:g" Makefile
  make prefix=/usr CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd ${pkgname}-${pkgver}

  make prefix=/usr DESTDIR="${pkgdir}" install
}
