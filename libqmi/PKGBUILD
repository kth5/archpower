# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libqmi
pkgname=(
  libqmi
  libqmi-docs
)
pkgver=1.36.0
pkgrel=1
pkgdesc="QMI modem protocol helper library"
url="https://www.freedesktop.org/wiki/Software/libqmi/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(GPL-2.0-or-later)
depends=(
  bash
  gcc-libs
  glib2
  glibc
  libgudev
  libmbim
  libqrtr-glib
)
makedepends=(
  bash-completion
  git
  gobject-introspection
  gtk-doc
  help2man
  meson
)
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqmi.git?#tag=${pkgver/[a-z]/-&}")
b2sums=('66df45a4b1775d72f78c8ba4539e2bc97a514aa00df55f5cfd23c9ed85f1ccd612642b34671f136103b018f2dc8e320e503734ed25a1e2b972769fb030a0bb3c')

prepare() {
  cd libqmi
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson libqmi build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libqmi() {
  provides=(libqmi-glib.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_libqmi-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
