# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maeparser
pkgver=1.2.4
pkgrel=6
pkgdesc="Maestro file parser"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://github.com/schrodinger/maeparser"
license=(MIT)
depends=(boost-libs)
makedepends=(cmake boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/schrodinger/maeparser/archive/v$pkgver.tar.gz")
sha256sums=('c62d5846c85a2982cfa8895ffbed8a6ce57678d936ddfebf429546fab4b0a36d')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
