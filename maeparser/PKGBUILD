# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maeparser
pkgver=1.3.3
pkgrel=3
pkgdesc='Maestro file parser'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/schrodinger/maeparser'
license=(MIT)
depends=(boost-libs
         gcc-libs
         glibc)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/schrodinger/maeparser#tag=v$pkgver
        fix-cmake.patch)
sha256sums=('9df2dda1586c46e205b465320896c518b485f07d3019e22d15945ede98d96aad'
            '74912bdc8b99d93c6488caf4c70f05e87c14d03a80ea8df78979ce14ddcef9d3')

prepare() {
  cd $pkgname
  # fix installation of cmake targets and config
  # https://github.com/schrodinger/maeparser/pull/87
  patch -p1 < ../fix-cmake.patch
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
