# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maeparser
pkgver=1.3.1
pkgrel=3
pkgdesc='Maestro file parser'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/schrodinger/maeparser'
license=(MIT)
depends=(boost-libs
         gcc-libs
         glibc)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/schrodinger/maeparser#tag=v$pkgver)
sha256sums=('2a2e75dcce1758b4917a700967bc4aa9db0166a7f436882f0f2360c58e1bcf26')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
