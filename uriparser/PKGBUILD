# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Juan Pablo Gonzalez Tognarelli <lord_jotape@yahoo.com.ar>

pkgname=uriparser
pkgver=0.9.9
pkgrel=1
pkgdesc='uriparser is a strictly RFC 3986 compliant URI parsing library. uriparser is cross-platform, fast, supports Unicode'
url='https://github.com/uriparser/uriparser'
makedepends=('cmake')
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('custom')
source=("https://github.com/uriparser/uriparser/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('85645df1bb220a3867c42f8adeba4f6a4b2fdc1563e91ad7d9f92d14d3dd377d')

build() {
  mkdir "${srcdir}"/build
  cd "${srcdir}"/build
  
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DURIPARSER_BUILD_DOCS=OFF \
    -DURIPARSER_BUILD_TESTS=OFF \
    "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
  install -D -m0644 "${srcdir}/${pkgname}-${pkgver}"/COPYING.BSD-3-Clause \
    "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING.BSD-3-Clause
}
