# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-declarative
_qtver=6.7.0
pkgver=${_qtver/-/}
pkgrel=1.1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for QML and JavaScript languages'
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git
             ninja
             python
             qt6-languageserver
             qt6-shadertools)
optdepends=('qt6-languageserver: for qmlls')
conflicts=(qt6-quickcontrols2)
provides=(qt6-quickcontrols2)
replaces=(qt6-quickcontrols2)
groups=(qt6)
_pkgfn=${pkgname/6-/}
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$pkgver
        big-endian-qtquick-software.patch)
sha256sums=('cc0d77b86177ff9edb85be335337935cf5f4911aa31806a06ca62e28491bfe98'
            '6979e7e3c181474aeba1b154589bb00bad4ba8e670426033a762b844ab5d4ed6')

prepare() {
  cd $_pkgfn
  patch -Np1 -i ${srcdir}/big-endian-qtquick-software.patch
}

build() {
  export CXXFLAGS+=" -ffat-lto-objects"
  cmake -B build -S $_pkgfn -G Ninja \
    -DINSTALL_PUBLICBINDIR=usr/bin \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Install symlinks for user-facing tools
  cd "$pkgdir"
  mkdir usr/bin
  while read _line; do
    ln -s $_line
  done < "$srcdir"/build/user_facing_tool_links.txt

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
