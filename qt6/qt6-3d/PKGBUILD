# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-3d
_pkgver=6.9.1
pkgver=${_pkgver/-/}
pkgrel=2
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.qt.io'
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
pkgdesc='C++ and QML APIs for easy inclusion of 3D graphics'
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(assimp
             cmake
             git
             ninja
             qt6-declarative
             qt6-shadertools)
optdepends=('assimp: assimp importer plugin'
            'qt6-declarative: QML bindings'
            'qt6-shadertools: RHI renderer')
groups=(qt6)
_pkgfn=${pkgname/6-/}
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$_pkgver
        assimp-6.patch)
sha256sums=('8ec08036b76670c69236497c8b53fea63cd8415341af8e2028e8486bd23763ed'
            '244589b0a353da757d61ce6b86d4fcf2fc8c11e9c0d9c5b109180cec9273055a')

prepare() {
  patch -d $_pkgfn -p1 < assimp-6.patch
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
