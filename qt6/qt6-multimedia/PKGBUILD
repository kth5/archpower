# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=qt6-multimedia
pkgname=(qt6-multimedia
         qt6-multimedia-ffmpeg
         qt6-multimedia-gstreamer)
_pkgver=6.9.2
pkgver=${_pkgver/-/}
pkgrel=3
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.qt.io'
license=(GPL-3.0-only
         LGPL-3.0-only
         LicenseRef-Qt-Commercial
         Qt-GPL-exception-1.0)
pkgdesc='Classes for audio, video, radio and camera functionality'
depends=(gcc-libs
         glibc
         libpulse
         qt6-base)
makedepends=(cmake
             ffmpeg
             git
             gst-plugins-bad-libs
             gst-plugins-base
             libxrandr
             ninja
             pipewire
             qt6-declarative
             qt6-quick3d
             qt6-shadertools
             vulkan-headers)
groups=(qt6)
_pkgfn=${pkgname/6-/}
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$_pkgver)
sha256sums=('e0c16079d04cf80c418f487defbc3c388d1eabf93af86c6af9a88f139fde5072')

build() {
  case "${CARCH}" in
    powerpc64)
      export CFLAGS='-O2 -pipe'
      export CXXFLAGS='-O2 -pipe'
    ;;
    powerpc)
      export CFLAGS='-O2 -pipe -mno-vsx -mno-altivec -DEIGEN_DONT_VECTORIZE=1'
      export CXXFLAGS='-O2 -pipe -mno-vsx -mno-altivec -DEIGEN_DONT_VECTORIZE=1'
    ;;
  esac

  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package_qt6-multimedia() {
  depends+=(qt6-multimedia-backend)
  optdepends=('qt6-declarative: QML bindings'
              'qt6-quick3d: for QtQuick3DAudio')

  DESTDIR="$pkgdir" cmake --install build
# Split plugins
  rm -r "$pkgdir"/usr/lib/qt6/plugins/
  rm "$pkgdir"/usr/lib/cmake/Qt6Multimedia/Qt6Q{FFmpeg,Gstreamer}*
  rm -r "$pkgdir"/usr/include/qt6/Qt{FFmpeg,Gstreamer}MediaPluginImpl \
        "$pkgdir"/usr/lib/cmake/Qt6{FFmpeg,Gstreamer}MediaPluginImplPrivate \
        "$pkgdir"/usr/lib/libQt6{FFmpeg,Gstreamer}MediaPluginImpl.a \
        "$pkgdir"/usr/lib/qt6/metatypes/qt6{ffmpeg,gstreamer}mediapluginimplprivate_relwithdebinfo_metatypes.json \
        "$pkgdir"/usr/lib/qt6/mkspecs/modules/qt_lib_{ffmpeg,gstreamer}mediapluginimpl_private.pri \
        "$pkgdir"/usr/lib/qt6/modules/{FFmpeg,Gstreamer}MediaPluginImplPrivate.json

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}

package_qt6-multimedia-gstreamer() {
  pkgdesc='Gstreamer backend for qt6-multimedia'
  depends=(gcc-libs
           glib2
           glibc
           gst-libav
           gst-plugins-bad-libs
           gst-plugins-base
           gst-plugins-good
           gstreamer
           libglvnd
           libpulse
           qt6-base
           qt6-multimedia)
  provides=(qt6-multimedia-backend)
  groups=()
  optdepends=('gst-plugins-bad: camera support, additional plugins'
              'gst-plugins-ugly: additional plugins')
  
  DESTDIR="$pkgdir" cmake --install build/src/plugins/multimedia/gstreamer    
}

package_qt6-multimedia-ffmpeg() {
  pkgdesc='FFMpeg backend for qt6-multimedia'
  depends=(ffmpeg
           gcc-libs
           glibc
           libglvnd
           libx11
           libxext
           libxrandr
           qt6-base
           qt6-declarative
           qt6-multimedia)
  provides=(qt6-multimedia-backend)
  groups=()

  DESTDIR="$pkgdir" cmake --install build/src/plugins/multimedia/ffmpeg
}
