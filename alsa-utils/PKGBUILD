# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.2.13
pkgrel=2
pkgdesc="Advanced Linux Sound Architecture - Utilities"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.alsa-project.org"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  pciutils
  psmisc
)
makedepends=(
  alsa-lib
  docbook-xsl
  fftw
  libsamplerate
  ncurses
  python-docutils
  systemd
  xmlto
)
optdepends=(
  'bash: for alsa-info.sh and alsabat-test.sh'
  'fftw: for alsabat'
)
source=(
  $url/files/pub/utils/$pkgname-$pkgver.tar.bz2{,.sig}
  0001-alsactl-90-alsa-restore.rules-fix-alsa_restore_go-std.patch
  $pkgname.tmpfiles
)
sha512sums=('0e2fb5b6e497b977badba2ebe2bddfc819654d24001622e0299e4034210caeeea2f3c2c2d1d260a48caefb6584e0b989fdb7036c6012108c8c38d89b3cb04c78'
            'SKIP'
            '1b47353970c9a80a7a41e2ff0faf0491b0b90ad1be59593a15a8ead22c43353f7a4bad6b509dade50617c31c06517e0333cc1ddbecfc78f51ec306472d4a9d84'
            '17ca2d760f383c02ccd00073d4fe73d6687ee58eb0ab5e458ad9d09845be7891cfe389e365bef00c7885e8afad45ea9ebf2925950f8d3fd8157feb818f986990')
b2sums=('9bc2bf8e21fb9308c2eabc6612da0848f9ddba45acb8bf8453d9cff7f73fa0267495430a150ea53b28fab8afb69a51e487e8b253dc7501e17d77ea3f6e90bcf7'
        'SKIP'
        'f6b58f928352b62360a600c441b2c949eac9bf6582d58f39f332b28042f4ab25394a031b59fef470195ef81f3e734bc1675bc604616d5925874392c596d9c69b'
        '24b8e44833c7e8b142162878a837b10e1c2a862568280f2ce16fcb81ecc1383802654dbe88930f2cc13c345635c5003cbc3712e121e589dffc2e25c540749ee3')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../0001-alsactl-90-alsa-restore.rules-fix-alsa_restore_go-std.patch
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --disable-alsaconf
    --sbindir=/usr/bin
    --with-udev-rules-dir=/usr/lib/udev/rules.d
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    alsa-lib libasound.so libatopology.so
    ncurses libformw.so libmenuw.so libncursesw.so libpanelw.so
    libsamplerate libsamplerate.so
  )

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"

  install -vDm 644 $pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
