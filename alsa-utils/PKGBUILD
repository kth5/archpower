# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.2.15.2
pkgrel=1
pkgdesc="Advanced Linux Sound Architecture - Utilities"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.alsa-project.org"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  pciutils
  psmisc
)
makedepends=(
  alsa-lib
  docbook-xsl
  fftw
  libsamplerate
  ncurses
  python-docutils
  systemd
  xmlto
)
optdepends=(
  'bash: for alsa-info.sh and alsabat-test.sh'
  'fftw: for alsabat'
)
source=(
  $url/files/pub/utils/$pkgname-$pkgver.tar.bz2{,.sig}
  $pkgname.tmpfiles
)
sha512sums=('13ca15a63bcbf81a4643ef03e391200d7523335bc300e290756089c4cfeccb9773cf6b77f9fcf4b80e6aa2e8c5e33a2c86c9173f1feb480aedf5ecda0f7c47cd'
            'SKIP'
            '17ca2d760f383c02ccd00073d4fe73d6687ee58eb0ab5e458ad9d09845be7891cfe389e365bef00c7885e8afad45ea9ebf2925950f8d3fd8157feb818f986990')
b2sums=('0688e668241917027b5dd161ebe3ea4ab6f8fede612e148dee74e033ab09f9557c7610a6b506d2507402cca7007a031b85c8c0cb9af80652ae9f3cc5ea157973'
        'SKIP'
        '24b8e44833c7e8b142162878a837b10e1c2a862568280f2ce16fcb81ecc1383802654dbe88930f2cc13c345635c5003cbc3712e121e589dffc2e25c540749ee3')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --disable-alsaconf
    --sbindir=/usr/bin
    --with-udev-rules-dir=/usr/lib/udev/rules.d
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    alsa-lib libasound.so libatopology.so
    ncurses libformw.so libmenuw.so libncursesw.so libpanelw.so
    libsamplerate libsamplerate.so
  )

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"

  install -vDm 644 $pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
