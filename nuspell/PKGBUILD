# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Conributor: MÃ©lanie Chauvel (ariasuni) <perso at hack-libre dot org>

pkgname=nuspell
pkgver=5.1.3
pkgrel=2
pkgdesc='Fast and safe spellchecking C++ library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://nuspell.github.io/'
license=('LGPL3')
depends=('icu' 'glibc' 'gcc-libs')
makedepends=('cmake' 'catch2-v2')
makedepends_x86_64=('pandoc')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/nuspell/nuspell/archive/v${pkgver}.tar.gz")
sha512sums=('72c868f734848be79c7cfb0502298626307213e4f3f422f8e9ee7dac24cb3d79ea55d04ef0443e7107bb11eb545c79a15a8aea8cb056dc2805c23bd5e3292562')

# 1st rebuild enchant
# 2nd rebuild epiphany against nuspell/enchant that checkdepens on both

build() {
  cmake -B "${pkgname}-${pkgver}"/build \
    -S ${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  cmake --build "${pkgname}-${pkgver}"/build
}

check() {
  ctest --test-dir "${pkgname}-${pkgver}"/build
}

package() {
  DESTDIR="${pkgdir}" cmake --install "${pkgname}-${pkgver}"/build
}
