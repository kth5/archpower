# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Laurent Carlier <lordheavym@archlinux.org>
# Contributor: Cyano Hao <c@cyano.cn>

pkgname=directx-headers
pkgver=1.614.0
pkgrel=1
pkgdesc="DirectX headers for using D3D12"
url="https://github.com/microsoft/DirectX-Headers"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(MIT)
makedepends=(
  git
  meson
)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
b2sums=('6bfbdf319090c5f42f248bc0861397a6b2d81c7de37cf8cf66a2e62a70173f23b4af49ac011270a58b9f138b3cacdd86e009c75be074be0a7761611b73779a69')

build() {
  arch-meson $pkgname build -D build-test=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE
}
