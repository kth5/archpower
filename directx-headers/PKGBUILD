# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Laurent Carlier <lordheavym@archlinux.org>
# Contributor: Cyano Hao <c@cyano.cn>

pkgname=directx-headers
pkgver=1.614.1
pkgrel=1
pkgdesc="DirectX headers for using D3D12"
url="https://github.com/microsoft/DirectX-Headers"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(MIT)
makedepends=(
  git
  meson
)
options=(!lto)
source=("$pkgname::git+$url#tag=v$pkgver")
b2sums=('6173135686ccf1e4f03e5adac056af9de50a76140596309e668a5b5eaedcfbd9fae3338a7b52c0b93d388e921775246d572caff886d8f517ea1ea88fc86c15b5')

build() {
  arch-meson $pkgname build -D build-test=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE
}
