# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libmspub
pkgver=0.1.4
pkgrel=18
pkgdesc="Microsoft Publisher file format parser library (latest snapshot)"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://wiki.documentfoundation.org/DLP/Libraries/libmspub"
license=('MPL-2.0')
depends=('libwpd' 'icu' 'librevenge'
         'zlib' 'gcc-libs' 'glibc')
makedepends=('libwpg' 'boost' 'doxygen')
source=(https://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz
        buildfix.diff
        includes.patch)
sha256sums=('ef36c1a1aabb2ba3b0bedaaafe717bf4480be2ba8de6f3894be5fd3702b013ba'
            'be1a871f9ebfb15d989605137d5de0c5dc67a3e383e0e1178037f8a9bdfc89dc'
            'a075c4592392a1df559ceac5b5d310707626ec474a906f92ed0f6b7ccd036938')

prepare() {
    cd "${pkgname}"-${pkgver}
    patch -Np1 -i ../buildfix.diff
    patch -Np1 -i ../includes.patch
    autoreconf -vfi
}

build() {
    cd "${pkgname}"-${pkgver}
    ./configure --prefix=/usr --with-docs
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "${pkgname}"-${pkgver}
    make DESTDIR="$pkgdir" install
}
