# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libzmf
pkgver=0.0.2
pkgrel=17
pkgdesc="a library for import of Zoner drawing and bitmap files"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://wiki.documentfoundation.org/DLP/Libraries/libzmf"
license=('MPL')
source=(https://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz)
depends=('librevenge' 'libpng'
         'zlib' 'icu' 'gcc-libs' 'glibc')
makedepends=('boost' 'cppunit' 'doxygen')
sha256sums=('27051a30cb057fdb5d5de65a1f165c7153dc76e27fe62251cbb86639eb2caf22')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-werror --build=${CHOST}
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
