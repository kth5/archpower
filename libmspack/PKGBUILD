# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=libmspack
epoch=1
pkgver=1.11
pkgrel=1
pkgdesc='A library for Microsoft compression formats'
url='https://www.cabextract.org.uk/libmspack/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
depends=('glibc')
makedepends=('git')
provides=('libmspack.so')
source=("git+https://github.com/kyz/libmspack#tag=v${pkgver}")
sha256sums=('8772ea127d0e09f6ae60e7780634fbf0467e26fa0dc429bc207395b0117c447a')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname/$pkgname
  autoreconf -vfi
}

build() {
  cd $pkgname/$pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname/$pkgname
  make check
}

package() {
  cd $pkgname/$pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
