# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Camille Moncelier <pix@devlife.org>, simo <simo@archlinux.org>

pkgname=kexec-tools
pkgver=2.0.30
pkgrel=1
pkgdesc="Load another kernel from the currently executing Linux kernel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://kernel.org/pub/linux/utils/kernel/kexec/"
source=("git+https://git.kernel.org/pub/scm/utils/kernel/kexec/kexec-tools.git?signed#tag=v${pkgver}"
        hotfix-error_msg.patch
        hotfix-ppc32.patch
        hotfix-ppc64-elfv2.patch
        hotfix-ppc64-mem-ranges.patch
        riscv.patch
        riscv-arch-do-exclude-segment.patch)
makedepends=('git')
validpgpkeys=('E27CD9A1F5ACC2FF4BFE7285D7CF64696A374FBE') # Simon Horman <horms@debian.org>
depends=('zlib' 'xz')
license=('GPL-2.0-or-later')
b2sums=('9092d6cb1f08018088c96bf42c734be97be072726888617b58505349c0c4945d6e1c382210bc1c176548615c7fdd169a6331a6fffc17b75ea79795669bfcb49b'
        'ed06ef6e38249105e2f6a6751edbd3975aada1563a89a55a6aa1eb0c29cccd629532f28d90726f6ed90ef3662afaf1f169ce405f01f35b313880eebf239bfdcc'
        'b3f4c16a04b22e909e5d68ee4e7c698f659cc44ebf4961a4c377de0f6bd0f9163352466226143068a9fff4cf07f990df393dac669bdc72f67f5602862c0294da'
        '5eabd0ed708b2c67aa4df52fd40db57edf165b88ce51b8cc753ee90763e96ae6460294f94432c3218e89f9a2ec3f1b2c5888adc2b830445329fe223b43468795'
        '91817d8c8d446186bbbf45d4117307eb134a8a3e35bd78f90e2c031b1a5769f0e8ece18a9b7b506ee0d60bf6cf9602d180ff34db0830773ccf20bb4c048284cf'
        'aa5599fc2cfb0a05a57349af2cd27a0a075dfe4d3ead0729d8b0908db6a45f330a9329c473a40d21074832ff68ca0e429575038d8d8bd0cc09e597eb7959433f'
        '37c0178de9b4b4b3e6d6122f358c148d8d6379e14feefe53fba0578e581cea6e89001e695db71e2fbd466c93da02a4cf4221539718467bf56945ede80f49621a')

prepare() {
  cd "${pkgname}"

  patch -Np1 -i ${srcdir}/hotfix-error_msg.patch
  patch -Np1 -i ${srcdir}/hotfix-ppc32.patch
  patch -Np1 -i ${srcdir}/hotfix-ppc64-elfv2.patch
  patch -Np1 -i ${srcdir}/hotfix-ppc64-mem-ranges.patch
  patch -Np1 -i ${srcdir}/riscv.patch
  patch -Np1 -i ${srcdir}/riscv-arch-do-exclude-segment.patch

  ./bootstrap
}

build() {
  cd "${pkgname}"

  case "${CARCH}" in
    powerpc) export CFLAGS+=' -Wno-incompatible-pointer-types' ;;
  esac

  ./configure --prefix=/usr --sbindir=/usr/bin --without-lzma
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 kexec/kexec.8 "$pkgdir"/usr/share/man/man8/kexec.8
}

