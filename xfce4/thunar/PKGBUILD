# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=4.20.1
pkgrel=1
pkgdesc="Modern, fast and easy-to-use file manager for Xfce"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/thunar/start"
license=('GPL-2.0-or-later' 'LGPL-2.1-only')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'pcre2' 'libgudev' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('git' 'glib2-devel' 'gobject-introspection' 'xfce4-panel' 'xfce4-dev-tools')
optdepends=('catfish: file searching'
            'gvfs: trash support, mounting with udisk and remote filesystems'
            'tumbler: thumbnail previews'
            'thunar-volman: removable device management'
            'thunar-archive-plugin: archive creation and extraction'
            'thunar-media-tags-plugin: view/edit ID3/OGG tags')
source=("git+https://gitlab.xfce.org/xfce/thunar.git#tag=$pkgname-$pkgver")
sha256sums=('5df14eef47158fcfe253fcb4eef4e64613847265ddb28fc368b56e92b6566a4c')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-gtk-doc \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
