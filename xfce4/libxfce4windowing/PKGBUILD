# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=libxfce4windowing
pkgver=4.20.2
pkgrel=2
pkgdesc="Windowing concept abstraction library for X11 and Wayland"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://docs.xfce.org/xfce/libxfce4windowing/start"
license=('LGPL-2.1-only')
depends=('gdk-pixbuf2' 'glib2' 'gtk3' 'libwnck3' 'libdisplay-info' 'libx11' 'wayland')
makedepends=('git' 'glib2-devel' 'gobject-introspection' 'gtk-doc' 'wayland-protocols' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/libxfce4windowing.git#tag=$pkgname-$pkgver"
        git+https://gitlab.freedesktop.org/wlroots/wlr-protocols.git
        fix_icon_size.patch)
sha256sums=('f7101514fde303d039befc118857286e29a46010607ac1868c693890c169f7fc'
            'SKIP'
            '74b1ca53a5bade348396b7318c7d97daf30ee94fa761b9be9219e818194cd4e7')

prepare() {
  cd $pkgname

  # Temporary patch to fix icon size in taskbar
  # See https://gitlab.xfce.org/xfce/libxfce4windowing/-/issues/38
  # and https://gitlab.xfce.org/xfce/libxfce4windowing/-/commit/e58801e1f8455c928ff133ccde206b105f2bcc01
  patch -Np1 -i "$srcdir/fix_icon_size.patch"

  git submodule init
  git config submodule.mate-submodules.url "$srcdir/protocols/wlr-protocols"
  git -c protocol.file.allow=always submodule update
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-x11 \
    --disable-debug
  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
