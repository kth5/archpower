# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-diskperf-plugin
pkgver=2.7.1
pkgrel=1
pkgdesc="Displays instant disk/partition performance in the Xfce4 panel"
arch=(x86_64 powerpc64le powerpc riscv64)
license=('BSD-2-Clause')
url="https://docs.xfce.org/panel-plugins/xfce4-diskperf-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-diskperf-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('43c82ab7a2c0df9c4c62bfb5d71e65773260e6f9d8af0a5d2e303f09c6eb7a89')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
