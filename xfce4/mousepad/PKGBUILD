# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: tobias [tobias at archlinux.org]

pkgname=mousepad
pkgver=0.6.3
pkgrel=2
pkgdesc="Simple text editor for Xfce"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/apps/mousepad/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('gtksourceview4' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('git' 'glib2-devel' 'gspell' 'libxfce4ui' 'polkit' 'xfce4-dev-tools')
optdepends=('gspell: spell checking plugin'
            'libxfce4ui: shortcuts editor plugin')
source=("git+https://gitlab.xfce.org/apps/mousepad.git#tag=$pkgname-$pkgver")
sha256sums=('1f2ab5b7652d6289f2643b32b4e067dddb11b7065e48b6203aa1d2c19c131f21')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
