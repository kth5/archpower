# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfconf
pkgver=4.18.2
pkgrel=2
pkgdesc="D-Bus-based configuration storage system"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/xfconf/start"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4util')
makedepends=('intltool' 'gobject-introspection' 'vala')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-fix-uncached-value.patch::https://gitlab.xfce.org/xfce/xfconf/-/commit/5877264948da.patch)
sha256sums=('dce24fb0555e9718d139c10e714759e03ab4e40a7ffcf3c990f046f7a17213cc'
            'affdb5304b99b5824abc93a92512006271105a20f1607c596dd3acc9598eb94a')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../$pkgname-fix-uncached-value.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
