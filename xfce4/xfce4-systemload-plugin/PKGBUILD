# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-systemload-plugin
pkgver=1.3.3
pkgrel=1
pkgdesc="System load plugin for the Xfce panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-2-Clause')
url="https://docs.xfce.org/panel-plugins/xfce4-systemload-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libgtop')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-systemload-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('531d8b2750345b2828a375d6c6133153e14763ab0cb1de25b9da5fc438fb2c7a')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
