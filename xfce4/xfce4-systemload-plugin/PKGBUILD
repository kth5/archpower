# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-systemload-plugin
pkgver=1.3.2
pkgrel=4
pkgdesc="System load plugin for the Xfce panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-2-Clause')
url="https://docs.xfce.org/panel-plugins/xfce4-systemload-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libgtop')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-systemload-plugin.git#tag=$pkgname-$pkgver"
        xfce4-systemload-plugin-1.3.2-32bit.patch)
sha256sums=('93fc07d6216ac83111f5271f1dd76c22799467deb172d502baeaf2d39904625b'
            'fb35cfd13c479ed7a3af1d7d36120fab1def9af10b1bcff6fa0cf40224d204f4')

prepare() {
  cd $pkgname

  patch -Np1 -i ${srcdir}/xfce4-systemload-plugin-1.3.2-32bit.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
