# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-genmon-plugin
pkgver=4.2.0
pkgrel=4
pkgdesc="plugin that monitors customizable programs stdout for the Xfce4 panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('LGPL-2.1-only')
url="https://docs.xfce.org/panel-plugins/xfce4-genmon-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'hicolor-icon-theme')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-genmon-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('209025af88768d6c206268d9cbe5ac6328bb4d9f187704bcb49251fd91f0d8f9')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
