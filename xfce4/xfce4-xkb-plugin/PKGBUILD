# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.9.0
pkgrel=1
pkgdesc="Plugin to switch keyboard layouts for the Xfce4 panel"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://docs.xfce.org/panel-plugins/xfce4-xkb-plugin/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier' 'librsvg' 'libwnck3' 'libnotify')
makedepends=('git' 'meson' 'xfce4-dev-tools')
optdepends=('xfce4-notifyd: show notifications on layout change')
install=$pkgname.install
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-xkb-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('381ef5b60f0759aa784e7b2920756e08e9be7376ce5495b53758ea6eea1e4026')

build() {
  arch-meson $pkgname build \
    --localstatedir=/var
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
