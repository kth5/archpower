# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.8.5
pkgrel=1
pkgdesc="Plugin to switch keyboard layouts for the Xfce4 panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/panel-plugins/xfce4-xkb-plugin/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier' 'librsvg' 'libwnck3' 'libnotify')
makedepends=('git' 'xfce4-dev-tools')
optdepends=('xfce4-notifyd: show notifications on layout change')
install=$pkgname.install
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-xkb-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('47eac24533720ccd9aa526dee093eb7c93c21bfbb47a7d180a197c62ee59f300')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
