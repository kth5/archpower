# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.13.3
pkgrel=1
pkgdesc="A fast and lightweight picture viewer for Xfce"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/apps/ristretto/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'xfconf' 'tumbler' 'libexif' 'file')
makedepends=('git' 'glib2-devel' 'python' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/apps/ristretto.git#tag=$pkgname-$pkgver")
sha256sums=('318568c0ca91db5e629a3a4f263fa4cb28383d2508e1e534bf9cf14a4789dedd')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
