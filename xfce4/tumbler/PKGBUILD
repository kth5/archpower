# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

pkgname=tumbler
pkgver=4.18.2
pkgrel=1
pkgdesc="Thumbnail service implementing the thumbnail management D-Bus specification"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/tumbler/start"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4util' 'gdk-pixbuf2')
makedepends=('intltool' 'python' 'ffmpegthumbnailer' 'poppler-glib' 'libgsf'
             'libgepub' 'libopenraw' 'freetype2')
optdepends=('ffmpegthumbnailer: video thumbnails'
            'poppler-glib: PDF thumbnails'
            'libgsf: ODF thumbnails'
            'libgepub: EPUB thumbnails'
            'libopenraw: RAW thumbnails'
            'freetype2: font thumbnails')
backup=('etc/xdg/tumbler/tumbler.rc')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('b530eec635eac7f898c0d8d3a3ff79d76a145d3bed3e786d54b1ec058132be7a')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug \
    --disable-gstreamer-thumbnailer
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
