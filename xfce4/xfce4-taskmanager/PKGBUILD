# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=xfce4-taskmanager
pkgver=1.5.6
pkgrel=2
pkgdesc="Easy to use task manager"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/apps/xfce4-taskmanager"
license=('GPL2')
depends=('libxfce4ui' 'xfconf' 'libxmu' 'libwnck3')
makedepends=('intltool')
groups=('xfce4-goodies')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-fix-broken-show-legend-setting-sync.patch::https://gitlab.xfce.org/apps/xfce4-taskmanager/-/commit/b525a8569108.patch)
sha256sums=('20979000761a41faed4f7f63f27bd18bb36fb27db4f7ecc8784a460701fb4abb'
            'effb4a621706ec9efab414af2b4f58dd991e523a3761c344fedf5d5212a6537c')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../$pkgname-fix-broken-show-legend-setting-sync.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
