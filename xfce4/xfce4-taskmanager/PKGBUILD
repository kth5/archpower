# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=xfce4-taskmanager
pkgver=1.5.8
pkgrel=1
pkgdesc="Easy to use task manager"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/apps/xfce4-taskmanager/start"
license=('GPL-2.0-or-later')
depends=('libxfce4ui' 'xfconf' 'libxmu' 'libwnck3')
makedepends=('git' 'xfce4-dev-tools')
groups=('xfce4-goodies')
source=("git+https://gitlab.xfce.org/apps/xfce4-taskmanager.git#tag=$pkgname-$pkgver")
sha256sums=('48f9453b01e066c78bd3e9c3779fe972e3ea45984cd13bf81f99653ec3ed1b46')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
