# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=4.18.0
pkgrel=3
pkgdesc="Application library for the Xfce desktop environment"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/exo/start"
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
groups=('xfce4')
depends=('libxfce4ui' 'hicolor-icon-theme')
makedepends=('git' 'glib2-devel' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/exo.git#tag=$pkgname-$pkgver")
sha256sums=('ac38c00112dca2da4ee27b79a113d527961991f7d13decc348da8c6fb85fb415')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
