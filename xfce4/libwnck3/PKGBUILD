# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
pkgver=43.1
pkgrel=1
pkgdesc="Library to manage X windows and workspaces (for e.g. pagers or tasklists)"
url="https://gitlab.gnome.org/GNOME/libwnck"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libx11
  libxrender
  libxres
  pango
  startup-notification
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
source=(
  "git+$url.git?signed#tag=$pkgver"
)
b2sums=('348354a2086dbd611967a3dc65641efcf8d3a5f53e47853204ceb867c70a2794c86b13169f99eeb2adf433eb01de8b5096c6c45c0c1efa4c8294409c7d15a883')
validpgpkeys=(
  7B44FD78E49334EC10B3B288A3D013EC303E1894 # Alberts MuktupƒÅvels <alberts.muktupavels@gmail.com>
)

prepare() {
  cd libwnck
}

build() {
  arch-meson libwnck build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
