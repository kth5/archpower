# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
pkgver=43.2
pkgrel=1
pkgdesc="Library to manage X windows and workspaces (for e.g. pagers or tasklists)"
url="https://gitlab.gnome.org/GNOME/libwnck"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libx11
  libxrender
  libxres
  pango
  startup-notification
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
source=(
  "git+$url.git?signed#tag=$pkgver"
)
b2sums=('0f113c99fcf6bce363ee4d195dce26c3ed4abc28a4812095473b0563aee81c49721e9d07944b7ab61cf3840405a8ded7e8516e5847e7b9fcb11cf2f23b5faabf')
validpgpkeys=(
  7B44FD78E49334EC10B3B288A3D013EC303E1894 # Alberts MuktupƒÅvels <alberts.muktupavels@gmail.com>
)

prepare() {
  cd libwnck
}

build() {
  arch-meson libwnck build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
