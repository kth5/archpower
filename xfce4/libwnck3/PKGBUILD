# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
pkgver=43.3
pkgrel=1
pkgdesc="Library to manage X windows and workspaces (for e.g. pagers or tasklists)"
url="https://gitlab.gnome.org/GNOME/libwnck"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libx11
  libxrender
  libxres
  pango
  startup-notification
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
)
source=(
  "git+$url.git?signed#tag=$pkgver"
)
b2sums=('5a9d6833d3de15c6c475ee271cb8d116ae6b8a2abe4538c2fbd3d24c9cdd4cd409b2efa835ca846332575d25711abaa2055552246bca47c21559061fdb946661')
validpgpkeys=(
  7B44FD78E49334EC10B3B288A3D013EC303E1894 # Alberts MuktupƒÅvels <alberts.muktupavels@gmail.com>
)

prepare() {
  cd libwnck
}

build() {
  arch-meson libwnck build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
