# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-appfinder
pkgver=4.20.0
pkgrel=2
pkgdesc="Application launcher and finder"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/xfce4-appfinder/start"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('libxfce4ui' 'garcon' 'xfconf' 'hicolor-icon-theme')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/xfce4-appfinder.git#tag=$pkgname-$pkgver")
sha256sums=('50e5da3517eeb06012f88eb1b337ec8e6de4e4d29cfbe86fe3a3b869c0d7a75c')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
