# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-appfinder
pkgver=4.18.1
pkgrel=1
pkgdesc="Application launcher and finder"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/xfce4-appfinder/start"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'garcon' 'xfconf' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('9854ea653981be544ad545850477716c4c92d0c43eb47b75f78534837c0893f9')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
