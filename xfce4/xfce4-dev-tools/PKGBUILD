# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-dev-tools
pkgver=4.18.1
pkgrel=2
pkgdesc="Xfce developer tools"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/xfce4-dev-tools/start"
license=('GPL-2.0-or-later')
depends=('glib2' 'intltool' 'gtk-doc')
makedepends=('git')
optdepends=('docker: xfce-build helper script')
source=("git+https://gitlab.xfce.org/xfce/xfce4-dev-tools.git#tag=$pkgname-$pkgver")
sha256sums=('29a978213b54fd02e532ee955513a6775be3652aa192893fe20c9da333843927')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
}

build() {
  cd $pkgname
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
