# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=libxfce4util
pkgver=4.20.1
pkgrel=1
pkgdesc="Basic utility non-GUI functions for Xfce"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://docs.xfce.org/xfce/libxfce4util/start"
license=('GPL-2.0-only')
depends=('glib2')
makedepends=('git' 'gobject-introspection' 'vala' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/libxfce4util.git#tag=$pkgname-$pkgver")
sha256sums=('b2661455ff96e716069b3cfda8c181c74d26961059b8d65a8a0ca43311ea7bae')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-gtk-doc \
    --disable-debug \
    --enable-maintainer-mode
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
