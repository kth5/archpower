# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=2.8.0
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-goodies')
license=('GPL2')
depends=('accountsservice' 'garcon' 'gtk3' 'gtk-layer-shell' 'xfce4-panel')
makedepends=('cmake' 'ninja')
source=("https://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('bcf8035ee9080a955b7ad6f2da0fdefa4db283f9ce2d1717ad0faef5c23244e6')

build() {
  cmake -B build -S "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
