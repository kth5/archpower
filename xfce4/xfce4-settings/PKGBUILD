# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.18.4
pkgrel=1
pkgdesc="Xfce's Configuration System"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/xfce4-settings/start"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'xfconf' 'libnotify' 'libcanberra'
         'colord' 'libxklavier' 'elementary-icon-theme' 'gnome-themes-extra')
makedepends=('intltool' 'xf86-input-libinput')
optdepends=('python: xfce4-compose-mail -- "mailto:" URI handling')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        default-xsettings-xml.patch)
sha256sums=('f10c55d0360308d9944f415645d9596d4352f952a20fc7c4a66f30fe511ca1dc'
            'ca1c7562e209d9ab791b02f2a886c61a9ed705a912d9575f590734eff21d6e5c')
prepare() {
  cd $pkgname-$pkgver

  # Enable Adwaita theme, elementary icon theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
