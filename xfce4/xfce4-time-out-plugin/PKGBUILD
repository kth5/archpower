# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=xfce4-time-out-plugin
pkgver=1.1.3
pkgrel=3
pkgdesc="Take a break from your computer with this plugin for Xfce4"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/panel-plugins/xfce4-time-out-plugin/start"
groups=('xfce4-goodies')
license=('GPL-2.0-or-later')
depends=('xfce4-panel')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-time-out-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('bea1f9a5bfae7cf2c59c764d8c0cb22a40cf61f9d3003bc49553454755ae08ea')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
