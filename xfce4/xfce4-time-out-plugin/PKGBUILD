# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=xfce4-time-out-plugin
pkgver=1.1.4
pkgrel=1
pkgdesc="Take a break from your computer with this plugin for Xfce4"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/panel-plugins/xfce4-time-out-plugin/start"
groups=('xfce4-goodies')
license=('GPL-2.0-or-later')
depends=('xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-time-out-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('ce345a90feb1838d0ee31368b2b34e488032d8c7a103c058876ab66450f004c3')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
