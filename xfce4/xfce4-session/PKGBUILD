# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-session
pkgver=4.20.0
pkgrel=5
pkgdesc="Xfce's session manager"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/xfce4-session/start"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck3' 'libxfce4windowing' 'xfconf' 'libsm' 'polkit' 'gtk-layer-shell'
         'xorg-iceauth' 'xorg-xinit' 'xorg-xrdb' 'polkit-gnome' 'hicolor-icon-theme')
makedepends=('git' 'glib2-devel' 'xfce4-dev-tools')
optdepends=('gnome-keyring: for keyring support when GNOME compatibility is enabled'
            'xfce4-screensaver: for locking screen with xflock4'
            'xscreensaver: for locking screen with xflock4'
            'light-locker: for locking screen with xflock4'
            'labwc: recommended compositor for the experimental Wayland session')
source=("git+https://gitlab.xfce.org/xfce/xfce4-session.git#tag=$pkgname-$pkgver"
        source-system-xinitrc-scripts.patch
        xfce-polkit-gnome-authentication-agent-1.desktop)
sha256sums=('e909155f9cbbdc9115f05a1a1b1d1c64d6c714556fc8857ef65e668b6f14711e'
            '5706e37a8ca692d66d23f478bc462c96fe173b35945020b550be36c56f310fb2'
            '74c94c5f7893d714e04ec7d8b8520c978a5748757a0cdcf5128492f09f31b643')

prepare() {
  cd $pkgname

  # https://bugzilla.xfce.org/show_bug.cgi?id=15440
  patch -Np1 -i ../source-system-xinitrc-scripts.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-x11 \
    --enable-wayland \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # Provide a default PolicyKit Authentication Agent (FS#42569)
  install -d "$pkgdir/etc/xdg/autostart"
  cp "$srcdir/xfce-polkit-gnome-authentication-agent-1.desktop" \
    "$pkgdir/etc/xdg/autostart/"
}

# vim:set ts=2 sw=2 et:
