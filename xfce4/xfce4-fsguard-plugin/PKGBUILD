# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=xfce4-fsguard-plugin
pkgver=1.1.4
pkgrel=1
pkgdesc="File system usage monitor plugin for the Xfce4 panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-2-Clause')
url="https://docs.xfce.org/panel-plugins/xfce4-fsguard-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-fsguard-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('38647f96147d7ecccdae63483a0b2ebd5ad04f2932aba5627be5cc63a4603da6')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
