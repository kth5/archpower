# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: aurelien <aurelien@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-mpc-plugin
pkgver=0.5.5
pkgrel=1
pkgdesc="Control the Music Player Daemon from the Xfce4 panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/panel-plugins/xfce4-mpc-plugin/start"
license=('BSD-1-Clause')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libmpd')
makedepends=('git' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-mpc-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('1113ead044e6c0d965e7faea656259449d1e31ec11d4fe7ba75b17ab0c41add6')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
