# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-terminal
pkgver=1.1.2
pkgrel=2
pkgdesc="A modern terminal emulator primarily for the Xfce desktop environment"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/apps/terminal/start"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'vte3' 'gtk-layer-shell' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-wrong-assert.patch::https://gitlab.xfce.org/apps/xfce4-terminal/-/commit/177fda86451c.patch)
sha256sums=('79a85ee216502c7248e04d560adf8fef86b9d0e047f81e9ea4fe26fbda34d810'
            '81d213427d307a4f428ea7dbfd217dec15b80d187002845b0e1c1873431e14b7')

prepare() {
  cd $pkgname-$pkgver

  # https://gitlab.xfce.org/apps/xfce4-terminal/-/issues/299
  patch -Np1 -i ../$pkgname-wrong-assert.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
