# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=garcon
pkgver=4.20.0
pkgrel=2
pkgdesc="Freedesktop.org compliant menu library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/garcon/start"
license=('LGPL-2.0-only')
groups=('xfce4')
depends=('libxfce4ui')
makedepends=('git' 'glib2-devel' 'python' 'gobject-introspection' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/garcon.git#tag=$pkgname-$pkgver")
sha256sums=('2febf44d472e9d597a722f5082458ad612e05b167f7a2f9a5b56d4633bdc99cc')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
