# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=garcon
pkgver=4.18.2
pkgrel=2
pkgdesc="Freedesktop.org compliant menu library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://docs.xfce.org/xfce/garcon/start"
license=('LGPL-2.0-only')
groups=('xfce4')
depends=('libxfce4ui')
makedepends=('git' 'glib2-devel' 'intltool' 'python' 'gobject-introspection' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/garcon.git#tag=$pkgname-$pkgver")
sha256sums=('488ff7e9631395cdf4a607b606fe87f1e8c6d9c16c465330379447bb28876269')

prepare() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk-doc \
    --disable-debug
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
