# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-weather-plugin
pkgver=0.12.0
pkgrel=2
pkgdesc="A weather plugin for the Xfce4 panel"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://docs.xfce.org/panel-plugins/xfce4-weather-plugin/start"
license=('GPL-2.0-or-later')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxml2' 'libsoup3' 'hicolor-icon-theme' 'upower')
makedepends=('git' 'meson' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-weather-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('6df8516c076453b5968a31972c33f1cd48ef1909d1a56d607eb7092252c321c4')

prepare() {
  #  meson-build: Add missing HAVE_UPOWER_GLIB definition
  git -C $pkgname cherry-pick -n 1d8e5e5dbbc4d53e4b810f9b01a460197cd47b64
}

build() {
  arch-meson $pkgname build \
    --localstatedir=/var
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
