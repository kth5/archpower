# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Andreas Radke <andyrtr at archlinux.org>
# Contributor: Ben Mazer <contrasutra@myrealbox.com>

pkgname=xfce4-notes-plugin
pkgver=1.11.1
pkgrel=1
pkgdesc="Notes plugin for the Xfce panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL-2.0-or-later')
url="https://docs.xfce.org/panel-plugins/xfce4-notes-plugin/start"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'gtksourceview4' 'hicolor-icon-theme')
makedepends=('git' 'vala' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/panel-plugins/xfce4-notes-plugin.git#tag=$pkgname-$pkgver")
sha256sums=('7a3b0f68f6013edd5ae89bf737c4284a7afedf546c0bf1fb26d77e27b4fce6e5')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
