# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Florian Zeitz <florob at babelmonkeys dot de>

pkgname=numactl
pkgver=2.0.17
pkgrel=1
pkgdesc="Simple NUMA policy support"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/numactl/numactl"
license=(
  GPL-2.0-only
  LGPL-2.1-only
)
depends=(glibc)
provides=(libnuma.so)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('a2a76170b673b334c3f456a52b87d4991ac8e117934230e4594dba221354d38d03ba80c91d6d43a15bc5e9e31b30c94211061552710a671aa39a1318ec2fab88')
b2sums=('cbd342ae9efe5050fb6ff34b316ce2cea3f956b30057d98e7cd9ad68000524374f80c842c949a8431864286c6be3e52e0bd688a4c70db11dec47b6536f9de3f9')

prepare(){
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k test -C $pkgname-$pkgver || echo "Tests known to fail depending on system load."
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # move_pages(2) is provided by man-pages and is more up-to-date there anyway
  rm -rf "$pkgdir/usr/share/man/man2"
}
