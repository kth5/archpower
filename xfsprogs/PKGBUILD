# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=xfsprogs
pkgver=6.7.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL-2.0-only' 'LGPL-2.1-only')
url="https://xfs.wiki.kernel.org"
depends=('sh' 'util-linux-libs' 'device-mapper' 'libinih' 'liburcu')
optdepends=('icu: for xfs_scrub'
	    'python: for xfs_scrub_all script'
            'smtp-forwarder: for xfs_scrub_fail script')
makedepends=('git' 'icu')
options=('!makeflags')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git?signed#tag=v${pkgver}")
b2sums=('810ffde66e8de47ce264406591ef6fd6f24b52ab44a0b7a58f7858c8eee442c9f33988f968940560ab5a455ceac7c9b2de1376f3fba816d19eec81f3e92d8529')
validpgpkeys=(
  '2B8185919E8D248981869DED20AE1692E13DDEE0' # Eric Sandeen <sandeen@sandeen.net>
  '4020459E58C1A52511F5399113F703E6C11CF6F0' # Carlos Maiolino <cem@kernel.org>
)

build() {
  cd ${pkgname}-dev
  make configure
  export OPTIMIZER="-O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib --with-systemd-unit-dir=/usr/lib/systemd/system
  make
}

package() {
  cd ${pkgname}-dev
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root "$pkgdir"
  chgrp -R root "$pkgdir"
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
