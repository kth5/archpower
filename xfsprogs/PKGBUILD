# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=xfsprogs
pkgver=6.13.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(
  GPL-2.0-only
  LGPL-2.1-only
)
url="https://xfs.wiki.kernel.org"
depends=(
  device-mapper
  libedit
  libinih
  liburcu
  sh
  util-linux-libs
)
optdepends=(
  'icu: for xfs_scrub'
  'python: for xfs_scrub_all script'
  'smtp-forwarder: for xfs_scrub_fail script'
)
makedepends=(
  git 
  icu
)
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git?signed#tag=v${pkgver}"
        xfsprogs-6.13.0-32-bit.patch)
b2sums=('27a21cb9d926f67df3d32d97a9b51028964e8c358d8170ddc4c6b22e92c97ec524d706ca1ea3446e3185bae2dedc17ce14fd77b11680cdf3f4a4ceef00bdcdcd'
        'e7770a5ec0397826628f6e87d2ea6263a9b3319cd28ab8b7732a440b2627e170f8c7d68dcb9f1e6b9c362432606a75d2d0b584787b5b244972db84b7e7c70732')
validpgpkeys=(
  'AE1B2A9562721A6FC4307C1F46A7EA18AC33E108' #  Andrey Albershteyn <aalbersh@redhat.com>
  '2B8185919E8D248981869DED20AE1692E13DDEE0' # Eric Sandeen <sandeen@sandeen.net>
  '4020459E58C1A52511F5399113F703E6C11CF6F0' # Carlos Maiolino <cem@kernel.org>
)

prepare() {
  cd ${pkgname}-dev
  patch -Np1 -i ${srcdir}/xfsprogs-6.13.0-32-bit.patch
}

build() {
  cd ${pkgname}-dev
  make configure

  # Inject libicuuc to fix link error:
  # /usr/bin/ld: /tmp/ccRHx17I.ltrans1.ltrans.o: undefined reference to symbol 'uiter_setString_76'
  # /usr/bin/ld: /usr/lib64/libicuuc.so.76: error adding symbols: DSO missing from command line
  sed -r -i 's/\$\(LIBICU_LIBS\)/\0 -licuuc/' scrub/Makefile

  # Package is honoring CFLAGS; No need to use OPTIMIZER anymore.
  # However, we have to provide an empty value to avoid default
  # flags.
  export OPTIMIZER=" "
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/lib \
              --with-systemd-unit-dir=/usr/lib/systemd/system \
              --localstatedir=/var --enable-editline
  make
}

package() {
  cd ${pkgname}-dev
  make PKG_USER=root PKG_GROUP=root DIST_ROOT="${pkgdir}" \
       PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
}
