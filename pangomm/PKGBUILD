# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=(
  pangomm
  pangomm-docs
)
pkgver=2.46.4
pkgrel=1
pkgdesc="C++ bindings for Pango"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  cairomm
  glibmm
  pango
)
makedepends=(
  cairomm-docs
  git
  glibmm-docs
  meson
  mm-common
)
options=(!emptydirs)
_commit=13629c78e2b3ed106bd3b3fbee3522e11bafd559  # tags/2.46.4^0
source=("git+https://gitlab.gnome.org/GNOME/pangomm.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd pangomm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pangomm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson pangomm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_pangomm() {
  depends+=(
    libcairomm-1.0.so
    libglibmm-2.4.so
    libsigc-2.0.so
  )
  provides=(libpangomm-1.4.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_pangomm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
