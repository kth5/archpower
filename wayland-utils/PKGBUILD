# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>

pkgname=wayland-utils
pkgver=1.2.0
pkgrel=2
pkgdesc='Wayland tools to display information about current compositor'
url='https://gitlab.freedesktop.org/wayland/wayland-utils'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(MIT)
depends=(glibc
         libdrm
         wayland)
makedepends=(git
             meson
             wayland-protocols)
source=(git+https://gitlab.freedesktop.org/wayland/$pkgname#tag=$pkgver)
sha256sums=('0e821bea8aad534b05c885946ca51e16251251296a3fbcae99a7a4033fa2946d')

build() {
  meson $pkgname build \
    --prefix /usr
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
