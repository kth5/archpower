# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=liborcus
pkgver=0.20.1
pkgrel=1
pkgdesc="File import filter library for spreadsheet documents."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://gitlab.com/orcus/orcus/blob/master/README.md"
license=('MPL-2.0')
depends=('libixion' 'glibc' 'boost-libs' 'gcc-libs' 'zlib' 'python')
makedepends=('boost' 'mdds')
optdepends=('python-requests: in tool bugzilla')
source=(https://gitlab.com/api/v4/projects/orcus%2Forcus/packages/generic/source/${pkgver}/liborcus-${pkgver}.tar.xz)
# https://gitlab.com/orcus/orcus/-/releases
sha256sums=('ec27f30e8445a2a3f307f7e829fc446fd48193150b7f8f23bb5bfb25ec6e4e27')

build() {
    cd "${pkgname}"-${pkgver}
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd "${pkgname}"-${pkgver}
    make check
}

package() {
    cd "${pkgname}"-${pkgver}
    make DESTDIR="$pkgdir" install
}
