# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>
# Contributor: Hervé YVIQUEL <elldekaa@gmail.com>

pkgname=hwloc
pkgver=2.11.1
pkgrel=1
pkgdesc='Portable Hardware Locality is a portable abstraction of hierarchical architectures'
url='https://www.open-mpi.org/projects/hwloc/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-3-Clause')
depends=(
  'glibc'
  'libpciaccess'
  'libtool'
  'ncurses'
  'systemd-libs'
)
makedepends=(
  'cairo'
  'libx11'
  'libxml2'
  'systemd'
)
optdepends=(
  'cairo: PDF, Postscript, and PNG export support'
  'libxml2: full XML import/export support'
)
options=('!docs')
source=("https://www.open-mpi.org/software/hwloc/v${pkgver%.*}/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('095afab5e7caf2099266b71d7803f2695987eed1c62d7315867b12f8db059d5a5e6f64d6303351cc1a14cfbcf0f7f5a3acedb775a0220f3648d158941c270b28')
b2sums=('05570b54235fe3ce31d974bcda025b2de465f36d43ed72bde7ac95af42a1e24000444323780f318bc0733c255169d6db436bcef230ead803fc2c1c68d12626ee')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-plugins \
    --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
