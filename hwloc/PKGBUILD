# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>
# Contributor: Hervé YVIQUEL <elldekaa@gmail.com>

pkgname=hwloc
pkgver=2.12.0
pkgrel=1
pkgdesc='Portable Hardware Locality is a portable abstraction of hierarchical architectures'
url='https://www.open-mpi.org/projects/hwloc/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-3-Clause')
depends=(
  'glibc'
  'libpciaccess'
  'libtool'
  'ncurses'
  'systemd-libs'
)
makedepends=(
  'cairo'
  'libx11'
  'libxml2'
  'systemd'
)
optdepends=(
  'cairo: PDF, Postscript, and PNG export support'
  'libxml2: full XML import/export support'
)
options=('!docs')
source=("https://www.open-mpi.org/software/hwloc/v${pkgver%.*}/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('0486eb8fc49ded213ae84eb78ca87429570230b46513f90b6015a2ebc61f02c2b4f1203074a36813515ada0a8be2509d823ed03dc7ca5a065fb957523462cf30')
b2sums=('4fc582a2204ce9edb786a2978c9c5cf0766981ecc54d98e4a07aeece0f696f404b6b069cc0974378c5bdc36d15865c7420ff1871733b1adf17cea23f706a7168')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-plugins \
    --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
