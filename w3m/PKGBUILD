# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=w3m
pkgver=0.5.5
pkgrel=1
pkgdesc='Text-based Web browser as well as pager'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://git.sr.ht/~rkta/w3m'
license=('LicenseRef-w3m')
depends=(
  'gc'
  'glibc'
  'gpm'
  'ncurses'
  'openssl'
  'perl'
  'zlib'
)
makedepends=(
  'git'
  'imlib2'
)
optdepends=(
  'imlib2: for graphics support'
  'libx11: for graphics support'
)
source=("git+https://git.sr.ht/~rkta/w3m#tag=v${pkgver}")
sha512sums=('d6e2615866026c630c967f906757f3146ce1ccd3c2cfde077fa559b6914ea72c42f1fb9ed37798715993eab3a7bb42eab725ed7fb9bfb2ee92c3a5e173dff38a')

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-image=x11,fb \
    --with-imagelib=imlib2 \
    --with-termlib=ncurses \
    --disable-w3mmailer
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -vDm644 -t "${pkgdir}/usr/share/doc/${pkgname}" doc/*
  install -vDm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING
}
