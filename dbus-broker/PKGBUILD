# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgbase=dbus-broker
pkgname=(
  dbus-broker
)
pkgver=34
pkgrel=1
pkgdesc="Linux D-Bus Message Broker"
url="https://github.com/bus1/dbus-broker/wiki"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=("Apache-2.0")
depends=(
  audit
  libcap-ng
  expat
  systemd-libs
)
makedepends=(
  meson
  python-docutils
  systemd
)
source=(
  https://github.com/bus1/dbus-broker/releases/download/v$pkgver/$pkgbase-$pkgver.tar.xz{,.asc}
)
b2sums=('9c135db01d3b8d436c39dd4cb07303b8faf2cc2651b1b0c95444fc8c5b42a2ba84e963b7e0df4668ab0584c3ac3572da295a75b718df65b13b4a96a417a35176'
        'SKIP')
validpgpkeys=(
  BE5FBC8C9C1C9F60A4F0AEAE7A4F3A09EBDEFF26  # David Herrmann <dh.herrmann@gmail.com>
)

# https://github.com/bus1/dbus-broker/releases
sha256sums=('4f5f55ba74f1f098014c97ec753ab75a2bbe92012a7afcac108783c83f39c307'
            'SKIP')

prepare() {
  cd $pkgbase-$pkgver
}

build() {
  local meson_options=(
    -D audit=true
    -D docs=true
    -D linux-4-17=true
    -D system-console-users=gdm,sddm,lightdm,lxdm
  )

  arch-meson $pkgbase-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_dbus-broker() {
  depends+=(
    libaudit.so
    libcap-ng.so
    libexpat.so
    libsystemd.so
  )

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
