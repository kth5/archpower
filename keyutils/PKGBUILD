# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=keyutils
pkgver=1.6.3
pkgrel=3.1
pkgdesc='Linux Key Management Utilities'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
url='https://www.kernel.org/'
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'sh')
makedepends=('git')
provides=('libkeyutils.so')
backup=('etc/request-key.conf')
validpgpkeys=('A86E54B0D5E1B4E0AB7C640FFBB7576BA7CB0B6B') # David Howells <dhowells@redhat.com>
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/dhowells/keyutils.git#tag=v${pkgver}?signed"
        'request-key.conf.patch' 'reproducible.patch')
sha256sums=('fb172bd1100b07e5ea3f4ba899ca369fd25dcd0ccadee88bd162a8e431e78d26'
            '203c602c61ed94ccd423a0a453d74143d678c641a9a4486367576ee8af2cb8d6'
            '7bb7400b2b8c8f0288c86ec9191f8964a1e682745a204013d5fc7c2e1a253d8e')
sha512sums=('1d7fa66cfa46f29e2a0e9c79b69a206986e437900b99c133e0be23bf8ec8f46a23064791a011f49189cf63c0e40c9453fadca83b261208a0b1c38e0e0680f5e2'
            '2dcfa835ef13ce884cca34d0f061fe6514f07ecb7acb485812626c593e3a51e19b45f65c11ef5535fd35836135e96abed853f446098a0eaab740b793f16620dd'
            '0f0ba7c6263ad1b710d3aa6166a041246517117ddc9e5ab7fe23f19993e3f52cf145d1767cca8bf4b8b7f49e37b793022ecd54c8091e73092167073d12d88fa9')

prepare() {
  cd ${pkgname}
  # fix paths of binaries in /etc/request-key.conf
  patch -Np0 -i ../request-key.conf.patch

  # make keyutils reproducible
  patch -Np1 -i ../reproducible.patch
}

build() {
  cd ${pkgname}
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" SBINDIR='/usr/bin' BINDIR='/usr/bin'
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" SBINDIR='/usr/bin' BINDIR='/usr/bin' LIBDIR='/usr/lib' USRLIBDIR='/usr/lib' install
}
