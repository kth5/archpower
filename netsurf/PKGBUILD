# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.11
pkgrel=9
pkgdesc='Lightweight and fast web browser'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.netsurf-browser.org/'
license=(GPL-2.0-only MIT)
depends=(curl gtk3 'libcss>=0.9.2' 'libdom>=0.4.2' 'libhubbub=0.3.8'
         libjpeg-turbo libjxl 'libnsbmp>=0.1.7' 'libnsgif>=1.0.0' 'libnsutils=0.1.1'
         'libsvgtiny>=0.1.8' 'libutf8proc>=2.9.0' 'libwapcaplet>=0.4.3' libwebp)
makedepends=(check 'nsgenbind>=0.9' perl-html-parser setconf tinyxxd git)
source=("git://git.netsurf-browser.org/netsurf.git#tag=release/$pkgver"
        netsurf.sh)
sha256sums=('11ca5a741019b576731e56326a0b101c136fcdb064b553cda7d0b5ceaab09e89'
            '7fe3ce6b7c7058ae5385614848154653840b83906b090fc90c4e70455d39affc')
b2sums=('b0dbba00bcc22a207e4bc41bc0c0a9883c442f46e98e5b41b18d6f4a6646745bad6e6e6faf9a57c903947cf35b1aa47fecdd9ffe8cd48b3889293fc3c228446b'
        'c12c85c42a9f4615199e61b97cf95edaad1f2f103a1dd6815876006c7a75793bca7afc595431e6147b7a5c7e0be23f33d97cc9dfce1d7d94c35b8b43dcf627d4')

prepare() {
  cd $pkgname
  sed -i 's:libutf8proc/::;s:UTF8PROC_CCC_VIRAMA:UTF8PROC_BIDI_CLASS_EN:' utils/idna.c
  setconf frontends/gtk/res/netsurf-gtk.desktop Exec '/usr/bin/netsurf %u'
  setconf frontends/gtk/res/netsurf-gtk.desktop Icon netsurf.xpm
}

build() {
  make \
    -C $pkgname \
    HOSTNAME=archpower \
    INCLUDEDIR=include \
    LIBDIR=lib \
    NETSURF_UA_FORMAT_STRING='"Mozilla/5.0 (Arch POWER; %s) NetSurf/%d.%d"' \
    PREFIX=/usr \
    TARGET=gtk3
}

package() {
  make \
    -C $pkgname \
    DESTDIR="$pkgdir" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    TARGET=gtk3 \
    install \
    HOSTNAME=archpower

  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  cd $pkgname
  install -Dm644 frontends/gtk/res/$pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 frontends/gtk/res/$pkgname-gtk.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
