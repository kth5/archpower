# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.11
pkgrel=6
pkgdesc='Lightweight and fast web browser'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.netsurf-browser.org/'
license=(GPL-2.0-only MIT)
depends=(curl gtk3 'libcss>=0.9.2' 'libdom>=0.4.2' 'libhubbub=0.3.8'
         libjpeg-turbo libjxl 'libnsbmp>=0.1.7' 'libnsgif>=1.0.0' 'libnsutils=0.1.1'
         'libsvgtiny>=0.1.8' 'libutf8proc>=2.9.0' 'libwapcaplet>=0.4.3' libwebp)
makedepends=(check inetutils 'nsgenbind>=0.9' perl-html-parser setconf tinyxxd)
source=("netsurf-$pkgver.tar.bz2::https://git.netsurf-browser.org/netsurf.git/snapshot/netsurf-release/$pkgver.tar.bz2"
        netsurf.sh)
sha256sums=('f53e2e4a7b6c43be3fac3ea63be5d0939bfc910a309aafc521de14d4a45e91ac'
            '03550807de201de6e4bdee5c5071163c5dba72fcd1b5089bcf3a0edc808f8bb0')
b2sums=('9714735389aa752b5e11a13c3e6e4fc456589517e98fe55025725e05e38a52afea377d53703553ec9d94e492c58f7e1b5ed04c7a0c4fb5158c3053eeedfddcdc'
        'c58dd71bdbb85d8eab2503f10a3d9e9b735559d62f1d6b7d011464b7b3e75bc3a397ca2e82357691e9e355718d7451f6f5fc7420bba6c4001138ab5e6ba186a6')

prepare() {
  cd $pkgname-release/$pkgver
  sed -i 's:libutf8proc/::;s:UTF8PROC_CCC_VIRAMA:UTF8PROC_BIDI_CLASS_EN:' utils/idna.c
  setconf frontends/gtk/res/netsurf-gtk.desktop Exec '/usr/bin/netsurf %u'
  setconf frontends/gtk/res/netsurf-gtk.desktop Icon 'netsurf.xpm'
}

build() {
  CFLAGS+=' -w' make -C $pkgname-release/$pkgver INCLUDEDIR=include LIBDIR=lib \
    NETSURF_UA_FORMAT_STRING='"Mozilla/5.0 (Arch Linux; %s) NetSurf/%d.%d"' \
    PREFIX=/usr TARGET=gtk3
}

package() {
  make -C $pkgname-release/$pkgver DESTDIR="$pkgdir" INCLUDEDIR=include LIBDIR=lib PREFIX=/usr TARGET=gtk3 install
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  cd $pkgname-release/$pkgver
  install -Dm644 frontends/gtk/res/$pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 frontends/gtk/res/$pkgname-gtk.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
