# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=phodav
pkgver=3.0
pkgrel=4
pkgdesc='WebDav server implementation using libsoup (RFC 4918)'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://wiki.gnome.org/phodav'
license=(LGPL-2.1-or-later)
depends=(
  avahi
  glib2
  glibc
  libsoup3
  libxml2
  systemd
)
makedepends=(
  asciidoc
  git
  gtk-doc
  meson
  xmlto
)
backup=(etc/conf.d/spice-webdavd)
source=("git+https://gitlab.gnome.org/GNOME/phodav.git?signed#tag=v$pkgver")
b2sums=('b27ed0f583980460cd51321d38a48f5c755c67a75fe06e4e9f7b7e6b8a68a31edc89ea1197a101dff8088e30dc1c04217035b2c835911dc93dcc8ed871de9411')
validpgpkeys=(87A9BD933F87C606D276F62DDAE8E10975969CE5) # Marc-André Lureau

prepare() {
  cd $pkgname
  sed -i 's|/etc/sysconfig/spice-webdavd|/etc/conf.d/spice-webdavd|
          s|/usr/sbin/spice-webdavd|/usr/bin/spice-webdavd|' data/spice-webdavd.service
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -dm755 "$pkgdir/etc/conf.d/"
  echo 'SPICE_WEBDAVD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-webdavd"
}
