# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kevin Song <kevin.s05@gmail.com>

pkgname=starship
pkgdesc='The cross-shell prompt for astronauts'
pkgver=1.24.2
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://starship.rs/
_url="https://github.com/$pkgname/$pkgname"
license=(ISC)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cmake
  git
  rust
)
checkdepends=(python)
optdepends=('ttf-font-nerd: Nerd Font Symbols preset')
source=("git+$_url.git#tag=v$pkgver")
sha256sums=('2f29d62da7901ba2f411e8dfe70035dd2d2d3f72fc20014b3f18ebd240a52b7c')

case "${CARCH}" in
  powerpc) 
    options=(!debug !lto) 
    export RUSTFLAGS="-C codegen-units=256 -C link-arg=-Wl,--no-keep-memory"
  ;;
esac

prepare() {
  cargo fetch \
    --locked \
    --target "$(rustc --print host-tuple)" \
    --manifest-path starship/Cargo.toml
}

build() {
  export CARGO_TARGET_DIR=target
  export OPENSSL_NO_VENDOR=true
  CFLAGS+=" -ffat-lto-objects"
  cargo build \
    --release \
    --frozen \
    --manifest-path starship/Cargo.toml
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cargo test \
    --frozen \
    --manifest-path starship/Cargo.toml
}

package() {
  install -Dm 755 target/release/starship -t "${pkgdir}"/usr/bin/
  install -Dm 644 starship/LICENSE -t "${pkgdir}"/usr/share/licenses/starship/
  install -dm 755 "${pkgdir}"/usr/share/{bash-completion/completions,elvish/lib,fish/vendor_completions.d,zsh/site-functions}/
  ./target/release/starship completions bash > "${pkgdir}"/usr/share/bash-completion/completions/starship
  ./target/release/starship completions elvish > "${pkgdir}"/usr/share/elvish/lib/starship.elv
  ./target/release/starship completions fish > "${pkgdir}"/usr/share/fish/vendor_completions.d/starship.fish
  ./target/release/starship completions zsh > "${pkgdir}"/usr/share/zsh/site-functions/_starship
}

# vim: ts=2 sw=2 et:
