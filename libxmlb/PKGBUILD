# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libxmlb
pkgver=0.3.19
pkgrel=1
pkgdesc="Library to help create and query binary XML blobs"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/hughsie/libxmlb"
license=(LGPL)
depends=(glib2 libstemmer xz zstd)
makedepends=(meson gobject-introspection gtk-doc)
checkdepends=(shared-mime-info)
source=("https://github.com/hughsie/libxmlb/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('0a3ec258b12dbf10e5fe840b2421c84137eb7cc1b09c3de6210f3f7d51733733'
            'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
  arch-meson ${pkgname}-${pkgver} build -D stemmer=true
  ninja -v -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -r "${pkgdir}"/usr/{lib,share}/installed-tests/
}
