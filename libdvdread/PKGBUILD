# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libdvdread
pkgver=7.0.0
pkgrel=1
pkgdesc='Library for reading DVD video disks'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.videolan.org/developers/libdvdnav.html'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(
  git
  meson
)
optdepends=('libdvdcss: Decoding encrypted DVDs')
source=("git+https://code.videolan.org/videolan/libdvdread.git#tag=$pkgver")
b2sums=(7716077f6587747521ea0061f8c73c4504c8f41aec10c91a9780921178ca9ed29c5ee40ea2b96661fe76730ad75ba08ab66c2a3f306485c15cf57c9494890832)
validpgpkeys=(65F7C6B4206BD057A7EB73787180713BE58D1ADC) # VideoLAN Release Signing Key (2015)

prepare() {
  cd $pkgname

  # Fix out of tree build
  sed -i "s|'git', 'log'|'git', '-C', meson.project_source_root(), 'log' |" meson.build
}

build() {
  arch-meson $pkgname build \
    -D libdvdcss=disabled
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
