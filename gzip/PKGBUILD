# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gzip
pkgver=1.14
pkgrel=2
pkgdesc='GNU compression utility'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.gnu.org/software/gzip/'
license=('GPL-3.0-or-later')
depends=('glibc' 'bash' 'coreutils' 'sed' 'grep')
makedepends=('git' 'less' 'python' 'wget')
optdepends=('less: zless support'
            'util-linux: zmore support'
            'diffutils: zdiff/zcmp support')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("git+https://git.savannah.gnu.org/git/gzip.git#tag=v${pkgver}?signed"
        "git+https://git.savannah.gnu.org/git/gnulib.git")
sha256sums=('b1c2422ee156ef11e9601d2651f8543dd71f941887cd407fe23584ab95417c93'
            'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git config submodule.gnulib.url "${srcdir}/gnulib"
  git -c protocol.file.allow=always submodule update

  sh bootstrap
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --disable-gcc-warnings
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make prefix="$pkgdir/usr" install
}
