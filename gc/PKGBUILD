# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Isenmann <daniel [at] archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=gc
pkgver=8.2.10
pkgrel=2
pkgdesc="A garbage collector for C and C++"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.hboehm.info/gc/"
license=(
  'LicenseRef-GC'
  'MIT'
)
depends=(
  gcc-libs
)
makedepends=(
  git
)
source=(
  git+https://github.com/ivmai/bdwgc.git#tag=v${pkgver}
  https://www.hboehm.info/gc/license.txt
)
sha512sums=('f174b630dd205fa9cc3c9a965a14da661131c17ed87c52f2bc1479f26e5cf12d80ebfa98876611f9e4a288d2fb9eec64ccc4596cdb778aee6fe5dfb4ac26470b'
            '73b381858db41cb757be67fa00778cb177fa147c892ae1e83d7d31b489c576ffe6c7339b688c41550c896aef1b8af5bfa3e58d1bb9902cc9640a8b837498a9b2')

prepare() {
  cd bdwgc

  git revert -n 2cd0f5e56
  git revert -n 74fc05d12

  ./autogen.sh
}

build() {
  cd bdwgc
  ./configure --prefix=/usr --enable-cplusplus --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd bdwgc
  make check
}

package() {
  cd bdwgc
  make DESTDIR="${pkgdir}" install
  sed 's|GC_MALLOC 1L|gc 3|g' doc/gc.man | install -Dm644 /dev/stdin "${pkgdir}/usr/share/man/man3/gc.3"
  install -Dm644 ../license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
