# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libgeotiff
pkgver=1.7.4
pkgrel=1
pkgdesc="A TIFF based interchange format for georeferenced raster imagery"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/OSGeo/libgeotiff"
license=(LicenseRef-libgeotiff)
depends=(
  glibc
  libjpeg-turbo
  libtiff
  proj
  zlib
)
changelog=$pkgname.changelog
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c581ff502c6b1dc012fad1031d95293ea5f5ff250d9502de382aced8c7d28565')

prepare() {
  cd $pkgname-$pkgver/$pkgname
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver/$pkgname
  ./configure --prefix=/usr --with-jpeg=yes --with-zlib=yes
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver/$pkgname
  make check
}

package() {
  cd $pkgname-$pkgver/$pkgname
  make DESTDIR="$pkgdir" install
  install -vDm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
