# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lucy <lucy@luz.lu>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=pkgconf
pkgver=2.4.3
pkgrel=1
pkgdesc="Package compiler and linker metadata toolkit"
url="https://github.com/pkgconf/pkgconf"
license=(ISC)
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
depends=(
  glibc
)
makedepends=(
  git
  meson
)
provides=(
  libpkgconf.so
  pkg-config
  pkgconfig
)
conflicts=(pkg-config)
replaces=(pkg-config)
source=(
  "git+$url#tag=pkgconf-$pkgver"
  {x86_64,i686}-pc-linux-gnu.personality
  {powerpc64le,powerpc64,powerpc,riscv64}-unknown-linux-gnu.personality
)
b2sums=('54fb510c563818199f1a5161cf5a2ba0fba70b09f4288a31ece936cf42197f5ee9cf2021733a927ae778a9bb1fa3dfbfbcba898826266c01b45b3f3154699db6'
        'c04583e19149c1035cb7777a59f0fbc4988b672d8e45105e33def3d0f9054464e33a400a3e7c7e8b99b334e4fe06aaa9d7e34662e806096d4374bd6b12d803db'
        '94c8fd12b5f33611fd6dbeae03b20e72bcf2937e764766e2633b2fd4c14925860d57eee234016c81b0563a47b112ac9ef4a8b3bb9fa3fc0dd266f355bc156c58'
        '48fff6b9a018708011d2fc474502880b0847cb99294e93148a2ba763ca5fa1798d73398bd0e8d51f3e64f4c5139c3dc769e0fe41f6ae4eb8deffaf8d53b72e56'
        '20364f2006a573c11e9fc3fd00e1d579a941f8234bc9bf20f3a2965a5e0f49d1d9f8a68f2171d299df7b57c1e7bfb6b0e30198ca5b343059b935c3679887121b'
        '324b798effe69ae69fc90ff72cd8cf651856733f539e2b03fb07599ad68c014e34520de2fb3f4e7ea62d24704a074900c0e5634f0d423c428e5912902c98b561'
        'cd7e6db2a8d4c00595bcafbbc2a1314b10853bec2987a35f2e536338d2452f6585cb3d1553d1b56fa2fe8d4c05edf1563642cbe70b418fe4550174cb5929e076')

prepare() {
  cd pkgconf
}

build() {
  local meson_options=(
    -D tests=disabled  # Tests need kyua
  )

  arch-meson pkgconf build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  local p
  for p in *.personality; do
    install -Dm644 "$p" -t "$pkgdir/usr/share/pkgconfig/personality.d"
    ln -s pkgconf "$pkgdir/usr/bin/${p%.*}-pkg-config"
  done

  ln -s pkgconf "$pkgdir/usr/bin/pkg-config"
  ln -s pkgconf.1 "$pkgdir/usr/share/man/man1/pkg-config.1"

  install -Dm644 pkgconf/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
