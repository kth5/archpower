# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libvirt-glib
pkgver=5.0.0
pkgrel=1
pkgdesc="GLib bindings for libvirt"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://libvirt.org"
license=('LGPL2.1')
depends=('libvirt' 'libcap-ng')
makedepends=('meson' 'intltool' 'gobject-introspection' 'vala' 'python')
optdepends=('python: support for python')
source=("https://libvirt.org/sources/glib/$pkgname-$pkgver.tar.xz"{,'.asc'})
sha512sums=('bb09474c0d6d4bc00844e7a1ede882f0834d0810804b70b8f8607f2e764f72cc9da14883794e58dd53640433ffaecc13dd3c1c147b82e86fa2fdca6e5903933c'
            'SKIP')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF') # Daniel P. Berrange <dan@berrange.com>

build() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir build
  meson setup build --prefix=/usr --buildtype=plain
  ninja -C build
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  DESTDIR="$pkgdir/" meson install
}

# vim:set ts=2 sw=2 et:
