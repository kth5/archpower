# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Thomas Jost <schnouki@schnouki.net>
# Contributor: JaDa <jada@archlinux.us>
# Contributor: Joaquim Pedro <osmano807@gmail.com>
# Contributor: Jan Rüegg <rggjan@gmail.com>

pkgname=gperftools
pkgver=2.17
pkgrel=1
pkgdesc='Fast, multi-threaded malloc and nifty performance analysis tools'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/gperftools/gperftools'
license=(BSD-3-Clause)
depends=(
  gcc-libs
  glibc
  libunwind
)
makedepends=(
  asciidoctor
  git
)
source=("git+https://github.com/gperftools/gperftools.git#tag=$pkgname-$pkgver")
b2sums=(b0cd59121adb2b1c3d6a298759fad19ce78c610dfe0a4353e03f2d817fc97d9df2fac0ef9de1c04deedb4305a2ebc37f2b1ae4174183cd600f06216b69d40422)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-frame-pointers \
    --enable-libunwind
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING
}
