# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=webrtc-audio-processing
pkgver=2.1
pkgrel=1
pkgdesc="AudioProcessing library based on Google's implementation of WebRTC"
url="https://freedesktop.org/software/pulseaudio/webrtc-audio-processing/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(BSD-3-Clause)
depends=(
  abseil-cpp
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
)
provides=(
  libwebrtc-audio-processing-2.so
)
source=("git+https://gitlab.freedesktop.org/pulseaudio/webrtc-audio-processing.git?signed#tag=v$pkgver")
b2sums=('835d68d138f6081eee657d7d454c0c3fbb6389638da5d011020a25a20002ec6e1bc8a3fd756bdfd24c0060323cd52e99e9fece4f069852139aa2d2f274f93689')
validpgpkeys=(
  52DFA7B8BAC74687C8A88EF48165E3D1987E2132 # Arun Raghavan <arun@asymptotic.io>
)

prepare() {
  cd webrtc-audio-processing
}

build() {
  local meson_options=(
    # must match abseil
    -D cpp_std=c++17

    -D neon=disabled
  )

  arch-meson webrtc-audio-processing build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 webrtc-audio-processing/COPYING \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
