# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=webrtc-audio-processing
pkgver=0.3.1
pkgrel=2
pkgdesc="AudioProcessing library based on Google's implementation of WebRTC"
url="https://freedesktop.org/software/pulseaudio/webrtc-audio-processing/"
arch=(x86_64 powerpc64le)
license=(custom)
depends=(gcc-libs)
makedepends=(git)
_commit=e882a5442ac22c93648e12837248d651d18b9247  # tags/v0.3.1^0
source=("git+https://anongit.freedesktop.org/git/pulseaudio/webrtc-audio-processing#commit=$_commit"
        "webrtc-audio-processing-add-ppc-platform-support.patch")
sha256sums=('SKIP'
            '1f37288f4021a5cd59f915f01f8955351ca034a3bff0e1e2db49fa35fb601ab4')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ${srcdir}/webrtc-audio-processing-add-ppc-platform-support.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
