# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=xapian-core
pkgname=(xapian-core{,-docs})
pkgver=1.4.27
pkgrel=1
epoch=1
pkgdesc='Open source search engine library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://www.xapian.org/"
license=('GPL-2.0-or-later')
makedepends=('util-linux' 'zlib')
source=("https://oligarchy.co.uk/xapian/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('5a9b75dfc61782012c4c2febd95532e6705f7fa45b0765b64dbc9ee3751c88fb3420f1d7d8fdadee21a59ff4143c5197870a4aa50993490a033fce6925e1e9f1'
            'SKIP')
b2sums=('4a61490ebb53738660ef7ff9ecd60e2b53474309d63fe9b9702323a6eba6c9d96d79048f67e8356a2a7c0436ab6ff38792bf9d9776b60f820eaddc77ca5b0f32'
        'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"

  # workaround for test failure in libzim
  #   23/27 search_iterator  FAIL  0.47s  killed by signal 6 SIGABRT
  CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  ./configure --prefix=/usr
  make
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac
  make check -C $pkgname-$pkgver
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_xapian-core() {
  depends=(
    'bash'
    'gcc-libs'
    'glibc'
    'util-linux-libs' 'libuuid.so'
    'zlib' 'libz.so'
  )
  optdepends=(
    'xapian-core-docs: for documentation'
  )
  provides=('libxapian.so')
  # xapian-config requires libxapian.la
  options=('libtool')

  make DESTDIR="${pkgdir}" install -C $pkgname-$pkgver

  (
    cd "$pkgdir"
    _pick $pkgbase-docs usr/share/doc
  )

  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,HACKING,NEWS,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_xapian-core-docs() {
  pkgdesc+=' - documentation'

  mv -v $pkgname/* "$pkgdir"
}
