# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.2.3
pkgrel=4
pkgdesc="Lightweight video thumbnailer that can be used by file managers"
url="https://github.com/dirkvdb/ffmpegthumbnailer"
license=('GPL-2.0-or-later')
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
depends=('ffmpeg' 'libjpeg-turbo' 'libpng')
makedepends=('cmake')
optdepends=('gvfs: support for gio uris')
source=(https://github.com/dirkvdb/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/dirkvdb/ffmpegthumbnailer/commit/40e050673851baa59ca45c900e24d68027d9d675.patch)
sha256sums=('8c9b9057c6cc8bce9d11701af224c8139c940f734c439a595525e073b09d19b8'
            'b144efb0e8d914e1950e8a16299c532d59819768ffcb1ec152f72abf0d3a65fb')

prepare() {
  cd "$pkgname-$pkgver"

  # Build fix for ffmpeg 8
  patch -p1 -i ../40e050673851baa59ca45c900e24d68027d9d675.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GIO=ON \
    -DENABLE_THUMBNAILER=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
