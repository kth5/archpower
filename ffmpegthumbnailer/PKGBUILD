# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.2.3
pkgrel=1
pkgdesc="Lightweight video thumbnailer that can be used by file managers"
url="https://github.com/dirkvdb/ffmpegthumbnailer"
license=('GPL2')
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('ffmpeg' 'libjpeg' 'libpng')
makedepends=('cmake')
optdepends=('gvfs: support for gio uris')
source=(https://github.com/dirkvdb/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8c9b9057c6cc8bce9d11701af224c8139c940f734c439a595525e073b09d19b8')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GIO=ON \
    -DENABLE_THUMBNAILER=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
