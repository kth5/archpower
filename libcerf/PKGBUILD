# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libcerf
epoch=1
pkgver=2.4
pkgrel=1
pkgdesc='Self-contained numeric library that provides an efficient and accurate implementation of complex error functions'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://jugit.fz-juelich.de/mlz/libcerf/'
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=(https://jugit.fz-juelich.de/mlz/libcerf/-/archive/v$pkgver/libcerf-v$pkgver.tar.gz)
sha256sums=('080b30ae564c3dabe3b89264522adaf5647ec754021572bee54929697b276cdc')

build() {
  cmake -B build -S $pkgname-v$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPORTABLE=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-v$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
