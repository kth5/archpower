# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=ibus
pkgname=(ibus libibus)
pkgver=1.5.33
pkgrel=3
pkgdesc="Intelligent input bus for Linux/Unix"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/ibus/ibus/wiki"
license=('LGPL-2.1-or-later')
depends=(
    'at-spi2-core'
    'cairo'
    'dconf'
    'gdk-pixbuf2'
    'glib2'
    'graphene'
    'gtk3'
    'gtk4'
    'hicolor-icon-theme'
    'libdbusmenu-glib'
    'libdbusmenu-gtk3'
    'libnotify'
    'libx11'
    'libxkbcommon'
    'libxfixes'
    'libxi'
    'pango'
    'python'
    'python-gobject'
    'wayland'
)
makedepends=(
    'glib2-devel'
    'gobject-introspection'
    'gtk-doc'
    'qt5-base'
    'unicode-character-database'
    'unicode-cldr'
    'unicode-emoji'
    'vala'
    'wayland-protocols'
)
options=('!emptydirs')
source=("https://github.com/$pkgname/$pkgname/releases/download/$pkgver/${pkgname}-${pkgver}.tar.gz"
		'ibus-HEAD.patch')
b2sums=('9ccd0c5bf1b98309acd1af02e29f05eb46120b1918584b5c9c7dd908ef13172f07ae1465e615a941e82dec1144c492208a4e0f79e9b02eadb79028177a2a7227'
        'ad4f29730bb2bc1ce22ed39af2d8e431563093630da15d782f990bbcb76307a1de4261b9a7885848d7ef43f06aae04ca3cbbad30e10c60db1d4240a9cf1e13f8')

prepare() {
	cd "${pkgname}-$pkgver"

	# can be removed with 1.5.34 update - backport of the fix for https://github.com/ibus/ibus/issues/2825
	patch -Np1 < "${srcdir}/ibus-HEAD.patch"
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/ibus \
        --sysconfdir=/etc \
        --enable-dconf \
        --enable-wayland \
        --enable-gtk-doc \
        --disable-gtk2 \
        --enable-gtk4 \
        --disable-memconf \
        --enable-ui \
        --disable-python2 \
        --with-python=python3 \
        --with-ucd-dir=/usr/share/unicode/
    sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package_ibus() {
    depends+=("libibus=$pkgver")

    cd ibus-${pkgver}
    make DESTDIR="$pkgdir" install
    make -C src DESTDIR="$pkgdir" uninstall
    make -C src DESTDIR="$pkgdir" install-dictDATA install-unicodeDATA
    make -C bindings DESTDIR="$pkgdir" uninstall
    make DESTDIR="$pkgdir" uninstall-pkgconfigDATA
}

package_libibus() {
    pkgdesc="IBus support library"
    depends=(libg{lib,object,io}-2.0.so)
    optdepends=('python-gobject: for Python integration')
    provides=('libibus-1.0.so')

    cd ibus-${pkgver}
    make -C src DESTDIR="$pkgdir" install
    make -C src DESTDIR="$pkgdir" uninstall-dictDATA uninstall-unicodeDATA
    make -C bindings DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" install-pkgconfigDATA
}
