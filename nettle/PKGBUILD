# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=nettle
pkgver=3.10.1
pkgrel=1.1
pkgdesc="A low-level cryptographic library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
url="https://www.lysator.liu.se/~nisse/nettle"
license=('LGPL-3.0-or-later OR GPL-2.0-or-later')
depends=('glibc' 'gmp')
provides=('libnettle.so' 'libhogweed.so')
checkdepends=('valgrind')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('b0fcdd7fc0cdea6e80dcf1dd85ba794af0d5b4a57e26397eee3bc193272d9132'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static
  make
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
