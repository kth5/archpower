# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=1.91.0
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
url="https://upower.freedesktop.org"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libgudev
  libimobiledevice
  libplist
  polkit
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  python
  systemd
  usbmuxd
)
optdepends=('usbmuxd: Read charge status of iOS devices')
checkdepends=(
  python-dbus
  python-dbusmock
  python-gobject
  python-packaging
  umockdev
)
backup=(etc/UPower/UPower.conf)
source=("git+https://gitlab.freedesktop.org/upower/upower.git#tag=v$pkgver")
b2sums=('bb380be157f1b32537a275ca5e90c3283f94f9642dd51531e59b9da8593880f13af09ca685776c6ed53cb3bc3df4b45b77306f950ee74b259ec8bc857bba0d78')

prepare() {
  cd upower
}

build() {
  local meson_options=(
    -D installed_tests=false
  )

  arch-meson upower build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libupower-glib.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
