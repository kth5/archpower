# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mkinitcpio-archiso
pkgver=73
pkgrel=1
pkgdesc='Initcpio scripts used by archiso'
arch=(any)
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio-archiso'
license=(GPL-3.0-or-later)
depends=(
  bash
  device-mapper
  mkinitcpio
)
makedepends=(git)
checkdepends_x86_64=(
  shellcheck
  shfmt
)
optdepends=(
  'curl: for PXE over HTTP'
  'gnupg: for PGP signature verification of rootfs over PXE'
  'mkinitcpio-nfs-utils: for PXE over NFS'
  'nbd: for PXE over NBD'
  'openssl: for CMS signature verification of rootfs over PXE'
  'pv: for status display during copy to RAM'
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('6c2325a74bff5e049a3e61314b7e79d0ddf9d247bbe155ec01d9a3bc67019d598d4635a7d865da4ac84e908ff0558a6cbe3ac54706047b012cf104173bd920bd')
b2sums=('f4a76b524aa53f2824938675b121fd4d8d1e3c7ce6ce3b52ca8acd739169f34f7e6158e103f44f300ce1b1356ffbea16137b0cf6d156170138b3c0077abd3d7c')
validpgpkeys=(
  '991F6E3F0765CF6295888586139B09DA5BF0D338'  # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF'  # nl6720 <nl6720@archlinux.org>
)

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac
  make -k check -C $pkgname
}

package() {
  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname
}
