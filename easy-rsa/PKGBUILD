# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=easy-rsa
pkgver=3.2.1
pkgrel=3
pkgdesc='Simple shell based CA utility'
arch=(any)
url='https://github.com/OpenVPN/easy-rsa'
depends=('bc' 'openssl' 'sh' 'grep')
license=('GPL-2.0-only')
backup=('etc/easy-rsa/openssl-easyrsa.cnf'
	'etc/easy-rsa/vars')
validpgpkeys=(
	'65FF3F24AA08E882CB444C94D731D97A606FD463'	# Josh Cepek <josh.cepek@usa.net>
	'6F4056821152F03B6B24F2FCF8489F839D7367F3')	# Eric Crist <ecrist@secure-computing.net>
source=("https://github.com/OpenVPN/easy-rsa/releases/download/v${pkgver}/EasyRSA-${pkgver}.tgz"{,.sig}
        "https://raw.githubusercontent.com/OpenVPN/easy-rsa/refs/tags/v${pkgver}/dev/easyrsa-tools.lib")
sha256sums=('ec0fdca46c07afef341e0e0eeb2bf0cfe74a11322b77163e5d764d28cb4eec89'
            'SKIP'
            'eea232959d41f49330e032bf94a49b4b6b0574d75e199dd219096f1739077b3e')

package() {
	cd EasyRSA-${pkgver}/

	install -D -m0755 easyrsa "${pkgdir}"/usr/bin/easyrsa

	install -D -m0644 openssl-easyrsa.cnf "${pkgdir}"/etc/easy-rsa/openssl-easyrsa.cnf
	install -D -m0644 vars.example "${pkgdir}"/etc/easy-rsa/vars
	install -d -m0755 "${pkgdir}"/etc/easy-rsa/x509-types/
	install -m0644 x509-types/* "${pkgdir}"/etc/easy-rsa/x509-types/

	install -d -m0755  "${pkgdir}"/usr/share/doc/easy-rsa/
	install -m0644 doc/* ChangeLog "${pkgdir}"/usr/share/doc/easy-rsa/

	install -D -m0644 COPYING.md "${pkgdir}"/usr/share/licenses/easy-rsa/COPYING

	# this is missing in release tarball...
	install -D -m0755 "${srcdir}"/easyrsa-tools.lib "${pkgdir}"/usr/share/easy-rsa/easyrsa-tools.lib
}

