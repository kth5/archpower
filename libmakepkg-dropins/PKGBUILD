# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=libmakepkg-dropins
pkgver=20
pkgrel=1
pkgdesc="Features that can be dropped into libmakepkg to extend the functionality of makepkg"
url="https://gitlab.archlinux.org/pacman/libmakepkg-dropins"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  awk
  bash
  coreutils
  findutils
  grep
  sed
  binutils
)
makedepends=(
  git
)
source=("git+${url}.git#tag=${pkgver}?signed")
sha256sums=('4adb641447d96071223db79e2113448222218391d75643fa499547824cd161ac')
b2sums=('73e075a3e062791cd3a163145e05541e6a20060f770683f3d7f4c3ea865e6177348a511c7d502d1f1f387e3d1afc51b00177829cfb3d5931bbd5b0cbaa70a32c')
validpgpkeys=(
  AC07187AF9155998CF6EFFC7F8112DA85D62567C  # Libmakepkg-Dropins Signer
)

package() {
  cd "$pkgname"

  install -dm755 "$pkgdir/usr/share/makepkg"
  local dir dirs
  mapfile -t dirs < <(find . -mindepth 1 -maxdepth 1 -type d -not -name .git)
  for dir in "${dirs[@]}"; do
    cp -ra "${dir}" --target-directory="$pkgdir/usr/share/makepkg"
  done
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: set ts=2 sw=2 et:
