# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-asl
pkgver=2.0.2
pkgrel=1
pkgdesc='COIN-OR autotools harness to build AMPL Solver Library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/coin-or-tools/ThirdParty-ASL/'
license=(MIT)
groups=(coin-or)
depends=(glibc)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or-tools/ThirdParty-ASL/archive/releases/$pkgver.tar.gz
        https://coin-or-tools.github.io/ThirdParty-ASL/solvers-64919f75f.tgz)
sha256sums=('f6e57d9752db939d2f55ff6917146406217eaa908539b351c840e0ff10b30c94'
            'e212926d1d797701adc901ef18eaab6b15edd13f9281dd8c9266e3cdaf8c2dd3')

prepare() {
  cd ThirdParty-ASL-releases-$pkgver
  ln -s ../solvers .
}

build() {
  cd ThirdParty-ASL-releases-$pkgver
  export CFLAGS+=' -Wno-implicit-function-declaration'
  ./configure --prefix=/usr --srcdir="$PWD" --build=${CHOST}
  make
}
package() {
  cd ThirdParty-ASL-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
