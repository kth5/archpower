# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>

pkgname=minisign
pkgver=0.12
pkgrel=2
pkgdesc="A dead-simple tool to sign files and verify digital signatures"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/jedisct1/minisign"
license=('custom:ISC')
depends=('libsodium')
makedepends=('cmake' 'git')
source=("git+https://github.com/jedisct1/minisign.git?signed#tag=$pkgver")
sha512sums=('cd14bab2bdf384f410012b6f3a9149554fb367eafae187f11d02f4dd66b301a065e47215f95cd29150dce9b59a04d319b3b84c70bb555b60c8ebf6dd8bf25e55')
validpgpkeys=(54A2B8892CC3D6A597B92B6C210627AABA709FE1) # Frank Denis

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
