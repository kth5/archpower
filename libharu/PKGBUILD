# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Auguste <auguste@gmail.com>
# Contributor: Jose Negron <josenj.arch@mailnull.com>

pkgname=libharu
pkgver=2.4.5
pkgrel=1
pkgdesc="C library for generating PDF documents"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/libharu/libharu"
license=(custom:ZLIB)
depends=('libpng' 'zlib')
makedepends=('cmake')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('677523f927ecc925d95c91ebb1cb3d1146c2ffc86031c6fc05fc038893fd38babde2abf16683e0b76d1e2b8554c64bf2278649a0f70b08a0f187c2135fc14220')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBHPDF_STATIC=OFF
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm755 build/src/libhpdf.so "${pkgdir}"/usr/lib/libhpdf.so
  install -Dm644 ${pkgname}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
