# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Arthur Darcet <arthur.darcet at m4x.org>
# Contributor: Techlive Zheng <techlivezheng at gmail.com>

pkgname=libiconv
pkgver=1.18
pkgrel=1
pkgdesc='GNU charset conversion library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://www.gnu.org/software/libiconv/'
license=('LGPL-2.1-only' 'GPL-3.0-only')
depends=('glibc')
source=("https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
options=(!libtool)
validpgpkeys=('9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D') # Bruno Haible <bruno@clisp.org>
sha512sums=('a55eb3b7b785a78ab8918db8af541c9e11deb5ff4f89d54483287711ed797d87848ce0eafffa7ce26d9a7adb4b5a9891cb484f94bd4f51d3ce97a6a47b4c719a'
            'SKIP')
b2sums=('374e7a7299d4814d372d8e63246c11f403f9fc990be5b129d016b92a875d2f8e9697ef4ef5cbd2e7b7fc97dff427c6b8abf6a99da792fcabd6fc36af38cd4723'
        'SKIP')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/libiconv \
    --enable-extra-encodings

  # workaround for insecure rpath
  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # move references from iconv to libiconv
  mv -v "$pkgdir"/usr/include/{iconv.h,libiconv.h}
  mv -v "$pkgdir"/usr/bin/{iconv,libiconv}
  mv -v "$pkgdir"/usr/share/man/man1/{,lib}iconv.1
  mv -v "$pkgdir"/usr/share/man/man3/{,libiconv_}iconv.3
  mv -v "$pkgdir"/usr/share/man/man3/{,libiconv_}iconvctl.3
  mv -v "$pkgdir"/usr/share/man/man3/{,libiconv_}iconv_open.3
  mv -v "$pkgdir"/usr/share/man/man3/{,libiconv_}iconv_close.3
  mv -v "$pkgdir"/usr/share/man/man3/{,libiconv_}iconv_open_into.3
}
