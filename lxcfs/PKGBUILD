# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=lxcfs
pkgver=5.0.4
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man' 'meson' 'python-jinja' 'systemd')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc})
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('c2b361edc881d5ca8fa2cd3260a4999b8f87afce8d937be2160e5cf0b482c52f'
            'SKIP')

build() {
    cd "$pkgname-$pkgver"
    arch-meson ../build
    meson compile -C ../build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" meson install -C ../build
    install -dm0755 "$pkgdir"/var/lib/lxcfs
}
