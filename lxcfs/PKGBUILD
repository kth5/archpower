# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=lxcfs
pkgver=6.0.4
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'libnih' 'fuse3' 'dbus-glib')
makedepends=('help2man' 'meson' 'python-jinja' 'systemd')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"{,.asc})
validpgpkeys=('602F567663E593BCBD14F338C638974D64792D67')
sha256sums=('359046f440eb8c4cf6221939658b713b10c4b1b1145a071ed1502c324bf4a5ac'
            'SKIP')

build() {
    cd "$pkgname-$pkgver"
    arch-meson ../build
    meson compile -C ../build
}

package() {
    cd "$pkgname-$pkgver"
    DESTDIR="$pkgdir" meson install -C ../build
    install -dm0755 "$pkgdir"/var/lib/lxcfs
}
