# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Fabian Schoelzel <myfirstname.mylastname@googlemail.com>
# Contributor: funkyou <spamopfer@nickname.berlin.de>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=mlt
pkgver=7.22.0
pkgrel=1
pkgdesc='An open source multimedia framework'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.mltframework.org'
license=(GPL)
depends=(gcc-libs
         glibc
         libebur128)
optdepends=('ffmpeg: ffmpeg plugin'
            'fftw: plus plugin'
            'fontconfig: gdk plugin'
            'frei0r-plugins: for additional effects'
            'gdk-pixbuf2: gdk plugin'
            'glib2: gdk plugin'
            'jack: JACK sound output plugin'
            'ladspa: LADSPA plugins'
            'libepoxy: opengl plugin'
            'libexif: auto rotate plugin'
            'libglvnd: opengl plugin'
            'libsamplerate: libavresample plugin'
            'libvorbis: vorbis plugin'
            'libx11: SDL1 plugin'
            'libxml2: XML plugin'
            'opencv: openCV plugin'
            'qt5-base: QT5 plugins'
            'qt5-svg: Qt5 plugins'
            'qt6-base: Qt6 plugin'
            'qt6-5compat: Qt6 plugin'
            'qt6-svg: Qt6 plugin'
            'pango: gdk plugin'
            'python: python bindings'
            'rtaudio: rtaudio plugin'
            'rubberband: audio pitch plugin'
            'sdl2: SDL2 plugin'
            'sdl_image: SDL1 plugin'
            'sdl12-compat: SDL1 plugin'
            'sox: SOX (Audio Swiss Army Knife) plugin'
            'vid.stab: video stabilize plugin'
            'zlib: glaxnimate plugin')
makedepends=(cmake
             eigen
             ffmpeg
             frei0r-plugins
             gdk-pixbuf2
             imagemagick
             jack
             ladspa
             libdv
             libexif
             libsamplerate
             opencv
             pango
             python)
#FIXME: movit on powerpc64
#FIXME: rtaudio 6.0.0 breaking API
optdepends_powerpc=('movit: opengl plugin')
optdepends_powerpc64le=('movit: opengl plugin')
optdepends_riscv64=('movit: opengl plugin')
optdepends_x86_64=('movit: opengl plugin')
makedepends=(cmake ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg qt6-svg qt6-5compat
             jack libexif python swig eigen opencv rubberband gdk-pixbuf2 pango imagemagick)
makedepends_powerpc=(movit)
makedepends_powerpc64le=(movit)
makedepends_riscv64=(movit)
makedepends_x86_64=(movit)
source=(https://github.com/mltframework/mlt/releases/download/v$pkgver/mlt-$pkgver.tar.gz)
sha256sums=('7cf4d9573a061d76902c6e12a2f09f5f461e7c697635233df82a63a3fe4d6da6')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMOD_OPENCV=ON \
    -DMOD_GLAXNIMATE=ON \
    -DMOD_QT6=ON \
    -DSWIG_PYTHON=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
