# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: francois <francois.archlinux.org>

pkgname=(texlive-bin libsynctex)
pkgver=2025.2
pkgrel=3
license=(GPL-2.0-or-later)
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
makedepends=(bash
             cairo
             clisp
             ffcall
             fontconfig
             freetype2
             gcc-libs
             gd
             git
             glibc
             gmp
             graphite
             harfbuzz
             icu
             libjpeg-turbo
             libpaper
             libpng
             libsigsegv
             libunistring
             libx11
             libxaw
             libxcrypt
             libxmu
             libxpm
             libxt
             mpfr
             ncurses
             perl
             pixman
             potrace
             readline
             subversion
             t1lib
             zlib
             zziplib)
url='https://tug.org/texlive/'
options=(!lto)
_commit=8115c4d97aa0e68da1f957a56bca780449d1be59
source=(git+https://github.com/Tex-Live/texlive-source.git#commit=$_commit
        ptex-debug-print.patch
        lua-root.patch
        6bf2b2d8ad6bfc810038ba8d426cc603f88ae99a.patch
        86.patch
        dvi2tty.h.diff
        dviljk_config.h.diff
        fontfcn.h.diff
        fontforge.diff
        gregorio_bool.h.diff
        more_lua.diff
        more_ps2pk.diff
        rev74891.diff
        rev74892.diff
        seetexk_tempfile.c.diff
        t1part.h.diff
        texlive_svn_r74888.diff
        texlive_svn_r75793.diff)
sha256sums=('842fc0a45cb4d363253eefcaae1f88be7b8446def80d78cce251dad32d3802ec'
            'aa838f09003c62c2efb5770a8de66f99b409df049fbd65098d80fd1957d06c50'
            'c2d6a8b14dd8197874c1d894e70df80ad076f28ee4d1cff81e3b7811d9264fb9'
            '8b1b28ed032d2d96c579eb93951595d512bac0bb5cad486766b9ce83e036ec49'
            '30e6bc47f47a7a3d4535b5104ad72ac3a0182951e05edcb8f19a288cda71bf0d'
            '488f32562ac45423704ce5548ccdc2ecfdb8c497a9ef7e6c8d2c9c5d8ca2f218'
            'ed430cdcbaa3e576f559e2ad779d11b756a5958da370d8774dbf4cf6818d7d96'
            'f87ea5cccbd714eb10789eee175e21d49decb21938a44d1c4678706c9fbf562d'
            '3882a2936e07b13e48dfd7ddcfda763a336daa829a399d9c58edfd76efb1a28a'
            '76fadce6d728ee8284afcb6e806d9d50440ee85e19d44f6ba9c85c3a95e726e6'
            '03d72f5b5bfd76229eb98353113e259c268974c400719e04f4e90ff85b6af5a9'
            'ab5ba04213a5d6260764ce9d235835b20417d5bbd0c886441eea6839943e9db9'
            '6207a83be33452be7e4232ea729aa1170f54db1b8db0786fcb3715e5d7f442d0'
            '6f7657fa0cfc42e208393d1b58aec0e8cfc167ca796b5d37a0c90a374bcb942c'
            '274e49be5677191e4ce5c78f7636c9f3a64ae2a073cc19d353376adbfd66cc53'
            '00d6d9d0d6421a69db10be6f17b0f0c3142b37270358308219437182d6cc2d87'
            'a131864911fe33d3036dc9e97b70476b73b855efddd55e614f0e091d8c96157b'
            '7a1765be70ab14410868cc0d8383278aa52531aa5d3a1e1faf324a99611d4bf2')

prepare() {
  cd texlive-source

# bibtex-x needs kpathsea flags
  sed -i '/AC_SEARCH_LIBS/a KPSE_KPATHSEA_FLAGS' texk/bibtex-x/configure.ac
  (cd texk/bibtex-x && autoreconf)
# t4ht expects to be un /usr/share/texmf/bin/t4ht (FS#27251)
  sed -i s/SELFAUTOPARENT/TEXMFROOT/ texk/tex4htk/t4ht.c
# Fix LUA_ROOT define
  patch -p1 -i ../lua-root.patch
# remove spurious ptex "guessed encoding" print
  patch -p1 -i ../ptex-debug-print.patch


  patch -Np1 -i ${srcdir}/6bf2b2d8ad6bfc810038ba8d426cc603f88ae99a.patch
  patch -Np1 -i ${srcdir}/86.patch
  patch -Np1 -i ${srcdir}/dvi2tty.h.diff
  patch -Np1 -i ${srcdir}/dviljk_config.h.diff
  patch -Np1 -i ${srcdir}/fontfcn.h.diff
  patch -Np1 -i ${srcdir}/fontforge.diff
  patch -Np1 -i ${srcdir}/gregorio_bool.h.diff
  patch -Np1 -i ${srcdir}/more_lua.diff
  patch -Np1 -i ${srcdir}/more_ps2pk.diff
  patch -Np1 -i ${srcdir}/rev74891.diff
  patch -Np1 -i ${srcdir}/rev74892.diff
  patch -Np1 -i ${srcdir}/seetexk_tempfile.c.diff
  patch -Np1 -i ${srcdir}/t1part.h.diff
  patch -Np1 -i ${srcdir}/texlive_svn_r74888.diff
  patch -Np1 -i ${srcdir}/texlive_svn_r75793.diff
}

build() {
  cd texlive-source

  mkdir -p build
  cd build
  CFLAGS=${CFLAGS/FORTIFY_SOURCE=3/FORTIFY_SOURCE=2} # https://gitlab.archlinux.org/archlinux/packaging/packages/texlive-bin/-/issues/3 

  case "${CARCH}" in
    powerpc64)
      export CFLAGS+=' -DU_IS_BIG_ENDIAN=1'
      export CXXFLAGS+=' -DU_IS_BIG_ENDIAN=1'
    ;;
  esac

  _bigendian=no
  case "${CARCH}" in
    powerpc|powerpc64)
      _bigendian=yes
      _configure_flags=(
        --disable-luajittex
        --disable-mfluajit
        --disable-luajithbtex
        --with-system-zzipli
      )
    ;;
    *) _configure_flags=(--enable-xindy --with-system-zziplib)
 ;;
  esac

  CFLAGS+=" -Wno-incompatible-pointer-types -std=gnu17" \
  ax_cv_c_float_words_bigendian=${_bigendian} \
  ../configure --prefix=/usr -C \
    --sysconfdir=/etc \
    --datarootdir=/usr/share \
    --datadir=/usr/share \
    --mandir=/usr/share/man \
    --disable-native-texlive-build \
    --with-banner-add="/Arch POWER" \
    --disable-multiplatform \
    --disable-dialog \
    --disable-psutils \
    --disable-t1utils \
    --disable-dvisvgm \
    --disable-xz \
    --enable-shared \
    --disable-static \
    --with-system-zlib \
    --with-system-pnglib \
    --with-system-ncurses \
    --with-system-t1lib \
    --with-system-gd \
    --with-system-freetype2 \
    --with-system-pixman \
    --with-system-cairo \
    --with-system-harfbuzz \
    --with-system-graphite \
    --with-system-icu \
    --with-system-gmp \
    --with-system-mpfr \
    --with-system-potrace \
    --with-system-libpaper \
    --with-freetype2-libdir=/usr/lib \
    --with-freetype2-include=/usr/include/freetype2 \
    --with-xdvi-x-toolkit=xaw \
    --disable-dump-share \
    --with-clisp-runtime=default \
    --disable-xindy-rules \
    --disable-xindy-docs ${_configure_flags[@]}
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_libsynctex() {
  pkgdesc='Library for synchronization between TeX files and resulting file'
  depends=(glibc zlib)
  provides=(libsynctex.so)

  cd texlive-source/build
  make -C texk/web2c DESTDIR="$pkgdir" \
    install-data-am install-libLTLIBRARIES

# remove docs included in texlive-doc
  rm -r "$pkgdir"/usr/share/man
}

package_texlive-bin() {
  pkgdesc='TeX Live binaries'
  depends=(bash
           cairo
           ffcall
           fontconfig
           freetype2
           gcc-libs
           gd
           glibc
           gmp
           graphite
           harfbuzz
           icu
           libpaper
           libpng
           libsigsegv
           libsynctex
           libunistring
           libx11
           libxaw
           libxcrypt
           libxmu
           libxpm
           libxt
           mpfr
           ncurses
           perl
           pixman
           potrace
           readline
           zlib
           zziplib)
  provides=(lcdf-typetools
            libptexenc.so
            libtexlua53.so
            libtexluajit.so
            kpathsea
            xindy)
  optdepends=('psutils: to manipulate the output of dvips')

  cd texlive-source
# fixes for xindy
  find utils/xindy -name Makefile -exec sed -i -e "s|^prefix =.\+$|prefix = $pkgdir/usr|" -e "s|^mandir =.\+$|mandir = \${prefix}/share/man|" -e "s|^datadir =.\+$|datadir = \${datarootdir}/texmf|" -e "s|^docdir =.\+$|docdir = \${datadir}/doc/xindy|" '{}' \;

  cd build
  make DESTDIR="$pkgdir" texmf="$pkgdir"/usr/share/texmf install
  LD_PRELOAD="$pkgdir"/usr/lib/libkpathsea.so.6 \
  make DESTDIR="$pkgdir" texlinks

# remove stuff included in texlive-texmf
  rm -r "$pkgdir"/usr/share/texmf-dist

# remove docs included in texlive-doc
  rm -r "$pkgdir"/usr/share/{info,man}

# remove links to scripts
  for _link in $(ls "$pkgdir"/usr/bin); do
    [[ $(readlink -m "$pkgdir"/usr/bin/$_link) == */scripts/* ]] && _rmlinks+="$pkgdir/usr/bin/$_link "
  done
  rm $_rmlinks

# remove libsynctex
  rm "$pkgdir"/usr/include/synctex/*
  rm "$pkgdir"/usr/lib/libsynctex.*
  rm "$pkgdir"/usr/lib/pkgconfig/synctex.pc
}
