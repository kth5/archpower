# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor:  <mathieu.clabaut@gmail.com>

pkgname=xdg-user-dirs
pkgver=0.19
pkgrel=2
pkgdesc="Manage user directories like ~/Desktop and ~/Music"
url="https://www.freedesktop.org/wiki/Software/xdg-user-dirs"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later)
depends=(
  glibc
  sh
)
makedepends=(
  docbook-xsl
  git
  meson
  systemd
)
backup=(etc/xdg/user-dirs.{conf,defaults})
install=xdg-user-dirs.install
source=("git+https://gitlab.freedesktop.org/xdg/xdg-user-dirs.git#tag=v$pkgver")
b2sums=('4da6b371454d37dae6f273ea90ff2b9f528b7dfbc87433a13d38aa286ac43bc93dbc06e0a3e96a5649f99979f96d7f2d04a551f0d67258d2fc825803203ea7a4')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
