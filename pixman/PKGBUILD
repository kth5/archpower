# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=pixman
pkgver=0.44.2
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://cgit.freedesktop.org/pixman/"
license=('MIT')
depends=('glibc')
makedepends=('meson' 'libpng')
provides=('libpixman-1.so')
source=(https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz)
sha512sums=('b24907f9a0bf6a9aa3278d31986c562c5f2d68f8c178021497c8648aca9f91e4c6f1ecfacf93eb01ba4f03ce4cfc5970fc40bb0fe6724bac9210fc57da09cba4')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  case "${CARCH}" in
    powerpc*) meson_opts=(-Dmmx=disabled -Dsse2=disabled -Dssse3=disabled -D vmx=enabled) ;;
    riscv64)  meson_opts=(-Dmmx=disabled -Dsse2=disabled -Dssse3=disabled -Dvmx=disabled) ;;
    x86_64)   meson_opts=(-Dvmx=disabled) ;;
  esac

  arch-meson $pkgbase-$pkgver build \
    -D loongson-mmi=disabled \
    -D arm-simd=disabled \
    -D a64-neon=disabled \
    -D neon=disabled \
    -D mips-dspr2=disabled \
    -D rvv=disabled \
    -D gtk=disabled ${meson_opts[@]}
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}
