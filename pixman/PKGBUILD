# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=pixman
pkgver=0.46.0
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://cgit.freedesktop.org/pixman/"
license=('MIT')
depends=('glibc')
makedepends=('meson' 'libpng')
provides=('libpixman-1.so')
source=(https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz)
sha512sums=('c7ef987b7db89c58278c537f862bc9eb17c19f8288a3b7e88f8501a0486662f1e2cb6a2c0b81d4bf0464310475192428d6e29d05a6ae3cfc1d2c0941fbb0b9a3')
#validpgpkeys=('') # Maarten Lankhorst <maarten.lankhorst@linux.intel.com>

build() {
  case "${CARCH}" in
    powerpc*) meson_opts=(-Dmmx=disabled -Dsse2=disabled -Dssse3=disabled -D vmx=enabled) ;;
    riscv64)  meson_opts=(-Dmmx=disabled -Dsse2=disabled -Dssse3=disabled -Dvmx=disabled) ;;
    x86_64)   meson_opts=(-Dvmx=disabled) ;;
  esac

  arch-meson $pkgbase-$pkgver build \
    -D loongson-mmi=disabled \
    -D arm-simd=disabled \
    -D a64-neon=disabled \
    -D neon=disabled \
    -D mips-dspr2=disabled \
    -D rvv=disabled \
    -D gtk=disabled ${meson_opts[@]}
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}
