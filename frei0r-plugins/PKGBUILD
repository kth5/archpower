# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=2.5.1
pkgrel=2
pkgdesc='Collection of video effect plugins'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://frei0r.dyne.org/'
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cairo
  cmake
  gavl
  git
  ninja
  opencv
)
optdepends=(
  'cairo: cairoaffineblend, cairoblend, cairogradient, cairoimagegrid and mirr0r plugins'
  'gavl: rgbparade, scale0tilt and vectorscope plugins'
  'opencv: facebl0r and facedetect plugins'
)
source=("git+https://github.com/dyne/frei0r#tag=v$pkgver")
b2sums=(8059c804a28f3a7c56e8171b0e0d296f9d8f89ab6f12cd65e60c293cf6da96d9d499306b7a93fbf328d2af0e8d71c8cacd9787a8b395ededd7d40b57a6a3c595)
validpgpkeys=(6113D89CA825C5CEDD02C87273B35DA54ACB7D10) # Denis Roio (Jaromil)

build() {
  cmake -S frei0r -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
