# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=2.3.1
pkgrel=1
pkgdesc='Collection of video effect plugins'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://frei0r.dyne.org/'
license=('GPL')
depends=('gcc-libs' 'glibc')
makedepends=('cairo' 'cmake' 'gavl' 'ninja' 'opencv')
optdepends=('cairo: cairoaffineblend, cairoblend, cairogradient and cairoimagegrid plugins'
            'gavl: rgbparade, scale0tilt and vectorscope plugins'
            'opencv: facebl0r and facedetect plugins')
source=("https://github.com/dyne/frei0r/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
validpgpkeys=('6113D89CA825C5CEDD02C87273B35DA54ACB7D10') # Denis Roio (Jaromil)
sha256sums=('dd6dbe49ba743421d8ced07781ca09c2ac62522beec16abf1750ef6fe859ddc9')

build() {
  cmake -S frei0r-$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
