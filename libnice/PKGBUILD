# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: William DÃ­az <wdiaz@archlinux.us>

pkgname=libnice
pkgver=0.1.22
pkgrel=1
pkgdesc="An implementation of the IETF's draft ICE (for p2p UDP data streams)"
url="https://nice.freedesktop.org"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MPL-1.1 OR LGPL-2.1-only')
depends=(
  glib2
  gnutls
  gupnp-igd
)
makedepends=(
  git
  gobject-introspection
  graphviz
  gstreamer
  gtk-doc
  meson
)
optdepends=('gstreamer: "nice" GStreamer plugin')
provides=(libnice.so)
options=(!ccache)
_commit=ae3eb16fd7d1237353bf64e899c612b8a63bca8a  # tags/0.1.22^0
source=("git+https://gitlab.freedesktop.org/libnice/libnice.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd libnice
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libnice

  # GUPnP-IGD 1.6
  sed -i 's/gupnp-igd-1\.0/gupnp-igd-1.6/' meson.build
}

build() {
  arch-meson libnice build -D gtk_doc=enabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
