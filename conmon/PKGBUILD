# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=conmon
pkgver=2.1.9
pkgrel=2
epoch=1
pkgdesc='OCI container runtime monitor'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/containers/conmon'
license=(APACHE)
depends=(glibc glib2 systemd)
makedepends=(git)
_commit=3a9715d28cb4cf0e671dfbc4211d4458534db189	#refs/tags/v2.1.9
source=(
  "git+https://github.com/containers/conmon#commit=${_commit}"
  https://patch-diff.githubusercontent.com/raw/containers/conmon/pull/476.patch
)
b2sums=('SKIP'
        '4fd862baa511162c4adcc70bc927686cdfe237603185a1618d0d2d0606b4564ade65f4bc89f18299f118c8c613143cf4c8e54f14b4fc13c685cb9a7c96e259c3')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 < ../476.patch
}

build() {
  cd conmon
  make PREFIX=/usr LIBEXECDIR=/usr/lib DESTDIR="$pkgdir"
}

package() {
  install -Dm755 conmon/bin/conmon "$pkgdir/usr/bin/conmon"
}
