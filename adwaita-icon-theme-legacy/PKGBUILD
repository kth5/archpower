# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=adwaita-icon-theme-legacy
pkgver=46.2
pkgrel=1
pkgdesc="GNOME fallback icons for legacy apps"
url="https://gitlab.gnome.org/GNOME/adwaita-icon-theme-legacy"
arch=(any)
license=("CC-BY-SA-3.0 OR LGPL-3.0-only")
depends=(hicolor-icon-theme)
makedepends=(
  git
  gtk-update-icon-cache
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/adwaita-icon-theme-legacy.git#tag=${pkgver/[a-z]/.&}")
b2sums=('0b36191ec591db2cd6d2d1943a0139cc95e7ac7ba2ad1828a8d104544d264d58eaac9acf8c1c6c6cc4a648bd302711d8aba7ea33b3eee9bf3e3d5a0751782b25')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Covered by common licenses
  rm -r "$pkgdir/usr/share/licenses"
}

# vim:set sw=2 sts=-1 et:
