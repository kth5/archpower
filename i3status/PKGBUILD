# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Ingo Bürk <ingo.buerk@airblader.de>

pkgname=i3status
pkgver=2.15
pkgrel=1
pkgdesc='Generates status bar to use with i3bar, dzen2 or xmobar'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://i3wm.org/i3status/'
license=('BSD')
groups=('i3')
depends=('alsa-lib' 'confuse' 'libnl' 'libpulse' 'yajl')
makedepends=('asciidoc' 'git' 'meson' 'xmlto')
backup=('etc/i3status.conf')
options=('docs')
source=("git+https://github.com/i3/i3status.git?signed#tag=${pkgver}")
sha256sums=('e37cbaa1f2d5b7f98e31709414f9ff67307e9f56d6056c7fe01edb0c82772ccd')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

build() {
  cd $pkgname/meson
  arch-meson -Dmans=true .. build
  meson compile -C build
}

package() {
  cd $pkgname/meson
  meson install -C build --destdir "$pkgdir"
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname ../LICENSE
}

# vim:set ts=2 sw=2 et:
