# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=mesa-revyos
pkgdesc="RevyOS patched mesa for riscv PowerVR"
pkgver=21.2.1
pkgrel=2
arch=(riscv64)
makedepends=('python-mako' 'libxml2' 'libx11' 'xorgproto' 'libdrm' 'libxshmfence' 'libxxf86vm'
             'libxdamage' 'wayland' 'wayland-protocols' 'zstd' 'elfutils' 'glslang'
             'libglvnd' 'libunwind' 'libxrandr' 'cmake' 'meson' 'llvm14' 'git')
url="https://www.mesa3d.org/"
license=('custom')
_commit="aa33b87be4483997c3f584dce5c4288d22c96be9"
source=("${pkgname}::git+https://github.com/revyos/mesa.git#commit=${_commit}"
        LICENSE)
sha512sums=('e42856463d4981b3f6b0dc825726b540ad7311a52a0cb0c06b665b83eb81ce615f3c1f05778144fc9e621de4f66a9eb77049d1f0c782cce1bca272d66e2a6720'
            'f9f0d0ccf166fe6cb684478b6f1e1ab1f2850431c06aa041738563eb1808a004e52cdec823c103c9e180f03ffc083e95974d291353f0220fe52ae6d4897fecc7')
#validpgpkeys=('57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>
validpgpkeys=('71C4B75620BC75708B4BDB254C95FAAB3EB073EC') # Dylan Baker <dylan@pnwbakers.com>

prepare() {
  cd ${pkgname}
  grep -v '^#' ../debian/patches/series | sed '/^\s*$/d' >series.stripped
  for patch in $(cat series.stripped); do patch -Np1 -i ../debian/patches/$patch; done
}

build() {
  arch-meson ${pkgname} build \
    -D b_lto=false \
    -D b_ndebug=true \
    -D gallium-drivers=etnaviv,nouveau,r300,r600,swrast,zink \
    -D vulkan-drivers=amd,pvr \
    -D dri-drivers=pvr \
    -D dri3=enabled \
    -D egl=enabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=true \
    -D glx=dri \
    -D libunwind=enabled \
    -D llvm=enabled \
    -D lmsensors=disabled \
    -D osmesa=true \
    -D shared-glapi=enabled \
    -D microsoft-clc=disabled \
    -D platforms=x11,wayland \
    -D valgrind=disabled \
    -D glx-direct=true \
    -D vulkan-layers=device-select,overlay \
    -D gallium-xvmc=disabled \
    -D gallium-omx=disabled

  # Print config
  meson configure build

  ninja -C build
  meson compile -C build
}

package() {
  depends=('libglvnd' 'libdrm' 'zstd' 'wayland' 'libxshmfence' 'libxxf86vm' 'libxfixes' 'llvm14-libs')
  conflicts=('mesa')
  provides=("mesa=${pkgver}" "opengl-driver")

  DESTDIR="${pkgdir}" meson install -C build

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
