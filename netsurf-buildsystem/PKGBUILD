# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=netsurf-buildsystem
pkgver=1.10
pkgrel=1
pkgdesc='NetSurf build system'
arch=(any)
url='https://source.netsurf-browser.org/buildsystem.git'
license=(MIT)
source=("$pkgname-$pkgver.tar.bz2::https://source.netsurf-browser.org/buildsystem.git/snapshot/buildsystem-release/$pkgver.tar.bz2")
b2sums=('a79a83c54f60c750693ab9cea9c80d4f63e2fe25813887c5b9e0a38593ea286883b6bda541656cfd171ae7dfc7e980c9cd70b6319a616a21a6c29f81287eb1f3')

build() {
  cd "buildsystem-release/$pkgver"
  sed -i 's:-Werror::' Makefile
  make PREFIX=/usr COMPONENT_TYPE=lib-shared
  make PREFIX=/usr COMPONENT_TYPE=lib-static
}

package() {
  cd "buildsystem-release/$pkgver"
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE=lib-shared
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE=lib-static
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
