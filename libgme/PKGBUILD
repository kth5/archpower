# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=libgme
pkgver=0.6.4
pkgrel=1
pkgdesc='Video game music file emulation/playback library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://github.com/libgme/game-music-emu'
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
  zlib
)
makedepends=(
  cmake
  git
  ninja
  sdl2
)
source=("git+https://github.com/libgme/game-music-emu.git#tag=$pkgver")
b2sums=(32a0582bd197a7a7f11fb0745d34545745dd1901d9a39b0e1abc0b24a3d9ab3b59dc6e228dc7e7eea9494474ded1beadce172121518bba3bf422820c74cdd84e)
validpgpkeys=(5406ECE83665DA9D201D35720BAF0C9C7B6AE9F2) # Michael Pyne <mpyne@kde.org>

build() {
  cmake -S game-music-emu -B build -G Ninja \
    -D CMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
