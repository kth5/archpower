# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=chrpath
pkgver=0.18
pkgrel=1
pkgdesc="Change or delete the rpath or runpath in ELF files"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://codeberg.org/pere/chrpath"
license=('GPL2')
depends=('glibc')
makedepends=('git')
source=("git+https://codeberg.org/pere/chrpath.git#tag=release-$pkgver")
sha256sums=('5c304cace5b15e820eeea341d8a98d24fca8593d440b2ffe1b41c6ad08002d4a')

prepare() {
  cd "${srcdir}"/$pkgname
  sh bootstrap
}

build() {
  cd "${srcdir}"/$pkgname
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}"/$pkgname
  make DESTDIR="${pkgdir}" docdir=/usr/share/doc/chrpath install
}
