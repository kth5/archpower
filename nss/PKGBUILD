# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=nss
pkgname=(
  nss
  ca-certificates-mozilla
)
pkgver=3.115.1
pkgrel=1
pkgdesc="Network Security Services"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(MPL-2.0)
depends=(
  glibc
  nspr
  p11-kit
  sh
  sqlite
  zlib
)
makedepends=(
  gyp
  mercurial
  perl
  python
)
source=(
  "hg+https://hg.mozilla.org/projects/nss#tag=NSS_${pkgver//./_}_RTM"
  bundle.sh
  certdata2pem.py
  hotfix-ppc-not-power8.diff
)
b2sums=('ff9033b6a3cd6b35b8ffbea634e714ca6d1854544ca33806463ecd1bb496ccfc2d3d9aa3d9ace04ff6daa2b7fe9bba4bea2efe08435c395440942a88011b7577'
        '4be5dd836c844fdd7b63302a6994d62149082c3bc81eef70f373f416fed80a61a923960e4390d1c391b81ab01b409370d788818a30ffdd3a4ed467b670f990f6'
        '6bb59dcc9289916dcbf8fb6d73db0c0cd7582dc12a3aa4e8be19ec62c9ede65fdd9470a2d92ec5a114506b78d2d21b8ae0a1b45a17dc1f90f7d75434a93da510'
        '39fde7f9d3d0c2b143acb2c51b644df03e72e9e3e3f032db9737ab7c8d70bebbc911579575e76df2c78c336f9550ea70f7351a29e3d0c476999f1536575a3aab')

prepare() {
  mkdir -p certs
  ln -srft certs nss/lib/ckfw/builtins/{certdata.txt,nssckbi.h}

  cd ${srcdir}/nss
  case "${CARCH}" in
    powerpc64)
      patch -Np1 -i ${srcdir}/hotfix-ppc-not-power8.diff
    ;;
  esac

}

build() {
  local buildsh_options=(
    --disable-tests
    --enable-libpkix
    --opt
    --system-nspr
    --system-sqlite
    --target x64
  )

  case "${CARCH}" in
    powerpc64le) buildsh_options+=(--target ppc64le ) ;;
    powerpc64) buildsh_options+=(--target ppc64
                                 -Ddisable_vsx) ;;
    powerpc) buildsh_options+=(--target ppc 
                               -Ddisable_vsx) ;;
    x86_64) buildsh_options+=(--target x64) ;;
  esac

  cd certs
  ../certdata2pem.py

  cd ..
  ./bundle.sh

  cd nss
  ./build.sh "${buildsh_options[@]}"
}

package_nss() {
  local nsprver="$(pkg-config --modversion nspr)"
  local libdir=/usr/lib includedir=/usr/include/nss

  sed nss/pkg/pkg-config/nss.pc.in \
    -e "s,%prefix%,/usr,g" \
    -e "s,%exec_prefix%,\${prefix},g" \
    -e "s,%libdir%,$libdir,g" \
    -e "s,%includedir%,$includedir,g" \
    -e "s,%NSPR_VERSION%,$nsprver,g" \
    -e "s,%NSS_VERSION%,$pkgver,g" |
    install -Dm644 /dev/stdin "$pkgdir$libdir/pkgconfig/nss.pc"

  ln -s nss.pc "$pkgdir$libdir/pkgconfig/mozilla-nss.pc"

  install -Dt "$pkgdir$libdir" dist/Release/lib/*.so

  local vmajor vminor vpatch
  { read vmajor; read vminor; read vpatch; } \
    < <(awk '/#define.*NSS_V(MAJOR|MINOR|PATCH)/ {print $3}' nss/lib/nss/nss.h)

  sed nss/pkg/pkg-config/nss-config.in \
    -e "s,@prefix@,/usr,g" \
    -e "s,@exec_prefix@,/usr,g" \
    -e "s,@libdir@,$libdir,g" \
    -e "s,@includedir@,$includedir,g" \
    -e "s,@MOD_MAJOR_VERSION@,$vmajor,g" \
    -e "s,@MOD_MINOR_VERSION@,$vminor,g" \
    -e "s,@MOD_PATCH_VERSION@,$vpatch,g" |
    install -D /dev/stdin "$pkgdir/usr/bin/nss-config"

  install -Dt "$pkgdir/usr/bin" \
    dist/Release/bin/{*util,shlibsign,signtool,signver,ssltap}

  install -Dt "$pkgdir$includedir" -m644 dist/public/nss/*.h

  install -Dt "$pkgdir/usr/share/man/man1" -m644 \
    nss/doc/nroff/{*util,signtool,signver,ssltap}.1

  # Replace built-in trust with p11-kit connection
  ln -sf pkcs11/p11-kit-trust.so "$pkgdir$libdir/libnssckbi.so"
}

package_ca-certificates-mozilla() {
  pkgdesc="Mozilla's set of trusted CA certificates"
  depends=('ca-certificates-utils>=20181109-3')

  install -Dm644 ca-bundle.trust.p11-kit \
    "$pkgdir/usr/share/ca-certificates/trust-source/mozilla.trust.p11-kit"
}

# vim:set sw=2 sts=-1 et:
