# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributors:
#   Andrei Antoukh - niwi@niwi.be - https://www.niwi.be
#   henning mueller <henning@orgizm.net>

pkgname=glusterfs
epoch=1
pkgver=11.2
_major=${pkgver%%.*}
pkgrel=2
pkgdesc='a cluster file-system capable of scaling to several peta-bytes.'
arch=(x86_64 powerpc64le powerpc64)
url='https://www.gluster.org/'
license=(GPL2 LGPL3)
install=glusterfs.install
backup=('etc/glusterfs/glusterd.vol'
        'etc/glusterfs/eventsconfig.json'
        'etc/glusterfs/gluster-rsyslog-5.8.conf'
        'etc/glusterfs/gluster-rsyslog-7.2.conf'
        'etc/glusterfs/glusterd.vol'
        'etc/glusterfs/glusterfs-georep-logrotate'
        'etc/glusterfs/glusterfs-logrotate')
depends=(fuse3 python libxml2 libaio attr rpcbind liburcu liburing gperftools)
makedepends=(rpcsvc-proto)
optdepends=('glib2: qemu-block'
            'python-prettytable: gluster-georep-sshkey')
#source=(https://download.gluster.org/pub/gluster/glusterfs/${_major}/$pkgver/glusterfs-$pkgver.tar.gz
source=(glusterfs-$pkgver.tar.gz::https://github.com/gluster/glusterfs/archive/refs/tags/v$pkgver.tar.gz
        glusterfs.sysusers
        glusterfs-11.0-extras-defer-invoking-of-gluster-volume-set-help-as-.patch
        glusterfs-11.2-sys-types.h-for-off64_t.patch
        glusterfs-11.2-remove-use-systemd-guards-around-several.diff)
sha256sums=('540683ab1acdc95c7fe940061fb24464e1d3d0955a8610d79376911b73ed4ce4'
            'dd5422c42d977348e0054328d5672bbb6624d856946f9be9c656449132da81ee'
            'cb142caab62c46f5b23a1b9e3aa00513cc4d5f2ed579369e699db8548070705f'
            '94fd8e698716a4b89dca68e0c07c19faa67b39eddec363bfbf2c6eec4631e1d8'
            '34ada7435dd35ef2cf072a826b62efae2160b53523679a8a203d510865e44a6e')

options=(!lto)

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -Np1 -i ${srcdir}/glusterfs-11.0-extras-defer-invoking-of-gluster-volume-set-help-as-.patch
  patch -Np1 -i ${srcdir}/glusterfs-11.2-sys-types.h-for-off64_t.patch
  patch -Np1 -i ${srcdir}/glusterfs-11.2-remove-use-systemd-guards-around-several.diff

  autoreconf -fi
}

build() {
  case "${CARCH}" in
    powerpc64*) _configure_flags=(--disable-gnfs --disable-lto) ;;
    x86_64)   _configure_flags=(--enable-gnfs) ;;
  esac

  cd "$srcdir"/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-mountutildir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --libexecdir=/usr/lib/$pkgname \
    --with-systemddir=/usr/lib/systemd/system \
    --with-tmpfilesdir=/usr/lib/tmpfiles.d ${_configure_flags[@]} \
    LEXLIB=
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make -j1 DESTDIR="$pkgdir" install

  # https://bugzilla.redhat.com/show_bug.cgi?id=1598900
  install -Dm644 "$srcdir"/glusterfs.sysusers \
    "$pkgdir"/usr/lib/sysusers.d/glusterfs.conf

  install -D -m 644 \
    "$srcdir"/$pkgname-$pkgver/{README.md,INSTALL,COPYING*} \
    "$pkgdir"/usr/share/doc/glusterfs/

  cp -rf \
    "$srcdir"/$pkgname-$pkgver/doc/* \
    "$pkgdir"/usr/share/doc/glusterfs/
  rm -rf "$pkgdir"/var/run
}

# vim:set ts=2 sw=2 et:
