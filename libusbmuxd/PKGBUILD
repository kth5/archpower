# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libusbmuxd
pkgver=2.1.1
pkgrel=1
pkgdesc='Client library to multiplex connections from and to iOS devices'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://libimobiledevice.org/'
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  gcc-libs
  glibc
  libimobiledevice-glue
  libplist
)
makedepends=(git)
source=("git+https://github.com/libimobiledevice/libusbmuxd.git#tag=$pkgver")
b2sums=('5f9be6c160946253442ba824065e48fd59b8a7cef5a9d54590e86dddf971389115efcce896a55fe49db8ff28663c5dfdc5a5d6b6402d2f6785e6cc46268ca0e8')

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
