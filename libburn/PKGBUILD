# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libburn
pkgver=1.5.6
pkgrel=2
pkgdesc='Library for reading, mastering and writing optical discs'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso) 
url='https://dev.lovelyhq.com/libburnia/libburn'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(git)
options=(!emptydirs)
source=("$pkgname::git+$url#tag=release-$pkgver")
sha512sums=('f4c8bcfd0e7594291a3733a87fca6cf6a8d722da7061c0e3460cdecbeffa8f396645de3184b4710ba4ac4951ef7af5eb92e2220d5df96e035cdbae4fe51bf760')
b2sums=('7b735851f2020e9c66208fb4e9ce09cc367d46d2be305e1ce44b47139fa919c4c1786590609ffa53e6561570bcced05d02b41ee5ef79bbcecfe216db9166eee9')

prepare() {
  cd "$pkgname"

  # FTBFS C23+
  git cherry-pick --no-commit d537f9dd35282df834a311ead5f113af67d223b3

  autoreconf -vfi
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --disable-static

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}
