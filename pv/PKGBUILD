# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=pv
pkgver=1.9.44
pkgrel=1
pkgdesc='A terminal-based tool for monitoring the progress of data through a pipeline'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.ivarch.com/programs/$pkgname.shtml"
# _url="https://codeberg.org/a-j-wood/$pkgname"
# _url="https://github.com/a-j-wood/$pkgname"
license=('GPL-3.0-or-later')
depends=(glibc)
_archive="$pkgname-$pkgver"
source=("${url%/*}/sources/$_archive.tar.gz"
        "$_archive.tar.gz.sig::${url%/*}/sources/$_archive.tar.gz.txt")
# source=("$_url/releases/download/v$pkgver/$_archive.tar.gz")
sha256sums=('e130c9e18b1e6e9e2ef95bec6117c72cb9be27a1b8ffe97fca787e4c8e014562'
            'SKIP')
validpgpkeys=(D8FF44A79CC8A61EF694FA7EB883E01314DA8E84  # https://ivarch.com/public-key.txt
              2189D5C29F041F2277AFD50AC1596BA33FC56F51  # andrew.wood@ivarch.com
              4267B4F90F2678A112169BD61D1EF7581B45E9A0) # https://codeberg.org/a-j-wood.gpg

build() {
	cd "$_archive"
	./configure --prefix=/usr
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" docs/COPYING
}
