# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.17.1.1
pkgrel=1
pkgdesc='Advanced feature-rich text-mode web browser'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/rkd77/elinks'
license=('GPL-2.0-only')
depends=(
  'glibc'
  'brotli'
  'bzip2'
  'gpm'
  'openssl'
  'lua'
  'libdom'
  'libcss'
  'libidn2'
  'xz'
  'libwapcaplet'
  'expat'
  'zlib'
  'curl'
  'tre'
)
makedepends=(
  'git'
  'meson'
  'dblatex'
  'xmlto'
  'libiconv'
  'gc'
  'cmake'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('d31e4561d0397dfe1ad3993d4ef3ad3fd5a7a3fb704cd1b1624d50639bf6dd16d490135bbd6e54b3d570698db2e6b3b1e262f2838ff234e050ad8f3a79948797')
b2sums=('e7095511f01c397910b63ba29711160d030fa7bbfab7780404df3da942ef376fd529edc3bf63925842ee0d31d0ddee929138340a88d7bed4ee6ab27195ca9288')

build() {
  arch-meson \
    "$pkgname" \
    build \
    -D cgi=true \
    -D true-color=true \
    -D html-highlight=true \
    -D bzlib=true \
    -D brotli=true \
    -D lzma=true \
    -D luapkg=lua54 \
    -D tre=true \
    -D gemini=true \
    -D reproducible=true \
    -D source-date-epoch="$SOURCE_DATE_EPOCH"

  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
