# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.18.0
pkgrel=1
pkgdesc='Advanced feature-rich text-mode web browser'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/rkd77/elinks'
license=('GPL-2.0-only')
depends=(
  'glibc'
  'brotli'
  'bzip2'
  'gpm'
  'openssl'
  'lua'
  'libdom'
  'libcss'
  'libidn2'
  'xz'
  'libwapcaplet'
  'expat'
  'zlib'
  'curl'
  'tre'
)
makedepends=(
  'git'
  'meson'
  'dblatex'
  'xmlto'
  'libiconv'
  'gc'
  'cmake'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('989d9ab51d29d1a093703ca67b4e5eca095f0b3438a03f5bf752b18b1d9f3bf11470ec3e3307138e04cfe9a59d9f4b81a71c6505463ba3969c3afc0c0660ce1b')
b2sums=('2208f4caef4b003faaa33d9c0a65a62eb203c8eec4049d8ef5413ec820673b6da7622d92e673d5211adfc4ea55cf0321ee409f73e32648a3d82ef7e55ad2b44c')

build() {
  arch-meson \
    "$pkgname" \
    build \
    -D cgi=true \
    -D true-color=true \
    -D html-highlight=true \
    -D bzlib=true \
    -D brotli=true \
    -D lzma=true \
    -D luapkg=lua54 \
    -D tre=true \
    -D gemini=true \
    -D reproducible=true \
    -D source-date-epoch="$SOURCE_DATE_EPOCH"

  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
