# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.19.0
pkgrel=1
pkgdesc='Advanced feature-rich text-mode web browser'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/rkd77/elinks'
license=(GPL-2.0-only)
depends=(
  glibc
  brotli
  bzip2
  gpm
  openssl
  lua54
  libdom
  libcss
  libidn2
  xz
  libwapcaplet
  expat
  zlib
  curl
  tre
)
makedepends=(
  git
  meson
  dblatex
  xmlto
  libiconv
  gc
  cmake
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('bf769107abf64e31efe858a67d33b7d8d81ccca29f84232323f5c18a32b8eb018a4f2cac8d93713c48aea4b372110012699c64999ddee8de0ff628b0291ea862')
b2sums=('05b46be267e7884f9db5dc685b40416189aa51359c992e4cd29ee28641f9eb31ab51470b69fd7f0dec91a68d9b8a923764cbd9b8e6458a06332d2802277f9dfb')

build() {
  local meson_opts=(
    "$pkgname"
    build
    -D cgi=true
    -D true-color=true
    -D html-highlight=true
    -D bzlib=true
    -D brotli=true
    -D lzma=true
    -D luapkg=lua54
    -D tre=true
    -D gemini=true
    -D reproducible=true
    -D source-date-epoch="$SOURCE_DATE_EPOCH"
  )
  arch-meson "${meson_opts[@]}"

  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
