# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: 
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.16.1.1
pkgrel=1
pkgdesc="Advanced, feature-rich text mode web browser"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://elinks.or.cz"
license=('GPL')
depends=('bzip2' 'expat>=2.0' 'gpm>=1.20.4' 'openssl' 'lua' 'libidn'
	 'gc' 'tre' 'zlib')
source=("elinks-$pkgver.tgz::https://github.com/rkd77/elinks/archive/v$pkgver.tar.gz")
sha256sums=('d07e55ede144d532212b2b5b92fb81df4360ddd2b156ccaf09cfa4aa218e1aed')

# todo: make it work with a modern 'js' package
# (upstream has no interest in writing that)

build() {
  cd elinks-$pkgver
  sed -i 's/Terminal=1/Terminal=true/' "contrib/debian/$pkgname.desktop"
  [ -x configure ] || sh autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --disable-smb --without-x --enable-cgi \
              --enable-leds --enable-256-colors --enable-html-highlight \
              --with-zlib --with-luapkg=lua54
  make -j1
}

package() {
  cd elinks-$pkgver
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/share/locale/locale.alias"

  install -D -m644 "contrib/debian/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
