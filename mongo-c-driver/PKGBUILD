# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongo-c-driver
pkgver=1.24.2
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libsasl' 'icu' 'snappy' 'zlib' 'zstd')
makedepends=('cmake')
provides=('libbson' 'libmongoc')
conflicts=('libbson' 'libmongoc')
replaces=('libbson' 'libmongoc')
options=(!distcc)
source=("https://github.com/mongodb/mongo-c-driver/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f8e91943d0419654886f937f19de8d163781b1156ba0c68d011eb20260f46e2cdd3910422b72f2a71613e0ac48bb13d035cb72a8b5303edce6905035c9d18af5')

prepare() {
  mkdir -p build
}

build() {
  cd build

  case "${CARCH}" in
    riscv64)
      export CFLAGS+=' -latomic'
      export CXXFLAGS+=' -latomic'
    ;;
  esac

  cmake ../mongo-c-driver-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_STATIC=OFF -DENABLE_TESTS=OFF
    # Test profiles are not found even with ENABLE_TESTS=ON
  make
}

check() {
  cd build
  # No test profile found
  # pifpaf run mongodb --port 27017 make test || echo "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
