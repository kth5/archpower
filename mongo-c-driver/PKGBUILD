# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongo-c-driver
pkgver=1.25.4
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libsasl' 'snappy' 'zstd')
makedepends=('cmake')
provides=('libbson' 'libmongoc')
conflicts=('libbson' 'libmongoc')
replaces=('libbson' 'libmongoc')
source=("https://github.com/mongodb/mongo-c-driver/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a80e20917edb752ac5eb42534beaa0122a383037f83a554ee00ce37ae690be68521eaa282b4a5802a5440b61038bcd5414356e16a2ce729ba1193d0738a6ce1c')

build() {
  cmake -B build -S mongo-c-driver-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_VERSION="$pkgver" \
    -DENABLE_STATIC=OFF -DENABLE_TESTS=OFF
    # Test profiles are not found even with ENABLE_TESTS=ON
  cmake --build build
}

check() {
  cd build
  # No test profile found
  # pifpaf run mongodb --port 27017 make test || echo "Tests failed"
}

package() {
  DESTDIR="$pkgdir/" cmake --install build
}
