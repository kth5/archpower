# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=zimg
pkgver=3.0.6
pkgrel=1
pkgdesc='Scaling, colorspace conversion, and dithering library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/sekrit-twc/zimg
license=(custom:WTFPL)
depends=(
  gcc-libs
  glibc
)
makedepends=(git)
provides=(libzimg.so)
options=(!emptydirs)
_tag=f819b14e8f39d1282400b0d9543e8ef73c1b2bbd
source=(git+https://github.com/sekrit-twc/zimg.git#tag=${_tag})
sha256sums=('3799202bd28d00bcf91fa772c303bb78f028394c9d2d123f694dae27aab9bea5')

prepare() {
  cd zimg
  ./autogen.sh
}

pkgver() {
  cd zimg
  git describe --tags | sed 's/^release-//'
}

build() {
  cd zimg
  ./configure \
    --prefix=/usr \
    --enable-x86simd \
    --disable-static
  make
}

package(){
  make DESTDIR="${pkgdir}" -C zimg install
  install -Dm 644 zimg/COPYING -t "${pkgdir}"/usr/share/licenses/zimg/
}

# vim: ts=2 sw=2 et:
