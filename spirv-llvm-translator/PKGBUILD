# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=spirv-llvm-translator
pkgver=21.1.4
pkgrel=1
pkgdesc="LLVM <-> SPIR-V converter for compilers targeting SPIR-V"
url="https://www.khronos.org/spirv/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(NCSA)
depends=(
  gcc-libs
  glibc
  llvm-libs
  spirv-tools
)
makedepends=(
  cmake
  git
  llvm
  ninja
  spirv-headers
)
checkdepends=(
  clang
  python
)
source=(
  git+https://github.com/KhronosGroup/SPIRV-LLVM-Translator#tag=v$pkgver
  endian.patch
)
b2sums=('aea99585e6c99b0268e8fb2e8b7945060b8e8a2ccba29754d934713676935271d569ee6db615350908a4bf0e6e528878de737278bdbe68181e1557f9becdb4f7'
        '901b3df8c0ff274932ddddf975c2858c563369ca4f1ffa5173be80d99011185258bdeef02a88d123cdbbc5aeab7f1a949e7f1bec46e09d7509df0f6fc9d0e8e8')

prepare() {
  cd SPIRV-LLVM-Translator

  # https://github.com/KhronosGroup/SPIRV-LLVM-Translator/pull/3301
  git cherry-pick -n fc5873ee760c333738c9e8e8d8c2eb906f0c40f5

  patch -Np1 -i ${srcdir}/endian.patch
}

build() {
  local cmake_options=(
    -D BUILD_SHARED_LIBS=ON
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_SYSCONFDIR=/etc
    -D CMAKE_POSITION_INDEPENDENT_CODE=ON
    -D CMAKE_SKIP_RPATH=ON
    -D LLVM_CONFIG=llvm-config
    -D LLVM_EXTERNAL_LIT=/usr/bin/lit
    -D LLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr/include/spirv
    -D LLVM_LIBDIR_SUFFIX=
    -D LLVM_SPIRV_ENABLE_LIBSPIRV_DIS=ON
    -D LLVM_SPIRV_INCLUDE_TESTS=ON
    -W no-dev
  )

  cmake -S SPIRV-LLVM-Translator -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  # Does not use ctest-compatible targets
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 SPIRV-LLVM-Translator/LICENSE.TXT \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
