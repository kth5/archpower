# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=link-grammar
pkgver=5.13.0
pkgrel=1
pkgdesc='Grammar checking library'
arch=(x86_64 powerpc powerpc64 powerpc64le espresso)
url='https://opencog.github.io/link-grammar-website/'
license=(LGPL-2.1-or-later)
depends=(
  glibc
  hunspell
  libedit
  pcre2
  sqlite
)
makedepends=(
  ant
  autoconf-archive
  git
  strip-nondeterminism
  swig
)
checkdepends=(hunspell-en_us)
optdepends=(
  'java-runtime: Java bindings'
  'python: Python bindings'
)
source=("git+https://github.com/opencog/link-grammar.git#tag=$pkgname-$pkgver")
b2sums=(8f4c77f92aba059f065e7f06ce7eee604b325d4c73671a89b429100ec68ea96e377ea38444aaa222cb0f5f49f904721ffd7d7fba72f4d6ddfdf1c329bdc3368a)
validpgpkeys=(
  6407453C98BECC19ADB03D82EB6AA534E0C0651C # Linas Vepstas <linasvepstas@gmail.com>
  8305252160000B5E89843F5464A99A8201045933 # Linas Vepstas <linasvepstas@gmail.com>
)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  # Strip non-deterministic information from the jar file (e.g. timestamps)
  # This is required for reproducible builds
  find . -name "linkgrammar-${pkgver}.jar" -exec strip-nondeterminism {} +
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
