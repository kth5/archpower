# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Robert McCathie <archaur at rmcc dot com dot au>

pkgbase=libwps
pkgname=(libwps libwps-doc)
pkgver=0.4.14
pkgrel=3
pkgdesc="a Microsoft Works file word processor format import filter library"
arch=(x86_64 powerpc64le powerpc64 riscv64)
url="https://sourceforge.net/projects/libwps/"
license=('LGPL-2.1-or-later OR MPL-2.0')
makedepends=('doxygen' 'libwpd' 'librevenge' 'gcc-libs' 'glibc')
source=("https://downloads.sourceforge.net/project/libwps/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz")
sha1sums=('e6f057f9b55448bc630726d2a863871a830a7e75')
sha256sums=('365b968e270e85a8469c6b160aa6af5619a4e6c995dbb04c1ecc1b4dd13e80de')

build() {
	cd $pkgbase-$pkgver
	./configure  --prefix=/usr \
	  --disable-static
	make
}

check() {
	cd $pkgbase-$pkgver
	make check
}

package_libwps() {
  depends=('libwpd' 'librevenge' 'gcc-libs' 'glibc')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-exec install-pkgconfDATA
  make -C inc/libwps DESTDIR="$pkgdir" install
}

package_libwps-doc() {
  pkgdesc+=" - documentation"

  cd $pkgbase-$pkgver
  make -C docs/doxygen/ DESTDIR="$pkgdir" install
}
