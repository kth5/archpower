# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Bermond <dbermond@archlinux.org>

pkgname=amf-headers
pkgver=1.4.34
pkgrel=1
pkgdesc='Header files for AMD Advanced Media Framework'
arch=(any)
url=https://github.com/GPUOpen-LibrariesAndSDKs/AMF/
license=(MIT)
makedepends=(git)
_tag=6d7bec0469961e2891c6e1aaa5122b76ed82e1db
source=(git+https://github.com/GPUOpen-LibrariesAndSDKs/AMF.git#tag=${_tag})
b2sums=('313ef05a3fbbb9e6084b723a7213df7fa710d811af16abedc2b028114150ddc5e2f4611eecef739444b3152bf8a2c76371ebbf4e1e2f4e7f81337d2682524bf0')

pkgver() {
  cd AMF

  git describe --tags | sed 's/^v//'
}

package() {
  install -dm 755 "${pkgdir}"/usr/include
  cp -dr --no-preserve=ownership AMF/amf/public/include "${pkgdir}"/usr/include/AMF
  install -Dm 644 AMF/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/amf-headers/
}

# vim: ts=2 sw=2 et:
