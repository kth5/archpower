# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-qt
pkgdesc='Qt-based client library for accessing the online accounts database'
pkgver=1.17
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://gitlab.com/accounts-sso/libaccounts-qt'
license=(LGPL)
depends=(gcc-libs
         glib2
         glibc
         libaccounts-glib
         qt6-base)
makedepends=(doxygen
             git)
# source=(https://gitlab.com/accounts-sso/libaccounts-qt/-/archive/VERSION_$pkgver/libaccounts-qt-VERSION_$pkgver.tar.gz)
_commit=c8fdd05f1a1ff5886f4649d24f2ba8c5f61cfa3a
source=(git+https://gitlab.com/accounts-sso/libaccounts-qt#commit=$_commit)
sha256sums=('70e93f8a7ae0c55c54c2ed39969f8e6c1d5c2f5f9a79b0ab2977b29c1b144f6c')

prepare() {
  cd $pkgname
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro
}

build() {
  cd $pkgname
  qmake6 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $pkgname
  make INSTALL_ROOT="$pkgdir" install

# Remove docs
  rm -r "$pkgdir"/usr/share
}
