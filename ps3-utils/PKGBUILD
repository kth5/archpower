# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=ps3-utils
pkgver=20220930.gba9b13f6
pkgrel=1
pkgdesc="Collection of utilities for running Linux on PS3"
arch=(powerpc64 powerpc)
url="https://archlinuxpower.org/share/src/ps3-utils"
license=('GPL-2.0-only')
depends=(glibc)
makedepends=(git)
_commit="ba9b13f6cec0c9083a67a0137ce3de1e46831531"
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/geoff/ps3-utils.git#commit=${_commit}")
sha256sums=('994ce4c3722e73d4e8f45caff8bce2fe135a6e453b8cbfc1dfc9f88249161132')

pkgver() {
  cd ${pkgname}
  ./version.sh
}

prepare() {
  cd ${pkgname}
  ./bootstrap
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}
  make DESTDIR=${pkgdir}/ install
}
