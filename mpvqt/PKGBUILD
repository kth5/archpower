# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mpvqt
pkgver=1.1.1
pkgrel=1
pkgdesc='A libmpv wrapper for QtQuick2 and QML'
license=(LGPL-2.1-only
         LGPL-3.0-only)
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://invent.kde.org/libraries/mpvqt'
depends=(gcc-libs
         glibc
         libglvnd
         mpv
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bdd1ea69338cf3017f628a886218b8c185ca24e8257f03207a3cf1bbb51e3d32'
            'SKIP')
validpgpkeys=(4E421C6554B89766DF9B7A37E12AB207C8755905) # George Florea Bănuș <georgefb899@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
