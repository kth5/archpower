# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=libcdr
pkgver=0.1.8
pkgrel=1
pkgdesc="CorelDraw file format importer library for LibreOffice"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://wiki.documentfoundation.org/DLP/Libraries/libcdr"
# the only Public Domain source is src/lib/CDRColorProfiles.h
license=('MPL-2.0' 'LicenseRef-libcdr-Public-Domain')
depends=('lcms2' 'icu' 'librevenge'
         'zlib' 'gcc-libs' 'glibc')
makedepends=('libwpg' 'boost' 'doxygen' 'cppunit')
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('ced677c8300b29c91d3004bb1dddf0b99761bf5544991c26c2ee8f427e87193c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING.MPL "${pkgdir}/usr/share/licenses/${pkgname}/"
}
