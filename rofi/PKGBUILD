# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Anatol Pomozov
# Contributor: Benjamin Chr√©tien <chretien + aur [at] lirmm [dot] fr>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Rasi <rasi@xssn.at>
# Contributor: Sean Pringle <sean.pringle@gmail.com>
# Contributor: SanskritFritz (gmail)

pkgname=rofi
pkgver=1.7.8
pkgrel=1
pkgdesc="A window switcher, application launcher and dmenu replacement"
url="https://davatorium.github.io/rofi"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MIT')
depends=('bash' 'cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'hicolor-icon-theme' 'libxcb' 'libxkbcommon' 'libxkbcommon-x11'
         'pango' 'startup-notification' 'xcb-imdkit' 'xcb-util' 'xcb-util-cursor' 'xcb-util-keysyms' 'xcb-util-wm')
makedepends=('check' 'git' 'meson')
source=("git+https://github.com/davatorium/rofi.git#tag=${pkgver}"
        'git+https://github.com/sardemff7/libgwater.git'
        'git+https://github.com/sardemff7/libnkutils.git'
        'fix_xfce4-terminal_binary_name.patch')
sha256sums=('8684b59ebc0046823ef89f1ef3636aa3fbd7e1c10e202433ca483cb2032bf947'
            'SKIP'
            'SKIP'
            'aa6c7bc39e4eb936e47a600beeadf0e11ff62bc5ce6e6d954b56439ec9a44543')

prepare() {
	cd "${pkgname}"
	git submodule init
	git config submodule.subprojects/libgwater.url "${srcdir}/libgwater"
	git config submodule.subprojects/libnkutils.url "${srcdir}/libnkutils"
	git -c protocol.file.allow=always submodule update

	# xfce4-terminal.wrapper is a Debian specific script that Arch does not provide
	patch -Np1 -i "${srcdir}/fix_xfce4-terminal_binary_name.patch"
}

build() {
	arch-meson "${pkgname}" build
	meson compile -C build
}

check() {
	meson test -C build --print-errorlogs
}

package() {
	meson install -C build --destdir "${pkgdir}"

	cd "${pkgname}"
	install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
	install -Dm 755 Examples/*.sh -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
