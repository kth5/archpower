# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=2.0.11
pkgrel=3
epoch=1
pkgdesc='An efficient and feature complete C++ BitTorrent library implementation'
url='https://www.rasterbar.com/products/libtorrent/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('BSD-3-Clause')
depends=(
    'boost-libs'
    'gcc-libs'
    'glibc'
    'openssl')
makedepends=(
    'boost'
    'cmake'
    'python'
    'python-setuptools')
source=("https://github.com/arvidn/libtorrent/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-libtorrent-rasterbar-fix-for-fortify-source-3.patch')
sha512sums=('756fb24c44b5dcf22d0bbc06a812abc28be7388a409e577c71fb02b1ca3005040947244c0ae83bd3388264dd518119736b869397fedd7bdbcd60699b04a19969'
            '9f18a2e71b3924897f88b1c4148d3afe3bb29d1fe41a3166f0e3dfb64f1641f50ac25551e942241c7207e6692e4765df53e160ea9d3a8eca30879d6e299e9e1d')

prepare() {
    # https://github.com/arvidn/libtorrent/issues/7519
    # remove usage of malloc_usable_size() for _FORTIFY_SOURCE=3 compatibility
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-libtorrent-rasterbar-fix-for-fortify-source-3.patch"
}

build() {
    export CFLAGS+=' -DNDEBUG'
    export CXXFLAGS+=' -DNDEBUG'
    cmake -B build -S "${pkgname}-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -Dboost-python-module-name:STRING='python' \
        -Dpython-bindings:BOOL='ON' \
        -Dpython-egg-info:BOOL='ON' \
        -Wno-dev
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -D -m644 "${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
