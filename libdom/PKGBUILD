# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=libdom
pkgver=0.4.2
pkgrel=1
pkgdesc='Implementation of the W3C DOM'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.netsurf-browser.org/projects/libdom/'
license=(MIT)
depends=(expat 'libhubbub>=0.3.8' 'libwapcaplet>=0.4.3')
makedepends=(netsurf-buildsystem)
source=("https://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
b2sums=('2a320452b50c3da719ef76683e133582bb0f657ceb8552a252204bbab89d85076af596cd615770dd357aaa9dbec31fabf39b2946019d004352b2273585134914')

prepare() {
  sed -i 's:_BSD_SOURCE:_DEFAULT_SOURCE:' "$pkgname-$pkgver/Makefile"
}

build() {
  export CFLAGS="$CFLAGS -shared -fPIC -w"
  export LDFLAGS="$LDFLAGS -shared -z,now"
  make -C "$pkgname-$pkgver" COMPONENT_TYPE="lib-shared" INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr
}

package() {
  make -C "$pkgname-$pkgver" COMPONENT_TYPE="lib-shared" DESTDIR="$pkgdir" \
    INCLUDEDIR=include LIBDIR=lib PREFIX=/usr install
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
