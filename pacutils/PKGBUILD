# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pacutils
pkgver=0.14.0
pkgrel=3
# from the pacman-7.0.0 branch
_commit=20516b5e9ac8cffc88edded6f57c896cd596eba0
pkgdesc='Helper tools for libalpm'
url='https://github.com/andrewgregory/pacutils'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MIT')

depends=(
  'glibc'
  'libarchive'
  'pacman'
)
makedepends=('git')

source=("git+https://github.com/andrewgregory/pacutils.git#commit=${_commit}")
sha256sums=('0b8ca94ebfa90f5108740221608709030c324fed5bdc1602007975a5bf63f956')
validpgpkeys=('0016846EDD8432261C62CB63DF3891463C352040')

build() {
  cd pacutils
  make CFLAGS="$CFLAGS $LDFLAGS" SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

check() {
  cd pacutils
  make check
}

package() {
  cd pacutils
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}

# vim: set ft=PKGBUILD et sw=2:
