# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pacutils
pkgver=0.15.0
pkgrel=1
pkgdesc='Helper tools for libalpm'
url='https://github.com/andrewgregory/pacutils'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('MIT')
depends=(
  'glibc'
  'libarchive'
  'pacman'
)
makedepends=('git')
source=("git+https://github.com/andrewgregory/pacutils.git?signed#tag=v${pkgver}")
sha256sums=('789b1ce9dfa6e6d6e0495651019cba121defa75eec598a63745c910de3371ac5')
validpgpkeys=('0016846EDD8432261C62CB63DF3891463C352040')

build() {
  cd pacutils
  make CFLAGS="$CFLAGS $LDFLAGS" SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

check() {
  cd pacutils
  make check
}

package() {
  cd pacutils
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}

# vim: set ft=PKGBUILD et sw=2:
