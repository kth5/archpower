# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pacutils
pkgver=0.11.1
pkgrel=2

pkgdesc='Helper tools for libalpm'
url='https://github.com/andrewgregory/pacutils'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MIT')

depends=(
  'glibc'
  'libarchive'
  'pacman'
)
makedepends=('git')

source=("git+https://github.com/andrewgregory/pacutils.git#commit=aa4ba003cb446907a71be4aa976546d113ecc688")
sha256sums=('SKIP')

prepare() {
  cd pacutils
  for commit in a8cb36d865a74ade470b6e71b434fc8b0e981e04 \
                9788717c8798b6accae0090a9bad49b504781f0d \
                045b55113c766efef01d636e48b37d3a1ead16a1; do
    git show "$commit" | git apply -
  done
}

build() {
  cd pacutils
  make CFLAGS="$CFLAGS $LDFLAGS" SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

check() {
  cd pacutils
  make check
}

package() {
  cd pacutils
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}

# vim: set ft=PKGBUILD et sw=2:
