# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mihai Bi≈üog <mihai.bisog@gmail.com>

pkgname=fmt
pkgver=11.0.2
pkgrel=1
pkgdesc='Open-source formatting library for C++'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://fmt.dev
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cmake
  doxygen
  git
  mkdocs
  mkdocs-material
  mkdocstrings
  ninja
  python-pymdown-extensions
  python-regex
)
makedepends_powerpc64=(npm)
makedepends_powerpc64le=(npm)
makedepends_riscv64=(npm)
makedepends_x86_64=(npm)
provides=(libfmt.so)
_tag=0c9fce2ffefecfdce794e1859584e25877b7b592
source=("git+https://github.com/fmtlib/fmt.git#tag=${_tag}")
b2sums=('18b6d66c2159b2f8bd0baf2e1df7514fde09cf6a25441710d40e386abd9baa49b62859c4d8a71b77f0d1550c32fc62826c95fdacf4397e24cc6ea205a0c50798')

pkgver() {
  cd fmt
  git describe --tags
}

build() {
  cmake -S fmt -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  cmake --build build
  cmake --build build --target doc
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm 644 fmt/LICENSE -t "${pkgdir}"/usr/share/licenses/fmt/
}

# vim: ts=2 sw=2 et:
