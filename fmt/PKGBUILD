# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Mihai Bi≈üog <mihai.bisog@gmail.com>

pkgbase=fmt
pkgname=(
  fmt
  fmt-docs
)
pkgver=11.2.0
pkgrel=1
pkgdesc='Open-source formatting library for C++'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://fmt.dev
license=(MIT)
makedepends=(
  cmake
  doxygen
  git
  mkdocs
  mkdocs-material
  mkdocstrings
  ninja
  python-pymdown-extensions
  python-regex
)
makedepends_powerpc64=(npm)
makedepends_powerpc64le=(npm)
makedepends_riscv64=(npm)
makedepends_x86_64=(npm)
_tag=40626af88bd7df9a5fb80be7b25ac85b122d6c21
source=("git+https://github.com/fmtlib/fmt.git#tag=$_tag")
b2sums=('6b2124216e51d5efab77e2c835e86f3803c544a90bb6ba01eaa2ddbe6824842f2c3be1b92a5bdee9e5a19e5f437b40316f2a25f47f2fb2ee6dd8ec663d98cc6a')

pkgver() {
  cd fmt
  git describe --tags
}

build() {
  cmake -S fmt -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  cmake --build build
  cmake --build build --target doc
}

check() {
  cmake --build build --target test
}

package_fmt() {
  depends=(
    gcc-libs
    glibc
  )
  provides=(libfmt.so)

  DESTDIR="$pkgdir" cmake --install build --component fmt-core
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" fmt/LICENSE
}

package_fmt-docs() {
  pkgdesc+=' (documentation)'

  DESTDIR="$pkgdir" cmake --install build --component fmt-doc
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" fmt/LICENSE
}

# vim: ts=2 sw=2 et:
