# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+2.0.1
pkgrel=3
pkgdesc='Compact Disc Digital Audio (CDDA) extraction tool using libcdio for CDROM access'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.gnu.org/software/libcdio/'
license=('GPL3')
depends=('glibc' 'libcdio')
source=("https://ftp.gnu.org/gnu/libcdio/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('28d7d00e4a83d0221acda0fd2eb3e3240bf094db4c00a85998922201939fa952'
            'SKIP')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein <rocky@panix.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
