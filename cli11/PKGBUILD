# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

pkgname=cli11
pkgver=2.6.1
pkgrel=1
pkgdesc="Command line parser for C++11"
arch=(any)
url="https://github.com/CLIUtils/CLI11"
license=(BSD-3-Clause)
makedepends=(cmake)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('377691f3fac2b340f12a2f79f523c780564578ba3d6eaf5238e9f35895d5ba95')

build() {
  local cmake_options=(
    -B build
    -S ${pkgname^^}-$pkgver
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCLI11_BUILD_TESTS=OFF
    -DCLI11_BUILD_EXAMPLES=OFF
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 ${pkgname^^}-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
