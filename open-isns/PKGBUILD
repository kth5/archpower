# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=open-isns
pkgver=0.102
pkgrel=3
pkgdesc='iSNS server and client for Linux'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('openssl')
makedepends=('meson')
url='https://github.com/gonzoleeman/open-isns'
license=('GPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gonzoleeman/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('9611344733c0cdf14395f60880950ea4c3c7d6b765565b6493ad3e1afbe216de')

build() {
  local _meson_options=(
    --default-library=shared
    -Dslp=disabled
    -Dlibdir=lib
  )

  arch-meson "${pkgname}-${pkgver}" build "${_meson_options[@]}"

  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
