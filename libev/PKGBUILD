# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Thomas Haider <t.haider@vcnc.org>

pkgname=libev
pkgver=4.33
pkgrel=4
pkgdesc="A full-featured and high-performance event loop"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://software.schmorp.de/pkg/libev.html"
license=('BSD')
depends=('glibc')
provides=('libev.so')
source=(http://dist.schmorp.de/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c662a65360115e0b2598e3e8824cf7b33360c43a96ac9233f6b6ea2873a10102551773cad0e89e738541e75af9fd4f3e3c11cd2f251c5703aa24f193128b896b')
b2sums=('8a6cae25ffde10b24a5bbf084f6a8559af326b37acdbdf47dda34b7f0c7955f3ebd26958594444a574cfa3e2b4011e4be93ad2bd994ffd4c094bf36620e67ba5')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr

  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  # fix conflict with libevent
  rm "${pkgdir}"/usr/include/event.h

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
