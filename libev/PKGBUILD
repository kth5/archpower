# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Thomas Haider <t.haider@vcnc.org>

pkgname=libev
pkgver=4.33
pkgrel=5
pkgdesc='A full-featured and high-performance event loop'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://software.schmorp.de/pkg/libev.html'
license=(BSD-2-Clause)
depends=(glibc)
makedepends=(signify)
provides=(libev.so)
# seems that pacman's hardcoded to attempt gpg verification if source array contains a *.sig file
source=(
  "$pkgname-$pkgver.tar.gz::https://dist.schmorp.de/$pkgname/$pkgname-$pkgver.tar.gz"
  "$pkgname-$pkgver.tar.gz.signature::https://dist.schmorp.de/$pkgname/$pkgname-$pkgver.tar.gz.sig"
  signing-key.pub
)
sha512sums=('c662a65360115e0b2598e3e8824cf7b33360c43a96ac9233f6b6ea2873a10102551773cad0e89e738541e75af9fd4f3e3c11cd2f251c5703aa24f193128b896b'
            'a94f6bcd4e61f8abe6541f39388f567099734347a3761056d8949a89293da8fe3e86f9bff50695047a2fef285706f26fad618280c6c04dc3a02f2a30fd5ed200'
            'a09a434387e22612a9225ddfa444044977712c6410efe8a77a33d8aa607bd1acbef2a1da08b62e21a4b070f0974ba94ae907ac0452b5d060e33a1c051a7780e4')
b2sums=('8a6cae25ffde10b24a5bbf084f6a8559af326b37acdbdf47dda34b7f0c7955f3ebd26958594444a574cfa3e2b4011e4be93ad2bd994ffd4c094bf36620e67ba5'
        '6c550178dbf6389c40a76ac4316c8bcd993e5605e72a00a12112d65e1fdfee32da4aa31aca982155a591bf06e24d16750c879e0b9f856923176922dcd37a28d1'
        '6d6bd90b77e67ccb876f0c78c710c9e1b82767a19aeadaac9310e5628b791586fc8475ad5179eaa2fee386ae80aae916226167ec92c5af309bba4052238326c8')

prepare() {
  # hacking around to validate with signify
  mv -v "$pkgname-$pkgver.tar.gz."{signature,sig}
  signify -V -p "signing-key.pub" -m "$pkgname-$pkgver.tar.gz"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr

  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # fix conflict with libevent
  rm "$pkgdir/usr/include/event.h"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
