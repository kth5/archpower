# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Maksim Sipos <maxsipos@gmail.com>

pkgname=plotutils
pkgver=2.6
pkgrel=12
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
pkgdesc='Set of utilities and libraries for plotting'
url='https://directory.fsf.org/graphics/plotutils.html'
license=(GPL-3.0-or-later)
depends=(libpng libxaw)
validpgpkeys=('C7823604DFEA27BC29DD4F179DEB46C0D679F6CF') # Karl Berry
source=(https://ftp.gnu.org/pub/gnu/plotutils/$pkgname-$pkgver.tar.gz{,.sig}
        plotutils-2.6-libpng-1.5.patch)
b2sums=('d77d62ba4e632001d056da7333d1300b2e3d7966029a130e6836fa88545a1749e15b5dc7322d03575432fc5e1342c839f4769ed821962f711848c2d8523904fb'
        'SKIP'
        '2ad24fa9f2022b5cb36ce3d1f23f5f993856b294ae8545c91bd4c3c5c411530e2c2452f1b2e4eba42ddf385c1c0a992af2a776e7cdf9e588f36e6a8d21eb0842')

build() {
  cd $pkgname-$pkgver
  patch -p0 -i ../plotutils-2.6-libpng-1.5.patch
  export CXXFLAGS+=' -std=c++11 -w'
  ./configure \
    --build=${CHOST} \
    --enable-libplotter \
    --prefix=/usr \
    --with-gnu-ld \
    --with-x
  make
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname-$pkgver install
}
