# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=guile
pkgver=3.0.8
pkgrel=3
pkgdesc='Portable, embeddable Scheme implementation written in C'
url='https://www.gnu.org/software/guile/'
arch=(x86_64 powerpc64le powerpc64  powerpc riscv64)
license=(GPL)
depends=(gmp ncurses libunistring gc libffi libxcrypt)
makedepends=(git texinfo gperf)
source=("git+https://git.sv.gnu.org/git/guile.git?tag=v${pkgver}"
        0001-Fix-32-bit-big-endian-builds-via-Oresolve-primitives.patch)
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5' # Ludovic Courtès <ludo@gnu.org>
              'FF478FB264DE32EC296725A3DDC0F5358812F8F2' # Andy Wingo
              '4FD4D288D445934E0A14F9A5A8803732E4436885') # Andy Wingo <wingo@pobox.com>"
sha256sums=('SKIP'
            '961e21d02f87a5c8404d9dbf8ade32d46d749d51986148186748f3adcdd3c5c9')

prepare() {
  [ ! -d ${pkgname}-${pkgver} ] && ln -s ${pkgname} ${pkgname}-${pkgver}

  cd $pkgname-$pkgver

  [ "$CARCH}" == 'powerpc' ] && patch -Np1 -i ${srcdir}/0001-Fix-32-bit-big-endian-builds-via-Oresolve-primitives.patch

  # remove pre-built binaries for powerpc, they coredump
  rm -rf prebuilt/32-bit-big-endian

  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static  \
    --disable-error-on-warning
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libguile-3.?.so.*-gdb.scm
}
