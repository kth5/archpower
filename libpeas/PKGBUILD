# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libpeas
pkgname=(
  libpeas
  libpeas-demos
  libpeas-docs
)
pkgver=1.36.0
pkgrel=6
pkgdesc="GObject Plugin System"
url="https://wiki.gnome.org/Projects/Libpeas"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgirepository
)
makedepends=(
  gi-docgen
  git
  glade
  glib2-devel
  gobject-introspection
  lua51
  lua51-lgi
  meson
  python
  python-gobject
  python-setuptools
  vala
)
makedepends_powerpc=(luajit)
makedepends_powerpc64le=(luajit)
makedepends_riscv64=(luajit)
makedepends_x86_64=(luajit)
checkdepends=(xorg-server-xvfb)
source=("git+https://gitlab.gnome.org/GNOME/libpeas.git#tag=libpeas-$pkgver")
b2sums=('8845366ccd5c0244a6ed1b28d5b44590496d260db0da47bc0ac87b2063c1063ee92541bc7f4eb056bb2c52d1299febedf517e90b9dc8765a0f3f9f954430e691')

prepare() {
  cd libpeas
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D vapi=true
  )

  arch-meson libpeas build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_libpeas() {
  optdepends=(
    'lua51-lgi: Lua loader'
    'python-gobject: Python loader'
  )
  provides=(libpeas{,-gtk}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"

  _pick demo usr/bin/peas-demo
  _pick demo usr/lib/peas-demo

  _pick docs usr/share/doc
}

package_libpeas-demos() {
  pkgdesc+=" (demo applications)"
  depends=(
    glib2
    glibc
    gtk3
    libgirepository
    libpeas
    python-gobject
  )
  mv demo/* "$pkgdir"
}

package_libpeas-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
