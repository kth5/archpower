# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=libavc1394
pkgver=0.5.4
pkgrel=7
pkgdesc='Library to control A/V devices using the 1394 TA AV/C commands'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://sourceforge.net/projects/libavc1394/'
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  glibc
  libraw1394
)
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
b2sums=(c27cf10b8769c1602488fa1a9a2536dee169e0e07185090e719946ca1da4b210aa61b37d816fb5b019d8b199b1c9285e2b212545f4332c8aad48af39c2dadf20)

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
