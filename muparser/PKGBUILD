# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir.archlinux.org>

pkgname=muparser
pkgver=2.3.5
pkgrel=1
pkgdesc="A fast math parser library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/beltoforion/muparser"
license=('BSD-2-Clause')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cmake')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('20b43cc68c655665db83711906f01b20c51909368973116dfc8d7b3c4ddb5dd4')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    $pkgname-$pkgver/LICENSE
}
