# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=libgusb
pkgname=(
  libgusb
  libgusb-docs
)
pkgver=0.4.9
pkgrel=2
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  libjson-glib-1.0.so
  libusb-1.0.so
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  umockdev
  vala
)
source=(git+https://github.com/hughsie/libgusb.git#tag=${pkgver})
sha256sums=('27ac4dd41aa0279416bc74f8d195339d439bd9fc0a9f6a0dc9745a07d251c942')

build() {
  arch-meson libgusb build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libgusb() {
  DESTDIR="${pkgdir}" ninja -C build install

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libgusb-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim: ts=2 sw=2 et:
