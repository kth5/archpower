# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.4.9
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL2.1)
depends=(
  glib2
  libjson-glib-1.0.so
  libusb-1.0.so
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  gtk-doc
  meson
  python-setuptools
  umockdev
  vala
)
_tag=ed31c8134d80d006bd45450e84180be2a7c0742e
source=(git+https://github.com/hughsie/libgusb.git#tag=${_tag})
sha256sums=('27ac4dd41aa0279416bc74f8d195339d439bd9fc0a9f6a0dc9745a07d251c942')

pkgver() {
  cd libgusb
  git describe --tags
}

build() {
  arch-meson libgusb build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
