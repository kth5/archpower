# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: André Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.6.5
pkgrel=1
pkgdesc='Library to parse XMP metadata'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://libopenraw.freedesktop.org/exempi/'
license=(BSD-3-Clause)
depends=(
  expat
  gcc-libs
  glibc
  zlib
)
makedepends=(
  boost
  git
)
source=("git+https://gitlab.freedesktop.org/libopenraw/$pkgname.git?signed#tag=$pkgver")
b2sums=(a6fe8a2e8b2d30c5b9d70434dc5d9093108eb5470935062616c3fa9b872bf7cbf4ef779c65c84844e543d6bc969155b9c447485fb61f0c6627a7e190ba3c171b)
validpgpkeys=(6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3) # Hubert Figuiere

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING
}
