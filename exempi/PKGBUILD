# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: André Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.6.6
pkgrel=2
pkgdesc='Library to parse XMP metadata'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://libopenraw.freedesktop.org/exempi/'
license=(BSD-3-Clause)
depends=(
  expat
  gcc-libs
  glibc
  zlib
)
makedepends=(
  boost
  git
)
source=("git+https://gitlab.freedesktop.org/libopenraw/$pkgname.git#tag=$pkgver")
b2sums=(aeaded22cfd41a51eebd6575cf3b701f78c883b06b060117734b51e1b23191a1a04e6a71858787b57ca841a82750394709159d44faa40a8a09f39b50414ff071)
validpgpkeys=(6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3) # Hubert Figuiere

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-unittest
  make
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING
}
