# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgname=liblc3
pkgver=1.1.1
pkgrel=1
pkgdesc="Low Complexity Communication Codec library and tools"
url="https://github.com/google/liblc3"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(Apache-2.0)
depends=(glibc)
makedepends=(
  git
  meson
)
provides=(liblc3.so)
source=("git+$url#tag=v$pkgver")
b2sums=('e6492333659b7560104a09917704222e427667dd88d7c87f3ac5b70e67292deede8fa03db870033363e0e4518e3ac2c3fb2a934aa4e5dfcca2140e4301d84944')

prepare() {
  cd liblc3
}

build() {
  arch-meson liblc3 build -D tools=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 liblc3/README.md
}

# vim:set sw=2 sts=-1 et:
