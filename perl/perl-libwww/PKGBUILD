# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-libwww
pkgver=6.79
pkgrel=2
pkgdesc='The World-Wide Web library for Perl'
arch=(any)
url='https://metacpan.org/release/libwww-perl'
license=('GPL-1.0-or-later OR Artistic-1.0-Perl')
depends=(
  perl
  perl-encode-locale
  perl-file-listing
  perl-html-parser
  perl-http-cookies
  perl-http-daemon
  perl-http-cookiejar
  perl-http-date
  perl-http-negotiate
  perl-lwp-mediatypes
  perl-net-http
  perl-try-tiny
  perl-uri
  perl-www-robotrules
)
optdepends=('perl-lwp-protocol-https: for https:// url schemes')
checkdepends=(
  perl-test-fatal
  perl-test-needs
  perl-test-requiresinternet
)
options=(!emptydirs)
source=(https://cpan.metacpan.org/authors/id/O/OA/OALDERS/libwww-perl-$pkgver.tar.gz)
sha512sums=('0546003f9b780875a684bfd6aeca9c213cb1bf9f021f0c5872871c39ff5415d53d005fff0e2ba968d8f5a43b811fab1fe7696dcd2296de8344939e7a8da20ebe')
b2sums=('a61f07cd2d08a9c7fbecb00a138c5494101c7ca108f4957d6438c28200577cca1081b1b123a5662008e35db010b686231d0b30444bd35374edff4684ece87476')

build() {
  cd "libwww-perl-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check()  {
  cd "libwww-perl-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "libwww-perl-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
