# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-libwww
pkgver=6.81
pkgrel=2
pkgdesc='The World-Wide Web library for Perl'
arch=(any)
url='https://metacpan.org/release/libwww-perl'
license=('GPL-1.0-or-later OR Artistic-1.0-Perl')
depends=(
  perl
  perl-encode-locale
  perl-file-listing
  perl-html-parser
  perl-http-cookies
  perl-http-daemon
  perl-http-cookiejar
  perl-http-date
  perl-http-negotiate
  perl-lwp-mediatypes
  perl-net-http
  perl-try-tiny
  perl-uri
  perl-www-robotrules
)
optdepends=('perl-lwp-protocol-https: for https:// url schemes')
checkdepends=(
  perl-test-fatal
  perl-test-needs
  perl-test-requiresinternet
)
options=(!emptydirs)
source=(https://cpan.metacpan.org/authors/id/O/OA/OALDERS/libwww-perl-$pkgver.tar.gz)
sha512sums=('7b3527ca7266cfb31a37bed58bb8258ddccf54af4cf8071605de868539954cd5997aa5edf8ef66d5f386aeb00bb9d5756ffcd1836957e0a9f1ec6f650108f3c6')
b2sums=('556bcc785ce925fc9d4a90585b791b8315916a6df4e7788f3dfbf1d42cc872cc88e1017a01ba5de805cc52a22b1e47a1d0c1ee6cff75f0be4c273677f64583ea')

build() {
  cd "libwww-perl-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check()  {
  cd "libwww-perl-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "libwww-perl-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
