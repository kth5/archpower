# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-mockmodule
pkgver=0.178.0
pkgrel=1
pkgdesc="Override subroutines in a module for unit testing"
arch=(any)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-super')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage' 'perl-test-warnings')
url='https://search.cpan.org/dist/Test-MockModule'
source=("https://search.cpan.org/CPAN/authors/id/G/GF/GFRANKS/Test-MockModule-v$pkgver.tar.gz")
sha512sums=('5f3169819475d251cc266fe018d98b8a744d9a030fab40f72334ee17f561f4602d4ca9759a70438cb897195e4757df0103cce30e7241191b13fbba57b6c03700')

build() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build install destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
