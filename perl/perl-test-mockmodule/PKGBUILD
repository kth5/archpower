# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-mockmodule
pkgver=0.179.0
pkgrel=1
pkgdesc="Override subroutines in a module for unit testing"
arch=(any)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-super')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage' 'perl-test-warnings')
url='https://search.cpan.org/dist/Test-MockModule'
source=("https://search.cpan.org/CPAN/authors/id/G/GF/GFRANKS/Test-MockModule-v$pkgver.tar.gz")
sha512sums=('e2b7a6ca8b5efcd9e2cec60a11e1293aac6a3b0c4d400fc7ee82f0837bb294df099c581bf4d31eec91f93832e236bcdaf1a5250b0ba3fa28f3c38c6f6a26c39a')

build() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build install destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
