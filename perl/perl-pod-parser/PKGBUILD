# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pod-parser
pkgver=1.67
pkgrel=1
pkgdesc="Modules for parsing/translating POD format documents"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/Pod-Parser"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MA/MAREKR/Pod-Parser-$pkgver.tar.gz")
sha512sums=('62cfbb9da1f4ccb224fb3e3d3f952f6c1579728ad7dcd7f2ef0b0393dabe8caa06cd5e259a875b58066fe0a519b3db1d26a40ae5aa23d1eff06e20547cac1e33')

build() {
  cd Pod-Parser-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pod-Parser-$pkgver
  make test
}

package() {
  cd Pod-Parser-$pkgver
  make DESTDIR="$pkgdir" install
}
