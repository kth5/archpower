# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=perl-dbd-mariadb
pkgver=1.24
pkgrel=1
pkgdesc='MariaDB and MySQL driver for the Perl5 Database Interface (DBI)'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL' 'PerlArtistic')
url='https://metacpan.org/release/DBD-MariaDB'
depends=('mariadb-libs' 'perl-dbi')
makedepends=('perl-devel-checklib')
checkdepends=('mariadb' 'perl-test-deep')
source=("https://cpan.metacpan.org/authors/id/P/PA/PALI/DBD-MariaDB-${pkgver}.tar.gz")
sha512sums=('ed069c39cc702d0c34235cd8336d83ed091eb07bd7f5d6decd0065cfd872c33970a14304301161c3a496c951bb4cb9d96580ddc25ee36a24e4019fd9ebbbbb1f')

build() {
  cd DBD-MariaDB-${pkgver}

  perl Makefile.PL INSTALLDIRS=vendor --testsocket=/tmp/socket.mysql
  make
}

check() {
  cd DBD-MariaDB-${pkgver}

  mkdir -p /tmp/mysql_test
  mariadb-install-db \
     --basedir=/usr \
     --datadir=/tmp/mysql_test
  mariadbd -P 17999 \
     --socket=/tmp/socket.mysql \
     --datadir=/tmp/mysql_test &
  sleep 10
  DAEMON_PORT=$!
  make test
  kill -9 $DAEMON_PORT
}

package() {
  cd DBD-MariaDB-${pkgver}

  make install DESTDIR="${pkgdir}"
}
