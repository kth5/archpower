# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: François Charette <firmicus ατ gmx δοτ net>
# Contributor: Alex Dioso <adioso->gmail*com>

pkgname=perl-clone
pkgver=0.47
pkgrel=1
pkgdesc='Recursive copy of nested objects.'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/garu/Clone'
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-b-cow')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/A/AT/ATOOMIC/Clone-$pkgver.tar.gz")
sha512sums=('dc5ebde05ecafbd3089cdb978083584ad77e1e4dce2e95e466b1cc8357092422a36489d2e9cda99c8009a8d7a0bd18466e3c21b580b95c29fb9ad843911bb9fe')

build() {
  cd Clone-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Clone-$pkgver
  make test
}

package() {
  cd Clone-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
