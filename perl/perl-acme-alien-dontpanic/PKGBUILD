# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-acme-alien-dontpanic
pkgver=2.7200
pkgrel=6
pkgdesc="Test Module for Alien::Base"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/Acme-Alien-DontPanic"
depends=('dontpanic' 'perl-alien-build')
checkdepends=('perl-inline' 'perl-inline-c' 'perl-inline-cpp')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Acme-Alien-DontPanic-$pkgver.tar.gz")
sha512sums=('c1d8428dfae268687a2e7cb6c4d01f21876f2fea9bd9d1e08a2a21ec3c73a2a513df490574553ab118b207d067e54f0c39454c1edd2e9d441f73cd87940447fb')

build() {
  cd Acme-Alien-DontPanic-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Acme-Alien-DontPanic-$pkgver
  make test
}

package() {
  cd Acme-Alien-DontPanic-$pkgver
  make DESTDIR="$pkgdir" install
}
