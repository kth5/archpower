# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-perl-critic
pkgver=1.152
pkgrel=1
pkgdesc="Critique Perl source code for best-practices"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/Perl-Critic"
depends=('perl-b-keywords' 'perl-config-tiny' 'perl-exception-class' 'perl-file-homedir'
         'perl-file-which' 'perl-list-someutils' 'perl-module-build' 'perl-module-pluggable'
         'perl-ppi' 'perl-ppix-quotelike' 'perl-ppix-regexp' 'perl-ppix-utils' 'perl-pod-spell'
         'perl-tidy' 'perl-readonly' 'perl-string-format')
checkdepends=('perl-test-memory-cycle')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PE/PETDANCE/Perl-Critic-$pkgver.tar.gz")
sha512sums=('273aeb46617cf614b044c76c4307faf80c8ee4702f115f4baa7d23306164a70e70394fd64e3d89e43650a7314a3bd21ab96ee6daba28f1a12d9d32bd5032779c')

build() {
  cd Perl-Critic-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Perl-Critic-$pkgver
  perl Build test
}

package() {
  cd Perl-Critic-$pkgver
  perl Build install destdir="$pkgdir"
}
