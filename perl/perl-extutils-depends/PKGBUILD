# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-depends
pkgver=0.8002
pkgrel=2
pkgdesc="The Perl depends module"
arch=(any)
url="https://search.cpan.org/dist/ExtUtils-Depends/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/E/ET/ETJ/ExtUtils-Depends-$pkgver.tar.gz")
sha512sums=('fe0d571174962df92dd9ef3dbce029383d216cd9c3910d2fce539e3a4da9053bb153aad4636d706f5103ec3c58a61736bdb6f0afb15d77374acf64ff45615980')

build() {
  cd ExtUtils-Depends-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ExtUtils-Depends-$pkgver
  make test
}

package() {
  cd ExtUtils-Depends-$pkgver
  make DESTDIR="$pkgdir" install
}
