# Maintainer: George Rawlinson <grawlinson@archlinux.org>

_perlmod=pod2pdf
pkgname=perl-$_perlmod
pkgver=0.42
pkgrel=2
pkgdesc="Converts Pod to PDF format"
arch=(any)
url="https://metacpan.org/release/pod2pdf"
license=(
  'GPL-1.0-or-later'
  'Artistic-2.0'
)
depends=(
  'perl'
  'perl-pod-escapes'
  'perl-pod-parser'
  'perl-pdf-api2'
  'perl-getopt-argvfile'
)
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/J/JO/JONALLEN/pod2pdf-$pkgver.tar.gz")
sha512sums=('f4d394ced8c8736e8003f20c1119e34eaf38ebc1f151ce2165e8198671ba569e1cb283ef1183ec54a5488868c2a087940d219824b574beddbe808be519a1bc69')
b2sums=('ea22d43c24dcf3f7104200314cc85697bddb38be92393ceb863af8d795a3559f3e2c7ebda153f4fd57b4e071e8213bebf89a9a0e3f98b08d605c75c74e70ad6a')

build() {
  cd "$_perlmod-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

package() {
  cd "$_perlmod-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
