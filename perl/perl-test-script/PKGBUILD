# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-script
pkgver=1.31
pkgrel=1
pkgdesc="Basic cross-platform tests for scripts"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/Test-Script"
depends=('perl-capture-tiny' 'perl-probe-perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Test-Script-$pkgver.tar.gz")
sha512sums=('4ed8e89a4606f28e281e04ac4bc667915b4dc1422eaed407b2a792ea5c9428ee7901947e22cd425d48701b3b6e3c1419a7c52f83d92f8378ad14c49e03f8cf94')

build() {
  cd Test-Script-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Script-$pkgver
  make test
}

package() {
  cd Test-Script-$pkgver
  make DESTDIR="$pkgdir" install
}
