# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-crypt-openssl-rsa
pkgver=0.35
pkgrel=2
pkgdesc='RSA encoding and decoding, using the openSSL libraries'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6' perl-crypt-openssl-random)
makedepends=('perl-crypt-openssl-guess>=0.11')
url=https://metacpan.org/release/Crypt-OpenSSL-RSA
source=("https://cpan.metacpan.org/authors/id/T/TO/TODDR/Crypt-OpenSSL-RSA-$pkgver.tar.gz")
md5sums=('69827a4e1b23f9cdbf3a60ac823ad524')
sha512sums=('27fbf7d0cc65778b07700b375e8ebed457f237a465582eca07b3fb50d4c966cd4afc71919a50e14bf660dfa3c4ecc202a6410ad9301994d454a78e6e7bcb9608')
_ddir="Crypt-OpenSSL-RSA-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
