# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-crypt-openssl-rsa
pkgver=0.37
pkgrel=1
pkgdesc='RSA encoding and decoding, using the openSSL libraries'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('Artistic-2.0')
options=(!emptydirs)
depends=('perl>=5.6' perl-crypt-openssl-random)
makedepends=('perl-crypt-openssl-guess>=0.11')
url=https://metacpan.org/release/Crypt-OpenSSL-RSA
source=("https://cpan.metacpan.org/authors/id/T/TO/TODDR/Crypt-OpenSSL-RSA-$pkgver.tar.gz")
md5sums=('e0a3a2249c59fd26270ad0dfd92afd98')
sha512sums=('e53f041c5b4f579678da8c7d8c950e52ecea85d09365f4387f5e5066ed8efda35d4448085720f47dcb7b3e78a467f9160a282c2288335c92e0e3d052ee8eea1b')
_ddir="Crypt-OpenSSL-RSA-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
