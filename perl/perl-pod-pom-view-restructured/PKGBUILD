# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

_perlmod=pod-pom-view-restructured
_module_namespace="Pod-POM-View-Restructured"
pkgname=perl-$_perlmod
pkgver=1.000003
pkgrel=1
pkgdesc="Converts Pod to RST format"
arch=('any')
url="https://metacpan.org/dist/Pod-POM-View-Restructured"
license=(
  'GPL-1.0-or-later'
  'Artistic-2.0'
)
depends=(
  'perl' # 'perl-carp' 'perl-data-dumper' 'perl-extutils-makemaker' 'perl-getopt-long'
  'perl-pod-pom' # 'perl-pod-pom-view-text'
)
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/A/AL/ALEXM/${_module_namespace}-${pkgver}.tar.gz")
sha512sums=('b3578ea86ef1c690bf652a11e2ba866c54b40e5b287d0dffeb7888de587ebc5f856826502940a23a22aeb5c9f943615d8a3f4b66048b691fea347a299b9c395b')
b2sums=('c5bea201da7dbdfb2db90ec605b18745e498d722cfda89dea67224f1a3bcab4fc68f9034838ab33481082f887aafd133f31709ef830095f0e4ab5e8ec0bcd0e6')

build() {
  cd "$_module_namespace-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

package() {
  cd "$_module_namespace-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
