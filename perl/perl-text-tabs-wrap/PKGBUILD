# Maintainer: George Rawlinson <grawlinson@archlinux.org>

_perlmod=text-tabs-wrap
_module_namespace="Text-Tabs+Wrap"
pkgname=perl-$_perlmod
pkgver=2024.001
pkgrel=2
pkgdesc="Format tabs & lines in documents"
arch=(any)
url="https://metacpan.org/dist/Text-Tabs+Wrap"
license=('TTWL')
depends=('perl')
provides=('perl-text-tabs' 'perl-text-wrap')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/A/AR/ARISTOTLE/${_module_namespace}-${pkgver}.tar.gz")
sha512sums=('54399cdc8acf435c0c80ed9d3128438af043fbacb60a1252e806de339ea2c1b53dca544bbfcc9bf97ba07cfde23b5e82147b4b4066e42e5c1bb61220d6a9c888')
b2sums=('40dc8aa855d922ae52228784d15d6989fccd63aa31dc02b29d2fccb3b5e8babbda5d0559e60ecb0788d3c49f8dbee1f5cc3f0a24e81181443a365204b69a6144')

prepare() {
  cd "$_module_namespace-$pkgver"

  # extract licenses
  sed \
    -e '1,/=head1 LICENSE/ d' \
    -e '/^$/d' lib.modern/Text/Tabs.pm \
    > Text-Tabs.LICENSE
  sed \
    -e '1,/=head1 LICENSE/ d' \
    -e '/^$/d' lib.modern/Text/Wrap.pm \
    > Text-Wrap.LICENSE
}

build() {
  cd "$_module_namespace-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

package() {
  cd "$_module_namespace-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" ./*.LICENSE
}

# vim:set ts=2 sw=2 et:
