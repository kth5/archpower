# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florian Pritz <bluewind@archlinux.org>

pkgname=perl-cgi
pkgver=4.68
pkgrel=1
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
url='https://metacpan.org/dist/CGI'
license=('Artistic-2.0')
depends=('perl>=5.8.1' 'perl-html-parser')
checkdepends=('perl-test-nowarnings' 'perl-test-warn>=0.3')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
sha512sums=('675dee1381aa15cdede45661d1a93b3b6a0ed8fe42bd669317266537a4ac39747f17c4bf02d8b5568d10216b6d1f5f8230102a36957a0abb2aabe0af47b2c965')
b2sums=('6baeb9f4f4f4275bdb663b21fc38518aaf3450f86ccbecbe105ff823798f2805877909c7b3462b051d5f71dfd1abcaf5a15dd8de9b7968a18b56ef4697b0b1ae')

build() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
