# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florian Pritz <bluewind@archlinux.org>

pkgname=perl-cgi
pkgver=4.71
pkgrel=1
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
url='https://metacpan.org/dist/CGI'
license=('Artistic-2.0')
depends=('perl>=5.8.1' 'perl-html-parser')
checkdepends=('perl-test-nowarnings' 'perl-test-warn>=0.3')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
sha512sums=('6e8c4c0fa95062cc515c6bae44e57235e430b93fcf5b5196e3e731d4def152ba3eb9f946b40ae058845216345f285e24c6df1eea89cbfab1df7838c5080eb889')
b2sums=('60e47219c55aa7a245a4d1bb624a693c3e0ebaccdafe9bbdc3c5555b3610d36d78347efacc96d8992c3d06cd3ef78d4ec2a3ee0d28b65ec5852f3b3aea3f9f41')

build() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
