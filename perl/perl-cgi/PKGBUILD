# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florian Pritz <bluewind@archlinux.org>

pkgname=perl-cgi
pkgver=4.70
pkgrel=2
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
url='https://metacpan.org/dist/CGI'
license=('Artistic-2.0')
depends=('perl>=5.8.1' 'perl-html-parser')
checkdepends=('perl-test-nowarnings' 'perl-test-warn>=0.3')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
sha512sums=('090cb28a759fdce87d5fa38bcd460445c505651dc52eebc9787888bab93fdca3f4958907bc94f221ee2ff2711940cf32d0e3d74ed20c5da76257e2a2a51a6065')
b2sums=('46e64fb52a2b8ecea54c6e9c392aa37bf2bbe97b648d1cf87a2e1c1617756f2215d1f8b4274d3f795a99cf2cc56d429f53699bd4885c5c7078e1ce47088ec974')

build() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "CGI-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
