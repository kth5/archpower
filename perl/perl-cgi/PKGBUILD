# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@archlinux.org>

pkgname=perl-cgi
pkgver=4.67
pkgrel=1
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' perl-html-parser)
checkdepends=(perl-test-nowarnings 'perl-test-warn>=0.3')
url=https://metacpan.org/release/CGI
source=("https://cpan.metacpan.org/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
md5sums=('3b3e504674399ef6e380807ddee80f6e')
sha512sums=('dc9f0b2c50e24eeaf3068f65aef7e0e5582ce8e1b004e79b89bf4d6067223d82b33bba64900d1eab5c78f318c151499ef6842ec8d32ed354dfbf3e43d2cb1d93')
_ddir="CGI-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
