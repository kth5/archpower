# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-par-dist
pkgver=0.52
pkgrel=1
pkgdesc="Perl bindings to create and manipulate PAR distributions"
arch=(any)
url="https://search.cpan.org/dist/PAR-Dist/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/PAR-Dist-$pkgver.tar.gz")
sha512sums=('46c79a3156bb66bcb8a63dfb53d67de01624bf1c5ca72c81ebb26820485b8144c13c4a9c6a4ad6475c328d2fbd10fa2d7889a2c89590cab7405fbe688b789864')

build() {
  cd "$srcdir/PAR-Dist-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/PAR-Dist-$pkgver"
  make test
}

package() {
  cd "$srcdir/PAR-Dist-$pkgver"
  make install DESTDIR="$pkgdir"
}
