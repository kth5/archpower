# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-par-dist
pkgver=0.53
pkgrel=1
pkgdesc="Perl bindings to create and manipulate PAR distributions"
arch=(any)
url="https://search.cpan.org/dist/PAR-Dist/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/PAR-Dist-$pkgver.tar.gz")
sha512sums=('19dcc9e6afe5de749d68da1f02b622c70c463fa00cedcda454a9efcd31403bd1e39cd29c8c4bf0d5be742cd6d340f8423663f2f30c6cd994a192f8e1fa55a64a')

build() {
  cd "$srcdir/PAR-Dist-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/PAR-Dist-$pkgver"
  make test
}

package() {
  cd "$srcdir/PAR-Dist-$pkgver"
  make install DESTDIR="$pkgdir"
}
