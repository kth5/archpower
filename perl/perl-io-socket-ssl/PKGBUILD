# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-io-socket-ssl
pkgver=2.094
pkgrel=2
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/IO-Socket-SSL/"
depends=('perl-net-ssleay' 'perl' 'perl-uri')
checkdepends=('perl-io-socket-inet6')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/S/SU/SULLR/IO-Socket-SSL-${pkgver}.tar.gz)
sha512sums=('9bab926baa425c557d2d020d5e31c9464f461e36e52e5d671261d91053e00a4c5b5c033c364ef52c90741384808d34420ee3b7ded50263082c9e13f83c0a21b3')

build() {
  cd IO-Socket-SSL-${pkgver}
  yes | perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Socket-SSL-${pkgver}
  make test || warning "TLS 1.3 related errors"
}

package() {
  cd IO-Socket-SSL-${pkgver}
  make DESTDIR="${pkgdir}" install
}
