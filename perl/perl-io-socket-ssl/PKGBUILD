# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-io-socket-ssl
pkgver=2.096
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/IO-Socket-SSL/"
depends=('perl-net-ssleay' 'perl' 'perl-uri')
checkdepends=('perl-io-socket-inet6')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/S/SU/SULLR/IO-Socket-SSL-${pkgver}.tar.gz)
sha512sums=('baf7996421fb8234953475edc95c14e2a14fb3db07f00c6adaae70d7417277b50647850e368d815b1344cfdb1ee591b264bb0996ec900e3e7ee87fee4fb3b0ee')

build() {
  cd IO-Socket-SSL-${pkgver}
  yes | perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Socket-SSL-${pkgver}
  make test || warning "TLS 1.3 related errors"
}

package() {
  cd IO-Socket-SSL-${pkgver}
  make DESTDIR="${pkgdir}" install
}
