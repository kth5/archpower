# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-image-exiftool
# We only package "production" releases
pkgver=13.44
pkgrel=1
pkgdesc="Read and write EXIF information"
arch=(any)
url="https://exiftool.org/"
license=('Artistic-1.0-Perl OR GPL-3.0-or-later')
makedepends=('git')
depends=('perl')
optdepends=('perl-archive-zip: Support for zip files, including epub'
            'perl-io-compress-brotli: Support for brotli-compressed metadata')
provides=(
  exiftool
  "perl-exiftool=${pkgver}"
)
replaces=("perl-exiftool")
options=('!emptydirs')
source=("git+https://github.com/exiftool/exiftool.git#tag=${pkgver}")
sha256sums=('a3bcef2ed40e52ad8b9f90674958ee76ec6bb4bf66f2f1d960c3d6753a1b186e')

build() {
  cd exiftool
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd exiftool
  make test
}

package() {
  cd exiftool
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -r fmt_files config_files arg_files "${pkgdir}/usr/share/${pkgname}"
}
