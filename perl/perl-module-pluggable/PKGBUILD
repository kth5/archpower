# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-module-pluggable
pkgver=6.2
pkgrel=1
pkgdesc='automatically give your module the ability to have plugins'
arch=(any)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='https://metacpan.org/release/Module-Pluggable'
source=("https://search.cpan.org/CPAN/authors/id/S/SI/SIMONW/Module-Pluggable-$pkgver.tar.gz")
md5sums=('02051fa6f1473a067276ca2e26e3da45')
sha512sums=('3d4b046d105406b7b4cbf66387603321f28cb7e1df6bf9d90c3762f1a1d44b8aed532591098fb158c49678a23d2ee8166d58b0badcaa072ba7bb96b4deb5e07c')
_ddir="Module-Pluggable-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
