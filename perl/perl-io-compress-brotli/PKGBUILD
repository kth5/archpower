# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-io-compress-brotli
epoch=1
pkgver=0.017
pkgrel=1
pkgdesc='Read/write Brotli buffers/streams'
url="https://metacpan.org/release/IO-Compress-Brotli"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'brotli' 'perl-file-slurper')
source=("https://search.cpan.org/CPAN/authors/id/T/TI/TIMLEGGE/IO-Compress-Brotli-$pkgver.tar.gz"
        use-system-brotli.patch)
sha512sums=('a20c6a569fef2820dc089175de90a7e889e6f4093552ab9579c2520f51c87863b55c796b3b1bd12839d173268d05625caef5f9f95eef977cfb7c0280f82bbce0'
            'fd131f26c93fb67eaffbbc4d36537931bd9efe780ccbbf3904174d3bd6e6810b18be286b6e3425462089039acd199646464444b0f1300473ee2f905b32869a32')

prepare() {
  cd IO-Compress-Brotli-$pkgver
  patch -p0 -i ../use-system-brotli.patch
}

build() {
  cd IO-Compress-Brotli-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Compress-Brotli-$pkgver
  make test
}

package() {
  cd IO-Compress-Brotli-$pkgver
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
