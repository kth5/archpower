# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-io-compress-brotli
epoch=1
pkgver=0.019
pkgrel=1
pkgdesc='Read/write Brotli buffers/streams'
url="https://metacpan.org/release/IO-Compress-Brotli"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'brotli' 'perl-file-slurper')
source=("https://search.cpan.org/CPAN/authors/id/T/TI/TIMLEGGE/IO-Compress-Brotli-$pkgver.tar.gz"
        use-system-brotli.patch)
sha512sums=('d81fb94dfbdae24af3985349ccccfac9d5a863452b173542f7718e3a1ffcf18819b4456e2e0c305afabe0eeb336c0ad648d4d3f0fdb1f6229e82e0fad4751628'
            '0a1993afd163e11cec04a5a305d0035b3fd98071ed051672e3adb46355e0809a559292e025e01bec02c42fe8015bccd2f1dbe3278c170ffdae75cb477c032dd8')

prepare() {
  cd IO-Compress-Brotli-$pkgver
  patch -p0 -i ../use-system-brotli.patch
}

build() {
  cd IO-Compress-Brotli-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Compress-Brotli-$pkgver
  make test
}

package() {
  cd IO-Compress-Brotli-$pkgver
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
