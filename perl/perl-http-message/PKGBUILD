# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-http-message
pkgver=7.01
pkgrel=1
pkgdesc="HTTP style messages"
arch=(any)
url='https://search.cpan.org/dist/HTTP-Message'
license=('Artistic-1.0-Perl OR GPL-1.0-or-later')
options=('!emptydirs')
depends=('perl' 'perl-clone' 'perl-encode-locale' 'perl-http-date' 'perl-io-html'
         'perl-lwp-mediatypes' 'perl-uri')
checkdepends=('perl-io-compress-brotli' 'perl-test-needs' 'perl-try-tiny')
conflicts=('perl-libwww<6')
source=("https://github.com/libwww-perl/HTTP-Message/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('642827a52c82b904c95a96c5bf9b00ed2beecd4414c24c19ae44d40090284da00e993987f4468b7568265a9e5ca19566cd63be202d80adc7253abc6d9c7ac4af')

build() {
  cd HTTP-Message-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd HTTP-Message-$pkgver
  make test
}

package() {
  cd HTTP-Message-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
