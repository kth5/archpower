# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-path-tiny
pkgver=0.148
pkgrel=1
pkgdesc='File path utility'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1')
url=https://metacpan.org/release/Path-Tiny
source=("https://cpan.metacpan.org/authors/id/D/DA/DAGOLDEN/Path-Tiny-$pkgver.tar.gz")
md5sums=('b6a3eae2cea1fa4336f6c8fcff946636')
sha512sums=('2adc60d3872ebd53dff4197b6c8fe4132f6bf3a5de4f27ef0d5885e8f2cdcc88d0fbce3bd12f2b547681d0cd59fd8ce9a7eaf896a580f54089f118a28e0a0393')
_ddir="Path-Tiny-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
