# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-path-tiny
pkgver=0.146
pkgrel=2
pkgdesc='File path utility'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1')
url=https://metacpan.org/release/Path-Tiny
source=("https://cpan.metacpan.org/authors/id/D/DA/DAGOLDEN/Path-Tiny-$pkgver.tar.gz")
md5sums=('656d3556bb7f30c77d0881d564e200b2')
sha512sums=('8939e7a42a2539db98825f2755399fb5eb38aac9e6ad17ec71d51edad99ddb4d5d891ef0119b57a79e4fa84c43faedff2a05c705a1e6d731f77d49798d6dc94e')
_ddir="Path-Tiny-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
