# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-crypt-openssl-random
pkgver=0.17
pkgrel=3
pkgdesc='Routines for accessing the OpenSSL pseudo-random number generator'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(PerlArtistic GPL)
options=(!emptydirs)
makedepends=('perl-crypt-openssl-guess>=0.11')
depends=(perl openssl )
url=https://metacpan.org/release/Crypt-OpenSSL-Random
source=("https://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/Crypt-OpenSSL-Random-$pkgver.tar.gz")
md5sums=('294f545c54a5a4855cb00c5648eb5c2b')
sha512sums=('a73a9c5c7d361467dace1ea34169e211ba7a11e29c84e9a87568a3cccf28c4cf317c39ee9c4b697e06dac408bb4c2446dd948289da9a77daf8283f27abdc2887')
_ddir="Crypt-OpenSSL-Random-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
