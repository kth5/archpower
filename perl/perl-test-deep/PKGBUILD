# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-test-deep
_dist='Test-Deep'
pkgver=1.205
pkgrel=1
pkgdesc="Extremely flexible deep comparison"
arch=(any)
url="https://search.cpan.org/dist/Test-Deep"
license=('GPL-1.0-or-later OR Artistic-1.0-Perl')
depends=('perl-test-nowarnings' 'perl-test-simple')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Test-Deep-$pkgver.tar.gz")
sha512sums=('ffdbad8361841e02bad99cad2e767b13bc5b94c11a85a2e10dc924b1268fa951931af22e7005009f3c9064090645f958eb9ccc098d16ecb00d1337a3a26c8d35')
b2sums=('ed53adc0eab9cdfa5589af3c36e1a67d91a429c392b6517bb674505a6ee4fc7be516cd463563f437706eb410f5761e8090af37b9e87fe9b85b89a5c23394f6fb')

build() {
  cd "${_dist}-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${_dist}-${pkgver}"

  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
