# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname='perl-extutils-libbuilder'
_dist="ExtUtils-LibBuilder"
pkgver=0.09
pkgrel=2
pkgdesc="A tools to build C libraries"
arch=(any)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-module-build')
url="https://metacpan.org/release/${_dist}"
source=("https://search.cpan.org/CPAN/authors/id/A/AM/AMBS/${_dist}-${pkgver}.tar.gz")
sha256sums=('dbfac85d015874189a704fa0a2f001d13b5a0c7d89f36c06ff32d569720a6cfb')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    perl Build.PL
    ./Build
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    ./Build test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    ./Build install installdirs=vendor destdir="$pkgdir"
}
