# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-file-copy-recursive-reduced
pkgver=0.007
pkgrel=1
pkgdesc="Recursive copying of files and directories within Perl 5 toolchain"
arch=(any)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-capture-tiny' 'perl-path-tiny')
url='https://search.cpan.org/dist/File-Copy-Recursive-Reduced'
source=("https://search.cpan.org/CPAN/authors/id/J/JK/JKEENAN/File-Copy-Recursive-Reduced-$pkgver.tar.gz")
sha512sums=('7b2029acc3cd4cf5775df60b0d716da3868283bbce4bf4717dcd95fad112973ccd4df25ab88312d5a2a4f332399312d412bb2fd81245f843a619eae0ee5be085')

build() {
  cd "$srcdir/File-Copy-Recursive-Reduced-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/File-Copy-Recursive-Reduced-$pkgver"
  make test
}

package() {
  cd "$srcdir/File-Copy-Recursive-Reduced-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
