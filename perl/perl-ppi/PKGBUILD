# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppi
pkgver=1.281
pkgrel=1
pkgdesc="Parse, Analyze and Manipulate Perl (without perl)"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/PPI"
depends=('perl-clone' 'perl-io-string' 'perl-list-moreutils' 'perl-params-util' 'perl-task-weaken' 'perl-safe-isa' 'perl-yaml-pp')
makedepends=('perl-class-inspector' 'perl-file-remove' 'perl-test-deep' 'perl-test-object'
             'perl-test-subcalls')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MI/MITHALDU/PPI-$pkgver.tar.gz")
sha512sums=('feabf11a55ba42cf08fc5ca6606d6ac412a68ca6ba71246cf9ebc824f03ac9ee6880e3fb0d84146576b462da80e5d56ec8de297618c089e75589f05b4f3b939e')

build() {
  cd PPI-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPI-$pkgver
  make test
}

package() {
  cd PPI-$pkgver
  make DESTDIR="$pkgdir" install
}
