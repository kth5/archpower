# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppi
pkgver=1.283
pkgrel=2
pkgdesc="Parse, Analyze and Manipulate Perl (without perl)"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/PPI"
depends=('perl-clone' 'perl-io-string' 'perl-list-moreutils' 'perl-params-util' 'perl-task-weaken' 'perl-safe-isa' 'perl-yaml-pp')
makedepends=('perl-class-inspector' 'perl-file-remove' 'perl-test-deep' 'perl-test-object'
             'perl-test-subcalls')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MI/MITHALDU/PPI-$pkgver.tar.gz")
sha512sums=('97f108567f1351afc871d824d38f003ab2e7542493ec7debd93685c792a92a504dc961796a03492ec8cb7a85f3ffd3b3d460fd88d2865d8bb9f834470107651a')

build() {
  cd PPI-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd PPI-$pkgver
  make test
}

package() {
  cd PPI-$pkgver
  make DESTDIR="$pkgdir" install
}
