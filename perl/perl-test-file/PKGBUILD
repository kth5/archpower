# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Dupuis <lisael@lisael.org>
# Contributor: Rafael Reggiani Manzo <rr.manzo@gmail.com>

pkgname=perl-test-file
pkgver=1.995
pkgrel=3
pkgdesc="A collection of test utilities for file attributes."
arch=(any)
url='https://github.com/briandfoy/test-file'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
source=("https://search.cpan.org/CPAN/authors/id/B/BR/BRIANDFOY/Test-File-$pkgver.tar.gz")
sha512sums=('25038672ae5bc50832972168292e47898eca8b3a6ec951da5eed2e3124fe4ceb7a4447e847ce0696e89ab4c320d60ff71a559ba403b4e3d331d7cab19844ed6d')

build() {
  cd Test-File-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-File-$pkgver
  make test
}

package() {
  cd Test-File-$pkgver
  make DESTDIR="$pkgdir" install
}
