# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ev
pkgver=4.34
pkgrel=3
pkgdesc='perl interface to libev, a high performance full-featured event loop'
arch=(x86_64 powerpc64le powerpc)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-common-sense')
makedepends=('perl-canary-stability')
url='https://metacpan.org/release/EV'
source=("https://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/EV-$pkgver.tar.gz"
         fix-build.patch)
sha512sums=('938f675ec960be40ebe98416450b2d8866581250560b4b6781cd36926e360ab2b3f8d4cc39145ed068753b9952e5c220316cefd4d99a6ca63c2bfde56329306e'
            'c6d110922b2cf4d5d333aefc5c5d64fbaf01625098699e564ca868ced4a543fea777f27c81b3a93def10da711b3ab3c38ac296e9b55b7abef7ea04bd7bd308ab')

prepare() {
  cd EV-$pkgver
  patch -p1 -i ../fix-build.patch # Fedora patch
}

build() {
  cd EV-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd EV-$pkgver
  make test
}

package() {
  cd EV-$pkgver
  make install DESTDIR="$pkgdir"
}
