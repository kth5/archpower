# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ev
pkgver=4.36
pkgrel=1
pkgdesc='perl interface to libev, a high performance full-featured event loop'
arch=(x86_64 powerpc64le powerpc)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-common-sense')
makedepends=('perl-canary-stability')
url='https://metacpan.org/release/EV'
source=("https://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/EV-$pkgver.tar.gz"
         fix-new-parseXS.patch)
sha512sums=('76c5c5e058c8e6bd5c23d82b14218e0a66e9c5fc8c902bb0c0860c05cb1288728c96c9bfc1bb647b5d13de6f22bc2e10114a1bfd3863471ea84fc27f79dc64e7'
            '1eef112d5061826b75edf5ac5b6dd714028839dabc7ee744ccdebf9019a15937447d2132abd38553055caba8fe81cc31da6c5fedb6f06b33fb8b43c963e44ed1')

prepare() {
  cd EV-$pkgver
  patch -i ../fix-new-parseXS.patch # https://rt.cpan.org/Public/Bug/Display.html?id=156069
}

build() {
  cd EV-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd EV-$pkgver
  make test
}

package() {
  cd EV-$pkgver
  make install DESTDIR="$pkgdir"
}
