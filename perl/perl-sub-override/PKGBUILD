# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=perl-sub-override
_cpanname=Sub-Override
pkgver=0.11
pkgrel=1
pkgdesc='Perl extension for easily overriding subroutines'
url='https://metacpan.org/dist/Sub-Override'
arch=(any)
license=(Artistic-1.0-Perl)
depends=(
  perl
  perl-sub-prototype
)
checkdepends=(
  perl-test-fatal
)
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/M/MV/MVSJES/${_cpanname}-${pkgver}.tar.gz)
sha512sums=('b9a3c06dea8f2e050d6749839828d965fab98c01eebb2278c7158a237a5368d09f18843a0a9664617c3f3896128d5c23633d4bea6a075f1832a5f0711ff66536')
b2sums=('317e7c831bf2187ae8195696ea31c44a1f0863068fc09e59bc2cc4fe39f18ded7e2602665d889119136857cebfe524f3dec09b0b879ce4f5fd1e53d5ee9f4686')

build() {
  cd ${_cpanname}-${pkgver}
  export PERL_MM_USE_DEFAULT=1
  export PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_cpanname}-${pkgver}
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd ${_cpanname}-${pkgver}
  make DESTDIR="${pkgdir}" install INSTALLDIRS=vendor
  install -Dm 644 README* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
