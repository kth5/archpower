# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=perl-sub-override
_cpanname=Sub-Override
pkgver=0.10
pkgrel=1
pkgdesc='Perl extension for easily overriding subroutines'
url='https://search.cpan.org/dist/Term-ExtendedColor'
arch=(any)
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-fatal')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/O/OV/OVID/${_cpanname}-${pkgver}.tar.gz)
sha512sums=('27782c301c7abd30291be8af79a251d2be27b7616f950c28cb83ceeeb6a91eb69fe29413992c229afb6d75974851e69986eaa232aa81484ad49fcad55b2bdead')
b2sums=('de2d19421280b4badaae0e1aea386c343d19d92023b4bd0aa9e2f92a66026e7b61361aeb3f724e30f842e1389260e6c7e85ab284b529bdf759e41d40bc4b8ebb')

build() {
  cd ${_cpanname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_cpanname}-${pkgver}
  make test
}

package() {
  cd ${_cpanname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
