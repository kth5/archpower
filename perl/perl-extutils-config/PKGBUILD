# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname=perl-extutils-config
_pkgname=ExtUtils-Config
pkgver=0.010
pkgrel=1
pkgdesc="ExtUtils::Config - A wrapper for perl's configuration"
arch=(any)
license=('PerlArtistic' 'GPL')
url="https://search.cpan.org/dist/ExtUtils-Config/"
options=(!emptydirs)
depends=('perl-data-dumper')
makedepends=('perl')
source=("https://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('64fdbe069388c4ae080622ea06ed6b3d8bd2d71e1af91dcdb53e9a99337212071d7758f9d1a1d4e032afdf5efa26b83374919c6d6a7bbc26bde75dcb7b81046a')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_pkgname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
