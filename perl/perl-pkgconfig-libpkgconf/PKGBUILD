# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pkgconfig-libpkgconf
pkgver=0.11
pkgrel=11
pkgdesc="Perl bindings for libpkgconf"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('Artistic-1.0-Perl OR GPL-1.0-or-later')
url="https://metacpan.org/release/PkgConfig-LibPkgConf"
depends=('glibc' 'perl' 'pkgconf')
options=('!emptydirs')
source=(
  "https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/PkgConfig-LibPkgConf-$pkgver.tar.gz"
  0001-Adapt-to-pkgconf-1.9.4.patch
  0002-Get-tests-to-pass.patch
  0003-Adapt-to-pkgconf-2.5.0.patch
)
sha512sums=('d9205b364c03dd9ab15128f65e4fbf79c81e3dbd8112d2d94c886e521aa8691907d490698c75c43cb2167ee0d96cf76c36c9930f032f65c5785257b658ca55d4'
            '6cd21fb447bc7ca8f3450349a76eedc8a6be7cc5a60106a36b593929887e1082bf4a0d63a41ad0440daa48d73829b6c7ae8c536402d47ae07214000d9913bf1f'
            '429d113470efdc7277cda8f52bcf4054f9f896b1b219bc3909b3f32677fba41e5b570c5271fd6a0ae17ff48ffa0926fe7367f8c9a6d4c661e73afd69e8861271'
            '7e9c5c275ca897ad8792aadbe1573be75d6fb49e6c80015f4d67f897e519fddb5d0df80b784fbaeba1d60c796bffcca217d3aa92e5b80e44a6591f4786bc3a7d')

prepare() {
  cd PkgConfig-LibPkgConf-$pkgver

  # Patch for compat with pkgconf 2.0
  # https://github.com/PerlAlien/PkgConfig-LibPkgConf/pull/17
  patch -Np1 -i ../0001-Adapt-to-pkgconf-1.9.4.patch

  # Further changes to get tests to pass
  patch -Np1 -i ../0002-Get-tests-to-pass.patch

  # Patch for compat with pkgconf 2.5
  patch -Np1 -i ../0003-Adapt-to-pkgconf-2.5.0.patch
}

build() {
  cd PkgConfig-LibPkgConf-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PkgConfig-LibPkgConf-$pkgver
  # https://github.com/PerlAlien/PkgConfig-LibPkgConf/pull/17#issuecomment-2161488848
  # Tests are still failing, but a rebuild is needed to fix the segfault
  make test || echo "Tests failed"
}

package() {
  cd PkgConfig-LibPkgConf-$pkgver
  make DESTDIR="$pkgdir" install
}
