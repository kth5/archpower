# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-build
pkgver=2.84
pkgrel=1
pkgdesc="Build external dependencies for use in CPAN"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Build"
depends=('perl-capture-tiny' 'perl-ffi-checklib' 'perl-file-chdir' 'perl-file-which'
         'perl-path-tiny')
makedepends=('perl-test2-suite')
checkdepends=('perl-alien-base-modulebuild' 'perl-alien-cmake3' 'perl-env-shellwords'
              'perl-pkgconfig' 'perl-pkgconfig-libpkgconf' 'perl-readonly' 'perl-sort-versions'
              'wget')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Build-$pkgver.tar.gz")
sha512sums=('48f67543e44651f73d4f7668ea4e34d2b522cf8ca6cf4eb0713f13809d5876ed1c1439340e3f0277cac989e17bc03fc0d7671bc26272a39e7fbeb7cef5862784')

build() {
  cd Alien-Build-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-Build-$pkgver
  make test
}

package() {
  cd Alien-Build-$pkgver
  make DESTDIR="$pkgdir" install
}
