# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cairo-perl
pkgver=1.109
pkgrel=6
pkgdesc="Perl wrappers for cairo"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('cairo' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(https://downloads.sourceforge.net/sourceforge/gtk2-perl/Cairo-${pkgver}.tar.gz
        no-incompatible-pointers.patch)
sha512sums=('b80841bff480acf95f69659cba02ca17c79b67a2dc5c9b9302efb15bbd79bd4bc2f1801c001c220723da11081e86a08a83e07ecc8866810ebf9406e1f6fa1c53'
            'f1e5f813c42b12bffdee1f44d6a4fbc95021132a35fa62468d189c4673d782df4fc3bd414c9539ab1c44dd366ff80856ace4359ca1394af16c4dc63605e30f49')

prepare() {
  cd Cairo-$pkgver
  patch -Np1 -i ${srcdir}/no-incompatible-pointers.patch
}

build() {
  cd Cairo-$pkgver

  case "${CARCH}" in
    powerpc) export CFLAGS+=' -Wno-incompatible-pointer-types' ;;
  esac

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Cairo-$pkgver
  make test
}

package() {
  cd Cairo-$pkgver
  make DESTDIR="${pkgdir}" install
}
