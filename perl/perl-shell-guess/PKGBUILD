# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-shell-guess
pkgver=0.10
pkgrel=1
pkgdesc="Make an educated guess about the shell in use"
arch=(any)
license=('PerlArtistic')
url="https://metacpan.org/release/Shell-Guess"
depends=('perl')
checkdepends=('tcsh')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Shell-Guess-$pkgver.tar.gz")
sha512sums=('060caba58126e4eb7f1f87ce62760d580a2d8b395ddba0bbdce6d96a73b2eab170ab4148f2560b47cda60fa57ff34c6ac5a80dfb8c56cb040ea4c0469fd5489b')

build() {
  cd Shell-Guess-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Shell-Guess-$pkgver
  make test
}

package() {
  cd Shell-Guess-$pkgver
  make DESTDIR="$pkgdir" install
}
