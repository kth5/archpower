# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-uri
pkgver=5.21
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=(any)
url="https://search.cpan.org/dist/URI/"
license=('PerlArtistic')
depends=('perl')
checkdepends=('perl-test-fatal' 'perl-test-needs' 'perl-test-warnings')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/URI-$pkgver.tar.gz")
sha512sums=('0fb15c93d6bfd1842a3129d31ffd4d7c9fb9c735d5f45b5aaa07609edecd1b16191b7475aebd013c7a0e92894000d6982503654d306d82d477b52b33b058644f')

build() {
  cd URI-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd URI-$pkgver
  make test
}

package() {
  cd URI-$pkgver
  make install DESTDIR="$pkgdir"
}
# vim: ts=2 sw=2 et ft=sh
