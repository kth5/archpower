# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-mail-spf
pkgver=3.20250505
pkgrel=2
pkgdesc="Perl module that provides SPF support"
arch=('any')
license=('custom')
url="https://search.cpan.org/dist/Mail-SPF/"
depends=('perl-error' 'perl-netaddr-ip' 'perl-uri' 'perl-net-dns' 'perl')
makedepends=('perl-module-build' 'perl-net-dns-resolver-programmable')
conflicts=('perl-mail-spf-query')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/A/AD/ADAVIS/Mail-SPF-${pkgver}.tar.gz")
sha256sums=('9ac60d00b957e849bafe90a8defdeee2e5ffab1c87ac5a4abc452875e9904863')

build() {
  cd Mail-SPF-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Mail-SPF-${pkgver}
  make test
}

package() {
  cd Mail-SPF-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
