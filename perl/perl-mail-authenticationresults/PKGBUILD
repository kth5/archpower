# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-mail-authenticationresults
_pkgname=Mail-AuthenticationResults
pkgver=2.20250709
pkgrel=3
pkgdesc='Object Oriented Authentication-Results Headers'
arch=(any)
url='https://metacpan.org/release/Mail-AuthenticationResults'
license=(GPL-1.0-or-later Artistic-1.0-Perl)
depends=(perl perl-clone perl-json)
checkdepends=(perl-test-exception)
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/M/MB/MBRADSHAW/Mail-AuthenticationResults-$pkgver.tar.gz")
sha512sums=('79b691daa41a2c265243e4a25b9b3bf3815187549b2213272b04f2c2335c3e536441b17c878c4b8068c2367a9912358072c15695d9d6ef5d02b5dc48cfac969b')
b2sums=('6c2bdf6bf7021bae059b07813ffae85d7b2d65ff6d2473651fb3210d210290bc2f18edccc64ea576f802355c8fff9d16ebe577c0571fa7e173438c011e1d5b79')

build() {
  cd "$_pkgname-$pkgver"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  perl Makefile.PL
  make
}

check() {
  cd "$_pkgname-$pkgver"

  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "$_pkgname-$pkgver"

  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
# vim:set ts=2 sw=2 et:
