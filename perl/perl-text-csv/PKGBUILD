# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=perl-text-csv
_dist=Text-CSV
pkgver=2.04
pkgrel=1
pkgdesc="comma-separated values manipulator"
url="https://metacpan.org/dist/$_dist"
arch=(any)
license=(
  GPL
  PerlArtistic
)
options=(!emptydirs)
depends=(perl)
source=("https://cpan.metacpan.org/authors/id/I/IS/ISHIGAKI/$_dist-$pkgver.tar.gz")
b2sums=('467149c73736d063b9f93a0d9cda8bd2625805702f92559fac91054df0d7948bf70a5cc5a0864bd47649653dd3c83e2d9a7264949a72f72dcfd0235c8930e6e3')

unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT

build() {
    cd $_dist-$pkgver
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd $_dist-$pkgver
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd $_dist-$pkgver
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# vim:set sw=2 sts=-1 et:
