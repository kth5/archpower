# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:

pkgname=perl-cpan-meta-requirements
_pkgname=CPAN-Meta-Requirements
pkgver=2.143
pkgrel=2
pkgdesc='A set of version requirements for a CPAN dist'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url='https://metacpan.org/dist/CPAN-Meta-Requirements'
source=(https://cpan.metacpan.org/authors/id/R/RJ/RJBS/$_pkgname-$pkgver.tar.gz)
sha512sums=('01283e54753678bbc731c254f84b989a104a7228faf8a6bc33561b1846d2c410e1ec9219b7ebc073bd41b7e025b22fd96893a6f83f3bf2b07e1eb5766a76833c')

build() {
  cd $_pkgname-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd $_pkgname-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd $_pkgname-$pkgver
  make install DESTDIR="${pkgdir}"
  rm -r "$pkgdir"/usr/lib
}
