# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=glib-perl
pkgver=1.329.4
_pkgver=1.3294
pkgrel=1
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(https://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${_pkgver}.tar.gz)
sha256sums=('d715f5a86bcc187075de85e7ae5bc07b0714d6edc196a92da43986efa44e5cbb')

build() {
  cd Glib-$_pkgver
  PERL_USE_UNSAFE_INC=1 \
  perl Makefile.PL INSTALLDIRS=vendor
  PERL_USE_UNSAFE_INC=1 \
  make
}

check() {
  cd Glib-$_pkgver
  make test
}

package() {
  cd Glib-$_pkgver
  make DESTDIR="${pkgdir}" install
}
