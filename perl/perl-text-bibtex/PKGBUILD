# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-text-bibtex
_dist=Text-BibTeX
pkgver=0.91
pkgrel=1
pkgdesc="Perl interface to read and parse BibTeX files"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://metacpan.org/release/${_dist}"
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-module-build'
             'perl-config-autoconf'
             'perl-extutils-libbuilder')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/A/AM/AMBS/${_dist}-${pkgver}.tar.gz)
# Checksums from http://search.cpan.org/CPAN/authors/id/A/AM/AMBS/CHECKSUMS
md5sums=('437862f801cfb7598dff52aa4ff7d888')
sha256sums=('3f0113cf8fe71dc7484636dc8e2a581637ecbcc82d0be29bbd46d0bf3f8cdb37')

prepare() {
    cd "${srcdir}/${_dist}-${pkgver}"
    # Don't install to /usr/lib64
    sed -i s,lib64,lib,g Build.PL
}

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Build.PL
    ./Build
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    ./Build test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    ./Build install installdirs=vendor destdir="$pkgdir"
}
