# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-crypt-openssl-bignum
pkgver=0.09
pkgrel=12
pkgdesc="OpenSSL's multiprecision integer arithmetic"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl openssl)
url=https://metacpan.org/release/Crypt-OpenSSL-Bignum
source=("https://search.cpan.org/CPAN/authors/id/K/KM/KMX/Crypt-OpenSSL-Bignum-$pkgver.tar.gz")
md5sums=(c8ae05771c70c69b3e5647b29a885ed2)
sha512sums=(3480a64e35e158a7a7c75c0e57ac0f578ddcb901a14f80a58ffabd1f318a68a83b411a72c22c53650273507198907b1d62c88fddbf5587885f6fe1a75deff3ed)
_ddir="Crypt-OpenSSL-Bignum-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
