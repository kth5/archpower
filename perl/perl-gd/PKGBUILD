# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-gd
pkgver=2.77
pkgrel=2
pkgdesc="Interface to Gd Graphics Library"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://search.cpan.org/dist/GD"
license=('GPL' 'PerlArtistic')
depends=('perl' 'gd')
makedepends=('perl-extutils-pkgconfig')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/GD-$pkgver.tar.gz)
sha512sums=('de794793bdd4d965bf42a5de0f0d622f037700d9db0e94a1cc20dad8dc1b35003dd15bf3352da1dc63072e079ee91367620806f992d0d3dd0c63baa016fa5268')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
    PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
    MODULEBUILDRC=/dev/null

  cd  "$srcdir"/GD-$pkgver
  perl Makefile.PL
  make
}

package() {
  cd  "$srcdir"/GD-$pkgver
  make install
}
