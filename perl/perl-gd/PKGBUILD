# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-gd
pkgver=2.79
pkgrel=1
pkgdesc="Interface to Gd Graphics Library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://search.cpan.org/dist/GD"
license=('GPL' 'PerlArtistic')
depends=('perl' 'gd')
makedepends=('perl-extutils-pkgconfig')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/GD-$pkgver.tar.gz)
sha512sums=('c2c1b298b469aa357aaab94a48bbff916e33a36d4d028ad5cb0c9dab8b59f304b261d08121f06512e221598d647d6c09a5360a17bd4378d06cfd3ee53c5fe03b')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
    PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
    MODULEBUILDRC=/dev/null

  cd  "$srcdir"/GD-$pkgver
  perl Makefile.PL
  make
}

package() {
  cd  "$srcdir"/GD-$pkgver
  make install
}
