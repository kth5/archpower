# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-gd
pkgver=2.84
pkgrel=1
pkgdesc="Interface to Gd Graphics Library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://search.cpan.org/dist/GD"
license=('GPL-1.0-or-later' 'Artistic-2.0')
depends=('perl' 'gd')
makedepends=('perl-extutils-pkgconfig' 'perl-file-which')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/GD-$pkgver.tar.gz)
sha512sums=('2de86591f9e1032389f79262649148e3a49f4633b1d749e8f02474d995f48942cc513a0c0ee0cac34030b75736e7e1ec9b0474d5ee5eea18ba40291f6ae3977c')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
    PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
    MODULEBUILDRC=/dev/null

  cd  "$srcdir"/GD-$pkgver
  perl Makefile.PL
  make
}

package() {
  cd  "$srcdir"/GD-$pkgver
  make install
}
