# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname=perl-file-sharedir-install
pkgver=0.14
pkgrel=5
pkgdesc="File::ShareDir::Install - Install read-only data files from a distribution"
arch=(any)
url="https://search.cpan.org/dist/File-ShareDir-Install"
license=('GPL' 'PerlArtistic')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.org/modules/by-module/File/File-ShareDir-Install-${pkgver}.tar.gz")
sha256sums=('8f9533b198f2d4a9a5288cbc7d224f7679ad05a7a8573745599789428bc5aea0')

build() {
	cd "File-ShareDir-Install-${pkgver}"
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "File-ShareDir-Install-${pkgver}"
	make test
}

package() {
	cd "File-ShareDir-Install-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
