# Maintainer: George Rawlinson <grawlinson@archlinux.org>

_perlmod=pod-pom
_module_namespace="Pod-POM"
pkgname=perl-$_perlmod
pkgver=2.01
pkgrel=2
pkgdesc="Parser to convert POD documents into Pod Object Models"
arch=(any)
url="https://metacpan.org/dist/Pod-POM"
license=(
  'GPL-1.0-or-later'
  'Artistic-2.0'
)
depends=(
  'perl'
  'perl-text-wrap'
)
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://cpan.metacpan.org/authors/id/N/NE/NEILB/${_module_namespace}-${pkgver}.tar.gz")
sha512sums=('617b1c15dbaee7dfd53f7b158c08e34c38763b11fbdbe44d3a1c65bfca15b44d6d1b19b6a59360dd62a7d9e703498e76237350fb57040f39837f8190fbdbd7b0')
b2sums=('50a0711469515f35cc98a5eaf537b35606bc9d49dac360944c4258516116ad4e9318a1a8d18ca943ebe14b626bd192aa5c6939882587f4ea735b001ee5135ccc')

build() {
  cd "$_module_namespace-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

package() {
  cd "$_module_namespace-$pkgver"

  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
