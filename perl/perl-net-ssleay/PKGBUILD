# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.94
pkgrel=3
pkgdesc='Perl bindings for OpenSSL and LibreSSL'
url='https://search.cpan.org/dist/Net-SSLeay/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('custom:BSD')
depends=('openssl')
makedepends=('chrpath')
replaces=('net-ssleay')
provides=('net-ssleay')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/C/CH/CHRISN/${_cpanname}-${pkgver}.tar.gz
        openssl-3.4.patch)
sha512sums=('f1461667269f3da56ff7e1724b5511b95c34582250f8336e9f93d30f3b30d4a5360eb27622b9730bba11df75e00d717c7f0cea2120d20206723db136a8509375'
            '51253f95461b41f95b4e3014cea7c9440951d01506078fafcec406e1dcc14fe8412e405d12a60ffdced4fe9f18115f5af5b4f492b50725e8cea163e78aef44b7')
b2sums=('97cef98bf3794ec992817e5ad91f8403ded95e29184512d9f23b5cbbd765912d2e7f8e835366b40aae9682dd13947cd79e2d9dcca2d10cd78e3c7869b1e197db'
        'f080109c0faf5d63fe5a8155a1cf89b897fc211820150eb23c898b63ca1dad758c45e5fa1a2482cd8c689f01b20bf32c00b9a8a593ffe4c26041dcb41b73ba90')

prepare() {
  cd ${_cpanname}-${pkgver}
  patch -p1 -i ../openssl-3.4.patch
}

build() {
  cd ${_cpanname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_cpanname}-${pkgver}
  make test
}

package() {
  cd ${_cpanname}-${pkgver}

  make install DESTDIR="${pkgdir}"
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # strip rpath
  find "${pkgdir}" -name '*.so' -exec chrpath -d '{}' \;
}

# vim: ts=2 sw=2 et:
