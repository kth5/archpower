# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Kevin <kevin@archlinux.org>
# Contributor: criminy <criminy@gmail.com>

pkgbase=cairomm-1.16
pkgname=(
  cairomm-1.16
  cairomm-1.16-docs
)
pkgver=1.18.0
pkgrel=2
pkgdesc="C++ bindings for Cairo"
url="https://www.cairographics.org/cairomm/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.0-or-later)
depends=(
  cairo
  gcc-libs
  glibc
  libsigc++-3.0
)
makedepends=(
  git
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://gitlab.freedesktop.org/cairo/cairomm.git#tag=$pkgver")
b2sums=('c567b0f8dd8dc1c02b85debf12100464e4c3dd61a5fc6619bc9d5aad1df7875a2cdf4b3671c161ba2c43e519e41f581ee805fec86503dc8930239e54e5bdaea0')

prepare() {
  cd cairomm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson cairomm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_cairomm-1.16() {
  depends+=(libsigc-3.0.so)
  provides=(libcairomm-1.16.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_cairomm-1.16-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
