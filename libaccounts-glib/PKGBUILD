# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-glib
pkgver=1.27
pkgrel=3
pkgdesc='Glib-based client library for accessing the online accounts database'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://gitlab.com/accounts-sso/libaccounts-glib'
license=(LGPL-2.1-or-later)
depends=(glib2
         glibc
         libxml2
         sqlite)
makedepends=(check
             glib2-devel
             gobject-introspection
             meson
             python-gobject
             python-packaging
             vala)
source=(https://gitlab.com/accounts-sso/libaccounts-glib/-/archive/VERSION_$pkgver/libaccounts-glib-VERSION_$pkgver.tar.gz)
sha256sums=('a8407a5897a2e425ea1aa955ecf88485dd2fd417919de275b27c781a5d0637a5')

build() {
  meson $pkgname-VERSION_$pkgver build \
    -Dprefix=/usr
  meson compile -C build
}

check() {
  meson test -C build || true
}

package() {
  meson install -C build --destdir "$pkgdir"
}
