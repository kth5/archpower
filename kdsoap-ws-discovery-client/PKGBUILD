# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdsoap-ws-discovery-client
pkgver=0.4.0
pkgrel=1
pkgdesc='WS-Discovery client library based on KDSoap'
license=(CC0-1.0
         GPL-3.0-or-later)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://caspermeijn.gitlab.io/kdsoap-ws-discovery-client/'
depends=(gcc-libs
         glibc
         kdsoap-qt6
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2cd247c013e75f410659bac372aff93d22d71c5a54c059e137b9444af8b3427a'
            'SKIP')
validpgpkeys=(E0A3EB202F8E57528E13E72FD7574483BB57B18D) # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_QCH=ON \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
