# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=libtlsrpt
pkgver=0.5.0
pkgrel=2
pkgdesc="A low-level C Library to implement TLSRPT into a MTA"
url="https://github.com/sys4/libtlsrpt"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('LGPL-3.0-or-later')
depends=('glibc')
provides=("${pkgname}.so")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.pc")
sha256sums=('8cf811f4feece4e00f9f0140c2f3211b67d7fd46db57d8381d3b35ad5d00135f'
            '2453d94ed066384097b210eb478a2c29f66fb081923cdd1d8c160fa116847903')

prepare() {
	# Upstream does not provide a pkg-config integration
	sed -e "s/VERSION/${pkgver}/" -i "${pkgname}.pc"

	cd "${pkgname}-${pkgver}"
	autoreconf -vif 
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
		--prefix='/usr' \
		--mandir='/usr/share/man'
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm 644 doc/* -t "${pkgdir}/usr/share/doc/${pkgname}/"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

	install -Dm 644 "${srcdir}/${pkgname}.pc" "${pkgdir}/usr/lib/pkgconfig/${pkgname}.pc"
}
