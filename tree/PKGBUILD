# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=tree
pkgver=2.1.3
pkgrel=1
pkgdesc="A directory listing program displaying a depth indented list of files"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/OldManProgrammer/unix-tree"
license=('GPL')
depends=('glibc')
source=("https://gitlab.com/OldManProgrammer/unix-tree/-/archive/${pkgver}/unix-tree-${pkgver}.tar.gz")
sha512sums=('610afbf93753dcfe2052ec3fbe9fdfaf05ba57694c5b08e13d252ce5048fbdd8463ff12f5a2e2135c7e3825bc40e106f7d19c0d8e205283be52fe81e7ec7f244')

prepare() {
  cd "unix-tree-${pkgver}"
  sed -i -e '/^CFLAGS/d' -e '/^LDFLAGS/d' Makefile
}

build() {
  cd "unix-tree-${pkgver}"
  make
}

package() {
  cd "unix-tree-${pkgver}"
  make PREFIX="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man" install
}
