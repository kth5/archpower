# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng
pkgver=1.6.53
pkgrel=2
pkgdesc='A collection of routines used to create PNG format graphics files'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='http://www.libpng.org/pub/png/libpng.html'
license=('libpng-2.0')
depends=('zlib' 'sh')
makedepends=('git')
provides=('libpng16.so')
source=("git+https://github.com/pnggroup/libpng.git?signed#tag=v${pkgver}?signed")
sha256sums=('08c7700264cf66f1700d47719cdaa45eac10743de5702f19164c84b67035b0ba')
validpgpkeys=('F57A55036A4D45837074FD92C9E384533403C2F8'  # Cosmin Truta <ctruta@gmail.com>
              '1FED507E3236B4A6E53E922BB292C64843FF5BCF') # Cosmin Truta <ctruta@gmail.com>


prepare() {
  cd $pkgname

  # fix: Use `png_voidp` instead of `voidp` in pngread.c and pngwrite.c
  # Fix a heap buffer over-read in `png_image_read_direct_scaled`
  # fix: Remove incorrect truncation casts from `png_write_image_*`
  git cherry-pick -n \
    0e894374dd7ee53039c6d84bd538dd7f16fc4cbe \
    e4f7ad4ea2a471776c81dda4846b7691925d9786 \
    cf155de014fc6c5cb199dd681dd5c8fb70429072

  autoreconf -fiv
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd contrib/pngminus
  make PNGLIB_SHARED="-L$pkgdir/usr/lib -lpng" CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" png2pnm pnm2png
  install -m0755 png2pnm pnm2png "$pkgdir/usr/bin/"
}
