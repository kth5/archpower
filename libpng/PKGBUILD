# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng
pkgver=1.6.47
pkgrel=1
pkgdesc='A collection of routines used to create PNG format graphics files'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
url='http://www.libpng.org/pub/png/libpng.html'
license=('custom')
depends=('zlib' 'sh')
makedepends=('git')
provides=('libpng16.so')
source=("git+https://github.com/pnggroup/libpng.git?signed#tag=v${pkgver}")
sha256sums=('6b2df2398f245234adbddf5b5b52db749c04658bf6ac6f6683898ab056de5e00')
validpgpkeys=('F57A55036A4D45837074FD92C9E384533403C2F8'  # Cosmin Truta <ctruta@gmail.com>
              '1FED507E3236B4A6E53E922BB292C64843FF5BCF') # Cosmin Truta <ctruta@gmail.com>


prepare() {
  cd $pkgname

  autoreconf -fiv
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd $pkgname

  make check
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd contrib/pngminus
  make PNGLIB_SHARED="-L$pkgdir/usr/lib -lpng" CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" png2pnm pnm2png
  install -m0755 png2pnm pnm2png "$pkgdir/usr/bin/"
}
