# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=libass
pkgver=0.17.2
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://github.com/libass/libass/
license=(BSD)
depends=(
  fontconfig
  fribidi
  glibc
  harfbuzz
  libfreetype.so
  libunibreak
)
makedepends=(
  git
)
makedepends_x86_64=(
  nasm
)
provides=(libass.so)
_tag=48c02e7a101a35ae613972474464275432a2fa04
source=(git+https://github.com/libass/libass.git?signed#tag=${_tag})
validpgpkeys=(
  5458C3100671F252B0F4C7708079D18C21AAAAFF # Oleg Oshmyan (Chortos-2) <chortos@inbox.lv>
  5EE63F2A71BF132CFE3567E1DFFE615F2824C720 # Oneric <oneric@oneric.de>
)
b2sums=('0e19f84ea73ca4b72a9a6f8ddb9a859301deefcf486e5f44178a7ad5b74ba48f8df2e20d8795260eaf87be2f4f48b6def97c942939bf158bbbb3959c1ec5623e')

prepare() {
  cd libass
  ./autogen.sh
}

pkgver() {
  cd libass
  git describe --tags
}

build() {
  cd libass
  ./configure \
    --prefix=/usr \
    --enable-fontconfig \
    --enable-libunibreak
  make
}

package() {
  cd libass
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libass/
}

# vim: ts=2 sw=2 et:
