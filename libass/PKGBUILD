# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=libass
pkgver=0.17.4
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/libass/libass/
license=(BSD)
depends=(
  fontconfig
  fribidi
  glibc
  harfbuzz
  libfreetype.so
  libunibreak
)
makedepends=(
  git
)
makedepends_x86_64=(
  nasm
)
provides=(libass.so)
source=(git+https://github.com/libass/libass.git?signed#tag=${pkgver})
validpgpkeys=(
  5458C3100671F252B0F4C7708079D18C21AAAAFF # Oleg Oshmyan (Chortos-2) <chortos@inbox.lv>
  5EE63F2A71BF132CFE3567E1DFFE615F2824C720 # Oneric <oneric@oneric.de>
)
b2sums=('e7790a6318205c8f3383d205acd9befe779014f17ef8d51c37ca19086efba25cf12d22b0af1eeec7e795e9dd824924ebc569ef6c6518276c9dbe10a26337ed27')

prepare() {
  cd libass
  ./autogen.sh
}

build() {
  cd libass
  ./configure \
    --prefix=/usr \
    --enable-fontconfig \
    --enable-libunibreak
  make
}

package() {
  cd libass
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libass/
}

# vim: ts=2 sw=2 et:
