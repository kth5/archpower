# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Daniel MÃ¼llner <muellner@math.stanford.edu>

pkgname=autoconf-archive
pkgver=2024.10.16
pkgrel=3
epoch=1
pkgdesc="A collection of freely re-usable Autoconf macros"
arch=(any)
url="https://www.gnu.org/software/autoconf-archive/"
license=('GPL-3.0-or-later WITH Autoconf-exception-3.0')
depends=('autoconf')
optdepends=('automake: macros for use with it')
source=("https://ftpmirror.gnu.org/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        "0001_fix_AX_CXX_COMPILE_STDCXX.patch::https://cgit.git.savannah.gnu.org/cgit/autoconf-archive.git/patch/?id=241f7ae5f857b0c9bab7866b79b4e47587126b96")
sha256sums=('7bcd5d001916f3a50ed7436f4f700e3d2b1bade3ed803219c592d62502a57363'
            'SKIP'
            '4bcc7a56332cd4f4f25fc9b1a67e8a3500e3bb6ea4d88ce1cc0fce31d4e7ea7f')
validpgpkeys=('1A4F63A13A4649B632F65EE141BC28FE99089D72') # Peter Simons <simons@cryp.to>

prepare() {
  cd ${pkgname}-${pkgver}
  # Generated detection logic is broken for AX_CXX_COMPILE_STDCXX for serial 21-23
  patch -Np1 -i ../0001_fix_AX_CXX_COMPILE_STDCXX.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vDm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING.EXCEPTION
}
