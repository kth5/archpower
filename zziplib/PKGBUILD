# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=zziplib
pkgver=0.13.74
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/gdraheim/zziplib"
license=('LGPL' 'MPL')
depends=('zlib')
makedepends=('python' 'xmlto' 'zip' 'cmake' 'ninja')
checkdepends=('unzip')
source=($pkgname-$pkgver.tar.gz::"https://github.com/gdraheim/zziplib/archive/v$pkgver.tar.gz")
sha512sums=('7dd27247cbc475e2c6beb3a96c620f9938ade0dff6f4e088f9c28432fc4cc01df9d6771ec3d5e07baabe27033f4764d60e31c4c54588559de64e894d0557c94f')

build() {
  cd ${pkgname}-${pkgver}
  cmake \
    -Bbuild \
    -GNinja \
    -DZZIPDOCS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

check() {
  cd ${pkgname}-${pkgver}
  ninja -C build check
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: set ts=2 sw=2 et:
