# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jan Koppe <post@jankoppe.de>

pkgname=yarn
pkgver=1.22.21
pkgrel=1
pkgdesc='Fast, reliable, and secure dependency management'
arch=(any)
url=https://classic.yarnpkg.com/
license=('BSD')
depends=('nodejs')
makedepends=('git' 'jq' 'yarn')
source=("git+https://github.com/${pkgname}pkg/$pkgname.git#tag=v$pkgver")
b2sums=('SKIP')

build() {
  cd $pkgname
  yarn --frozen-lockfile
  yarn build
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d  "$pkgdir"/{usr/bin,$mod_dir/bin}
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/${pkgname}pkg

  cd $pkgname
  # Prune unnecessary packages
  cp package.json{,.bak}
  read -ra devDependencies < <(jq -r '.devDependencies | keys | join(" ")' package.json)
  yarn remove --frozen-lockfile "${devDependencies[@]}"
  mv package.json{.bak,}

  cp -r lib node_modules package.json "$pkgdir"/$mod_dir
  install -t "$pkgdir"/$mod_dir/bin bin/$pkgname.js
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
