# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Marcus Ben Spencer <marcus@marcusspencer.xyz>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=llhttp
pkgver=9.3.0
pkgrel=1
pkgdesc='Port of http_parser to llparse'
arch=(x86_64 powerpc64le)
url='https://llhttp.org'
license=(MIT)
depends=(glibc)
makedepends=(cmake
             git
             npm
             ts-node)
source=(git+https://github.com/nodejs/llhttp#tag=v$pkgver)
sha256sums=('e50bb063f12869f5af99afb991bb3cad5fcebb1e28d82be09b6b9616ac894920')

build() {
  cd $pkgname

  # codegen
  npm install
  make release RELEASE="$pkgver"

  # build
  cd ..
  cmake -B build -S $pkgname/release \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE
}
