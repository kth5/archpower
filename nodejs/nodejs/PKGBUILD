# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=25.0.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript ("Current" release)'
arch=(x86_64 powerpc64le powerpc64)
url='https://nodejs.org/'
license=('MIT')
depends=(
#  'ada'
  'brotli'
  'c-ares'
#  'http-parser'
  'icu'
  'libnghttp2'
  'libnghttp3'
  'libngtcp2'
  'libuv'
  'openssl'
  'simdutf'
  'simdjson'
#  'v8'
  'zlib'
)
makedepends=(
  'git'
  'ninja'
  'procps-ng'
  'python'
)
optdepends=('npm: nodejs package manager')
options=('!debug' '!lto')
source=("git+https://github.com/nodejs/node.git#tag=v$pkgver?signed"
        hotfix-hwy-vsx.patch
        hotfix-ada-inline.patch)
sha512sums=('1cc74927dec9958c579b64a414de64167ac5375af3e1c9fc956199bd10fa3ad373bc6d1d1b264fc69811989e9eaad4a9b3539e4ee340fc882cd8d701b99b752d'
            'f927dbbbc3df4421a00718588ad654db1a0dce3df695b3471d762e7a6a68546c295d0e6b4043c1f137317174d9c162e27ca983f4747d5b5b375283ac30c4a74a'
            'a0f570b7348fb6671d2461ada98731a1c90623f047987d6fb2a5e48af6b846ccf118f0682cd674f2c677c3a044e3215d0ca1882cca81863338d2e1c026af95d0')
validpgpkeys=(
  '8FCCA13FEF1D0C2E91008E09770F7A9A5AE15600' # Michaël Zasso (Targos) <targos@protonmail.com>
  '890C08DB8579162FEE0DF9DB8BEAB4DFCF555EF4' # RafaelGSS <rafael.nunu@hotmail.com>
  'C82FA3AE1CBEDC6BE46B9360C43CEC45C17AB93C' # Richard Lau <rlau@redhat.com>
  'C0D6248439F1D5604AAFFB4021D900FFDB233756' # Antoine du Hamel <duhamelantoine1995@gmail.com>
  '5BE8A3F6C8A5C01D106C0AD820B1A390B168D356' # Antoine du Hamel <antoine.duhamel@rosa.be>
)

case "${CARCH}" in
  powerpc64)
    export CFLAGS='-O2 -pipe -Wno-deprecated-declarations'
    export CXXFLAGS='-O2 -pipe -Wno-deprecated-declarations'
  ;;
esac

_set_flags() {
  # /usr/lib/libnode.so uses malloc_usable_size, which is incompatible with fortification level 3
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
}

prepare() {
  cd node
  patch -Np1 -i ${srcdir}/hotfix-hwy-vsx.patch
  patch -Np1 -i ${srcdir}/hotfix-ada-inline.patch
}

build() {
  _set_flags
  cd node

  case "${CARCH}" in
    powerpc64*) _configure_flags=(--shared-simdutf --shared-simdjson --shared-openssl --dest-cpu=ppc64) ;;
    x86_64) _configure_flags=(--shared-simdjson) ;;
  esac

  ./configure \
    --prefix=/usr \
    --without-npm \
    --with-intl=system-icu \
    --shared-brotli \
    --shared-cares \
    --shared-libuv \
    --shared-nghttp2 \
    --shared-nghttp3 \
    --shared-ngtcp2 \
    --shared-zlib \
    --ninja ${_configure_flags[@]}
    # --shared-ada
    # --shared-http-parser
    # --shared-simdutf
    # --shared-v8

  ninja -C out/Release 
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  _set_flags
  cd node
  # ignore failing tests, they work when compiled locally
  rm test/parallel/test-http2-client-set-priority.js
  rm test/parallel/test-http2-priority-event.js
  rm test/parallel/test-http-outgoing-end-cork.js
  make test-only
}

package() {
  _set_flags
  cd node
  DESTDIR="$pkgdir" make install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/nodejs/
}

# vim:set ts=2 sw=2 et:
