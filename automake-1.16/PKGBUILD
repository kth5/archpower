# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>

pkgname=automake-1.16
_pkgname=automake
pkgver=1.16.5
_pkgver=1.16
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="https://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
provides=("automake=1.15")
options=(!emptydirs)
source=("ftp://ftp.gnu.org/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('07bd24ad08a64bc17250ce09ec56e921d6343903943e99ccf63bbf0705e34605')

build() {
  cd "$srcdir"/${_pkgname}-$pkgver
  ./configure --build=$CHOST --prefix=/usr
  make
}

package() {
  cd "$srcdir"/${_pkgname}-$pkgver
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir"/usr/bin/{automake,aclocal}
  rm -rf "$pkgdir"/usr/share/aclocal
  rm -fv "$pkgdir"/usr/share/man/man1/{automake,aclocal}.1*

  rm -rf "$pkgdir"/usr/share/info
  rm -rf "$pkgdir"/usr/share/doc
}
