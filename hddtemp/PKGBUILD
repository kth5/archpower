# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=hddtemp
_pkgver=0.3-beta15
_patchver="$_pkgver-53"
pkgver="${_patchver//-/.}"
pkgrel=4
pkgdesc='Gives you the temperature of your hard drive by reading S.M.A.R.T. information'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL)
url='https://savannah.nongnu.org/projects/hddtemp/'
backup=('etc/hddtemp.db')
_archive="$pkgname-$_pkgver"
source=("https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$_pkgver.tar.bz2"
        "https://deb.debian.org/debian/pool/main/h/$pkgname/${pkgname}_$_patchver.diff.gz"
        "$pkgname.service")
sha256sums=('618541584054093d53be8a2d9e81c97174f30f00af91cb8700a97e442d79ef5b'
            '6f1ddaa9fbc90ea5c00b949e0981b78c5014e109de88804ae2512209091eae56'
            '7f6fda4379630a21a8014d8f471eef6451fa15d73df32ec90a85d6a3f9d0c7d4')

prepare() {
	cd "$_archive"
	patch -p1 -i "../${pkgname}_$_patchver.diff"
}

build() {
	cd "$_archive"
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man \
		--with-db-path="/etc/hddtemp.db" \
		--build=${CHOST}
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
	install -Dm0644 -t "$pkgdir/etc/" debian/hddtemp.db
	install -Dm0644 -t "$pkgdir/usr/lib/systemd/system/" "../$pkgname.service"
}
