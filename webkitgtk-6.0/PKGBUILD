# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgbase=webkitgtk-6.0
pkgname=(
  webkitgtk-6.0
  webkitgtk-6.0-docs
)
pkgver=2.48.1
pkgrel=1
pkgdesc="Web content engine for GTK"
url="https://webkitgtk.org"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(
  # :sort ui /\v^\s*['"]?/
  'AFL-2.0 OR GPL-2.0-or-later'
  Apache-2.0
  'Apache-2.0 WITH LLVM-exception'
  BSD-2-Clause
  BSD-2-Clause-Views
  BSD-3-Clause
  BSD-Source-Code
  BSL-1.0
  bzip2-1.0.6
  GPL-2.0-only
  'GPL-3.0-only WITH Autoconf-exception-3.0'
  'GPL-3.0-or-later WITH Bison-exception-2.2'
  ICU
  ISC
  LGPL-2.1-only
  LGPL-2.1-or-later
  MIT
  MPL-1.1
  MPL-2.0
  NCSA
  'NCSA OR MIT'
  OFL-1.1
  SunPro
  Unicode-TOU
)
depends=(
  at-spi2-core
  atk
  bubblewrap
  cairo
  enchant
  expat
  fontconfig
  freetype2
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  gtk4
  harfbuzz
  harfbuzz-icu
  hyphen
  icu
  lcms2
  libavif
  libdrm
  libegl
  libepoxy
  libgcrypt
  libgl
  libgles
  libjpeg-turbo
  libjxl
  libmanette
  libpng
  libseccomp
  libsecret
  libsoup3
  libsystemd
  libtasn1
  libwebp
  libx11
  libxml2
  libxslt
  mesa
  openjpeg2
  pango
  sqlite
  wayland
  woff2
  xdg-dbus-proxy
  zlib
)
makedepends=(
  clang
  cmake
  gi-docgen
  glib2-devel
  gobject-introspection
  gperf
  gst-plugins-bad
  lld
  ninja
  python
  ruby
  ruby-stdlib
  systemd
  unifdef
  wayland-protocols
)
source=(
  $url/releases/webkitgtk-$pkgver.tar.xz{,.asc}
  fix-typo-denormaldisabler.patch
  reduce-memory-overheads.patch
)
sha256sums=('98efdf21c4cdca0fe0b73ab5a8cb52093b5aa52d9b1b016a93f71dbfa1eb258f'
            'SKIP'
            '08a5fc35ed0d65f1c47a00f2bcf0e6c335321ee5db7f2eb1ad4cbfb1a563a36c'
            '13df7f0b0a61866e767f280bbe77667126ae7de4ad4f463c682040da6366a864')
b2sums=('d0723ded65230915954a66366c82d0d8c45b423c55cc5d6dabed72378a9f8cf498e8a525729e964daa3e8dd93e06fde30b6f02b103c5e88b3c29cc175026f668'
        'SKIP'
        '9320fb1e591d1254c8ccafa2d3b14aa2aa802eb90aaf2f564933003f4051dca834c35e059b6740a81d98aa19c1654e6dcbc77433933c1b9ca40e4cf5fec4adc1'
        'cc16462ce22df999b47ef477d4272ef3df2fbbfe5cdc585f3ce47e56cde65a1d7dd1105e6e7bbff41695f79b6394bc3e403e53a669bb7f456c733ed216372c9b')
validpgpkeys=(
  # https://www.webkitgtk.org/verifying.html
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B # Adrián Pérez de Castro <aperez@igalia.com>
  013A0127AC9C65B34FFA62526C1009B693975393 # Carlos Garcia Campos <cgarcia@igalia.com>
)

prepare() {
  cd webkitgtk-$pkgver

  patch -Np1 -i ${srcdir}/fix-typo-denormaldisabler.patch
  patch -Np1 -i ${srcdir}/reduce-memory-overheads.patch
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_INSTALL_LIBEXECDIR=lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_SKIP_RPATH=ON
    -D ENABLE_DOCUMENTATION=ON
    -D ENABLE_MINIBROWSER=ON
    -D ENABLE_SPEECH_SYNTHESIS=OFF
    -D PORT=GTK
    -D USE_FLITE=OFF
    -D USE_GTK4=ON
    -D USE_LIBBACKTRACE=OFF
    -D USE_SOUP2=OFF
  )

  case "${CARCH}" in
    powerpc64le)
      export CFLAGS+=' -mminimal-toc'
      export CXXFLAGS+=' -mminimal-toc'
      export LDFLAGS+=' -Wl,--no-keep-memory'
    ;;
    powerpc64)
      export CFLAGS+=' -mminimal-toc'
      export CXXFLAGS+=' -mminimal-toc'
      export LDFLAGS+=' -Wl,--no-keep-memory'
    ;;
    powerpc)
      cmake_options+=(
        -DENABLE_WEBGL=OFF
      )
      export LDFLAGS+=" -Wl,--no-keep-memory"
    ;;
    x86_64)
      # Upstream prefers Clang
      # https://gitlab.archlinux.org/archlinux/packaging/packages/webkitgtk-6.0/-/issues/4
      export CC=clang CXX=clang++
      LDFLAGS+=" -fuse-ld=lld"

      # JITted code crashes when CET is used
      CFLAGS+=' -fcf-protection=none'
      CXXFLAGS+=' -fcf-protection=none'
    ;;
  esac

  # Produce minimal debug info: 4.3 GB of debug data makes the
  # build too slow and is too much to package for debuginfod
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  cmake -S webkitgtk-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package_webkitgtk-6.0() {
  provides+=(
    libjavascriptcoregtk-6.0.so
    libwebkitgtk-6.0.so
  )
  optdepends=(
    'geoclue: Geolocation support'
    'gst-libav: nonfree media decoding'
    'gst-plugins-bad: media decoding'
    'gst-plugins-good: media decoding'
  )

  DESTDIR="$pkgdir" cmake --install build

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  cd webkitgtk-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_webkitgtk-6.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
