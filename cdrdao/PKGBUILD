# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgbase=cdrdao
pkgname=('cdrdao' 'gcdmaster')
pkgver=1.2.6
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL-2.0-or-later')
url="http://cdrdao.sourceforge.net/"
pkgdesc='Records audio/data CD-Rs in disk-at-once (DAO) mode'
depends=('atkmm' 'cairomm' 'dconf' 'gcc-libs' 'glib2' 'glibc' 'glibmm' 'gtk3' 'gtkmm3' 'lame'
         'libao' 'libmad' 'libsigc++' 'libvorbis' 'pangomm')
makedepends=(git)
options=('!emptydirs')
source=(git+https://github.com/cdrdao/cdrdao#tag=rel_${pkgver//./_})
sha256sums=('bd0207dbb114b18105251641b5d8164e53aed9624493dec76a94b4f84835d133')

prepare() {
  cd ${pkgbase}

  autoreconf -fiv
}

build() {
  cd $pkgbase
  ./configure --prefix=/usr \
	--mandir=/usr/share/man \
	--sysconfdir=/etc \
	--with-lame \
	--with-ogg-support \
	--with-mp3-support
  make
}

package_cdrdao() {
  depends=('gcc-libs' 'glibc' 'lame' 'libao' 'libmad' 'libvorbis')

  cd $pkgbase
  make DESTDIR="${pkgdir}" install
  make -C gcdmaster DESTDIR="${pkgdir}" uninstall
}

package_gcdmaster() {
  pkgdesc='GUI frontend for creating audio CDs and burning them using cdrdao'
  depends=('atkmm' 'cairomm' 'cdrdao' 'dconf' 'gcc-libs' 'glib2' 'glibc' 'glibmm' 'gtk3' 'gtkmm3'
           'libao' 'libmad' 'libsigc++' 'libvorbis' 'pangomm')

  cd $pkgbase
  make -C gcdmaster DESTDIR="${pkgdir}" install
}
