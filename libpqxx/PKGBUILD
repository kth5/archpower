# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=7.10.1
pkgrel=1
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('python')
provides=("libpqxx-${pkgver%.*}.so")
source=(https://github.com/jtv/libpqxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('cfbbb1d93a0a3d81319ec71d9a3db80447bb033c4f6cee088554a88862fd77d7')
b2sums=('c8b805c67554abce3b4b052274e6aab73edc70101747404394ca010a55ec83742de0d9deec7b987d646f79748b92eaada67df34651b5ead6c0a884a653be003e')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-doc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
