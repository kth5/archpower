# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=7.9.2
pkgrel=1
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('python')
provides=("libpqxx-${pkgver%.*}.so")
source=(https://github.com/jtv/libpqxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('e37d5774c39f6c802e32d7f418e88b8e530404fb54758516e884fc0ebdee6da4')
b2sums=('e961af7372f5dd5a214044a9c25cd8f320579c3176c31205d068a0e5ae310d5a5fd8af058293377e1ede78cb5877acdf3880c8a867aa4cd270ab9ecaacb0958f')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-doc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
