# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Pekka Ristola <pekkarr [at] protonmail [dot] com>

pkgname=rapidfuzz-cpp
pkgver=3.3.2
pkgrel=1
pkgdesc='Rapid fuzzy string matching in C++ using the Levenshtein Distance'
arch=(any)
url='https://github.com/rapidfuzz/rapidfuzz-cpp'
license=('MIT')
depends=('cmake')
makedepends=('git' 'catch2')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('3f71cb89e455b9591446d417c2955198438d40564b8e81cd288ea18595e33758f89dbd09e0ac54ef427051d6e42ca1a97b05488924a8ca3ff8970a565b55815b')
b2sums=('39a50d4b147423fbab94e4171c72d7f81f8bbd8f4062a806bc7d08c94c9998485c32fca86050cb7f5ce13b90909f5e28b595c6aaba467a6fefd2928dbd081a08')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -D RAPIDFUZZ_BUILD_TESTING=ON \
    -D RAPIDFUZZ_ENABLE_LINTERS=ON \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -W no-dev

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
