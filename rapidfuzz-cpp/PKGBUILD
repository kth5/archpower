# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Pekka Ristola <pekkarr [at] protonmail [dot] com>

pkgname=rapidfuzz-cpp
pkgver=3.3.3
pkgrel=1
pkgdesc='Rapid fuzzy string matching in C++ using the Levenshtein Distance'
arch=(any)
url='https://github.com/rapidfuzz/rapidfuzz-cpp'
license=(MIT)
depends=(cmake)
makedepends=(git catch2)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('203d5d760cf814b2a0756d2154496c1fc68208ebc4e9b268d9f167ec2657d87eaab20697fa8baaf0187939440bd4cce985890a0f54aa36e41a1128c8f9f0b519')
b2sums=('1533dac4ad27338dafb86f68b6f9607b355dd106dc831dc7de9ec654762f20d49d581a7b7324cf6060a89272eac1178e2ca04152b847d0e3a236ef7da8c9ab91')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -D RAPIDFUZZ_BUILD_TESTING=ON \
    -D RAPIDFUZZ_ENABLE_LINTERS=ON \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -W no-dev

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
