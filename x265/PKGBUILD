# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz@interia.pl>

pkgname=x265
pkgver=4.0
pkgrel=2
pkgdesc='Open Source H265/HEVC video encoder'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://bitbucket.org/multicoreware/x265_git
license=(GPL)
depends=(gcc-libs)
makedepends=(
  cmake
  git
  ninja
)
makedepends_x86_64=(nasm)
provides=(libx265.so)
_tag=6318f223684118a2c71f67f3f4633a9e35046b00
source=(git+https://bitbucket.org/multicoreware/x265_git#tag=${_tag}
        x265-3.5-r5-cpp-std.patch
        x265-3.5-r5-gcc15.patch
        x265-3.6-cmake-cleanup.patch
        x265-3.6-code-cleanup.patch)
sha256sums=('633998b9f17d335c24b3ac34eb446ac376971ce7323ef9befb86d134ee3e6453'
            '36a1dc4c41f36b183fe019156352ec08ceab0e35bffa49bbc62b9c335914bd26'
            '022d004fd9e03bfd478b955e46dca992dc588ec798f9f397e18955268b29fece'
            '46a21599fb39c8db67f5062ed44934d736178f9799078aaba59c17ed3cebab3f'
            'c20b125325c205763ac6f0867db57fcd5233173f44a655977e7f4c7a1a89c792')

pkgver() {
  cd x265_git

  git describe --tags
}

prepare() {
  cd x265_git/source
  patch -Np1 -i ${srcdir}/x265-3.5-r5-cpp-std.patch
  patch -Np1 -i ${srcdir}/x265-3.5-r5-gcc15.patch
  patch -Np1 -i ${srcdir}/x265-3.6-cmake-cleanup.patch
  patch -Np1 -i ${srcdir}/x265-3.6-code-cleanup.patch
}

build() {
  case "${CARCH}" in
    powerpc64le) 
      cmake_options_12=(-DENABLE_ALTIVEC=OFF -DCPU_POWER8=ON)
      cmake_options_10=(-DENABLE_ALTIVEC=OFF -DCPU_POWER8=ON)
      cmake_options=(-DENABLE_ALTIVEC=ON -DCPU_POWER8=ON -DENABLE_PIC=ON)
    ;;
    powerpc64)
      cmake_options_12=(-DENABLE_ALTIVEC=OFF -DCPU_POWER8=OFF)
      cmake_options_10=(-DENABLE_ALTIVEC=OFF -DCPU_POWER8=OFF)
      cmake_options=(-DENABLE_ALTIVEC=OFF -DCPU_POWER8=OFF -DENABLE_PIC=ON)
    ;;
    powerpc)
      cmake_options_12=(-DENABLE_ALTIVEC=OFF -DCPU_POWER8=OFF)
      cmake_options_10=(-DENABLE_ALTIVEC=OFF -DCPU_POWER8=OFF)
      cmake_options=(-DENABLE_ALTIVEC=ON -DCPU_POWER8=OFF -DENABLE_PIC=ON)
    ;;
  esac


  cmake -S x265_git/source -B build-12 -G Ninja \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DHIGH_BIT_DEPTH=TRUE \
    -DMAIN12=TRUE \
    -DEXPORT_C_API=FALSE \
    -DENABLE_CLI=FALSE \
    -DENABLE_SHARED=FALSE \
    -Wno-dev ${cmake_options_12[@]}
  ninja -C build-12

  cmake -S x265_git/source -B build-10 -G Ninja \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DHIGH_BIT_DEPTH=TRUE \
    -DEXPORT_C_API=FALSE \
    -DENABLE_CLI=FALSE \
    -DENABLE_SHARED=FALSE \
    -Wno-dev ${cmake_options_10[@]}
  ninja -C build-10

  cmake -S x265_git/source -B build -G Ninja \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_SHARED=TRUE \
    -DENABLE_HDR10_PLUS=TRUE \
    -DEXTRA_LIB='x265_main10.a;x265_main12.a' \
    -DEXTRA_LINK_FLAGS='-L .' \
    -DLINKED_10BIT=TRUE \
    -DLINKED_12BIT=TRUE \
    -Wno-dev ${cmake_options[@]}
  ln -s ../build-10/libx265.a build/libx265_main10.a
  ln -s ../build-12/libx265.a build/libx265_main12.a
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
