# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz@interia.pl>

pkgname=x265
pkgver=3.4
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=(x86_64 powerpc64le)
url=https://bitbucket.org/multicoreware/x265
license=(GPL)
depends=(gcc-libs)
makedepends=(
  cmake
  mercurial
)
provides=(libx265.so)
source=(hg+https://bitbucket.org/multicoreware/x265#tag=2a65b720985096bcb1664f7cb05c3d04aeb576f5)
sha256sums=(SKIP)

pkgver() {
  cd x265

  hg id --tags
}

prepare() {
  cd x265

  for d in 8 10 12; do
    if [[ -d build-$d ]]; then
      rm -rf build-$d
    fi
    mkdir build-$d
  done
}

build() {
  export CFLAGS=${CFLAGS/-mabi=altivec -maltivec/}
  export CXXFLAGS=${CXXFLAGS/-mabi=altivec -maltivec/}

  cd x265/build-12

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DHIGH_BIT_DEPTH=TRUE \
    -DMAIN12=TRUE \
    -DEXPORT_C_API=FALSE \
    -DENABLE_CLI=FALSE \
    -DENABLE_SHARED=FALSE \
    -DENABLE_ALTIVEC='OFF' \
    -DENABLE_PIC='ON'
  make

  cd ../build-10

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DHIGH_BIT_DEPTH=TRUE \
    -DEXPORT_C_API=FALSE \
    -DENABLE_CLI=FALSE \
    -DENABLE_SHARED=FALSE \
    -DENABLE_ALTIVEC='OFF' \
    -DENABLE_PIC='ON'
  make

  cd ../build-8

  ln -s ../build-10/libx265.a libx265_main10.a
  ln -s ../build-12/libx265.a libx265_main12.a

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_SHARED=TRUE \
    -DENABLE_HDR10_PLUS=TRUE \
    -DEXTRA_LIB='x265_main10.a;x265_main12.a' \
    -DEXTRA_LINK_FLAGS='-L .' \
    -DLINKED_10BIT=TRUE \
    -DLINKED_12BIT=TRUE \
    -DENABLE_ALTIVEC='OFF' \
    -DENABLE_PIC='ON'
  make
}

package() {
  cd x265/build-8

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
