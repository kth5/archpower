# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=db
pkgver=6.2.32
pkgrel=3
pkgdesc="The Berkeley DB embedded database system"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://www.oracle.com/technology/software/products/berkeley-db/index.html'
license=(AGPL-3.0-only)
depends=(gcc-libs sh)
source=(https://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz
        db-4.8.30-tls-configure.patch)
b2sums=('d3254fba1300d6c7dda5d872c5fa349e8704d71be9c4d37ccf3357782adeb879ce30e066cf083bd97554405c2e94071a96cee16090ad9f797a4fa657ff7e8c46'
        '0649b13b88b6fd999bcf94fd531c9b013ec599877d6f30dfd7daff97c7db0d405a4bb709bd01b3080dec440785c7156f6716e96aaaf63ca6e706fa2da7357da4')

prepare() {
  cd ${pkgname}-${pkgver}

  # fix dbstl_container build error
  patch -Np1 -i ../db-4.8.30-tls-configure.patch

  cd dist
  ./s_config
}

build() {
  cd ${pkgname}-${pkgver}/build_unix
  export CFLAGS+=" -std=gnu99"
  ../dist/configure \
    --prefix=/usr \
    --enable-compat185 \
    --enable-shared \
    --enable-static \
    --enable-cxx \
    --enable-dbm \
    --enable-stl
  make LIBSO_LIBS=-lpthread
}

package() {
  make -C ${pkgname}-${pkgver}/build_unix DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/docs
}
