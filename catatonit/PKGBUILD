# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Axel Hinrichs <hinrichs@bitscape.de>

# NOTE: this package is fully statically linked to work in containers
pkgname=catatonit
pkgver=0.2.1
pkgrel=1
pkgdesc="A container init that is so simple it's effectively brain-dead."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/openSUSE/catatonit/"
license=(GPL-2.0-or-later)
source=(
  $pkgname-$pkgver.tar.xz::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz
  $pkgname-$pkgver.tar.xz.asc::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz.asc
)
sha256sums=('9950425501af862e12f618bdc930ea755c46db6a16072a1462b4fc93b2bd59bc'
            'SKIP')
b2sums=('46b5ec8ca737628dc8250e6c23ff3342b741bed3ec636b64a63211bb8847498e7d8f46bb4fb2ec043e9f92b9ba6097f1c0eb470ef5db8f5eb196329e01528ff4'
        'SKIP')
validpgpkeys=('5F36C6C61B5460124A75F5A69E18AA267DDB8DB4')  # Aleksa Sarai <asarai@suse.com>

prepare(){
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make V=1
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vdm 755 "$pkgdir/usr/lib/podman/"
  ln -sv /usr/bin/$pkgname "$pkgdir/usr/lib/podman/"
}

# vim: ft=sh syn=sh
