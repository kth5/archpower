# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Axel Hinrichs <hinrichs@bitscape.de>

# NOTE: this package is fully statically linked to work in containers
pkgname=catatonit
pkgver=0.2.0
pkgrel=3
pkgdesc="A container init that is so simple it's effectively brain-dead."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/openSUSE/catatonit/"
license=(GPL-2.0-or-later)
source=(
  $pkgname-$pkgver.tar.xz::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz
  $pkgname-$pkgver.tar.xz.asc::https://github.com/openSUSE/catatonit/releases/download/v$pkgver/$pkgname.tar.xz.asc
)
sha256sums=('9843c2b47421f0f1959b18b786f736cde1fbf124b1d1fa0d292be2773d2d2b90'
            'SKIP')
b2sums=('bb4adf83da1e21bc54e86ec0f5c5368aeef29d3f9d71d68dcdd911d3f58813a0fb0ab45fbeb1626d167e92d1e73ac757a904e87d0432b56c7d59d3eeee04455d'
        'SKIP')
validpgpkeys=('5F36C6C61B5460124A75F5A69E18AA267DDB8DB4')  # Aleksa Sarai <asarai@suse.com>

prepare(){
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make V=1
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vdm 755 "$pkgdir/usr/lib/podman/"
  ln -sv /usr/bin/$pkgname "$pkgdir/usr/lib/podman/"
}

# vim: ft=sh syn=sh
