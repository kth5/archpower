# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=libmpeg2
pkgver=0.5.1
pkgrel=11
pkgdesc='Library for decoding MPEG-1 and MPEG-2 video streams'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://libmpeg2.sourceforge.io/'
license=('GPL-2.0-or-later')
depends=('glibc')
source=("https://download.videolan.org/contrib/libmpeg2/$pkgname-$pkgver.tar.gz"
        altivec.patch)
b2sums=('29b71740fa601c668a8f5b0a43aa763bda2fc66587f5bff847d4bc6d03dd831abe46428616b770aaaabf2d42877daad8c305ab4dd988cc91a4c90decfcc19c9a'
        '9800dd1648ffbb14ecd7b199c0333cce0654e41119684e68195099299264ebe50505f7b430c05308e2f2542cde09d55d68fedaa3fedbad2ab0fb380f53240d20')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i "${srcdir}/altivec.patch"

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
