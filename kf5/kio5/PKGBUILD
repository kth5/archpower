# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kio
pkgname=${_name}5
pkgver=5.116.0
pkgrel=5
pkgdesc='Resource and network access abstraction'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid5 kjobwidgets5 kbookmarks5 kwallet5 ktextwidgets5 kded5 knotifications5)
makedepends=(extra-cmake-modules qt5-tools)
optdepends=('kio5-extras: extra protocols support (sftp, fish and more)'
            'switcheroo-control: hybrid GPU support')
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('efb719d6659c39a03b165dca3b6c84f729a833290fc44e7e1f99625690b6115a'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DKF6_COMPAT_BUILD=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
