# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=seatd
pkgver=0.9.1
pkgrel=1
pkgdesc="A minimal seat management daemon, and a universal seat management library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://sr.ht/~kennylevinsen/seatd/"
license=('MIT')
provides=("libseat.so")
replaces=("libseat")
makedepends=(
    'meson'
    'ninja'
    'scdoc'
    'systemd'
)
depends=(
    'systemd-libs'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://git.sr.ht/~kennylevinsen/seatd/archive/$pkgver.tar.gz"
    "sysuser.conf"
)
sha256sums=('819979c922a0be258aed133d93920bce6a3d3565a60588d6d372ce9db2712cd3'
            'e833292adf5da25b5258a5209c258f0e4c12e1d6fe81cf228fd0cfb575080d78')
build() {
    mkdir build
    meson --prefix=/usr \
          --buildtype=plain \
          -Dexamples="disabled" \
          -Dserver="enabled" \
          -Dlibseat-logind="systemd" \
          -Dman-pages="enabled" \
          "seatd-$pkgver" \
          build
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "seatd-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

    # Allow users in the "seat" group to access seatd, and provide a service
    # that will be necessary for e.g. wlroots-based seat management
    install -Dm644 "seatd-$pkgver/contrib/systemd/seatd.service" \
            -t "$pkgdir/usr/lib/systemd/system/"
    install -Dm644 "sysuser.conf" "$pkgdir/usr/lib/sysusers.d/seatd.conf"
}
