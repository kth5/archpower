# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=2.2.0
pkgrel=1
pkgdesc='C library for finding and handling crystal symmetries'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://spglib.readthedocs.io/en/latest/'
license=(custom)
depends=(glibc)
makedepends=(cmake
             gcc-fortran
             gtest
             python
             python-numpy)
optdepends=('gcc-libs: Fortran interface'
            'python-numpy: Python interface')
source=(https://github.com/spglib/spglib/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        fix-duplicate-libs.patch)
sha256sums=('ac929e20ec9d4621411e2cdec59b1442e02506c1e546005bbe2c7f781e9bd49a'
            '07e887901ddc521907eaf8d9e1632e5935554f544690c2744f9f66b05d104341')

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-duplicate-libs.patch # Don't duplicate shared libs in python tree
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSPGLIB_WITH_Fortran=ON \
    -DSPGLIB_WITH_Python=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
