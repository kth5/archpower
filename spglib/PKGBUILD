# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=2.4.0
pkgrel=2
pkgdesc='C library for finding and handling crystal symmetries'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://spglib.readthedocs.io/en/latest/'
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(cmake
             gcc-fortran
             gtest
             python
             python-numpy)
optdepends=('gcc-libs: Fortran interface'
            'python-numpy: Python interface')
source=(https://github.com/spglib/spglib/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e33694b189c6864f719a59c31e2af55301a524fb68ba9fb65f08e95af471847d')

prepare() {
  sed -e 's|TARGET Spglib_symspg|0|' -i $pkgname-$pkgver/python/CMakeLists.txt # Don't duplicate shared libs in python tree
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSPGLIB_WITH_Fortran=ON \
    -DSPGLIB_WITH_Python=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
