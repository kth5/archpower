# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=darkhttpd
pkgver=1.17
pkgrel=1
pkgdesc='A small and secure static webserver'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://unix4lyfe.org/darkhttpd/'
license=('ISC')
makedepends=('git')
depends=('glibc')
checkdepends=('python')
backup=('etc/conf.d/mimetypes')
source=("git+https://github.com/emikulic/darkhttpd.git#tag=v${pkgver}"
        'darkhttpd.service'
        'darkhttpd@.service')
sha256sums=('6186e91fd4bcb692a42ff80dae85fddd6ec9adfd1d3358535b9c19468c8fb1f6'
            'e53ae82993f6c996c0c54ccbbd9307811501cf01404c441b48b2c79f9384fa27'
            '0bfbe13c93ba87d8ea08ab85745c92cebcbc6b627616e7c58d6099a234a59841')

case "${CARCH}" in
  powerpc)
    export CFLAGS+=' -latomic'
  ;;
esac

build() {
  cd "${pkgname}"

  make
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd "${pkgname}"

  make test
}

package() {
  # install systemd files
  install -Dm0644 'darkhttpd.service' "${pkgdir}/usr/lib/systemd/system/darkhttpd.service"
  install -Dm0644 'darkhttpd@.service' "${pkgdir}/usr/lib/systemd/system/darkhttpd@.service"
  install -dm0755 "${pkgdir}"/etc/conf.d/
  touch "${pkgdir}"/etc/conf.d/mimetypes

  cd "${pkgname}"

  # install darkhttpd
  install -Dm0755 'darkhttpd' "${pkgdir}/usr/bin/darkhttpd"

  # install license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  sed -n '1,/^$/p' darkhttpd.c > "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
