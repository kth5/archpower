# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=gavl
pkgver=1.4.0
pkgrel=9
pkgdesc='Low level library, upon which multimedia APIs can be built'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='http://gmerlin.sourceforge.net/'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(doxygen)
source=("https://downloads.sourceforge.net/gmerlin/$pkgname-$pkgver.tar.gz")
b2sums=(2ed47cf63d8ce3424d0714e202a11ecef89c5a9c160a6045e238bae4179c02398600fc9c6d914784bb24a86c501f93971631461bdecaa8a70743a674748a2ec3)

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/LDFLAGS="$GMERLIN_DEP_RPATH"/LDFLAGS="$LDFLAGS $GMERLIN_DEP_RPATH"/' configure{,.ac}
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-cpuflags=none
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
