# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=compface
pkgver=1.5.2
pkgrel=13
pkgdesc="Utils & libs to convert from/to X-Face format."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://ftp.xemacs.org/pub/xemacs/aux/"
license=('MIT')
depends=('glibc')
options=('!makeflags')
source=(#$url/$pkgname-$pkgver.tar.gz
        http://mirrors.ibiblio.org/xemacs/aux/$pkgname-$pkgver.tar.gz
        compface-1.5.2-build.patch
        compface-c99.patch
        compface-modern-c-porting.patch
        license-from-upstream)
sha256sums=('a6998245f530217b800f33e01656be8d1f0445632295afa100e5c1611e4f6825'
            '5002ad6bd405af37515314b33184d5539603ffad7ad97e50e76e5d00d67eebca'
            'f416ade84d9caae593a3f069ae32acd6f2040be702d4aca16329fa68db2df3c4'
            'd4b3bf18b543043a35895345b7660025c4eb99aad9a88600c94f83e9b1415de3'
            '6f44301a736c5a5ff047d78f6a5e5b32dd6f05974ce0380ec4437ad73912168d')

prepare() {
  cd $pkgname-$pkgver
  patch -Np0 -i ${srcdir}/compface-1.5.2-build.patch
  patch -Np1 -i ${srcdir}/compface-c99.patch
  patch -Np1 -i ${srcdir}/compface-modern-c-porting.patch
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver
  CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" STRIP=/bin/true install

  install -D -m644 ../license-from-upstream "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
