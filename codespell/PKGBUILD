# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=codespell
pkgver=2.2.5
pkgrel=1
pkgdesc='check code for common misspellings'
arch=(any)
url='https://github.com/codespell-project/codespell'
license=('GPL2')
depends=('python-chardet')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools-scm')
source=("https://pypi.io/packages/source/c/codespell/${pkgname}-${pkgver}.tar.gz")
sha256sums=('6d9faddf6eedb692bf80c9a94ec13ab4f5fb585aabae5f3750727148d7b5be56')

build() {
	cd "${pkgname}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${pkgname}-${pkgver}"
	python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}
