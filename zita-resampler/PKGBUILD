# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=zita-resampler
pkgver=1.8.0
pkgrel=1
pkgdesc="A C++ library for resampling audio signals"
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL3')
depends=('gcc-libs' 'glibc')
makedepends=('libsndfile')
optdepends=('libsndfile: for zresample and zretune')
provides=('libzita-resampler.so')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2"
	disable-sse2.patch)
sha512sums=('adf1f465ac09104be5a02c1cf0f052a44b4c1a448312f3aca016a889e98cfcdebfded38f60300ad85d18ae03eadca7e8e95efb2875267bc36435ccfac881a8e7'
            '5be593bc6e74e3ecb05a86d1a1a9d26d6c13b6df14013a58eb1d0eabfc0fa8cea26564cdb8a276bad875b9fefda3a91ea8f8a9fe278c44a8222a03a5d13981bf')
b2sums=('1ba5d255fff9c66e18e6177bc7e48a4710fdf54f58c07f162f8c8801f4f449b22dea9922645e7e33b70e07739c2b381176714e704f9840305f194f3c23d66de2'
        'd7ae8d31dc53d8ba55b75c9228ca4f538d520e14faec5932ff350313905f03c51cf75e816a780fad5456c8f53936d1a28125b9a891cc9414c3aa34e76c5ec142')

prepare() {
  cd "${pkgname}-${pkgver}"

  [ "${CARCH}" != 'x86_64' ] && patch -Np1 -i ${srcdir}/disable-sse2.patch

  # FS#36248 - strip march=native
  sed -e '/native/d' \
      -i {source,apps}/Makefile
  # removing local call to ldconfig
  sed -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
      -i source/Makefile
  # fix use of BINDIR without preceeding DESTDIR
  # fix use of MANDIR without preceeding DESTDIR
  sed -e 's/\ $(BINDIR)/\ $(DESTDIR)$(BINDIR)/' \
      -e 's/\ $(MANDIR)/\ $(DESTDIR)$(MANDIR)/' \
      -i apps/Makefile
}

build() {
  cd "${pkgname}-${pkgver}"
  # libs
  make -C source
  # create lib link for building apps
  ln -sv "lib${pkgname}.so.${pkgver}" "source/lib${pkgname}.so"
  # apps
  make LDFLAGS+=" -L../source" CXXFLAGS+=" -I../source" -C apps
  # cleanup link
  rm -rv "source/lib${pkgname}.so"
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C source DESTDIR="$pkgdir/" PREFIX=/usr SUFFIX="" install
  make -C apps DESTDIR="$pkgdir/" PREFIX=/usr install
  # docs
  install -vDm 644 {AUTHORS,README} docs/* \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
