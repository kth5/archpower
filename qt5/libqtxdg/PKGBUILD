# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Co-Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=libqtxdg
pkgver=4.1.0
pkgrel=1
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/lxqt/$pkgname"
license=("LGPL" "custom")
depends=("libsm" "libxkbcommon-x11" "qt6-base" "qt6-svg")
makedepends=("cmake" "lxqt-build-tools")
checkdepends=("xorg-server-xvfb")
optdepends=(
  'xterm: the default terminal for running .desktop files'
)
provides=("libQt6Xdg.so" "libQt6XdgIconLoader.so")
source=(
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=('0604d397d9561a6a6148930a2b131f2bdee86cec6cca304f7513a8ec7b8e8809'
            'SKIP')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=ON
  make
}

check() {
  cd build

  xvfb-run --auto-display make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname-$pkgver"/{AUTHORS,COPYING,Digia-Qt-LGPL-Exception-1.1} \
    -t "$pkgdir"/usr/share/licenses/$pkgname
}
