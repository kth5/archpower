# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-multimedia
_basever=5.15.18
pkgver=5.15.18+kde+r2
pkgrel=1
_commit=ff83d119c75cd8406f73ccc08958fe36747e7390
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for audio, video, radio and camera functionality'
depends=('qt5-base' 'gst-plugins-base' 'libpulse' 'openal')
makedepends=('qt5-declarative' 'gst-plugins-bad' 'git')
optdepends=('qt5-declarative: QML bindings' 'gst-plugins-good: camera support, additional plugins'
            'gst-plugins-bad: camera support, additional plugins' 'gst-plugins-ugly: additional plugins'
            'gst-libav: ffmpeg plugin')
groups=('qt5')
_pkgfqn=${pkgname/5-/}
source=(kde-$_pkgfqn::git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit)
sha256sums=('58295d37951a57b15cbe62cc2f9428f76e286a3b58fa9dbaafc684e573ab39d1')

pkgver() {
  cd kde-$_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit` | sed -e 's|+kde+r0||'
}

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../kde-$_pkgfqn
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
