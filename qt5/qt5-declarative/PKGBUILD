# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-declarative
_basever=5.15.6
pkgver=5.15.6+kde+r20
pkgrel=1
_commit=b3aaf1482c48bbc0ca4f7c7934597c055afe4b6a
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QML and JavaScript languages'
depends=('qt5-base')
makedepends=('python' 'vulkan-headers' 'git')
groups=('qt' 'qt5')
conflicts=('qtchooser')
_pkgfqn=qtdeclarative
_pkgfqn=${pkgname/5-/}
source=(git+https://invent.kde.org/qt/qt/$_pkgfqn#commit=$_commit
        qtdeclarative-5.15.2-riscv-atomic.patch
        big-endian-qtquick-software.patch)
sha256sums=('SKIP'
            '4e5dbd2c0afa081aa6aa7bc70ab205bbdfd86f91e574183f1a7a98372d31d680'
            '39cc449ad6b5303773902094d86fad27e814922c4e63c5f7bd175a909ae21728')
options=(debug)

pkgver() {
  cd $_pkgfqn
  echo "$_basever+kde+r"`git rev-list --count v$_basever-lts-lgpl..$_commit`
}

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -Np1 -i ${srcdir}/qtdeclarative-5.15.2-riscv-atomic.patch
  patch -Np2 -i ${srcdir}/big-endian-qtquick-software.patch
}

build() {
  cd build

  case "${CARCH}" in
    riscv64) CFLAGS+=' -latomic'; CXXFLAGS+=' -latomic'; LDFLAGS+=' -latomic' ;;
  esac

  qmake ../${_pkgfqn} CONFIG+=fat-static-lto
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Symlinks for backwards compatibility
  for b in "$pkgdir"/usr/bin/*; do
    ln -s $(basename $b) "$pkgdir"/usr/bin/$(basename $b)-qt5
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
