# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovský <lahwaacz at archlinux dot org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=arrow
pkgver=20.0.0
pkgrel=3
# parquet-testing and arrow-testing projects have no releases, commits may need to be updated on pkgver bumps
_parquet_testing_commit=6e851ddd768d6af741c7b15dc594874399fc3cff
_arrow_testing_commit=d2a13712303498963395318a4eb42872e66aead7
pkgdesc="Columnar in-memory analytics layer for big data."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://arrow.apache.org"
license=(Apache-2.0)
depends=(
  abseil-cpp
  apache-orc
  aws-crt-cpp
  aws-sdk-cpp-core
  aws-sdk-cpp-iam
  aws-sdk-cpp-s3
  brotli
  bzip2
  gcc-libs
  glibc
  google-glog
  grpc
  libutf8proc
  lz4
  openssl
  protobuf
  re2
  snappy
  thrift
  zlib
  zstd
)
makedepends=(
  boost
  cmake
  flatbuffers
  git
  gmock
  ninja
  rapidjson
  xsimd
)
checkdepends_x86_64=(
  minio
  python
)
provides=(parquet-cpp)
conflicts=(parquet-cpp)
source=(
  https://archive.apache.org/dist/$pkgname/$pkgname-$pkgver/apache-$pkgname-$pkgver.tar.gz
  git+https://github.com/apache/parquet-testing.git#commit=$_parquet_testing_commit
  git+https://github.com/apache/arrow-testing.git#commit=$_arrow_testing_commit
)
sha512sums=('067e62d7d311cebfca43473b0aacaacc534da47c3450b75328517df69281d8be1e79b0430cc7e975eb613e05c62a62d6ca92a4c4f7882ae7733f826d774d9081'
            '65e1539eaa2b0b5dc9fd6d5b5d382412ba82b31661cdceb67b68f56961ca701ad005dcc898ac114d36ecafffc37e5a2e65371fa2eba8d2b7a49d17cd16e9b200'
            'e78205c84d4c893968fd93a0ffe9392bc9d09ec78ae176e8fae53787aba06c8fef3f68baa25b99bfc33c46760ff0ae87a06231b0145888ec61581be75f8a5d6b')
validpgpkeys=(265F80AB84FE03127E14F01125BCCA5220D84079  # Krisztian Szucs (apache) <szucs.krisztian@gmail.com>
              08D3564B7C6A9CAFBFF6A66791D18FCF079F8007  # Kouhei Sutou <kou@cozmixng.org>
              AF6AADA4C9835B75973FF5DA275C532289DD0F4A) # Raúl Cumplido Domínguez (CODE SIGNING KEY) <raulcd@apache.org>

export LTOFLAGS="-flto=2" # don't bomb cpu (nproc * nproc LTO)

prepare() {
  cd apache-$pkgname-$pkgver
  # Patch out unused AWS-SDK component https://github.com/apache/arrow/pull/45191
  sed -i 's|COMPONENTS config|COMPONENTS|' cpp/cmake_modules/FindAWSSDKAlt.cmake
}

build() {
  case "${CARCH}" in
    powerpc*) export CXXFLAGS+=' -latomic' ;;
  esac

  # workaround for cmake 4.0 - arrow bundles several old dependencies https://github.com/apache/arrow/issues/45985
  # (note that ExternalProject does not propagate this as a CMake variable so we need to export an environment variable)
  export CMAKE_POLICY_VERSION_MINIMUM=3.5

  # Arrow options are defined in https://github.com/apache/arrow/blob/main/cpp/cmake_modules/DefineOptions.cmake
  # Documentation for building from source: https://arrow.apache.org/docs/developers/cpp/building.html
  local cmake_options=(
    -B build
    -S apache-$pkgname-$pkgver/cpp
    -G Ninja
    -W no-dev
    # one pyarrow test fails when the build type is none of 'debug', 'release', 'minsizerel', 'relwithdebinfo'
    -DCMAKE_BUILD_TYPE=RelWithDebInfo
    -DCMAKE_INSTALL_PREFIX="/usr"
    -DCMAKE_INSTALL_LIBDIR="lib"
    -DARROW_SIMD_LEVEL=NONE
    -DARROW_RUNTIME_SIMD_LEVEL=MAX
    -DARROW_DEPENDENCY_SOURCE=SYSTEM
    -DARROW_BUILD_STATIC=OFF
    -DARROW_BUILD_TESTS=ON
    -DARROW_BUILD_UTILITIES=ON
    -DARROW_COMPUTE=ON
    -DARROW_CSV=ON
    -DARROW_FILESYSTEM=ON
    -DARROW_FLIGHT=ON
    -DARROW_FLIGHT_SQL=ON
    -DARROW_GANDIVA=OFF  # depends on LLVMAlt
    -DARROW_HDFS=ON
    -DARROW_IPC=ON
    -DARROW_JEMALLOC=ON
    -DARROW_JSON=ON
    -DARROW_ORC=ON
    -DARROW_PARQUET=ON
    -DARROW_S3=ON
    -DARROW_SUBSTRAIT=ON
    -DARROW_TENSORFLOW=ON
    -DARROW_USE_GLOG=ON
    -DARROW_WITH_BROTLI=ON
    -DARROW_WITH_BZ2=ON
    -DARROW_WITH_LZ4=ON
    -DARROW_WITH_SNAPPY=ON
    -DARROW_WITH_ZLIB=ON
    -DARROW_WITH_ZSTD=ON
    -DPARQUET_REQUIRE_ENCRYPTION=ON
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  PARQUET_TEST_DATA="$srcdir"/parquet-testing/data \
  ARROW_TEST_DATA="$srcdir"/arrow-testing/data \
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  # remove testing libraries
  find "$pkgdir"/usr/lib/ -name '*testing*' -delete
}
