# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=arrow
pkgver=8.0.0
pkgrel=3
pkgdesc="Columnar in-memory analytics layer for big data."
arch=(x86_64 powerpc64le powerpc)
url="https://arrow.apache.org"
license=(Apache)
depends=(apache-orc boost-libs brotli bzip2 double-conversion c-ares gflags
         grpc google-glog jemalloc libutf8proc lz4 openssl protobuf rapidjson
         snappy thrift uriparser xsimd zlib zstd)
provides=(parquet-cpp)
conflicts=(parquet-cpp)
makedepends=(boost cmake flatbuffers gmock python-numpy git clang)
source=(https://archive.apache.org/dist/${pkgname}/${pkgname}-${pkgver}/apache-${pkgname}-${pkgver}.tar.gz{,.asc}
        git+https://github.com/apache/parquet-testing.git
        git+https://github.com/apache/arrow-testing.git)
sha256sums=(ad9a05705117c989c116bae9ac70492fe015050e1b80fb0e38fde4b5d863aaa3 SKIP SKIP SKIP)
validpgpkeys=(265F80AB84FE03127E14F01125BCCA5220D84079  # Krisztian Szucs (apache) <szucs.krisztian@gmail.com>
              08D3564B7C6A9CAFBFF6A66791D18FCF079F8007) # Kouhei Sutou <kou@cozmixng.org>

build(){
  case "${CARCH}" in
    powerpc|riscv64) _cmake_options+=(-DCMAKE_CXX_FLAGS="-latomic") ;;
  esac

  CC=clang \
  CXX=clang++ \
  ARROW_BUILD_TOOLCHAIN=/usr \
  ORC_HOME=/usr \
  DOUBLE_CONVERSION_HOME=/usr \
  cmake \
    -B build -S apache-${pkgname}-${pkgver}/cpp \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INSTALL_LIBDIR="lib" \
    -DCMAKE_BUILD_TYPE=Release \
    -DARROW_BUILD_STATIC=OFF \
    -DARROW_DEPENDENCY_SOURCE=SYSTEM \
    -DARROW_BUILD_TESTS=ON \
    -DARROW_COMPUTE=ON \
    -DARROW_SUBSTRAIT=ON \
    -DARROW_FLIGHT=ON \
    -DARROW_FLIGHT_SQL=ON \
    -DARROW_GANDIVA=OFF \
    -DARROW_HDFS=ON \
    -DARROW_IPC=ON \
    -DARROW_JEMALLOC=ON \
    -DARROW_ORC=ON \
    -DARROW_PARQUET=ON \
    -DARROW_PLASMA=ON \
    -DARROW_PYTHON=ON \
    -DARROW_TENSORFLOW=ON \
    -DARROW_SIMD_LEVEL=AVX2 \
    -DARROW_USE_GLOG=ON \
    -DARROW_WITH_BROTLI=ON \
    -DARROW_WITH_BZ2=ON \
    -DARROW_WITH_LZ4=ON \
    -DARROW_WITH_SNAPPY=ON \
    -DARROW_WITH_ZLIB=ON \
    -DARROW_WITH_ZSTD=ON \
    -DPARQUET_REQUIRE_ENCRYPTION=ON ${_cmake_options[@]}
  make -C build
}

check(){
  PARQUET_TEST_DATA="${srcdir}"/parquet-testing/data \
  ARROW_TEST_DATA="${srcdir}"/arrow-testing/data \
  make -C build test
}

package(){
  make -C build DESTDIR="${pkgdir}" install
  find "${pkgdir}"/usr/lib/ -name '*testing*' -delete
}
