# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=squashfuse
pkgver=0.4.0
pkgrel=1
pkgdesc="FUSE filesystem to mount squashfs archives"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/vasi/squashfuse"
license=(custom)
depends=(fuse zstd)
makedepends=()
source=(https://github.com/vasi/squashfuse/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('bddbfeed1b50e795a4a466b2977617e48904108e261d64563234e98ea2cd2f19')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

# install missing headers
  install -Dm644 *.h -t "$pkgdir"/usr/include/squashfuse
}
