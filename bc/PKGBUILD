# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=bc
pkgver=1.08.2
pkgrel=1
pkgdesc='An arbitrary precision calculator language'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.gnu.org/software/bc/'
license=('GPL-3.0-only')
depends=('readline')
makedepends=('ed')
replaces=('bc-readline')
conflicts=('bc-readline')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('0876a4c5bfc23da79479519c6a8e03ac9f59ae54077eb71ffdcc6ddaccb76c4b7595b088e89e6ad82d833c072eeff5b378178084276584813c00eedace4c6f8c'
            'SKIP')
b2sums=('6ff6d5083aa67d4075116c691b1f975127ab5079e1a1b63d916fbd3e6d5e95c7f4bc7fe196cac0e2a51a7f4c56d91da60495f3ce60f08bee8f2ba4278a791834'
        'SKIP')
validpgpkeys=('00E426232F384BF6D32D8B1881C24FF12FB7B14B' # Phil Nelson <philnelson@acm.org>
              '325B54E4653F935A25CF32BDA186278D426A38E9') # Ken Pizzini <ken@gnu.org>

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --with-readline

  make -j1
}

check () {
  cd "${pkgname}-${pkgver}"

  echo "quit" | ./bc/bc -l Test/checklib.b
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
