# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=bc
pkgver=1.08.1
pkgrel=1
pkgdesc='An arbitrary precision calculator language'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.gnu.org/software/bc/'
license=('GPL-3.0-only')
depends=('readline')
makedepends=('ed')
replaces=('bc-readline')
conflicts=('bc-readline')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('256c94833ead8029122d3ba6475cf4ff030c208a5bd3e2e9c5c8888467215e0fa9ab34e4226791bbd6e47498c0c61f0dc6399eecb0c8cf6a43453e84911c1847'
            'SKIP')
b2sums=('51d1bac17f31bb7dbb044c28752cccfe59dd5bcb78a2c3bfa66a345577ea3aee37fef8d7efae83a57e41ae77d7317008f3682dfbc3165c76888770636deead01'
        'SKIP')
validpgpkeys=('00E426232F384BF6D32D8B1881C24FF12FB7B14B' # Phil Nelson <philnelson@acm.org>
              '325B54E4653F935A25CF32BDA186278D426A38E9') # Ken Pizzini <ken@gnu.org>

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --with-readline

  make -j1
}

check () {
  cd "${pkgname}-${pkgver}"

  echo "quit" | ./bc/bc -l Test/checklib.b
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
