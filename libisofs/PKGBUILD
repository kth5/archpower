# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Hugo Doria <hugodoria@gmail.com>
# Contributor: Bjorn Martensen

pkgname=libisofs
pkgver=1.5.6.1
pkgrel=1
pkgdesc='Library to pack up hard disk files and directories into a ISO 9660 disk image'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://dev.lovelyhq.com/libburnia/libisofs'
license=(GPL-2.0-or-later)
depends=(glibc acl zlib)
makedepends=(git)
source=("$pkgname::git+$url#tag=release-${pkgver//.1/.pl01}")
sha512sums=('e027b8bfaaa3fa1c2ce51e1476be3ac21543b6d050a968f393f370d2be4fd24d4a71b950e20ca48b48b6669cf14b5f7c936326497ab9f4d3a5917b33553fd020')
b2sums=('d09ea7bf1f6fa7f27f198c272ff9343925ee6f481977b063c61f14ab608acab7c2559f2463bcfcf8be7dba9aac43c5d60fd8d22ca9657702e53ca5ba0f29cfe6')

prepare() {
  cd "$pkgname"

  autoreconf -vfi
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --enable-libacl \
    --enable-xattr \
    --disable-static

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}
