# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=yaz
pkgver=5.35.1
pkgrel=1
pkgdesc="A toolkit supporting the development of Z39.50/SRW/SRU clients and servers"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.indexdata.dk/yaz"
license=('LicenseRef-yaz')
depends=('gnutls' 'libxslt' 'icu')
makedepends=('zsh')
changelog=$pkgname.changelog
source=(http://ftp.indexdata.dk/pub/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('db030d6d66880398a44215e26132630ee94f5e462d838809e43f97e6399c1353')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vif
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-shared=yaz
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#License
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
