# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20251022
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-or-later')
makedepends=(
  'git'
  'perl'
  'libreoffice-fresh'
  'graphviz'
  'jdk-openjdk'
  'perl-pod2pdf'
  'perl-pod-pom-view-restructured'
  # pod2texi provided by texinfo in base-devel
)
source=(
  "$pkgname::git+https://git.savannah.gnu.org/git/parallel.git?signed#tag=${pkgver}"
  '0001-Remove-citation-things.patch'
)
sha512sums=('5e5dc507f251baf81acddc749eb399143827dec791a559ecf112e856e74afa0ed1d7489bf0708317c6ca0557712e2f15c60c2a51057025ebbcf58e659a8d4fec'
            '63931bd6211de25bd343f4846c651d2b33dc40f9d4e5889455f2873ebf7e3800cd8a1a5f13b134b773a2f6407b969348c04d7f7152b05cf9b590228666dd3cec')
b2sums=('6ab821da9f0ee264a455ce1aa8846cf2fb2eaddbd42c5f68090fd9932ee9c00cbcf1f94adce62158bf872154d252f75df379e931ef8efe04c467c171df90ffaf'
        '93d21e6e581fcab1f2fca54a9e23de9bbde522d1dd4fed264b6509ff65eebde293c8bcb4c4f9999c820570e6ceb77d9cc36fa26bcdb7610230b69fe31407ffe6')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"

  autoreconf -fiv
}

build() {
  cd "$pkgbase"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}
# vim:set ts=2 sw=2 et:
