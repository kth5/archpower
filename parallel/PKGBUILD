# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20240722
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-only')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('f3dbcf8e6f5c7f011f6a21b8395d256e856b805f798531995b5000232d34bd0cf9b105f2cc6da24af5d8cdcdc63b5323ce72f435f835b353d0ab44b2e87d0129'
            'SKIP'
            '3b312c33fa093cf0c63d31ce89b7ca400c9af6cde7d768aff2149957f5bd72ebcdd2dd03abed87ba183c89d8ab8b7db99aa65a782803ddfc8de464ac16bab237')
b2sums=('b9dbf3ef945f8dd3df09c9e9d0c70e0cca5ab12d2ed29f96a5c9c6bed6ce8ca700c5c9d8ce1f28a394b911407ff1439907cdbfa4edee24ffbe24bc75fb250546'
        'SKIP'
        'b2fbe128353122dfbbf3bb3206fa91cc3c9c5100261ca2474fef53339cde900c4ec77fe6b92e7f3be10616fc098a7fc03a366e101c43f2d4412c0969c77b3799')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
