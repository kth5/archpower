# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20230622
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('07fe4f278d0e177e2825c0b98cc01ef53334e21f4e77e22e009c99c774d5641ea746bfbd416a11da60132135cd4f412ec818439558044cca872b05b93f6e5423'
            'SKIP'
            'd91b0b07d8de0a471e5bd88e2da2e01f1858ff5516d2394ad54a02514456f156009b336b89a01c690b8b676962b42a7f47cbf3fa8ac8c24a2aa117cca0e16fe6')
b2sums=('cd0c7ba266f056ea72703f59a16127218ba56422b130a38341f4052be8dc37711194933f226587aa663789a00ec36a18c0a7c24471502cadce2a5dd1a9fa2400'
        'SKIP'
        '38d22c396e9cbf6bddf5bff77a1b69b2d9ce3749b00067d0d2b51f97ac4a2a8f1f94b8b8ce49976944e65bc95852a551320d9248446f59764425c821479a997b')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
