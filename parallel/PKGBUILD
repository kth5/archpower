# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20240322
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('5eefc4dd8c320b3607eabc14924317340d74bbc56e4b0ede8cf7091380b433e6c8b2dac61711921516591396997b3e18a7737e0a06724ed7190cf3a8b37258af'
            'SKIP'
            '294c737db78dbee64769833a193f8c88bd50b3a80b73cb44b10df1e4d9f73d15f8bdee1d7e4c27ec9a42a16658322aa29d442a1c3b0d56e4eb10c68f13edf4bd')
b2sums=('20bd793421ec576ceb5643f933e87fc2b67d7ee92fc8e7a5e62142f69736ca720169c55e326ee083d77e365f4552a6b842025378e0e1fcb9190b1e31175e7ef6'
        'SKIP'
        'ae5465c21cbe4ce3f2f5c2628c3cd70d4b30d7fee5a715aa251098bd66af37305c8bff98c0d269891cba577f2f8d9fdec93a6abfe875281f5b7882cece66a8b8')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
