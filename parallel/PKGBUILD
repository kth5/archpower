# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20250522
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-only')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('33e54ae4f859daf10a499436b6342783a42193cb3a89c201a32574b86db7018f079b58705eca831e1cc90bb0ec84bc746bc9b20509e4692d7092e948fba7ea38'
            'SKIP'
            'c0510798f36bf0d4794fc4e7e2ee212b10e44ae8899a47c74e0b5a900ee3facd06dd26c55e3a46c31596bdcf5f432d01b6858f81b1897d734231455813dc34e5')
b2sums=('5973970d2a04a0301435376a39e79efc34d2b589f6bdaaaf4c3ada33e74079630a59f529d7d07e244e856a084889f1c2c928b0709fbe264074c4adfcc9d638c3'
        'SKIP'
        '1feb7861676d525c86972eba7f655d9628a6c337d8f3f5d00835e7b7daad539f32bd20a84a293d6f4702bf9c82466551ed92368bef18b31eae54e8c6871747de')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
