# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20240122
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('1fc7f1ceda8ef794f66e60c0f8fdf589aaa77ced243409741892373af9a2d698541d4c365ad61056868a2f11f4934db02374da4c121ebc418785c5a72fd47b3e'
            'SKIP'
            '4fb5730671ddd2b88aaac923f445174ea097709d059cf975591209caba9dfb7e7e6646acea3361d670c62032afdaa86c3b24571bba0b73757663a3430d5cabce')
b2sums=('3563d5f7f29701b71531501ce070c712d06e16d70005bd7a18f890ad13e0c6035ea091239073ccceabcf4406bba6ea96275531e0ef67cf4b304ff56649100d60'
        'SKIP'
        'fc3b7544249565168457a91746023341184a78b08a3ab300568c41477879f681d016504f7f1a39777a7de550f2c228a87bc4333b9ec8e38e44c29c39b82af47c')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
