# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20230722
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('cbc9b5742760f7714f75e7a5eae859ae8d13dc9970220761e7c3183df2ab66d9d148afea53b2292f93d6fae2ea83c2aa4b9a4deb3d2565a5715f9197063e9552'
            'SKIP'
            '146d8efc9140d4c8d0848700bea88ebe8b7b28367bb4f961a7419bcdfd96a775a802fb73ae545fa55e15d82271a75c196975a727ba60169dcc4f641bf0cc14d9')
b2sums=('d045e0cf2cbd2c1b9be69facdd3150fa460f8133dcf933816ebc0309966c124607ff3888377027c8598e5774ee47f39598fafd32a0cfa40c8cd8ce518955333c'
        'SKIP'
        '084d372f48e4d8a7b16730b1379db9d08e62bfc93d825e0b0328710d1e3d58acf8480d66a4dc72d4d2b8759ec087092c4af1683646f995c3874ffc369d484a75')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
