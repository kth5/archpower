# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20230822
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('b28480abc83b819163c69b6cbba6ff51d9b4c9bdff08a910222be0a99307c21435473c42fc961ca5599c01f77f06870760ce01b9a9bbb1c80883ac756ac20f1d'
            'SKIP'
            '516bbb535c48b20a2a468e2e080fe97cba6d5989cb847c93c3fa229e750d473f1cfb02cc0cf88c8d34739a85f39201dd627fd8b01e300aa3da9013209f1bf6c1')
b2sums=('cbf272623b92ad1fd1411e5f89e44855e07a2711b4b2573bf1bdca318efc3a5f3544eb8eec06f148e9af8d39d5a89c9e206029caad93d190091f14c8f158d046'
        'SKIP'
        '6de7ae46b3423e45acb9f7deb7c06488f1da02324bcc03c1082adcb1b259cc80d7149e77fb85c69acfebe1f79538d0d626127ed8a5ac36abd728cadafc73f1be')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
