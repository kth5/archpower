# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20250222
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-only')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('65b4928a592e95adc57b88ba16e50befb9ecd3f9a3bfb1eb54e1ce784c6b0e1427b8401db99c0816e1cc3333da998c40942c4ca5153ae0bd992d3e606fee7374'
            'SKIP'
            'b89eec0f705a488d330a7bef4dba5f016830f111acd6d0805977cbf7fb273edd57e3990a9535770d5e8950efe29ec7dc8181252774b65a9e2fc12a5da46156f1')
b2sums=('94c6b6bcfd3e6c1a4b823f6f0c1de451d8d7254c3e274d8a83675bfc21ab36789013363906d35c769fc111ce31fe2e77d0fd706c0ff72e9ad72a47e183ed4a75'
        'SKIP'
        'd4d8707ca2e9d1712d39f86f52c2c0b800b2559e6c72174aed1d062f2600dc9dd395789ee71d8796430f06033c82263f549be9f5a954a8e3ca1aa8562b5836a4')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
