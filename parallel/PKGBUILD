# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20230922
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('296aa4e1b53de9c0fe1d73bc1a1e96b436d95a053097053f6451ad4b0ad4489d6136b19623390070b60f84ce4a1248716d53addf47ba72a2f33013524d037da0'
            'SKIP'
            'a8d5359781637f213cf0c9291e495dd4a363e6ddf306d7b9eebbc4f128b96eef9e8063a2b1e87d2c1e5a569ce045eba63336e15cefbed5e8c0beed53534cafd8')
b2sums=('88053502e8c663d795f0752b4b13d1440629b1d657a1be6ea355f9afb625527875e5018baaa68e923b74d5b68ee854787062b76420480a1c63f3b4aad8247d82'
        'SKIP'
        'd3cc72ae4ab232dc6c75229e3abd4f794ffb7bf7485f05f1601057ca2bdd9d364f4dc6543d11cc407720a206f5a57b094323f217a908c38b373516dbc436293b')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
