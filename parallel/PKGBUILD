# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=(parallel parallel-docs)
pkgver=20260122
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=(GPL-3.0-or-later)
makedepends=(
  git
  perl
  libreoffice-fresh
  graphviz
  jdk-openjdk
  perl-pod2pdf
  perl-pod-pom-view-restructured
  # pod2texi provided by texinfo in base-devel
)
source=(
  "$pkgname::git+https://git.savannah.gnu.org/git/parallel.git?signed#tag=${pkgver}"
  0001-Remove-citation-things.patch
)
sha512sums=('864f6b321101d35ec54663439bd29ca0247db72cf63261895f94c5034452042cfb1ef7868b51734ca724590c06ad3f4fc748cc643b40db11f79a0617052d34db'
            'e17ed896d77b63b2ea00395091d4a5feeead79a66d14a8d7bf8d0fdbf7de6bebcada676a887dd6b196f1f6e0a30263ef070332fed4291e84e1b602e763b637f2')
b2sums=('c193e9b0ff95ae4ffc74fa29ab9edb77f8ec41ff64460711d19507535a972b318c76009d8991d0f956f19dfb867845af4d0f08f5ac5374127fed3bcdffec2f7d'
        'f0c758421b9a56af4e00d53801f984bed4704b1a115ab48db73bd3d47e184bfc9ee110ba4d03b231147a1738b11c3adb57530c9315f735257da1a2376a1cbdd1')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"

  autoreconf -fiv
}

build() {
  cd "$pkgbase"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=(perl procps)
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}
# vim:set ts=2 sw=2 et:
