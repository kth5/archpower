# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20250322
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-only')
makedepends=(
  'git'
  'perl'
  'libreoffice-fresh'
  'graphviz'
  'jdk-openjdk'
  'perl-pod2pdf'
  'perl-pod-pom-view-restructured'
  # pod2texi provided by texinfo in base-devel
)
source=(
  "git+https://git.savannah.gnu.org/git/parallel.git?signed#tag=${pkgver}"
  '0001-Remove-citation-things.patch'
)
sha512sums=('738a14894ea771f7af62c1cdebc80965ffda6fe07017eb4d8c0a9a7aa7d908fa6cdbad1017bfc28bf6787c83e54c4ad6d5a1453d99cb00c66a92230370194fa0'
            '050a8db256682d00d63e96800594369af3b3b67051a96a9b3fae21367e4a79b597b252488bf303f7022a81da2be59ce1c2bc034e14ad138a6bf827f0e6d86f2b')
b2sums=('5ebea4c1ab5ea21e5a27d82b87f9812d1d2f45fc63649be2aecdf9d5d5f607d509c30d84e9d9f3f7b9aa656bfcec555694a630f57099b54d7ab9643fb66d8a1b'
        'aa9111d86089fa0532da4cd41b32184403456f0566643b7668268267ba816c9f3f1e5722f63910e9437fab1362fb7f0f512372f4ef45d83364ebbbaeaf867efe')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"

  autoreconf -fiv
}

build() {
  cd "$pkgbase"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
