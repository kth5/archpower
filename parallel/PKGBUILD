# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20241022
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL-3.0-only')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('100cb032e93e054f17d3b987e9aedb63ece5b58ba43baccd420c1e83085ed846dfc4c5c0941e4d8de128058352a336d46a69a96fd259039996dcfa2db9e0786c'
            'SKIP'
            'a2cb69903b331fd37f6f46db9dc89ca7625661a45af40468ff9bdbb1a375a30c1266406e28de28b1f00e3318662cdce3d1bd92a8cf484a34535385fb499d4759')
b2sums=('3783185c26686b4ddc37472e01d814ebb5464b39864194a5b1604e3f592cff3d8277a104a7c98a428c0d340d1e1cbacd125dc4292db13c503c0f985edd1ed8dd'
        'SKIP'
        '4ee261562baacd39f9f049c49e7eedee909d4b50d361465fa0b6055044ebd6e1cd566cb8e6027281d93c2fdcbfa368144dd8b8ab970ccd3a775b65282cd9d7bf')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
