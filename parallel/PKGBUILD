# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Vianney le Clément <vleclement AT gmail·com>
# Contributor: Peter Simons <simons@cryp.to>

pkgbase=parallel
pkgname=('parallel' 'parallel-docs')
pkgver=20231222
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel'
arch=(any)
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
source=(
  "https://ftp.gnu.org/gnu/$pkgbase/$pkgbase-$pkgver.tar.bz2"{,.sig}
  '0001-Remove-citation-things.patch'
)
sha512sums=('18d3dd5a7aab4cbf7f9ea8c5d8c8a2dd6b2bd5c9deb56daba85a3ff1c3b3ea84403913d7266b7ea04c0dec432d1288f98c911ac29120d775ee795c56cc31c0a2'
            'SKIP'
            'c571bd159642f75aa0a6e236f14e21712ed584e0215ceb619f5c5b4a64377b04f0a3098debe63198644a10f7e6a46e9b826ef30ba8902d5c752fe5a154038740')
b2sums=('677b36c2562908f89d4eaae483555a840ea41e9359c77a17fa1dde2871c17a520ae8b948d9fedb1d67d220b7795df9bcc080d3bff67a9b126ff1ea2c329cdb15'
        'SKIP'
        '26812be41ca8212267626027582510b7de9b412c5a5bc1885775e308f28007fcba939a80d9537b29b951d58701f90f61f0d3f316505403dbb33acac09f06a716')
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')

prepare() {
  cd "$pkgbase-$pkgver"

  # remove citation warnings
  # https://en.opensuse.org/openSUSE:Packaging_guidelines#Donation_requests
  patch -p1 -i "$srcdir/0001-Remove-citation-things.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr

  make
}

package_parallel() {
  depends=('perl' 'procps')
  optdepends=('parallel-docs: documentation')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # split documentation out of main package
  mv "$pkgdir/usr/share/doc" docs
}

package_parallel-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase-$pkgver"

  install -vd "$pkgdir/usr/share/doc"
  mv -v docs/* "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
