# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=asio
pkgver=1.34.0
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=(any)
license=('BSL-1.0')
makedepends=('boost' 'git')
source=(git+https://github.com/chriskohlhoff/asio.git#tag=asio-${pkgver//./-})
sha512sums=('b12251a2402328eb2a559967b0dec97f6aa791ffbc4fd8ad591e76e4aa0dc7bb0f18f9a32c9a53d11b76c0684993686e4f83a747e23696dcfb27dbf226b3392b')
b2sums=('ecb5ae3e13f8e3019450b50f14fbb6f498feb3ca7ab900e075d1dba74bb3890e375341486b8159a69c53fb0c71337002476e2636ef8c34db7a677ec248bd4b6b')

prepare() {
  cd ${pkgname}/${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}/${pkgname}
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}/${pkgname}
  make check
}

package() {
  cd ${pkgname}/${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
