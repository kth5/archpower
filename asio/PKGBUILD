# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=asio
pkgver=1.34.2
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=(any)
license=('BSL-1.0')
makedepends=('boost' 'git')
source=(git+https://github.com/chriskohlhoff/asio.git#tag=asio-${pkgver//./-})
sha512sums=('eee167743e45bf1db5e9d8a282c748c5ec8ced4e8c8c2209828fd91c0921af001f0d202237f8ecebeddf0cbebd697e643acec1d4922d71d06d6c0e4badc994b2')
b2sums=('57209936a1909ce0ab1041d018881291e374930ae093f180d797fedaee465a3f2b30a1ad76659014c9e4871ca4c027b25829136eccea8812732c13f7ce387415')

prepare() {
  cd ${pkgname}/${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}/${pkgname}
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}/${pkgname}
  make check
}

package() {
  cd ${pkgname}/${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
