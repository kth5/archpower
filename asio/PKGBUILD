# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=asio
pkgver=1.36.0
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=(any)
license=('BSL-1.0')
makedepends=('boost' 'git')
source=(git+https://github.com/chriskohlhoff/asio.git#tag=asio-${pkgver//./-})
sha512sums=('189b57c7e38bc92baf9bf3310924d855435ec3bbea48074f5a0a5daf4f4aa567214f4f1f14872ba92bac23265112b0a7554d3ae7b591b2cb1136eca817fc7461')
b2sums=('d389612a2de377ea6743905953fa91fb122e57caed6e2e832a7bf1d979fac08f6e2e74ee4b1db7ea769519ca76cbdc3de59f617f2a744f1be7ab1b5c919b5b55')

prepare() {
  cd ${pkgname}/${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}/${pkgname}
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}/${pkgname}
  make check
}

package() {
  cd ${pkgname}/${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
