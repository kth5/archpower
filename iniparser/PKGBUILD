# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=iniparser
pkgver=4.2
pkgrel=1
pkgdesc='A free stand-alone ini file parsing library written in portable ANSI C'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/ndevilla/iniparser'
license=('MIT')
depends=('glibc')
provides=('libiniparser.so')
makedepends=('chrpath')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ndevilla/iniparser/archive/v${pkgver}.tar.gz")
md5sums=('f52458ace1466c5798c47aa3e7a9b949')
sha256sums=('dbcbaf3aedb4f88a9fc0df4b315737ddd10e6c37918e3d89f0ecc475333bde4d')

build() {
  cd "${pkgname}-${pkgver}"

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm0644 src/iniparser.h "${pkgdir}"/usr/include/iniparser.h
  install -Dm0644 src/dictionary.h "${pkgdir}"/usr/include/dictionary.h
  install -Dm0644 libiniparser.so.1 "$pkgdir"/usr/lib/libiniparser.so.1
  install -Dm0644 LICENSE "${pkgdir}"/usr/share/licenses/iniparser/LICENSE
  ln -sf libiniparser.so.1 "${pkgdir}"/usr/lib/libiniparser.so
  chrpath -d "${pkgdir}"/usr/lib/libiniparser.so
}
