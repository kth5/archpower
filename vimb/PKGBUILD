# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Robert Timm <mail@rtti.de>
# Contributor: Jason Ryan <jasonwryan@gmail.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

pkgname=vimb
pkgver=3.7.0
pkgrel=1.1
pkgdesc='The vim like browser'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://fanglingsu.github.io/vimb/
license=(GPL3)
depends=(webkit2gtk-4.1)
makedepends=(git)
_tag=7864d001d29a04cf9526f1f6714516a8923755e7
source=(git+https://github.com/fanglingsu/vimb.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd vimb
  git describe --tags
}

build() {
  make PREFIX=/usr -C vimb
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C vimb install
}

# vim: ts=2 sw=2 et:
