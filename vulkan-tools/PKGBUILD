# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-tools
pkgver=1.4.304.1
pkgrel=1
pkgdesc="Vulkan tools and utilities"
url="https://www.vulkan.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  libx11
  libxcb
  vulkan-icd-loader
  wayland
)
makedepends=(
  cmake
  git
  glslang
  libxrandr
  ninja
  python
  spirv-tools
  volk
  vulkan-headers
  wayland-protocols
)
groups=(vulkan-devel)
source=("git+https://github.com/KhronosGroup/Vulkan-Tools#tag=vulkan-sdk-$pkgver")
b2sums=('859aa98f4a378328031355012d1ffe977c4133c9e436db4d03724a5f78a1d8c0c0764cbc130e52d6ccd194075f31b00b2af12226d489e9b9117a011543b50b92')

build() {
  local cmake_options=(
    -D BUILD_ICD=OFF
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_SYSCONFDIR=/etc
    -D CMAKE_SKIP_INSTALL_RPATH=ON
  )

  cmake -S Vulkan-Tools -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
