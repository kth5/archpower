# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: philefou <tuxication AT gmail DOT com>
# Contributor: lindquist <tomas@famolsen.dk>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Mihai Militaru <mihai.militaru@ephemeros.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgbase=glfw
pkgname=('glfw-x11' 'glfw-wayland' 'glfw-doc')
pkgver=3.3.9
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.glfw.org/"
license=('custom:ZLIB')
makedepends=('mesa' 'cmake' 'doxygen' 'vulkan-headers' 'vulkan-icd-loader'
             'extra-cmake-modules' 'wayland-protocols' 'libxi' 'libxrandr'
             'libxcursor' 'libxkbcommon' 'libxinerama')
source=("$pkgname-$pkgver.tar.gz::https://github.com/glfw/glfw/archive/${pkgver}.tar.gz")
sha512sums=('9fcccd650990fa88fcb6383afa90bc6c2eca7f4a4a3d4fc58fa5bd6b7995c32989bb29d2753f438984043b294244b82ba9f094426e12f3d4da4547e73bb62e3f')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  mkdir build-x11 build-wayland
}

build() {
  cd "$srcdir/$pkgbase-$pkgver/build-x11"

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON
  make docs

  cd ../build-wayland

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON \
      -DGLFW_USE_WAYLAND=ON
}

package_glfw-x11() {
  pkgdesc="A free, open source, portable framework for graphical application development (x11)"
  depends=('libxi' 'libxrandr' 'libxinerama' 'libxcursor' 'libgl')
  replaces=('glfw')
  conflicts=('glfw')
  provides=("glfw=$pkgver")

  cd "$srcdir/$pkgbase-$pkgver"/build-x11

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_glfw-wayland() {
  pkgdesc="A free, open source, portable framework for graphical application development (wayland)"
  depends=('wayland' 'libxkbcommon' 'libgl')
  conflicts=('glfw')
  provides=("glfw=$pkgver")

  cd "$srcdir/$pkgbase-$pkgver"/build-wayland

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_glfw-doc() {
  pkgdesc="Set of HTML documentation for GLFW"

  mkdir -p "${pkgdir}/usr/share/doc/glfw/"
  cp -r "${srcdir}/glfw-${pkgver}/build-x11/docs/html" "${pkgdir}/usr/share/doc/glfw"
}
