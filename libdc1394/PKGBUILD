# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libdc1394
pkgver=2.2.7
pkgrel=1.1
pkgdesc='Library to control IEEE 1394 based cameras'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://damien.douxchamps.net/ieee1394/libdc1394/'
license=('LGPL')
depends=('glibc' 'libraw1394' 'libusb')
makedepends=('doxygen')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('537ceb78dd3cef271a183f4a176191d1cecf85f025520e6bd3758b0e19e6609f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-doxygen-html
  make
  make doc
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  cp -r doc/html "$pkgdir/usr/share/doc/$pkgname/"
}
