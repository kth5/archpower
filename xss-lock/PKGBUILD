# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Raymond Wagenmaker <raymondwagenmaker@gmail.com>

pkgname=xss-lock
# It seems that development has stopped but the latest commit is more stable
# than the latest tag.
_commit=ac39fdc20f8051a2460e9af84dd8f3d761785a50
pkgver=0.3.0.r30.gac39fdc
pkgrel=1
pkgdesc="Use external locker as X screen saver"
url="https://github.com/wavexx/xss-lock"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(MIT)
depends=(
  bash
	glib2
	glibc
	libxcb
	xcb-util
)
makedepends=(
	cmake
	git
	python-docutils
)
optdepends=(
	"bash-completion: for bash completion"
)
source=("${pkgname}::git+https://github.com/wavexx/xss-lock.git#commit=${_commit}")
b2sums=('23209af2f10e3f538ff49a507c54729433cf098434f8ee586187ed6496bed9f075cde7c7bc7d3a44a5780acde7ab28197ed76cc43a5222c22b44b6f328dd9530')

pkgver() {
	cd ${pkgname}
	git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd ${pkgname}
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make
}

package() {
	cd ${pkgname}
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
