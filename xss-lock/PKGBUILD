# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Raymond Wagenmaker <raymondwagenmaker@gmail.com>

pkgname=xss-lock
# It seems that development has stopped but the latest commit is more stable
# than the latest tag.
_commit=0c562bfa5d9073c6a47060ee06946aa39eff2fae
pkgver=0.3.0.r27.g0c562bf
pkgrel=1
pkgdesc="Use external locker as X screen saver"
url="https://github.com/wavexx/xss-lock"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(MIT)
depends=(
	glib2
	glibc
	libxcb
	xcb-util
)
makedepends=(
	cmake
	git
	python-docutils
)
optdepends=(
	"bash-completion: for bash completion"
)
source=("${pkgname}::git+https://github.com/wavexx/xss-lock.git#commit=${_commit}")
b2sums=('7a9abca7870b96f951667030b64f9b0e8c596db4f8ae9c4ac809f8a24f1a74da187fce2f16bae61ed8734a20676cbc07b4414ffc487bbaac7346880b45931e6c')

pkgver() {
	cd ${pkgname}
	git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd ${pkgname}
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make
}

package() {
	cd ${pkgname}
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
