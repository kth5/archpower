# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=libtraceevent
pkgname=(
  libtraceevent
  libtraceevent-docs
)
pkgver=1.8.3
pkgrel=1
epoch=1
pkgdesc="Linux kernel trace event library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://git.kernel.org/pub/scm/libs/libtrace/libtraceevent.git/about/"
license=(
  GPL-2.0-only
  LGPL-2.1-only
)
makedepends=(
  asciidoc
  xmlto
)
source=(
  https://git.kernel.org/pub/scm/libs/libtrace/$pkgname.git/snapshot/$pkgname-$pkgver.tar.gz
  $pkgname-1.5.0-documentation.patch
)
sha512sums=('25aebdd3f8bd5f9e0fadf4f03af73effdfe6e40c5667dfbe5011f4bfaf536657dde1c085e7c26d13a9da04fa8ce2d7c27210a5d3ab8aabc7e7a485a46a4456ea'
            '74b34a722d3fb9d672826e0b6b137f94a7fcd41bb372f8944bb6a0c652c0e725aaef8e95284fc091c2e9954bcf1656b5428d7e0f121682d40c25623178ee4a1e')
b2sums=('91df67b7ba2bb76f594b9b6ab457394d3fad8e1db5ec2206e58c821ebf8101c4e1c87adb00aa7c59520d1ac2b0820f43ebe01a062e5c0bb586a4f99078c1b3f4'
        'f66f4f20dfe562407a0d4bb53785d1515c4b5f4be64dd96e06ea6c9e03c299b2f0f613901c30893a976b3874d13e768791632a7cb89be92f3788330e4f5ab97d')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../$pkgname-1.5.0-documentation.patch
}

build() {
  make -C $pkgname-$pkgver EXTRA_CFLAGS="$CFLAGS" -j1
  make -C $pkgname-$pkgver/Documentation
}

package_libtraceevent() {
  depends=(glibc)
  optdepends=('libtraceevent-docs: for documentation')
  provides=(libtraceevent.so)

  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/Documentation
  (
    cd "$pkgdir"
    _pick libtraceevent-docs usr/share/doc
  )
}

package_libtraceevent-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}
