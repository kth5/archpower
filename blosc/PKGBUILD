# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=blosc
pkgver=1.21.4
pkgrel=1
pkgdesc='A blocking, shuffling and loss-less compression library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.blosc.org'
license=('BSD')
depends=(
  'glibc'
  'lz4'
  'snappy'
  'zlib'
  'zstd'
)
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Blosc/c-blosc/archive/v${pkgver}.tar.gz")
sha512sums=('cbe8406dc88318a582b6ea5e5ba1a25b66814f741743dd512b1a61664691977bfe0896baf24d2e998cdbb209a3ba83c6eb79e86bc1e7a2eb5ef2cde697e8eb65')
b2sums=('5b58e5069ed99e67adaef832871d45e4e3b88dede169833b07bb1800d35d6d43909c59645f79c003f9a80604e432c81f7d5da35fd19899fbb0378e81f015b0fa')

build() {
  cmake \
    -B build \
    -S "c-blosc-$pkgver" \
    -D CMAKE_INSTALL_PREFIX="/usr" \
    -D BUILD_STATIC="OFF" \
    -D DEACTIVATE_SNAPPY="OFF" \
    -D PREFER_EXTERNAL_LZ4="ON" \
    -D PREFER_EXTERNAL_ZLIB="ON" \
    -D PREFER_EXTERNAL_ZSTD="ON"

  cmake --build build
}

check() {
  cd build

  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 \
    "c-blosc-$pkgver/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
