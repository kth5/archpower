# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=blosc
pkgver=1.21.6
pkgrel=2
pkgdesc='A blocking, shuffling and loss-less compression library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.blosc.org'
license=(BSD-3-Clause)
depends=(
  glibc
  lz4
  snappy
  zlib
  zstd
)
makedepends=(git cmake)
source=("$pkgname::git+https://github.com/Blosc/c-blosc#tag=v$pkgver"
        c23.patch)
sha512sums=('ea5e14ed1ece973e8cb31544cc016ae01b4f59ae7b6e6425c8703f98bda68eff8fc6382fb6e2cb4ec406db22a9229de8148df9a9aca8570f218c431866f709cc'
            '77f25a39209282066df41b0a3c6cb3dadc91e493b837a5fc9b8d4680595a34a31518ba820cd64732130fd4025a1f4cf9df3d9e0fd5d373da0bc5527a1453ff27')
b2sums=('e9027864a8a2f67cda8d7f71f22cfd4c99ace91b57ca02c1de274e36c546cad7e9dcb387323ca2924c3daf006f60f1f8a918f1e8c4b5a1cc3b0e248bfd895d0d'
        '0afeccae9e62a2e3f9aedea6eef7583d5cdf9b59b804b2b73f471976750f5d92e73f7a9e2e1bdef412a330bf44fedcf51aa555a888f9755ead841bf261e3c10b')

prepare() {
  cd "$pkgname"

  # update minimal cmake version
  git cherry-pick --no-commit 051b9d2cb9437e375dead8574f66d80ebce47bee

  patch -Np1 -i ${srcdir}/c23.patch
}

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_STATIC=OFF
    -D DEACTIVATE_SNAPPY=OFF
    -D PREFER_EXTERNAL_LZ4=ON
    -D PREFER_EXTERNAL_ZLIB=ON
    -D PREFER_EXTERNAL_ZSTD=ON
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

check() {
  cd build

  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}
