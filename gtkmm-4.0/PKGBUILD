# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm-4.0
pkgname=(
  gtkmm-4.0
  gtkmm-4.0-docs
)
pkgver=4.14.0
pkgrel=1
pkgdesc="C++ bindings for GTK 4"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  gtk4
  pangomm-2.48
)
makedepends=(
  cairomm-1.16-docs
  git
  glibmm-2.68-docs
  meson
  mm-common
  pangomm-2.48-docs
)
checkdepends=(xorg-server-xvfb)
_commit=5cafb9bb49b9ec907ba1637dec0d1159a98c6a73  # tags/4.14.0^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
b2sums=('aba16054f535ad4d6b6d0f6113f21fc480e2a854e69179840e69b25aa19ce02cf5e1b16c297313ded506fdf018555e2201240e223168b6384992f0f76569f8bc')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson gtkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

package_gtkmm-4.0() {
  depends+=(
    libcairomm-1.16.so
    libglibmm-2.68.so
    libpangomm-2.48.so
    libsigc-3.0.so
  )
  provides=(libgtkmm-4.0.so)
  options=(!emptydirs)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_gtkmm-4.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
