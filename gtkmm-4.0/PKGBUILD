# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm-4.0
pkgname=(
  gtkmm-4.0
  gtkmm-4.0-docs
)
pkgver=4.18.0
pkgrel=2
pkgdesc="C++ bindings for GTK 4"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(LGPL-2.1-or-later)
depends=(
  cairo
  cairomm-1.16
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glibmm-2.68
  graphene
  gtk4
  libsigc++-3.0
  pangomm-2.48
  vulkan-icd-loader
)
makedepends=(
  cairomm-1.16-docs
  git
  glibmm-2.68-docs
  libsigc++-3.0-docs
  meson
  mm-common
  pangomm-2.48-docs
)
checkdepends=(xorg-server-xvfb)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#tag=$pkgver")
b2sums=('fc40eb820f611fdb094e369a73c09bba1b9b702c3eef6ebad678f4e3afd853d0e5e0ddb245a6dc5f0bdfa72070b15e7cd01dab62ccc5d280a5ce3e6a4db35318')

prepare() {
  cd gtkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson gtkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  case "${CARCH}" in
    riscv64) return 0 ;;
  esac

  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

package_gtkmm-4.0() {
  depends+=(
    libcairomm-1.16.so
    libglibmm-2.68.so
    libpangomm-2.48.so
    libsigc-3.0.so
  )
  provides=(libgtkmm-4.0.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_gtkmm-4.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
