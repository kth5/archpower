# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm-4.0
pkgname=(
  gtkmm-4.0
  gtkmm-4.0-docs
)
pkgver=4.16.0
pkgrel=1
pkgdesc="C++ bindings for GTK 4"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  cairo
  cairomm-1.16
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glibmm-2.68
  graphene
  gtk4
  pangomm-2.48
  vulkan-icd-loader
)
makedepends=(
  cairomm-1.16-docs
  git
  glibmm-2.68-docs
  meson
  mm-common
  pangomm-2.48-docs
)
checkdepends=(xorg-server-xvfb)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#tag=$pkgver")
b2sums=('fb8b80dda527df62161e9fc88be3e03437ff56df03af4333569509ab6604e54292ccbac33a83dc9ac9f6a18fa177465afa45b5e19aaf779bebf5970dd4048258')

prepare() {
  cd gtkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson gtkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

package_gtkmm-4.0() {
  depends+=(
    libcairomm-1.16.so
    libglibmm-2.68.so
    libpangomm-2.48.so
    libsigc-3.0.so
  )
  provides=(libgtkmm-4.0.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_gtkmm-4.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
