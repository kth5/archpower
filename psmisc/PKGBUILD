# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=psmisc
pkgver=23.7
pkgrel=1
pkgdesc='Miscellaneous procfs tools'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gitlab.com/psmisc/psmisc'
license=('GPL')
depends=('ncurses')
makedepends=('git' 'po4a')
validpgpkeys=('5D2FB320B825D93904D205193938F96BDF50FEA5') # Craig Small <csmall@debian.org>
source=("git+https://gitlab.com/psmisc/psmisc.git#tag=v${pkgver}?signed")
sha256sums=('80ae6e626ba83232b7af1c02f3f528834ebdd96325367b701f69125d3e196f2f')

prepare() {
  cd ${pkgname}

  ./autogen.sh
}

build() {
  cd ${pkgname}

  ./configure \
    --prefix=/usr
  make
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install
}
