# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=blosc2
_name=c-blosc2
pkgver=2.15.1
pkgrel=2
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.blosc.org"
license=(BSD-3-Clause)
depends=(glibc lz4 zlib zstd)
makedepends=(cmake)
source=(https://github.com/Blosc/$_name/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('56e7395297ce8e336b2e881bf4dae169268ef19328bae8c037aa297696f723196f65eb3f66c03728d1c6492b768e17ad586f8aae29c9033b571f736d391e9ab9')
b2sums=('0009afc8e9357a9d9787a60ee549bb21d68e952ca076f529add0f67500f2055a43463b3d612b600197579fd640b937024070508cb92b911d77e8ff4d8c7f16e5')

build() {
  cmake -B build -S $_name-$pkgver \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON \
    -D BUILD_STATIC=OFF \
    -W no-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 $_name-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
