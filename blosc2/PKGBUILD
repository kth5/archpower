# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

# LTO passes may not produce any output for quite some time
# especially on slower systems
apb_output_timeout=7200

pkgname=blosc2
_pkgname=c-blosc2
pkgver=2.22.0
pkgrel=1
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.blosc.org"
license=(BSD-3-Clause)
depends=(
  glibc
  lz4
  zlib
  zstd
)
makedepends=(cmake)
provides=(libblosc2.so)
source=("https://github.com/Blosc/c-blosc2/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('10471a3bdefc0b8e6a9f02655d3a5e922faab9a73127685f5996643c2e85f8da7733c273083a600d7451f9381ed289922e06dcf9c9e31422a5508386254f610d')
b2sums=('4518913dd0daaf9cdb71c703f7524daec49021d1b82835b8e072f57114c1c6eff81710c6e01dcb8d02a514aee8bfc355bd3ce6fbf01a829ede8d8a38d59acbee')

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON \
    -D BUILD_STATIC=OFF \
    -W no-dev
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac

  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" $_pkgname-$pkgver/LICENSE.txt
}
