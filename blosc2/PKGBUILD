# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=blosc2
_name=c-blosc2
pkgver=2.17.1
pkgrel=1
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://www.blosc.org"
license=(BSD-3-Clause)
depends=(glibc lz4 zlib zstd)
makedepends=(cmake)
source=("https://github.com/Blosc/$_name/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f5f55cd7aecc397e57183e3000ccfef12544b69493d8f0d055ac18878d7b39b06a1c3ecacfef663b120b13170a822dce8e29c2903d18307b2fe30644826d88f7')
b2sums=('00adf947ed7f42bae807bb5b85f23e0be08df87ed0d05032e83b61726f520b542233672e9d5efc18dd842e509a67dfc49d6d5adccdb36cd5966a300fe754cc74')

build() {
  cmake -B build -S $_name-$pkgver \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON \
    -D BUILD_STATIC=OFF \
    -W no-dev
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 $_name-$pkgver/LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
}
