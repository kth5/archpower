# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

# LTO passes may not produce any output for quite some time
# especially on slower systems
apb_output_timeout=7200

pkgname=blosc2
_pkgname=c-blosc2
pkgver=2.23.0
pkgrel=1
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.blosc.org"
license=(BSD-3-Clause)
depends=(
  glibc
  lz4
  zlib
  zstd
)
makedepends=(cmake)
provides=(libblosc2.so)
source=("https://github.com/Blosc/c-blosc2/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('199f6aa6bcc061c38523e73c26bdc913ac65e446f3390e65bd687973a8da6cb25b35b2614a1a4b51c287250b021454f2875378da0ab8ec16525fd7510b1c4686')
b2sums=('a40ad07f3be7cb839b459c98e9cc04d29626b98e4a601e51277223fcb5cf5855c7bbd98052c0b867b157ee14e6e7a0a5dc1e0e849cbb129d995353a8ce598149')

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON \
    -D BUILD_STATIC=OFF \
    -W no-dev
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac

  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" $_pkgname-$pkgver/LICENSE.txt
}
