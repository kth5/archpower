# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: David Manouchehri <manouchehri@riseup.net>
# Contributor: Sven Lechner <SirWindfield@users.noreply.github.com>

pkgname=pam-u2f
_name="${pkgname/-/_}"
pkgver=1.3.2
pkgrel=1
pkgdesc="Universal 2nd Factor (U2F) PAM authentication module from Yubico"
url="https://developers.yubico.com/pam-u2f"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-2-Clause')
replaces=("${_name}")
depends=('libfido2' 'openssl' 'pam')
makedepends=('asciidoc' 'git')
source=("git+https://github.com/Yubico/pam-u2f.git#tag=${_name}-${pkgver}?signed")
b2sums=('0def50826827165f948552162ed401ca1fc9e12cee7a9d758f95d55e71ddb9b126c5632ad7325ae447a393ec24e02f852d9dc166e1f35de528b1e78a3ffd4a39')
validpgpkeys=('B70D62AA6A31AD6B9E4F9F4BDC8888925D25CA7A' # Alessio Di Mauro <alessio@yubico.com>
              '78D997D53E9C0A2A205392ED14A19784723C9988') # Ludvig Michaelsson <ludvig.michaelsson@yubico.com>

prepare() {
	cd "${pkgname}"
	autoreconf --install
}

build() {
	cd "${pkgname}"
	./configure \
	    --prefix='/usr' \
	    --with-pam-dir='/usr/lib/security'
	make
}

check() {
	cd "${pkgname}"
	make check
}

package() {
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
	install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" 
}
