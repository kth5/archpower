# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Mike Roll <scaryspiderpig@gmail.com>

pkgname=nvidia-texture-tools
pkgver=2.1.1
pkgrel=5
pkgdesc="Texture processing tools with support for Direct3D 10 and 11 formats"
arch=(x86_64 powerpc64le)
url="https://github.com/castano/nvidia-texture-tools"
license=('MIT')
depends=('libpng' 'libjpeg' 'libtiff' 'openexr')
makedepends=('cmake')
source=("git+https://github.com/castano/nvidia-texture-tools.git#tag=${pkgver}"
	"https://patch-diff.githubusercontent.com/raw/castano/nvidia-texture-tools/pull/274.patch"
	cmake-linking.patch)
sha512sums=('SKIP'
            '4429fb03a67d82f9b96275539381e6a0bf83cf822b00569354d76328a193b3bdea8d2a3695040e8366942916540b1575ecb0309808ea2e9eae6127a29b82efae'
            '73ee56eebef0e21b3a3c39c5fa2987516b940eeb3699ba2deef2e2b6263af4d9fdc423da5de443f89bd528c6353f1164f8828a57510ba38914491a074cde676b')
	       
prepare() {
  cd "${srcdir}/${pkgname}"
  git cherry-pick -n 95454e902420547bc74079f2742c69be44678650
  patch -Np1 -i ${srcdir}/cmake-linking.patch
  patch -Np1 -i ${srcdir}/274.patch
}

build() {
  cd "${srcdir}/${pkgname}"
  cmake . -DNVTT_SHARED=1 -DCMAKE_BUILD_TYPE=Release -DBINDIR=bin -DLIBDIR=lib
  make
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir} install
  mv -v ${pkgdir}/usr/local/* ${pkgdir}/usr/
  rm -rf ${pkgdir}/usr/local
}
