# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=liburing
pkgver=2.14
pkgrel=1.1
pkgdesc="Linux-native io_uring I/O access library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/axboe/liburing"
license=(
  'GPL-2.0-only WITH Linux-syscall-note OR MIT'
  LGPL-2.0-or-later
  MIT
)
makedepends=(git)
provides=(
  liburing.so
  liburing-ffi.so
)
source=("git+https://github.com/axboe/liburing.git?signed#tag=$pkgname-$pkgver")
sha512sums=('5257cdc64e57c549814f136ebcd0850d531159cfc6832241f857dcc74bf717e1f8c2b6252b9a59d3228685cb0b2cc52632d54bf1e3b04331099a80bc9110fe8b')
b2sums=('cd1f572ba8ad27c4fbf5333f4585e593bb69d7ae6bde18b2dc1756882d2d6b09a2ac2056992db3c9bbb7538c4a417b50e5b7e9628f326bbd04eb649fbd284614')
validpgpkeys=(C0FC392DCADE26D975FA5E4AF7D358FB2971E0A6)  # Jens Axboe <axboe@kernel.dk>

build() {
  local configure_options=(
    --prefix=/usr
    --mandir=/usr/share/man
    --use-libc
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
