# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=liburing
pkgver=2.10
pkgrel=2
pkgdesc="Linux-native io_uring I/O access library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://git.kernel.dk/cgit/liburing"
license=(
  'GPL-2.0-only WITH Linux-syscall-note OR MIT'
  LGPL-2.0-or-later
  MIT
)
makedepends=(git)
provides=(
  liburing.so
  liburing-ffi.so
)
source=("git+https://git.kernel.dk/liburing.git?signed#tag=$pkgname-$pkgver")
sha512sums=('3a391208aea703715d6a797244b597170a809af12d9f6dac5d5396e0baa985e061dd66c71bb8687ef3abfbd20479eca36618c17889691d687088afc0e818d0ee')
validpgpkeys=(C0FC392DCADE26D975FA5E4AF7D358FB2971E0A6)  # Jens Axboe <axboe@kernel.dk>

prepare() {
  cd liburing
  git cherry-pick -n 6836160ae6ae9fdc20d703de55162bfc97169542 # Build fix for samba
}

build() {
  local configure_options=(
    --prefix=/usr
    --mandir=/usr/share/man
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
