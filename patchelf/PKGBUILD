# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: xduugu

pkgname=patchelf
pkgver=0.18.0
pkgrel=3.1
pkgdesc='Small utility to modify the dynamic linker and RPATH of ELF executables'
url='https://nixos.org/patchelf.html'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL-3.0-or-later')
makedepends=('git')
depends=('gcc-libs')
source=(git+https://github.com/NixOS/patchelf.git#tag=${pkgver})
sha512sums=('aaef42f053ad423cc7319d8e91a9cdab4370c53e1b222b87656815332cb578a71794dca18a4265e51dba500b1c641c65002e78d08ebdd8aade05c125d6697236')
b2sums=('53ab064726205671c511698359a617aca78f86cf5af9529649b8dda5f505a44bf8f8a4ed06272fe3eb923f9e34ff4de5dfc6f43b33fa8d22a9ff24076aa5b6c7')

prepare() {
  cd ${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}
  make -C tests -j1 check
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
