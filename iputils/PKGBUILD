# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
# Commit date + git rev-parse --short origin/master
_rev=84cdf26
pkgver=20240117
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('BSD-3-Clause' 'GPL-2.0-or-later')
url="https://github.com/iputils/iputils"
depends=('libcap' 'libidn2')
makedepends=('perl-sgmls' 'git' 'docbook-xsl' 'meson' 'systemd' 'iproute')
conflicts=('netkit-base' 'arping')
replaces=('netkit-base')
source=("git+https://github.com/iputils/iputils.git#tag=${pkgver}?signed")
validpgpkeys=('2016FEA4858B1C36B32E833AC0DEC2EE72F33A5F') # Petr Vorel
sha256sums=('SKIP')

build() {
  mkdir -p build
  cd build

  arch-meson ../$pkgname 
  ninja
}

package() {
  cd build

  DESTDIR="$pkgdir" ninja install
  install -vDm 644 "../$pkgname/Documentation/LICENSE.BSD3" -t "$pkgdir/usr/share/licenses/$pkgname"
}
