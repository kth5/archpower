# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=laz-perf
pkgver=3.4.0
pkgrel=1
epoch=2
pkgdesc="Alternative LAZ implementation for C++ and JavaScript"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/hobu/laz-perf"
license=('LGPL')
makedepends=('cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hobu/laz-perf/archive/${pkgver}.tar.gz")
sha512sums=('ec3e133d671a388f9cc448599035a57d0334015f18e6787ed05e463b4d3eddb5a4a09336a410f23c24d590d0d3242f3621ab49d4ce1400f226112e26f0759311')

build() {
  cd laz-perf-$pkgver

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

check() {
  cd laz-perf-$pkgver

  ninja -C build test
}

package() {
  cd laz-perf-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
