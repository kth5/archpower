# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.19
pkgrel=3
pkgdesc='Portable Open Source UPnP Development Kit'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://pupnp.sourceforge.io/"
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(cmake)
provides=(
  libixml.so
  libupnp.so
)
source=(https://github.com/$_name/$_name/archive/release-$pkgver/$_name-release-$pkgver.tar.gz
        https://github.com/pupnp/pupnp/commit/9d261799.patch
        https://github.com/pupnp/pupnp/commit/61b7944c.patch)
sha512sums=('76ae3bf8b69fd649ede0b92e03b36a6cbc7a9df9fa7828386c97761df12f2ae6346dec9ab45f78e17eb793d4d225707ecb3a7628d7678287979115cd4ba65c49'
            '412472ca0dfb1c5aa54e85147002e31b41e3edc7db630ba55fc0de12078bd977363737f4d2e75fa00a3d5b59cb2f4f2277c6803717f298e7c07766cfcb668b53'
            'adb58b80aa03c816afb48a8bb5d0a55258041e1694fdae1f88375d306ed6f586f751fe33ec3fb5dc88045e5eb4e005aaaa9532298f9b1f3d50407f81c9bb611a')
b2sums=('119281a2e220b3b9398566b649d76fc047741a251097059920e84ef9376db03ee953ebf10934423a56613ad85aa9f77b777e33d92095b1468dfffb12e37e86bd'
        '638583accde1a22704550e5c933ab65898d2732c4c73a4189c35326f6b3c3daa7f41d798d59629009014d2f0ce470f78c0710d9a9c5e690f5478ca69c91776ee'
        'dc115ee6b6b56b84646845a28924c6b6d814dff42e816d70ad0a46c0147983a2f7e8d679f0fddb3dd42eb854a03e95c7bbf0af8ff362cc17dc0089e2dee920a7')

prepare() {
  # upstream derives project/pkgconfig version from autotools macros and this is broken:
  # https://github.com/pupnp/pupnp/issues/442
  # https://gitlab.archlinux.org/archlinux/packaging/packages/libupnp/-/issues/1
  patch -d $_name-release-$pkgver -p1 -i ../9d261799.patch
  patch -d $_name-release-$pkgver -p1 -i ../61b7944c.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D UPNP_BUILD_STATIC=OFF
    -D blocking_tcp_connections=OFF
    -D reuseaddr=ON
    -S $_name-release-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-release-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $_name-release-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
