# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.24
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://pupnp.sourceforge.io/"
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(cmake)
provides=(
  libixml.so
  libupnp.so
)
source=(https://github.com/$_name/$_name/archive/release-$pkgver/$_name-release-$pkgver.tar.gz)
sha512sums=('0ec4d2af846e1f85be6e5142540ed5f61c65cfc1dc6a30668c94cbced615d53abb126699c6ee557646dd30f47a9f29ff2554d6a4345f7cbf8f0a998dc9df3048')
b2sums=('a8b6a9a39a2b4f4e85af09eb8950a39fa51e9fb3199652ade580ec07a73e4d6c7ff7f9d4de4059c1032a581926187a181b1083cb7c6fc0c907ca43e695c1ee33')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D UPNP_BUILD_STATIC=OFF
    -D blocking_tcp_connections=OFF
    -D reuseaddr=ON
    -S $_name-release-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-release-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $_name-release-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
# Fix broken cmake files
  sed -e 's|/COMPONENT||g' -e 's|/UPNP_Development||g' -i "$pkgdir"/usr/lib/cmake/*/*.cmake
}

# vim: ts=2 sw=2 et:
