# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libsoup3
pkgname=(
  libsoup3
  libsoup3-docs
)
pkgver=3.6.0
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later)
depends=(
  brotli
  glib-networking
  glib2
  glibc
  krb5
  libnghttp2
  libpsl
  libsysprof-capture
  sqlite
  zlib
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  python-quart
  samba
  vala
)
checkdepends=(
  apache
  php-apache
)
source=("git+https://gitlab.gnome.org/GNOME/libsoup.git#tag=$pkgver")
b2sums=('1e2caa4a7caae83ff9ed75c4056532d9883d71ad645ac8941e1312c9209a516774bb7df89503d494bc8320aa598cfba56c254c2682ccf0df50d584593b51ea5b')

prepare() {
  cd libsoup
}

build() {
  local meson_options=(
    -D autobahn=disabled
  )

  arch-meson libsoup build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # Python's output buffering messes with the tests reading stdout lines from
  # http2-server.py through a pipe
  PYTHONUNBUFFERED=1 meson test -C build --print-errorlogs
}

package_libsoup3() {
  depends+=(
    libbrotlidec.so
    libgssapi_krb5.so
    libg{lib,object,io}-2.0.so
    libpsl.so
  )
  optdepends=('samba: Windows Domain SSO')
  provides+=(libsoup-3.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libsoup3-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
