# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup3
pkgver=3.0.1
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64 powerpc64le powerpc)
license=(LGPL)
depends=(glib2 glib-networking sqlite krb5 libpsl brotli)
makedepends=(gobject-introspection python vala git gtk-doc meson samba
             python-quart)
checkdepends=(apache php-apache)
optdepends=('samba: Windows Domain SSO')
_commit=9be41160825b8238b482208b46bfe16b2f239ce3  # tags/3.0.1^0
source=("git+https://gitlab.gnome.org/GNOME/libsoup.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libsoup
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libsoup
}

build() {
  arch-meson libsoup build \
    -D gtk_doc=true \
    -D autobahn=disabled \
    -D sysprof=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libgssapi_krb5.so libpsl.so
            libbrotlidec.so)
  provides+=(libsoup-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
