# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: mgushee

pkgname=djvulibre
pkgver=3.5.29
pkgrel=1
pkgdesc='Library and utilities to create, manipulate and view DjVu (déjà vu) documents'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://djvu.sourceforge.net/'
license=(GPL-2.0-or-later)
depends=(
  bash
  gcc-libs
  glibc
  hicolor-icon-theme
  libjpeg-turbo
  libtiff
)
makedepends=(git)
source=(
  "$pkgname::git+https://git.code.sf.net/p/djvu/djvulibre-git#tag=release.$pkgver"
)
b2sums=('971076a3ba16b3250e23590263346eb27b4b39b431cb8649a88e3ad7f42ba2b327dbae7949f18cba755acdb088b699a8c49dc97db67d0628ccc30db801eb8bdb')

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
