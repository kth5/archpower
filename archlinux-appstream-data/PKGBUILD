# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20260107
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('a05fdb209624f2a04c11090ae300879540ff9f5a675e9bcc589fbae75be1b4f1'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '69478bb1727e5aa7095f829f2e6d2e207fb9157dc58a55754079f2351ce20f5b'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'd3c6e1b839ba3620836383c8b21940c83c1d7574563d7eb44e1a9007f2f87e4d'
            'ad8c082fe856b15da94e28c414454bf150d9a551155252d5683fb8e0510431f9'
            '8069140445075695de3143002f7672b14bac06f4b075ca363aa2f75153f268da'
            '55422a9da55154c7e6e194724bc32ab6f449184eec5e7d626134fdf5296a2d7b'
            'edeabaaa89ec92c07ded81138da7e4041696c4c9da2af3529b3c8ad37650e5d7'
            '4906cbd72d313c7c40995cf5aece4eaa7cb7836d89ec06d073282f644f55cceb'
            '2f81b73f167911bb1e8c8d328db92315accb8c91b80308b143d3545f8a0e99c1'
            '5de51a579ddb133b341f79daace0ed00b9210e2708da7a3292e44a802ac109c4')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
