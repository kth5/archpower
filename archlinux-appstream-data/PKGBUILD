# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20251025
pkgrel=2
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('4d9c285054fbedf1f88b0d4c99a8bc8cc3aab59a4325e38ba05ee2e3055390f2'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'c1c4b4e6e5240d667c45e4c4bd08d03b8f3aeb6429f3925c4351f23fc8ad30f6'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '6a26b62d3ef8511d0b201abfbb09683661a3949f3a249f3d74ab6504929dbf2d'
            '1f6a2d08dbd2de958de62b73256f8673473fd9fed1163d9156f17b42cfccc438'
            '30a04602d136c4ab40b278a6b3248a79fcbfbe2285585fbb088811f40e3dfb86'
            '388284488304023d3577afab94e2e5c1b72ef7ed56204244a7a22909c5e96d74'
            '56a5f0cbc8d1971cfab6b52684f4d9a115ee6cf7c28151d5f65354968f40ce3b'
            '07597d4b632b00e0e0d87804d0ede325030f993f7dcb677e27dd76599e7ddc3d'
            'a4781960704d3619d6d3cd5f87112a736a1f13deab1950eb7ae77c9a0f2372d0'
            'c5a164be37f03dde0a87f50bc83fb74cccb762e87ccebe95bc6ebd6373322f5c')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
