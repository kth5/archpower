# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20241214
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('2914efdbe930c42cd29b922f18aaa6ba00421aab2f3cad9987ced24be7c60f5e'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '426c10d5e74fe458fc351ed46b5444321ff9aa1d634f8e6f980bc86600f22c8e'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '8ffbf80df54eb8d3106b6399a6113921d664d2ef11cf86efe659b66676900f83'
            '4e8a3cac0c5ac5a8f76996cb0111c38ac1f2585b8d2eb59fa47ae9955e4de889'
            'b487d63e9a08081e6caf29bff38b1b1214c98c2834ba7aa606dbb2574d8d0ce5'
            'b545f4f3107dea63914d27f697c768f56d4ba29399a7939b2cef308b6ab3f056'
            'e5e0c628b08e7c39247a1869d2e95d75d18367865cb4fc6a66419656278c8569'
            '7ed52b9972e80a37bfdd933e477965287d3b87ae90f06684603a6af520cd14a3'
            '3c3127767f34e7683bfe78467f5030bd03261473e6ac4fe6b82ae9a9950c3411'
            'df63e37b723d6c66d73c5f37495a9f60f91a00ca21fc8d7552bc483b0f566c1f')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
