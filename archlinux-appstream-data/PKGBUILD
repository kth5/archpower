# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20250317
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('95409aba042c53cbb13edd891e2c7e8eae96913e5e5fe2ee5bbbcfb9ac26a88a'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '2c92d892df1b01f2c7281c1f338699b224cdd4e34900a1fec77ae606998d7bae'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'da995c9b4d11e3839ff291c74ed529d73ebd8dfc30471e92cb8c770dfecfa6a9'
            '25146e8a2474526c1b700c6f71868859d6c46398ffd01e3a9b10b3133362d7c4'
            '444a42d19cd480ad5dc559fd7cb47a011a16df1d4038a02c12d6044d2eb1ba3e'
            'e6b2fce42d0ddeee5342158b528e65bae80dfd982cd8aac8c6d3cb0ced37fc70'
            'c8f17fb192d50ecad70a76bf7a87af966e9e82c86a2b43d15688d1b7d280d762'
            'fabc2b835711e34a4b59d0a9a35a2be5fe6a91cec3b3538cf5c7c542ff8a556f'
            'f58e3984b796b85d96080e6a9d5850cca8ab23862778c445dc3dd9a4f3fdadb3'
            'fc6d7f7a7f9b94610984c426e0f1880e5b720bb120bcfe0e0611639126b0284c')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
