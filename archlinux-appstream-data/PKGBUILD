# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20240112
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('0dbdf52fa939c99d66efe83bf944b30ed3abb8d7581f47facdf1cb6fcc3b5456'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'a83d77dd1f840c131cc4bcf9c67ac74608a1abe2544a3a19b7bbe06761547153'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '4bd2ace8d5d6c5790702d426acd4718122f5554b8343b173e51cce238eaefff4'
            '6299584b996ad797ea366bf27449485fdccf47e16948ec9b2064754b8327cd48'
            '9d5c69bc49ace6223aaa1550f715b02019d146854c5a5596bc970304c6626978'
            '6e594ba3a2e068e241b71d1cf913e4c3e6ef845adca1ae7ef57d1a88b099ec16'
            'c465eceb2a9ba877ffdf67b7966135bc06845176822104db22c6ef19792b8790'
            'b351bcbfecdf7a8b75dcad07ab947ee28df9fe5b1b9f1989634fad28a221fd83'
            'a32ffe4eefbd5f4719e64c047321708f5f6148a83021eb4cc12d117ed9d2d30d'
            'ad1230a7b987893a4e0676679caf704dd25982507928350e48fe68c4403c0eec')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
