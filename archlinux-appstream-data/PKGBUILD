# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20240306
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('616fc4e905338fcb72896ce67ffa18af6c177e2abe57b7599f0dafc85979be10'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '17d338eb3bb4c7116aa830cdf97c8a008ec98cbc7756a11e4da245e0dee44107'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '4cfe3f06eb8e554fc3be85910349ca00b8be65ab14e7dc441b412f6f643e302b'
            '141fc70d6c39fdf9be2b359db8091eb5dd56f8de5c9ce994a7ea8f7f2f15b628'
            '2bc035d9eebb913e4783e18e1bb07fe8160ff9060630717d6062b0bc012c156e'
            '93888301175a0ee9d76c7b2571f86095bf351045a30ac0f114b65d915bc08c7f'
            'b3a01fbe6aacc7c259b6db3e92a7170e90246fdef1d9302199010431518c2d21'
            '7386cbad79483ff4f856340239aef40b851c54a1d4b81f02830fea967c631703'
            'aa78698647f1a30eaa87c1db08a234caf0f2e3db8337308b37d6cdbc3ee8b342'
            '5487d42fda422ff033670e165fdc4ed32bc74ee1e0a234739e5f2aba923414ab')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
