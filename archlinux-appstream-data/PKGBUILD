# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20240914
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('eb282288d5a06126e98a8574d73f363c3f63642d2d08637538c9a5648d023f6e'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '5f90d1e384ebdb4bee0c0c845762b079d05f426a12df7d6fd409714904cd11ba'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'b52fbc8330ff3522210e2ae0af4ff59bb0583267485d24f96579ffe80955398f'
            'dc24020d291c6ba26bc37887529f38ae50903d9982bcc40a1efa8337485b7b2f'
            '5aacd273a1f0bb8e69fdfd66450a0c75fc83b9e6d7d23359c8996962b4e6308a'
            '9ec06a760ba87e62e457ebebc3b18cdc2762c19e3a5f83b9100807f1b8f486ce'
            'e87e5dd1550badc3d38c74bb459ae3a97934208ab775d0e93191fb4bc4cb4118'
            '30b686d65b81272489635f92c0d922ec5219d6e3a339ea5a5c4247a585f00908'
            'd016fde2cd066053854b71b5da6868914070eba8f5e6d67e58f6fae28112028a'
            '8bcf02f71f4fc09bce62a4053583a3151486ba576817edebc205933c78c06839')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
