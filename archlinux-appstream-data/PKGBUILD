# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20240415
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('9ef8457ae60331b14eece5c979fa1e7cc336a9931efb86ef01c5bc24b682b808'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '02baa6c9fb654838b77379e6167ceaf9cfaf60075d026f10c20819a4b1fa42ae'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'd9af30a32e32265661930bdbd67280c53ee3a67a5c87cc16acbed736a16269ed'
            'f7d47062fc6bf32b9daf0cbcfddd666ba3e4fb8663c797d9aae57544fc7a8754'
            '794c7de7c0f5611e30ba109f8805f6a9da5e93611e3ea5fa9cbb5a8e431f8800'
            'ac5775b40ebb3db5043c8c751d689e8be344c8ca02c6fc05813867c9b1c703ac'
            '01d0dc6fc2ca4ce53296d9988f16f0ff72d4cf26786050f70afce23da9117d7c'
            '0703f2b37392f4788231dae9ee7eb58de381b1d630ece56cf6073bcfebf1987e'
            '6e8be806efc148683b3d9c9d5dc12dcd28a8d53b484377969594a55c16f8e53a'
            'f2798eb8fed2f1042b3ba4683b96f6b935e0d8f35a5dead62d4b3e5993bb890b')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
