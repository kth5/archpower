# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20230827
pkgrel=2
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('69fed34ea4046a7dc1c9b8e415a52954556116094184a7b8d10ec479c30a26c6'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '3a0aa0ba145932d6a450f79f91983112a2744a2ce2aa606da1ba846824fd0db9'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '139d55746e9f6a3a83ea2f62f7a669e586e5ca78b79dca5c7817d72ac9d3a022'
            'd82b07c8cd452d8212cd222419affad8589cc1a476ccec5685c7f63b66c5bade'
            '06a8363d297e4158a461a99d5609c223f4d3aa89fc7a12217e21b251873d3740'
            '7670de8afa15b0832fbf9e21d901092acb9a2f16da13b29a5bdc2262daa372f4'
            'e0bd501cf5d8586bf1767765f12712ac0c2ace531def60131b23cf7b0634c55c'
            '8255b777eb36e6142d9485db0da9f400986b55dd75d4e70d64d0432ddc167af1'
            'be40299af83eecd3f9b9b1f4d8166508be05986b90951fc62e5c66252aada3c7'
            '475c7388a71a9ede23b7ff0e5bd78aa61c56166246755955c0c1d82b3d528c72')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
