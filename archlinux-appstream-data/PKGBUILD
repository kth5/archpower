# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20240526
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('abdb8c11465c83de3c8cd3e8ee4f96ed70ffa026f4d825e2750e90e579bd5e2d'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'b77554487e1064f749312596106c9e0e2e151bd2369b3c3f36b13215261bf739'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '107b10645848b4783548247ccb2d1f9c427a7e091901a9a6b8471e35d97b6d24'
            'd4126a1531d96b45902544c566a91966b3733f5b1c93679d6578bbc053a7ea37'
            '900d867313cf723997cee71305f138142b53f2c0770e130c5d042b9bb8749384'
            '0beb69967ab35868e27bf1f90fd75267bd9ecc12e5042ec2b247e4be55b01331'
            'c0354deb86b641a3c71502e9b2e2466f5106b4b76da1f617669eb192122d39fd'
            '996b3be334dedc153bcb24b013c511f9c5613f2c5cf60c33e1b6065dd5eae07f'
            '712d6ffd2c93c80130cc8075f3c10609d6bd0b8d8ba67cbf6b6be384e102835d'
            '4eb089d3f280b2591e8a7b52fa52a56e0b912ecfcce6c32e27c1d6b097f8850e')

package() {
  mkdir -p "$pkgdir"/usr/share/swcatalog/{icons/archlinux-arch-{core,extra,multilib}/{48x48,64x64,128x128},xml}
  for _repo in core extra multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/swcatalog/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/swcatalog/xml/$_repo.xml.gz
  done
}
