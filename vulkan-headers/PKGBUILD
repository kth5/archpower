# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-headers
pkgver=1.4.313.0
pkgrel=1
epoch=1
pkgdesc="Vulkan header files and API registry"
url="https://www.vulkan.org/"
arch=(any)
license=("Apache-2.0 OR MIT")
depends=()
makedepends=(
  cmake
  git
  ninja
  python
)
optdepends=(
  "python: Registry tools"
)
provides=("vulkan-hpp=$pkgver")
groups=(vulkan-devel)
source=(
  "git+https://github.com/KhronosGroup/Vulkan-Headers#tag=vulkan-sdk-$pkgver"
  0001-Remove-Werror.patch
)
b2sums=('5e1cb2ba4a68fb4224ce5badb9b7d2d5da28ae2f7b440b502cc8433c82537458fc12c398539606981180e2937afdfca7e5179d082ece14bb597c08b3b7358f81'
        '5d7201f11850cdf8242057034e90c60ad7518c14fdcb091dc63ca6be6b10567c229f96b3dcdb3ecf57856f7b72bceb4048e4a6e5e1faf6bb98262324f408dbb8')

prepare() {
  cd Vulkan-Headers

  # GCC 15 fix
  git apply -3 ../0001-Remove-Werror.patch
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_SYSCONFDIR=/etc
    -D CMAKE_SKIP_INSTALL_RPATH=ON
  )

  cmake -S Vulkan-Headers -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  python -m compileall -d /usr "$pkgdir/usr"
  python -O -m compileall -d /usr "$pkgdir/usr"

  install -Dm644 Vulkan-Headers/LICENSES/MIT.txt \
    -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
