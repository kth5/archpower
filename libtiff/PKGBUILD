# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: HurricanePootis <hurricanepootis@protonmail.com>

pkgname=libtiff
pkgver=4.7.1
pkgrel=1
pkgdesc='Library for manipulation of TIFF images'
url='http://www.simplesystems.org/libtiff/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(libtiff)
depends=(
  gcc-libs
  glibc
  jbigkit
  libdeflate
  libjpeg-turbo
  libjpeg.so
  libwebp
  xz
  zlib
  zstd
)
makedepends=(
  cmake
  freeglut
  git
  glu
  mesa
)
optdepends=(
  'freeglut: for using tiffgt'
)
provides=(libtiff.so libtiffxx.so)
source=("git+https://gitlab.com/libtiff/libtiff.git?signed#tag=v${pkgver}")
sha256sums=('dd0fa3cf0f330d1cbfd504386e456a2988b30adac72e1f55e3fae734a2ed0d8c')
b2sums=('28fb783db36e184b578f19d1141c82df46fd60da323b592c73e6da97f3e2dddb5cb38bd2977eb4b93e361a65ee116cd249e980cebfd558d4d57182d1c8f8b3a9')
validpgpkeys=(
  'EBDFDB21B020EE8FD151A88DE301047DE1198975' # Bob Friesenhahn <bfriesen@simple.dallas.tx.us>
  'B1FA7D81EEB8E66399178B9733EBBFC47B3DD87D' # Even Rouault <even.rouault@spatialys.com>
)

build() {
  cmake -B build -S $pkgname \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=None \
  -DCMAKE_POLICY_VERSION_MINIMUM=3.5

  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac

  ctest --test-dir build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 libtiff/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ts=2 sw=2 et:
