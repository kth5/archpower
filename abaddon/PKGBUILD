# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: NexAdn <git@nexadn.de>
# Contributor: Techflash <officialtechflashyt@gmail.com>
# Maintainer: javsanpar <javsanpar@riseup.net>
pkgname=abaddon
_pkgname=abaddon
pkgver=0.2.2.1.g34d73e3a21c3
pkgrel=1
pkgdesc='An alternative Discord client made with C++/gtkmm'
url='https://github.com/uowuo/abaddon'
_commit="34d73e3a21c3952e0f0b94a31381d999cfe81a59"
source=("git+https://github.com/uowuo/abaddon#commit=${_commit}"
        ${pkgname}.desktop)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('GPL3')
makedepends=('git' 'cmake' 'nlohmann-json')
# 'rnnoise' not packaged in ArchPOWER, and is optional according
# to abaddon's readme.
depends=('gtkmm3' 'libhandy' 'spdlog' 'opus' 'libsodium')
sha256sums=('6ef13f447678395eac26a345ae225dc990c049b37a03b0f194e0dd74183e434d'
            '7840732362b8f2202cf79b7d7c2eb0e2cbd5a83dc45c5fca4609ec15b8bea6df')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=12 --tags | sed 's/v//;s/-/./g'
}

prepare () {
  cd "$_pkgname"
  git submodule update --init --filter=tree:0 subprojects/{ixwebsocket,keychain,miniaudio,qrcodegen}
}

build () {
  cmake -B build -S "$_pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DENABLE_RNNOISE=OFF
  make -C build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -D -m644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
