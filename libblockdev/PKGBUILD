# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=2.28
pkgrel=4
pkgdesc="A library for manipulating block devices"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/storaged-project/libblockdev"
license=('LGPL')
depends=('cryptsetup' 'device-mapper' 'e2fsprogs' 'gcc-libs' 'glib2' 'glibc' 'gptfdisk' 'kmod'
         'libbytesize' 'mdadm' 'nss' 'parted' 'systemd-libs' 'util-linux' 'volume_key')
makedepends=('autoconf-archive' 'gobject-introspection' 'gtk-doc' 'ndctl' 'python')
optdepends=('btrfs-progs: BTRFS plugin'
            'dosfstools: VFAT filesystem support'
            'lvm2: LVM plugin'
            'ndctl: NVDIMM plugin'
            'ntfs-3g: NTFS filesystem support'
            'xfsprogs: XFS filesystem support')
source=("https://github.com/storaged-project/libblockdev/releases/download/$pkgver-1/libblockdev-$pkgver.tar.gz")
sha512sums=('9e01c41db4f728c8e81e710c6bb4725bbe34b3a7de25ff7f57b7b88ca6b7b5debbe858947585d314770036b2c3c533f9f8ff3d038b7ca50eee3d18f9fbe0d392')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-bcache \
    --without-dmraid \
    --without-kbd \
    --without-lvm_dbus \
    --without-mpath \
    --without-vdo
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
