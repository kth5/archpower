# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=3.0.3
pkgrel=3
pkgdesc="A library for manipulating block devices"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/storaged-project/libblockdev"
license=(LGPL-2.1-or-later)
depends=(
  'cryptsetup'
  'device-mapper'
  'e2fsprogs'
  'gcc-libs'
  'glib2'
  'glibc'
  'kmod'
  'keyutils'
  'libbytesize'
  'libnvme'
  'mdadm'
  'nss'
  'parted'
  'systemd-libs'
  'util-linux-libs'
  'volume_key'
)
makedepends=(
  'autoconf-archive'
  'gobject-introspection'
  'gtk-doc'
  'ndctl'
  'python'
  'util-linux'
)
optdepends=(
  'btrfs-progs: BTRFS plugin'
  'dosfstools: VFAT filesystem support'
  'exfatprogs: for exFAT filesystem support'
  'f2fs-tools: for F2FS filesystem support'
  'gobject-introspection-runtime: for Python integration'
  'lvm2: LVM plugin'
  'ndctl: NVDIMM plugin'
  'nilfs-tools: for nilfs filesystem support'
  'ntfs-3g: NTFS filesystem support'
  'python: for Python integration'
  'udftools: for UDF filesystem support'
  'xfsprogs: XFS filesystem support'
)
source=("https://github.com/storaged-project/libblockdev/releases/download/$pkgver-1/libblockdev-$pkgver.tar.gz")
sha512sums=('f2cc6fbe1df6fd9886016177a4a94984d704f3b402c7090344c581afb89f7cb050c9516c52974157a99ea6c6239f6a1f6079cde0b43a2336af0daca7311ca788')
b2sums=('c26a806797effc7231a0eb92f1b8cd74e548da5b1800cddd75a21e7df327be8371edabc2e9a39a727291f365b6a4f6242f78b66396adc4526b41749219f0429f')

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --without-bcache
    --without-lvm_dbus
    --without-mpath
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent libtool from overlinking everything
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
