# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>

pkgname=supertuxkart
pkgver=1.4
pkgrel=5
pkgdesc='Kart racing game featuring Tux and his friends'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://supertuxkart.net'
license=('GPL-3.0-or-later')
depends=('openal' 'libvorbis' 'freetype2' 'harfbuzz' 'curl' 'bluez-libs'
         'sqlite' 'libjpeg-turbo' 'libpng' 'hicolor-icon-theme' 'sdl2' 'shaderc')
makedepends=('cmake' 'setconf' 'ninja')
source=("https://github.com/supertuxkart/stk-code/releases/download/${pkgver}/SuperTuxKart-${pkgver}-src.tar.xz"
        "fix-missing-rotation.patch::https://github.com/supertuxkart/stk-code/commit/0c2b81ac1f9ff29f5012a98f530880b87f416337.patch"
        "$pkgname-$pkgver-fix-crash.patch::https://github.com/supertuxkart/stk-code/commit/8544f19b59208ae93fc3db0cf41bd386c6aefbcb"
        "$pkgname-$pkgver-gcc-13-missing-includes.patch::https://github.com/supertuxkart/stk-code/commit/0163e3fa88b72634c3ddff5304c9086b649f53b1.patch")
sha512sums=('e88f3fb5ccdb38d0ef75e92d73b43c5cc7a1f6c76ac9502033df099a00d6dc3e86b2c5442b806237d2420c44ada7dd23a2c90ecf94785a44d2348c933770a5ab'
            '52bbdbf070212b31f8b179ea47eda40c9f37b20932921604d209de8be56791328d0dd363bba56f8c7c0aaaf46036b675991758b224c7e1812f2870a0e2f266be'
            '6023932c9670a8cb6b7e9730fdc5bc2947426979ce6302c3341ce5e6229b6874ec322d46064a7f6e40f8d1c889211cfa4c153ce31bf66d9fabf54a58cfa11832'
            'c3bca1d49299569cb3c1e2adba2b08d7147de3c7c3bb285e3c3e7f88779a13d0350cf40939c61c5aab7f1401622ce9e456b7ae2db59f92bf6466de8b4177f852')

prepare() {
  cd "$srcdir"/SuperTuxKart-${pkgver}-src
  patch -Np1 -i ../fix-missing-rotation.patch
  patch -Np1 -i ../$pkgname-$pkgver-fix-crash.patch
  patch -Np1 -i ../$pkgname-$pkgver-gcc-13-missing-includes.patch
}

build() {
  cd "$srcdir"/SuperTuxKart-${pkgver}-src

  _fn="data/${pkgname}.desktop"
  setconf "$_fn" Exec "$pkgname"
  setconf "$_fn" TryExec "$pkgname"
  setconf "$_fn" Icon "$pkgname"

  cmake . \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_RECORDER=0

  ninja -C build
}

package() {
  cd "$srcdir"/SuperTuxKart-${pkgver}-src

  DESTDIR="$pkgdir" ninja -C build install

  # Why is this even in here?
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}

# vim:set ts=2 sw=2 et:
