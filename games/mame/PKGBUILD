# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

pkgbase=mame
pkgname=(mame
         mame-tools)
pkgver=0.285
pkgrel=2
pkgdesc='Port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support'
url='https://mamedev.org/'
license=(BSD-2-Clause
         BSD-3-Clause
         BSL-1.0
         CC0-1.0
         GPL-2.0-only
         LGPL-2.1-only
         MIT
         Zlib)
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
makedepends=(asio
             flac
             git
             glm
             libpulse
             libutf8proc
             libxi
             libxinerama
             lua
             portaudio
             portmidi
             pugixml
             python
             qt6-base
             rapidjson
             sdl2_ttf)
makedepends_x86_64=(
             nasm
)
source=(git+https://github.com/mamedev/mame#tag=mame${pkgver/./}?signed
        mame.desktop
        mame.sh
        mame.svg
        lua-5.5.patch
        constexpr.patch)
sha256sums=('537a191626e176b23a4cf631e2f93ea9f67457a94834e526ad1890374f1d868d'
            '0e5e93f84673c2d17a1bf7a6b256cfa54ef89a086a828df19bc054c12776d821'
            'ee1c59bafc5e5441e99fa4c58108a3e18048e60672f34de865c8a5a976094dba'
            '17c442c933d764175e4ce1de50a80c0c2ddd5d733caf09c3cd5e6ba697ac43f4'
            '714e4ae47bfb9a3d3d477a41ed86b3b9848610a3b3511c53d87bf4b73b1f877a'
            '113f6a4bb229fc10195caf400b0376c2cacbd1c1be2065bd44cff17a6e935dad')
options=(!debug
         !lto) # debug build fails
validpgpkeys=(D0362DB01844E0433ADBBFBAC174B1018C40710E) # Vas Crabb <vas@vastheman.com>

prepare() {
  cd mame

# Use system libraries
  sed -e 's|\# USE_SYSTEM_LIB|USE_SYSTEM_LIB|g' -i makefile
# Use C++ LUA
  sed -e 's|ext_lib("lua")|ext_lib("lua++")|' -i scripts/src/main.lua -i scripts/src/3rdparty.lua
# Drop hardcoded libexec
  sed -e 's|libexec/||' -i scripts/src/osd/modules.lua
# Use bundled asio, 1.34 is too new
  export CXXFLAGS+=" -I$PWD/3rdparty/asio/include"

# Fix build with LUA 5.5
  patch -p1 -i ../lua-5.5.patch

# Fix constexpr errors during build on powerpc*
  patch -p1 -i ../constexpr.patch
}

build() {
  cd mame
  export CXXFLAGS+="  -Wp,-U_GLIBCXX_ASSERTIONS" # FS#73202

  export CXXFLAGS+=" -std=c++17"

  make \
    -j10 \
    VERBOSE=1 \
    NOWERROR=1 \
    OPTIMIZE=2 \
    TOOLS=1 \
    QT_HOME=/usr/lib/qt6 \
    ARCHOPTS=-flifetime-dse=1 \
    USE_WAYLAND=1
}

package_mame() {
  depends=(expat
           flac
           fontconfig
           gcc-libs
           glibc
           libglvnd
           libjpeg-turbo
           libpulse
           libutf8proc
           libx11
           libxi
           lua
           portaudio
           portmidi
           pugixml
           qt6-base
           sdl2
           sdl2_ttf
           sh
           sqlite
           zlib
           zstd)
  conflicts=(sdlmame)
  replaces=(sdlmame) 

  cd mame

  # Install the mame script
  install -Dm755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname

  # Install the binaries
  install -Dm755 mame -t "$pkgdir"/usr/lib/mame

  # Install the extra bits
  install -Dm644 src/osd/modules/opengl/shader/glsl*.*h -t "$pkgdir"/usr/lib/$pkgname/shader/
  cp -ar {artwork,bgfx,plugins,language,ctrlr,keymaps,hash} "$pkgdir"/usr/lib/$pkgname/

  # Install the license
  install -Dm644 docs/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # FS#28203
  sed -i 's|KEYCODE_2_PAD|KEYCODE_2PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_4_PAD|KEYCODE_4PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_6_PAD|KEYCODE_6PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_8_PAD|KEYCODE_8PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg

  # documentation
  install -dm0755 "$pkgdir"/usr/share/doc
  cp -a docs "$pkgdir"/usr/share/doc/$pkgname
  rm -r "$pkgdir"/usr/share/doc/$pkgname/man
  install -Dm644 docs/man/*.6* -t "$pkgdir"/usr/share/man/man6/

  # install desktop file and icon
  install -Dm644 "$srcdir"/mame.desktop -t "$pkgdir"/usr/share/applications
  install -Dm644 "$srcdir"/mame.svg -t "$pkgdir"/usr/share/icons/hicolor/scalable/apps
}

package_mame-tools() {
  pkgdesc='Port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support (tools)'
  depends=(flac
           gcc-libs
           glibc
           libutf8proc
           sdl2
           zlib
           zstd)

  cd mame
  for _i in castool \
            chdman \
            floptool \
            imgtool \
            jedutil \
            ldresample \
            ldverify \
            nltool \
            nlwav \
            pngcmp \
            regrep \
            romcmp \
            split \
            srcclean \
            testkeys \
            unidasm; do
    install -Dm755 $_i -t "$pkgdir"/usr/bin
  done
  mv "$pkgdir"/usr/bin/{,mame-}split # Fix conflicts

  install -Dm644 docs/man/*.1* -t "$pkgdir"/usr/share/man/man1/

  # Install the license
  install -Dm644 docs/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
