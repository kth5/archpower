# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=freeciv
pkgver=3.1.4
pkgrel=3
pkgdesc="A multiuser clone of the famous Microprose game of Civilization"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://www.freeciv.org/"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'cairo'
  'curl'
  'gdk-pixbuf2'
  'glib2'
  'glibc'
  'gtk3'
  'hicolor-icon-theme'
  'icu'
  'libtool'
  'lua'
  'pango'
  'readline'
  'sdl2-compat'
  'sdl2_mixer'
  'sqlite'
  'xz'
  'zlib'
  'zstd'
)
makedepends=('python')
backup=('etc/freeciv/database.lua')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('71a4220c3d6cd3771d5f039a50932d0f80f9ca829821d778fe2bc16609110d8ab3b13b9483b3bb0e6860557f68e53d308865139224ae814dc5366e7fcf154a23')

prepare() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-shared\
    --enable-sys-lua \
    --enable-fcmp=gtk3 \
    --enable-fcdb=sqlite3 \
    --enable-sdl-mixer=sdl2 \
    --enable-aimodules
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
