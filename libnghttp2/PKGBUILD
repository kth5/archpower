# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Contributor: Zhuoyun Wei <wzyboy@wzyboy.org>

pkgname=libnghttp2
_tag='6c83ba437df9834305ccf68083775d7742974ecb' # git rev-parse v${pkgver}
pkgver=1.59.0
pkgrel=1
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://nghttp2.org/'
license=('MIT')
depends=('glibc')
makedepends=('git')
conflicts=('nghttp2<1.20.0-2')
provides=('libnghttp2.so')
validpgpkeys=('F4F3B91474D1EB29889BD0EF7E8403D5D673C366') # Tatsuhiro Tsujikawa <tatsuhiro.t@gmail.com>
source=("git+https://github.com/nghttp2/nghttp2.git#tag=${_tag}?signed")
sha512sums=('SKIP')

prepare() {
  cd nghttp2

  autoreconf -i
}

build() {
  cd nghttp2

  ./configure \
    --prefix=/usr \
    --disable-examples \
    --disable-python-bindings \
    --enable-lib-only
  make
}

check() {
  cd nghttp2
  make check
}

package() {
  cd nghttp2/lib

  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/libnghttp2/COPYING"
}
