# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Aurélien Wailly <aurelien.wailly@gmail.com>

pkgname=protobuf-c
pkgver=1.4.1
pkgrel=5
pkgdesc='Protocol Buffers implementation in C'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/protobuf-c/protobuf-c'
license=('BSD')
depends=('protobuf')
provides=('libprotobuf-c.so')
options=('!lto') # https://github.com/protobuf-c/protobuf-c/issues/488
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
         protobuf-c-protobuf-23.patch::https://patch-diff.githubusercontent.com/raw/protobuf-c/protobuf-c/pull/556.patch)
sha256sums=('4cc4facd508172f3e0a4d3a8736225d472418aee35b4ad053384b137b220339f'
            '14de3b6c0a75596fbfd3d5f73bc0c45cd0e41eb546cf2632fd2fd9b3cf864f48')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../protobuf-c-protobuf-23.patch # Fix build with protobuf 23
  autoreconf -vif
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
