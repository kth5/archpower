# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Aurélien Wailly <aurelien.wailly@gmail.com>

pkgname=protobuf-c
pkgver=1.5.1
pkgrel=1
pkgdesc='Protocol Buffers implementation in C'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/protobuf-c/protobuf-c'
license=('BSD')
depends=('protobuf')
provides=('libprotobuf-c.so')
options=('!lto') # https://github.com/protobuf-c/protobuf-c/issues/488
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
        "protobuf-30.patch")
sha256sums=('20d1dc257da96f8ddff8be4dd9779215bbd0a6069ed53bbe9de38fa7629be06b'
            '23c54e2c3385b62420961a1e572706f446a1d25e84a3b3bd32109617d06cd321')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 < ../protobuf-30.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
