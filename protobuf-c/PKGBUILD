# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Aurélien Wailly <aurelien.wailly@gmail.com>

pkgname=protobuf-c
pkgver=1.5.0
pkgrel=3
pkgdesc='Protocol Buffers implementation in C'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/protobuf-c/protobuf-c'
license=('BSD')
depends=('protobuf')
provides=('libprotobuf-c.so')
options=('!lto') # https://github.com/protobuf-c/protobuf-c/issues/488
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
         protobuf-26.patch)
sha256sums=('7b404c63361ed35b3667aec75cc37b54298d56dd2bcf369de3373212cc06fd98'
            'cd5dbf90624a6a11ec1313b1a9d7f3d5b8acb27b2cfe5a47b6d380ae47730ed6')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../protobuf-26.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
