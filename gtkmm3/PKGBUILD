# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
pkgname=(
  gtkmm3
  gtkmm3-docs
)
pkgver=3.24.10
pkgrel=1.1
pkgdesc="C++ bindings for GTK+ 3"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  atkmm
  cairomm
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  glibmm
  gtk3
  libsigc++
  pangomm
)
makedepends=(
  atkmm-docs
  cairomm-docs
  git
  glibmm-docs
  libsigc++-docs
  meson
  mm-common
  pangomm-docs
)
checkdepends=(xorg-server-xvfb)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#tag=$pkgver")
b2sums=('b19ef42b6e29035a2ce5475333de9aefb2f91e7daa42cd0632ca1cbf6bbd4b3ebae900094eec1e35180185c65b9077396de5cb5d8d5c40d990578e76b8901e4b')

prepare() {
  cd gtkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson gtkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

package_gtkmm3() {
  depends+=(
    libatkmm-1.6.so
    libcairomm-1.0.so
    libglibmm-2.4.so
    libpangomm-1.4.so
    libsigc-2.0.so
  )
  provides=(
    libgdkmm-3.0.so
    libgtkmm-3.0.so
  )

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_gtkmm3-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
