# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
pkgname=(
  gtkmm3
  gtkmm3-docs
)
pkgver=3.24.9
pkgrel=1
pkgdesc="C++ bindings for GTK+ 3"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  atkmm
  gtk3
  pangomm
)
makedepends=(
  atkmm-docs
  cairomm-docs
  git
  glibmm-docs
  meson
  mm-common
  pangomm-docs
)
checkdepends=(xorg-server-xvfb)
_commit=2ae5c7ec2daf58f8320c411465c61c228ec8bd27  # tags/3.24.9^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
b2sums=('593d3251802bc586ff6965a3d4ae4bd082280a1f6e30b6e26578ea9ef214ea7f02a90947cd3af058414044dadda5a0c0e9a40f0319babcd3c44ace57d3708106')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson gtkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' meson test -C build --print-errorlogs
}

package_gtkmm3() {
  depends+=(
    libatkmm-1.6.so
    libcairomm-1.0.so
    libglibmm-2.4.so
    libpangomm-1.4.so
    libsigc-2.0.so
  )
  provides=(libgtkmm-3.0.so)
  options=(!emptydirs)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_gtkmm3-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
