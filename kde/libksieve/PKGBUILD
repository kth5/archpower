# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libksieve
pkgver=23.08.4
pkgrel=1
pkgdesc='KDE PIM library for managing sieves'
arch=(x86_64 powerpc64le)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmime kidentitymanagement pimcommon kmailtransport)
makedepends=(extra-cmake-modules kdoctools5 qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0e81c87ef2ff2ad324bc9eed0dc3ea05da5050265d6d2f9d9fdcfaa1a98fb947'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
