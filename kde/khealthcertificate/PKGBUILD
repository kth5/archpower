# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=khealthcertificate
pkgver=24.05.0
pkgrel=1
pkgdesc='Handling of digital vaccination, test and recovery certificates'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcodecs
         ki18n
         openssl
         qt6-base
         zlib)
makedepends=(extra-cmake-modules
             qt6-declarative)
optdepends=('qt6-declarative: QML bindings')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3e497238fece77050c177d7c4701362dac2a358cc2aa2264150c6424bea94c12'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
