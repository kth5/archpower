# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kcolorpicker
pkgver=0.3.1
pkgrel=5
pkgdesc='Qt based Color Picker with popup menu'
arch=(x86_64 powerpc64le powerpc64 powerpc64 powerpc espresso)
url='https://github.com/DamirPorobic/kColorPicker'
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(cmake
             git)
source=(git+https://github.com/ksnip/kColorPicker#tag=v$pkgver)
sha256sums=('30e6914d81a79648d8430e30498a7952f4f93b50cd5078ad5d9724ab8ae569eb')

build() {
  cmake -B build -S kColorPicker \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
