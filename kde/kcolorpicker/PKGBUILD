# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgbase=kcolorpicker
pkgname=(kcolorpicker-qt5
         kcolorpicker-qt6)
pkgver=0.3.0
pkgrel=3
pkgdesc='Qt based Color Picker with popup menu'
arch=(x86_64 powerpc64le powerpc64 powerpc64 powerpc riscv64)
url='https://github.com/DamirPorobic/kColorPicker'
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc)
makedepends=(cmake
             qt5-base
             qt6-base)
source=(https://github.com/ksnip/kColorPicker/archive/v$pkgver/$pkgbase-$pkgver.tar.gz)
sha256sums=('cab1efae1ca345a831dc6975b81c344da5c9425d934d73584dd7d0d512af3946')

build() {
  cmake -B build5 -S kColorPicker-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build5

  cmake -B build6 -S kColorPicker-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_WITH_QT6=ON
  cmake --build build6
}

package_kcolorpicker-qt5() {
  depends+=(qt5-base)
  conflicts=(kcolorpicker)
  replaces=(kcolorpicker)

  DESTDIR="$pkgdir" cmake --install build5

# Symlinks for backwards compatibility
  mkdir -p "$pkgdir"/usr/lib/cmake/kColorPicker
  ln -sr "$pkgdir"/usr/lib/cmake/kColorPicker-Qt5/kColorPicker-Qt5Config.cmake "$pkgdir"/usr/lib/cmake/kColorPicker/kColorPickerConfig.cmake
  ln -sr "$pkgdir"/usr/lib/cmake/kColorPicker-Qt5/kColorPicker-Qt5Config-version.cmake "$pkgdir"/usr/lib/cmake/kColorPicker/kColorPickerConfig-version.cmake
  ln -sr "$pkgdir"/usr/lib/cmake/kColorPicker-Qt5/kColorPicker-Qt5-targets* "$pkgdir"/usr/lib/cmake/kColorPicker/
}

package_kcolorpicker-qt6() {
  depends+=(qt6-base)
  DESTDIR="$pkgdir" cmake --install build6
}
