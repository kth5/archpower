# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.27.9
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=3
pkgdesc='KDE and Plasma resources management GUI'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knewstuff5 kidletime5 qt5-graphicaleffects appstream-qt5 archlinux-appstream-data
         kirigami2 discount kuserfeedback5 purpose5)
makedepends=(extra-cmake-modules plasma-framework5 packagekit-qt5 flatpak)
makedepends_powerpc64le=(qt5-webview)
makedepends_x86_64=(fwupd qt5-webview)
optdepends=('packagekit-qt5: to manage packages from Arch Linux repositories (not recommended, use at your own risk)'
            'flatpak: Flatpak packages support'
            'fwupd: firmware update support')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
        appstream-1.0.patch)
sha256sums=('e739aec39cba2e68477e38e03968dfa0ae44b05177abaa601304098185fd27d5'
            'SKIP'
            'fe1d40a7192906058950b4ff860bdf8757dae00eb4beb98d7f77099f87f2bfbf')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < appstream-1.0.patch # Support appstream 1.0
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
