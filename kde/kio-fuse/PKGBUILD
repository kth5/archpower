# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-fuse
pkgver=5.1.1
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
pkgdesc='FUSE interface for KIO'
url='https://kde.org/'
license=(GPL-3.0-or-later)
depends=(fuse3
         gcc-libs
         glibc
         kcoreaddons
         kio
         qt6-base)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('adf6aa7ce055c0987e716a93ac01f3c0a97c1280421443cd6b21e0e71d763d14'
            'SKIP')
validpgpkeys=('21EC3FD75D26B39E820BE6FBD27C2C1AF21D8BAD') # Fabian Vogt <fabian@ritter-vogt.de>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DQT_MAJOR_VERSION=6 \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
