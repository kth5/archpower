# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=ksanecore
pkgname=(ksanecore
         ksanecore5)
pkgver=24.02.0
pkgrel=1
pkgdesc='Library providing logic to interface scanners'
url='https://www.kde.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         sane)
makedepends=(extra-cmake-modules
             ki18n
             ki18n5)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgbase-$pkgver.tar.xz{,.sig})
sha256sums=('93087737fc734c54cf2324921b6b5eaf0f7864306c4496942944b9352cdb21db'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build

  cmake -B build5 -S $pkgbase-$pkgver \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=5
  cmake --build build5
}

package_ksanecore() {
  depends+=(ki18n
            qt6-base)

  DESTDIR="$pkgdir" cmake --install build
}

package_ksanecore5() {
  depends+=(ki18n5
            ksanecore # translations
            qt5-base)
  conflicts=('ksanecore<24')

  DESTDIR="$pkgdir" cmake --install build5
  rm -r "$pkgdir"/usr/share/locale # Conflicts with Qt6 version
}
