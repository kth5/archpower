# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=24.02.2
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
pkgdesc='KIO Slave to access Google Drive'
url='https://apps.kde.org/kio_gdrive/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kaccounts-integration
         kaccounts-providers
         kcoreaddons
         kdeclarative
         ki18n
         kio
         knotifications
         kwidgetsaddons
         libaccounts-qt
         libkgapi
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules
             intltool
             kdoctools
             purpose)
optdepends=('purpose: to use the share plugin')
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('123bf4b52816dd06ac0455c1b0acd2f684cad735075ff6ed2244ef1e3a923923'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
