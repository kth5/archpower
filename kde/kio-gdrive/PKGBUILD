# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=23.08.2
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
pkgdesc='KIO Slave to access Google Drive'
url='https://apps.kde.org/kio_gdrive/'
license=(GPL)
depends=(libkgapi kaccounts-providers)
makedepends=(extra-cmake-modules kdoctools5 intltool purpose5)
optdepends=('purpose5: to use the share plugin')
groups=(kde-applications kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8c08d3c911ba81e239f76067e8da83aba36ecfda079427d6eaba4faa776a487f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
