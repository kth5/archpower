# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>

pkgname=telepathy-glib
pkgver=0.24.2
pkgrel=3
pkgdesc='GLib bindings for the Telepathy D-Bus protocol'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://telepathy.freedesktop.org/'
license=('LGPL-2.1-or-later')
depends=('dbus' 'dbus-glib' 'glib2' 'glibc')
makedepends=('gobject-introspection' 'vala')
source=("https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('b0a374d771cdd081125f38c3abd079657642301c71a543d555e2bf21919273f0'
            'SKIP')
validpgpkeys=('AA33B0D27868E36C151780F0FE0B6D736B1195ED') # Alexandr Akulich <akulichalexander@gmail.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
      --libexecdir=/usr/lib/telepathy \
      --enable-vala-bindings
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
