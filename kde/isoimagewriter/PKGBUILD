# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=isoimagewriter
pkgver=24.02.2
pkgrel=1
pkgdesc='Program to write hybrid ISO files onto USB disks'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/isoimagewriter/'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         gpgme
         karchive
         kcoreaddons
         kcrash
         ki18n
         kiconthemes
         kwidgetsaddons
         qgpgme-qt6
         qt6-base
         solid)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/isoimagewriter/-/commit/2240d06d.patch)
sha256sums=('96c75d58d14ef72490dad4ab62b72bf5a79d5cd9029ecfa7a289df1a83f8b186'
            'SKIP'
            'b31923ed2855847423446bde8dfba7fca7016efe69eebf114e4e5be29b7a0982')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 2240d06d.patch # Use Qgpgme
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

