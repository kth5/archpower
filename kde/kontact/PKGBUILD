# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kontact
pkgver=24.01.95
pkgrel=1
pkgdesc='KDE Personal Information Manager'
arch=(x86_64 powerpc64le)
url='https://apps.kde.org/kontact/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         grantleetheme
         kcmutils
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kio
         kjobwidgets
         kontactinterface
         kmail-account-wizard
         kparts
         kservice
         kwidgetsaddons
         kxmlgui
         libkdepim
         pimcommon
         qt6-base
         qt6-webengine)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=(akregator
            kaddressbook
            kjots
            kmail
            knotes
            korganizer
            zanshin)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('74aeb2a99c2daa0a25050dbe698fc965dbe0387b3b5ca69e224babb58fad6b37'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
