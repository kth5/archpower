# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okteta
pkgver=0.26.23
pkgrel=1
epoch=1
pkgdesc='KDE hex editor for viewing and editing the raw data of files'
url='https://apps.kde.org/okteta/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-only
         LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcmutils5
         kcodecs5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kdbusaddons5
         ki18n5
         kiconthemes5
         kio5
         kjobwidgets5
         knewstuff5
         kparts5
         kservice5
         kwidgetsaddons5
         kxmlgui5
         qca-qt5
         qt5-base
         qt5-declarative
         qt5-script)
makedepends=(extra-cmake-modules
             kdoctools5
             qt5-tools)
optdepends=('konsolepart5: terminal plugin')
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b04c50988eac26c50768ab5bd40f5b35a348c44d6e0e6a8d560563cf0eb1a3b1'
            'SKIP')
validpgpkeys=(E191FD5BE6F46870F09E82B2024E7FB43D015474  # Friedrich W. H. Kossebau <kossebau@kde.org>
              0A48BC961075B4BA8523E3790A345FB086E797D9  # https://share.kde.org/s/8dKaJTw8pR8iiwJ
              E6EBD6EB0518FE5BCCA5F6A76AACDD263C2CEFD4) # https://collaborate.kde.org/s/DsLy35eqG6qH7MJ

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
