# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=24.12.3
pkgrel=1
pkgdesc='Additional components to increase the functionality of KIO'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://www.kde.org/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcmutils
         kconfig
         kcoreaddons
         kdbusaddons
         kdnssd
         kdsoap-qt6
         kdsoap-ws-discovery-client
         ki18n
         kio
         kservice
         ktextwidgets
         kwidgetsaddons
         libimobiledevice
         libkexiv2
         libmtp
         libplist
         libssh
         libxcursor
         phonon-qt6
         plasma-activities
         qt6-5compat
         qt6-base
         qt6-svg
         smbclient
         solid
         syntax-highlighting)
depends_powerpc64=(ripgrep-all)
depends_powerpc64le=(ripgrep-all)
depends_riscv64=(ripgrep-all)
depends_x86_64=(ripgrep-all)
makedepends=(extra-cmake-modules
             gperf
             kdoctools
             libappimage
             openexr
             plasma-activities-stats
             qcoro
             taglib)
optdepends=('icoutils: Windows executable thumbnails'
            'kimageformats: thumbnails for additional image formats'
            'libappimage: AppImage thumbnails'
            'openexr: EXR format thumbnails'
            'perl: info kioslave'
            'plasma-activities-stats: recently used kioslave'
            'qt6-imageformats: thumbnails for additional image formats'
            'taglib: audio file thumbnails')
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c4374fe36e87e7b0eb745a1bba8f4b066f58b84139f9cd7b42cab6711bfb6e2f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
