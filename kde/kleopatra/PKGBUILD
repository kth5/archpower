# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kleopatra
pkgver=24.01.95
pkgrel=1
pkgdesc='Certificate Manager and Unified Crypto GUI'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/kleopatra/'
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(akonadi-mime
         gcc-libs
         glibc
         gpgme
         kcmutils
         kcodecs
         kcolorscheme
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kiconthemes
         kidentitymanagement
         kio
         kitemmodels
         kmailtransport
         kmime
         kstatusnotifieritem
         ktextwidgets
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libassuan
         libgpg-error
         libkleo
         mimetreeparser
         qgpgme-qt6
         qt6-base
         sh)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bfa0c57975cd9e8ab7b0efb7e39efa18003957251ec387c87b9f7429707b150e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
