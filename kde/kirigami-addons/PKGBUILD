# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami-addons
pkgver=1.8.1
pkgrel=1
pkgdesc='Add-ons for the Kirigami framework'
url='https://invent.kde.org/libraries/kirigami-addons'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later
         LGPL-2.1-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kglobalaccel
         kguiaddons
         ki18n
         kirigami
         kitemmodels
         ksvg
         qt6-base
         qt6-declarative
         qt6-multimedia
         sonnet)
makedepends=(extra-cmake-modules
             qt6-tools)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('00028ae4df99e250518345ea28d9cdf49d700e9acac482734bb51384da11f945'
            'SKIP')
validpgpkeys=(0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D  # Bhushan Shah (mykolab address) <bshah@mykolab.com>
              39FFA93CAE9C6AFC212AD00202325448204E452A) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
