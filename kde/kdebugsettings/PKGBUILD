# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdebugsettings
pkgver=25.08.3
pkgrel=1
pkgdesc='An application to enable/disable qCDebug'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://apps.kde.org/kdebugsettings/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kiconthemes
         kwidgetsaddons
         kxmlgui
         qt6-base)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('76990eab816c45f116c8231db33f16af955ecc733c4eb7c89fe83977f3293da0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

