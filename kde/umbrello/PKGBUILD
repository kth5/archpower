# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=24.02.2
pkgrel=1
pkgdesc='UML modeller'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/umbrello/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         ki18n5
         kiconthemes5
         kio5
         kjobwidgets5
         ktexteditor5
         ktextwidgets5
         kwidgetsaddons5
         kxmlgui5
         libxml2
         libxslt
         qt5-base
         qt5-svg)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools5)
groups=(kde-applications
        kdesdk)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('892d0bbf62a4a43dd22d61f8207aac012bbeb20d8301b7e70516b0d95617542c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
