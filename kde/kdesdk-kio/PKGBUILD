# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kio
pkgver=23.08.2
pkgrel=1
pkgdesc='KDE SDK KIO-Slaves'
url='https://www.kde.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kio5 perl)
makedepends=(extra-cmake-modules)
conflicts=(kdesdk-kioslaves)
replaces=(kdesdk-kioslaves)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('aaa1099354eec3a6c389330c6a87ff660824b2fed250f29c85a4f42de2a50d8a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
