# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kimageannotator
pkgver=0.7.2
pkgrel=1
pkgdesc='Tool for annotating images'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/ksnip/kImageAnnotator'
license=(LGPL-3.0-only)
depends=(gcc-libs
         glibc
         kcolorpicker
         libx11
         qt6-base
         qt6-svg)
makedepends=(cmake
             git
             qt6-tools)
source=(git+https://github.com/ksnip/kImageAnnotator#tag=v$pkgver)
sha256sums=('da6d1dd2b3c333c11247e3e64734ec3b9d610b296955506d7af60cf715302942')

build() {
  cmake -B build -S kImageAnnotator \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
