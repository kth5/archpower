# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmix
pkgver=24.02.0
pkgrel=1
pkgdesc='KDE volume control program'
url='https://apps.kde.org/kmix/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(alsa-lib
         gcc-libs
         glibc
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kdbusaddons5
         kglobalaccel5
         ki18n5
         knotifications5
         kwidgetsaddons5
         kwindowsystem5
         libcanberra
         libpulse
         qt5-base
         kxmlgui5
         sh
         solid5)
makedepends=(extra-cmake-modules
             kdoctools5)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        dependencies.patch)
sha256sums=('91c988e9fab9c553cc59a9202b3a64bd0866a4e3c3236fbed47c3853b676817f'
            'SKIP'
            '46ded11bd8207946bd97ed711a8ef9cec1416fa438011d13dbd3778138b028fd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < dependencies.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
