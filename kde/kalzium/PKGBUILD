# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=25.08.3
pkgrel=1
pkgdesc='Periodic Table of Elements'
url='https://apps.kde.org/kalzium/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(avogadrolibs
         avogadrolibs-qt
         gcc-libs
         glibc
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         ki18n
         kio
         kitemviews
         knewstuff
         kplotting
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         kunitconversion
         openbabel
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-scxml
         qt6-svg)
makedepends=(eigen
             extra-cmake-modules
             facile
             kdoctools
             ocaml
             python)
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        eigen-5.patch)
sha256sums=('36e6c4f46e1a5daaace14ebb5b1f6dac3429ad5df3d4fc1f0adeaec3e28e420b'
            'SKIP'
            '372c596e0e22b093c29d3b3f9fff5779fdacfe114f6eef6e8114e4c3c35b8860')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < eigen-5.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
