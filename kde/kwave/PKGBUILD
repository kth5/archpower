# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Contributor: Elvis Angelaccio <elvis.angelaccio@kdemail.net>
# Contributor: Gustavo alvarez <sl1pkn07@gmail.com>

pkgname=kwave
pkgver=24.12.2
pkgrel=2
pkgdesc='A sound editor'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/kwave/'
license=(GPL-2.0-or-later)
depends=(alsa-lib
         audiofile
         flac
         fftw
         gcc-libs
         glibc
         id3lib
         karchive
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kiconthemes
         kio
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libmad
         libogg
         libpulse
         libsamplerate
         libvorbis
         qt6-base
         qt6-multimedia)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools
             librsvg
             opus)
optdepends=('opus: OGG plugin')
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0c731a8ad8e0da37e72d9464b29f92719edb44f30c4255c5eab4ab8e95ff7916'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
# https://gitlab.archlinux.org/archlinux/packaging/packages/kwave/-/issues/1
  CFLAGS=${CFLAGS/-fno-omit-frame-pointer/}
  CXXFLAGS=${CXXFLAGS/-fno-omit-frame-pointer/}
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
