# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Daniel Balieiro <daniel@balieiro.com>

pkgname=telepathy-salut
pkgver=0.8.1
pkgrel=11
pkgdesc="A link-local XMPP connection manager for Telepathy"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL')
depends=('telepathy-glib' 'avahi' 'libsoup' 'util-linux')
makedepends=('glib2-devel' 'gtk-doc' 'libxslt' 'xmldiff')
install=telepathy-salut.install
options=(!lto)
source=(https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz
        telepathy-salut-0.8.1-python3.patch
        telepathy-salut-0.8.1-gcc14-fix-incompatible-pointer-types.patch)
sha256sums=('70b9108ac8b511b7688e1b580de4ddb8981603c3cbde01f287ef8f9cb708618e'
            '04fb03cf1f085473712dbe3aa64285266dea88b1c423149e0540d7490dbb042b'
            '017297070b624007ad30a768eec27cd20acac428ad376ff9834e23227d536e9c')

prepare() {
    cd "$pkgname-$pkgver"
    # patch for python3, taken from Fedora
    patch -Np1 -i ../telepathy-salut-0.8.1-python3.patch
    # patch for gcc14, taken from Fedora
    patch -Np1 -i ../telepathy-salut-0.8.1-gcc14-fix-incompatible-pointer-types.patch
    autoreconf -fiv
}

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib/telepathy \
        --disable-plugins --disable-Werror \
        --with-tls=gnutls \
        --enable-avahi-tests=no \
        --enable-static=no \
        --build=${CHOST}
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
