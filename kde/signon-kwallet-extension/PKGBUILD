# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=24.01.95
pkgrel=1
pkgdesc='KWallet integration for signon framework'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kwallet
         qt6-base
         signond)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b779682b4a2d09f5b07722126ab2653f20948d9c99ee41fefe5eff9a1f32673a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
