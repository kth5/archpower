# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dolphin-plugins
pkgver=24.01.95
pkgrel=1
pkgdesc='Extra Dolphin plugins'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/dolphin_plugins/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         dolphin
         kcompletion
         kconfig
         kcoreaddons
         ki18n
         kio
         kjobwidgets
         kservice
         ktexteditor
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         qt6-5compat
         qt6-base
         solid)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kdesdk)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ab9ed7bb92e27fde0a74e764254a433689487552a9e3260436e2a4c989e8f0d7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
