# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kosmindoormap
pkgver=23.04.3
pkgrel=1
pkgdesc='OSM multi-floor indoor map renderer'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.kde.org'
license=(LGPL)
depends=(kpublictransport kopeninghours protobuf)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        protobuf-23.patch)
sha256sums=('83e26d1c09fc1cc667a0ba0d68a07c5dbdf5d6e1744140e3951c2533d8cb36f2'
            'SKIP'
            '6624b5eb8340a571555f9f78ed3dc20031b3518c1370cc9ee3c16de880802b31')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < protobuf-23.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_FIND_PACKAGE_PREFER_CONFIG=ON \
    -Dprotobuf_MODULE_COMPATIBLE=ON \
    -DBUILD_STANDALONE_APP=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
