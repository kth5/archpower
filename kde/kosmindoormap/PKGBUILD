# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kosmindoormap
pkgver=24.02.2
pkgrel=1
pkgdesc='OSM multi-floor indoor map renderer'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.kde.org'
license=(LGPL-2.0-or-later)
depends=(abseil-cpp
         gcc-libs
         glibc
         kcoreaddons
         ki18n
         kirigami
         kopeninghours
         kpublictransport
         kservice
         protobuf
         qt6-base
         zlib)
makedepends=(extra-cmake-modules
             python)
optdepends=('qt6-declarative: QML bindings')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f9333e5a8a3c696a7eb4c347e25aea9da550c957a819f2c0f5c82461c2e72e14'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_FIND_PACKAGE_PREFER_CONFIG=ON \
    -Dprotobuf_MODULE_COMPATIBLE=ON \
    -DBUILD_STANDALONE_APP=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
