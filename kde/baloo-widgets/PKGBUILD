# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-widgets
pkgver=24.05.0
pkgrel=1
pkgdesc='Widgets for Baloo'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.kde.org/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         baloo
         kconfig
         kcoreaddons
         kfilemetadata
         ki18n
         kio
         kservice
         kwidgetsaddons
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3d21efd641c2b7cdeecebc4a4b938cdd3b4f3c87cc260014e3da711d22a9f6b7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

