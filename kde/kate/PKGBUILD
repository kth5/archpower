# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kate
pkgver=25.08.1
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
pkgdesc='Advanced text editor'
groups=(kde-applications
        kde-utilities)
url='https://apps.kde.org/kate/'
depends=(gcc-libs
         glibc
         karchive
         kbookmarks
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kguiaddons
         ki18n
         kiconthemes
         kio
         knewstuff
         kparts
         kservice
         ktexteditor
         kuserfeedback
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         qt6-base
         syntax-highlighting)
optdepends=('clang: C and C++ LSP support'
            'git: git-blame plugin' 
            'konsole: open a terminal in Kate'
            'python-lsp-server: Python LSP support'
            'qt6-declarative: RBQL plugin'
            'qtkeychain-qt6: SQL plugin'
            'rust: Rust LSP support'
            'texlab: LaTeX LSP support')
makedepends=(extra-cmake-modules
             kdoctools
             qtkeychain-qt6)
conflicts=(kwrite)
provides=(kwrite)
replaces=(kwrite)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6b5f9d240da5a7668e2643e4153ab63bf7842ec0605d5d58b3f5aa3e5f2be8e1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
