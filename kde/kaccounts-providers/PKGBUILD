# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaccounts-providers
pkgver=25.04.1
pkgrel=1
pkgdesc='Online account providers for the KAccounts system'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.kde.org/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kaccounts-integration
         kcoreaddons
         ki18n
         kio
         kirigami
         kpackage
         qt6-base
         qt6-declarative
)
depends_x86_64=(
         qt6-webengine
)
makedepends=(extra-cmake-modules
             intltool
             qcoro)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bf400b1aef13f235ffa25741b45a6a2452f063aac4de1fd94a8ffec55fba1e4d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
