# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=24.02.0
pkgrel=1
pkgdesc='PHP language and documentation plugin for KDevelop'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://www.kdevelop.org/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcmutils5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kdevelop
         ki18n5
         kio5
         ktexteditor5
         kwidgetsaddons5
         kxmlgui5
         qt5-base
         threadweaver5)
makedepends=(extra-cmake-modules
             kdevelop-pg-qt
             kdoctools)
groups=(kde-applications
        kdevelop)
source=(https://download.kde.org/stable/release-service/$pkgver/src/kdev-php-$pkgver.tar.xz{,.sig})
sha256sums=('73f482ef46b71a5cfb78b886c26cfd0d618274848681e6047528faca96d0eb49'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>


build() {
  cmake -B build -S kdev-php-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
