# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=plasma-integration
pkgname=(plasma-integration
         plasma5-integration)
pkgver=6.3.1
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=2
pkgdesc='Qt Platform Theme integration plugins for the Plasma workspaces'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcolorscheme
         kcompletion
         kconfig
         kcoreaddons
         kguiaddons
         kiconthemes
         ki18n
         kio
         kjobwidgets
         kservice
         kstatusnotifieritem
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libxcb
         libxcursor
         qqc2-breeze-style
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-wayland
         wayland)
makedepends=(extra-cmake-modules
             plasma-wayland-protocols
             kconfig5
             ki18n5
             kiconthemes5
             kio5
             knotifications5
             kwayland5
             kwindowsystem5
             kxmlgui5
             qt5-base
             qt5-declarative
             qt5-quickcontrols2
             qt5-wayland
             qt5-x11extras)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/plasma-integration/-/commit/153da40b.patch)
sha256sums=('446567571dc8b43447b7b7afd7b82d9ba2a60421393c85cbb109bb65f540a9c2'
            'SKIP'
            'f4774cce352bab4b40130a16ba9cdd59bc333dd8293e2317c82e602be64e0c82')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -Rp1 < 153da40b.patch # Temporary revert until Qt 6.9 beta 3 is out
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QT5=OFF
  cmake --build build

  cmake -B build5 -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QT6=OFF
  cmake --build build5
}

package_plasma-integration() {
  depends+=(noto-fonts
            ttf-hack
            xdg-desktop-portal-kde)
  DESTDIR="$pkgdir" cmake --install build
}

package_plasma5-integration() {
  pkgdesc='Qt5 Platform Theme integration plugins for the Plasma workspaces'
  depends=(gcc-libs
           glibc
           kcompletion5
           kconfig5
           kconfigwidgets5
           kcoreaddons5
           ki18n5
           kiconthemes5
           kio5
           kjobwidgets5
           knotifications5
           kservice5
           kwayland5
           kwidgetsaddons5
           kwindowsystem5
           kxmlgui5
           libxcb
           libxcursor
           qt5-base
           qt5-declarative
           qt5-quickcontrols2
           qt5-wayland
           qt5-x11extras
           wayland)
  conflicts=('plasma-integration<5.27.80')
  replaces=('plasma-integration<5.27.80')
  groups=()

  DESTDIR="$pkgdir" cmake --install build5
}
