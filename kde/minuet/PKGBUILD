# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=minuet
pkgver=24.12.3
pkgrel=1
pkgdesc='A KDE Software for Music Education'
url='https://apps.kde.org/minuet/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcoreaddons5
         kcrash5
         ki18n5
         fluidsynth
         qt5-base
         qt5-declarative
         qt5-quickcontrols2
         qt5-svg)
makedepends=(extra-cmake-modules
             kdoctools5)
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('cdc73e908282e4d087cc9f6932c134835bbff784cd1bf6b56e3d3dc9749a2f77'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
