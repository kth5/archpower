# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=k3b
pkgver=24.02.0
pkgrel=1
epoch=1
pkgdesc='Feature-rich and easy to handle CD burning application'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/k3b/'
license=(GPL-2.0-or-later)
depends=(ffmpeg
         flac
         gcc-libs
         glibc
         karchive5
         kauth5
         kbookmarks5
         kcmutils5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kfilemetadata5
         ki18n5
         kiconthemes5
         kio5
         kjobwidgets5
         knewstuff5
         knotifications5
         knotifyconfig5
         kwidgetsaddons5
         kxmlgui5
         lame
         libburn # cdrskin
         libdvdread
         libkcddb5
         libmad
         libmpcdec
         libogg
         libsamplerate
         libsndfile
         libvorbis
         qt5-base
         solid5
         taglib)
makedepends=(extra-cmake-modules
             kdoctools5)
optdepends=('cdparanoia: for CD ripping support' 
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for DVD burning support'
            'emovix: for bootable multimedia CD/DVD support'
            'vcdimager: for VCD burning support')
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2f24a5c7ce29b3abbcf14aaee924762356dcf0f5eac0051f83a92def9bf7b51c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
