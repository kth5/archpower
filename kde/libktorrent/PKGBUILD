# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libktorrent
pkgver=25.08.1
pkgrel=1
pkgdesc='A BitTorrent protocol implementation'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://apps.kde.org/ktorrent/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         gmp
         karchive
         kconfig
         kcoreaddons
         ki18n
         kio
         openssl
         qt6-base)
makedepends=(boost
             extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('85e3150a100180fa5267f55f0740dab644dc735cfd80f741d298bfbe78ac1602'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
