# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=syntax-highlighting
pkgname=${_name}5
pkgver=5.114.0
pkgrel=1
pkgdesc='Syntax highlighting engine for structured text and code'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(GPL2)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-declarative qt5-xmlpatterns qt5-tools qt5-doc doxygen)
optdepends=('qt5-declarative: QML bindings')
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('c3d8510de69058f91fb9f4485cea7b3d329d38785ace97f130596950c5a36bea'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  case "${CARCH}" in
    powerpc) CXXFLAGS+=' -latomic' ;;
  esac

  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF \
    -DQRC_SYNTAX=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
