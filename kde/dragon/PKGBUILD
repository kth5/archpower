# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dragon
pkgver=25.08.3
pkgrel=1
pkgdesc='A multimedia player where the focus is on simplicity, instead of features'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://apps.kde.org/dragonplayer/'
license=(GPL-2.0-or-later)
depends=(ffmpeg
         gcc-libs
         glibc
         kconfig
         kcoreaddons
         kcrash
         ki18n
         kio
         kirigami
         kservice
         kwindowsystem
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-multimedia-ffmpeg)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/multimedia/dragon/-/commit/1ffaa540.patch
        https://invent.kde.org/multimedia/dragon/-/commit/110a39d6.patch)
sha256sums=('9e1d2fe93018166418cbfc82fc2f9384d1c5c99bc1b500c69072c8fe2cf9d038'
            'SKIP'
            'f7423616074e817f1004427d4c4dcc1383df17b83e1045a0d6f48cccaef0a176'
            '0b5ffea5d52699146835cd848cc634c92e107b04595348aeb21ef6ad9c76f1c9')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 1ffaa540.patch # Fix open file dialog
  patch -d $pkgname-$pkgver -p1 < 110a39d6.patch # Fix missing window icon
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

