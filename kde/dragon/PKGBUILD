# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dragon
pkgver=25.08.1
pkgrel=1
pkgdesc='A multimedia player where the focus is on simplicity, instead of features'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://apps.kde.org/dragonplayer/'
license=(GPL-2.0-or-later)
depends=(ffmpeg
         gcc-libs
         glibc
         kconfig
         kcoreaddons
         kcrash
         ki18n
         kio
         kirigami
         kservice
         kwindowsystem
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-multimedia-ffmpeg)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/multimedia/dragon/-/commit/f7344d27.patch
        https://invent.kde.org/multimedia/dragon/-/commit/eb42aa21.patch)
sha256sums=('bf6f3e198d1e9768ceabc77dccc130f2a6b43260ec23f2964f94abdc5d5f771f'
            'SKIP'
            '2bbcd65c3aaa122f54ef1fc8a4199913e5ab89d393b54ef7119ad6cf84abc7aa'
            'fd462b96481c2b2b13867f6bfad0fd84e222ed3c62d612486a7c2f96b0af96f6')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < f7344d27.patch # Fix open file dialog
  patch -d $pkgname-$pkgver -p1 < eb42aa21.patch # Fix missing window icon
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

