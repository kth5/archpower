# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=24.12.3
pkgrel=1
pkgdesc='Common libraries for KDE PIM'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-search
         gcc-libs
         glibc
         kcmutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kcontacts
         kcoreaddons
         ki18n
         kimap
         kio
         kitemmodels
         kitemviews
         kldap
         knewstuff
         ktextaddons
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkdepim
         plasma-activities
         purpose
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b50eca817359359fcbe8c067813c764bc8e419b59d608ff69de10b4f7ff95b2f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
