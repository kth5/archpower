# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=23.08.2
_clangver=16.0.6
pkgrel=1
pkgdesc='C++ IDE using KDE technologies'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://www.kdevelop.org/'
license=(GPL)
depends=(clang=$_clangver libksysguard kitemmodels5 knotifyconfig5 ktexteditor5 grantlee libkomparediff2 kcmutils5 threadweaver5)
makedepends=(extra-cmake-modules kdoctools5 kdevelop-pg-qt llvm qt5-tools plasma-framework5 krunner5 okteta boost purpose5 subversion astyle)
optdepends=('konsole: embedded terminal'
            'git: Git support' 'subversion: SVN support'
            'gdb: GNU debugger support' 'lldb-mi: LLDB debugger support'
            'qt5-doc: Qt documentation integration'
            'cmake: cmake integration'
            'kate: for additional plugins'
            'meson: meson integration'
            'qt5-tools: qthelp plugin'
            'okteta: GNU debugger support'
            'plasma-framework5: for the plasma addons'
            'purpose5: patch review plugin'
            'astyle: astyle plugin'
            'cppcheck: code analyzer' 'heaptrack: memory profiler'
            'clazy: clazy analyzer plugin'
            'kio-extras: man pages plugin')
conflicts=(kdevelop-qmljs kdevplatform)
replaces=(kdevelop-qmljs kdevplatform kdevelop-clang-tidy)
groups=(kde-applications kdevelop)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/kdevelop/kdevelop/-/commit/9e8b3a03.patch)
sha256sums=('577715eeb0d6bce068987a7e66988616e9b483e8ad384901e90254fd209cc513'
            'SKIP'
            '4059be8c0a8e08db3e7dafe5ab38839d510878d7e58a93fc0320cacc0fdf237a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 9e8b3a03.patch # Port LLDB support to Python 3
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
