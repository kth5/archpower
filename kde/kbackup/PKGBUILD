# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: BluePeril <blueperil (at) blueperil _dot_ de>

pkgname=kbackup
pkgver=24.08.3
pkgrel=1
pkgdesc='A program that lets you back up any directories or files'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/kbackup/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kguiaddons
         ki18n
         kiconthemes
         kio
         kservice
         kstatusnotifieritem
         kwidgetsaddons
         kxmlgui
         libarchive
         qt6-5compat
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9ff0f9feb8fff2d05a2de978b7bab24bde6c5da9621b6496e91d693152e32f8d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>


build() {

  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
