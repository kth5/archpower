# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=(akonadi libakonadi)
pkgver=25.08.0
pkgrel=2
pkgdesc='PIM layer, which provides an asynchronous API to access all kind of PIM data'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         ki18n
         libaccounts-qt
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             postgresql
             qt6-doc
             qt6-tools
             kaccounts-integration
             kconfigwidgets
             kiconthemes
             kitemmodels
             kxmlgui)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/akonadi/-/commit/8a2dfd13.patch)
sha256sums=('d7b79d8ad021f3bbe1410bbd2c1755d3fa54a727c70eb9008dccf94e642a664e'
            'SKIP'
            'eddad7ba227c50bde5b82267910c42873b0bc0b7f240957eee4f8079956ae313')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 8a2dfd13.patch # Fix opening agent configuration
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package_libakonadi() {
  pkgdesc='Libraries used by applications based on Akonadi'
  depends+=(kaccounts-integration
            kcolorscheme
            kconfigwidgets
            kcrash
            kiconthemes
            kitemmodels
            kwidgetsaddons
            kxmlgui
            libxml2
            xz)
  optdepends=('kirigami-addons: QML bindings')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/bin # Provided by akonadi
}

package_akonadi() {
  depends+=(kcrash
            kwidgetsaddons
            libakonadi
            mariadb)
  optdepends=('postgresql: PostgreSQL backend')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}} # Provided by libakonadi
}
