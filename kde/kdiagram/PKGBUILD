# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdiagram
pkgver=3.0.1
pkgrel=3
pkgdesc='Powerful libraries for creating business diagrams'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.kde.org/'
license=(GPL2)
depends=(gcc-libs
         glibc
         qt6-base
         qt6-svg)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4659b0c2cd9db18143f5abd9c806091c3aab6abc1a956bbf82815ab3d3189c6d'
            'SKIP')
validpgpkeys=(42A60D06F449E895F40F09C01638B8A616108B8A  # Dag Andersen <danders@get2net.dk>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D) # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
