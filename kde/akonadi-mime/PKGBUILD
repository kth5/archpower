# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-mime
pkgver=25.12.2
pkgrel=1
pkgdesc='Libraries and daemons to implement basic email handling'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         ki18n
         kio
         kmime
         kwidgetsaddons
         kxmlgui
         libakonadi
         qt6-base)
makedepends=(extra-cmake-modules
             qt6-declarative)
optdepends=('qt6-declarative: QML bindings')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ad7456cccdad965bc7586136f3f1220b83af0746b305a1880beb5714cbf6a389'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
