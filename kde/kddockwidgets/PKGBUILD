# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Tomaz Canabrava <tomaz.canabrava@archlinux.org>

_inner_folder_name=KDDockWidgets
pkgname=kddockwidgets
pkgver=2.4.0
pkgrel=2
pkgdesc="Dock Widgets Library by KDAB for Qt6"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/KDAB/KDDockWidgets"
license=(GPL-2.0-only
         GPL-3.0-only)
depends=(
    fmt
    gcc-libs
    glibc
    qt6-base
    qt6-declarative
    spdlog
)
makedepends=(
    cmake
    nlohmann-json
    qt6-tools
)

source=($url/releases/download/v$pkgver/kddockwidgets-$pkgver.tar.gz{,.asc})

sha256sums=(
    '51dbf24fe72e43dd7cb9a289d3cab47112010f1a2ed69b6fc8ac0dff31991ed2'
    'SKIP'
)

validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # info@kdab.com

build() {
    cd $_inner_folder_name-$pkgver
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDDockWidgets_FRONTENDS='qtwidgets;qtquick' \
          -DKDDockWidgets_QT6=true \
          .
    cmake --build .
}

package() {
    depends+=(libfmt.so libspdlog.so)
    cd $_inner_folder_name-$pkgver
    DESTDIR="$pkgdir" cmake --install .
}
