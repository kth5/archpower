# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Roja <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=rocs
pkgver=25.08.0
pkgrel=1
pkgdesc='Graph Theory IDE'
url='https://apps.kde.org/rocs/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         grantlee
         karchive5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         ki18n5
         kitemviews5
         ktexteditor5
         ktextwidgets5
         kparts5
         kwidgetsaddons5
         kxmlgui5
         qt5-base
         qt5-declarative
         qt5-quickcontrols
         qt5-script
         qt5-xmlpatterns)
makedepends=(boost
             extra-cmake-modules
             kdoctools5)
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b4e8e702303767269463473707cf4b24a3d99f5f9433cc7c2ce2e5e40df31c89'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
