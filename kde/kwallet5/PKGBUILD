# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kwallet
pkgname=${_name}5
pkgver=5.115.0
pkgrel=2
pkgdesc='Secure and unified container for user passwords'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(knotifications5 kservice5 kconfigwidgets5 gpgme qca-qt5)
makedepends=(extra-cmake-modules kdoctools5 boost doxygen qt5-tools qt5-doc)
optdepends=('kwalletmanager: Configuration GUI')
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
provides=(org.freedesktop.secrets)
replaces+=(kwallet-secrets)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('f2b3fc21c89440c9290703929a8bcb77461fa510fd060c33d64787c9e0eaef3d'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -DBUILD_KWALLET_QUERY=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
