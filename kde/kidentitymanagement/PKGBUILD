# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kidentitymanagement
pkgver=24.01.95
pkgrel=1
pkgdesc='Library to assist in handling user identities'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcodecs
         kcompletion
         kconfig
         kcoreaddons
         ki18n
         kio
         kpimtextedit
         ktextaddons
         kwidgetsaddons
         kxmlgui
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             kirigami-addons
             qt6-doc
             qt6-tools)
optdepends=('kirigami-addons: QML bindings'
            'qt6-declarative: QML bindings')
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d1b42746a7ee66f38dacdd88202dedf201de8a1970f8e982816e7a09c4baf5fb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
