# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=purpose
pkgver=5.109.0
pkgrel=1
pkgdesc='Framework for providing abstractions to get the developers purposes fulfilled'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kaccounts-integration kirigami2 accounts-qml-module)
makedepends=(extra-cmake-modules intltool)
optdepends=('kdeconnect: sharing to smartphone via KDE Connect'
            'telegram-desktop: sharing via Telegram'
            'bluedevil: sharing via Bluetooth'
            'prison: QR code plugin')
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('31335b2fa3d502d8d8474e67f2f1a671e9ea3693b290a6ff57b93b047afdcc12'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
