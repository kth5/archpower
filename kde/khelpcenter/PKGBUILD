# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khelpcenter
pkgver=24.01.95
pkgrel=1
pkgdesc='Application to show KDE Applications documentation'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/khelpcenter/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kbookmarks
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kdbusaddons
         kdoctools
         ki18n
         kio
         kservice
         ktexttemplate
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libxml2
         perl
         qt6-base
         xapian-core)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-system)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c2b2b87baa4169007caf70ff39410000f3872c7e12a589804dea5113cb8b6e89'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
