# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Daniel Balieiro <daniel@balieiro.com>
# Contributor: Rodrigo L. M. Flores <mail@rodrigoflores.org>

pkgname=telepathy-gabble
pkgver=0.18.4
pkgrel=6
pkgdesc="A Jabber/XMPP connection manager for Telepathy"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://telepathy.freedesktop.org"
groups=(telepathy)
license=(LGPL2.1)
depends=(telepathy-glib libsoup3 libnice)
makedepends=(libxslt glib2-devel gnome-common)
source=(https://telepathy.freedesktop.org/releases/telepathy-gabble/telepathy-gabble-$pkgver.tar.gz{,.asc}
        telepathy-gabble-0.18.4-python3.patch
        telepathy-gabble-0.18.4-aviod-errno-name-confusion.patch
        telepathy-gabble-0.18.4-xmlerror-constness.patch
        telepathy-gabble-0.18.4-libsoup-3.0.patch)
install=telepathy-gabble.install
sha256sums=('115c91572c72d4a40f0b25b606167b4f2f09441dc7bf1036ccbb1450f1a4969c'
            'SKIP'
            '2873cdb3b65deab1418acd25fc5ec4bf120f52dcceda129ae7ac315026ea2749'
            'b841d666d9f4046015bb7dbd14c1f936119b965effd2e3e8b25196cae00ae74a'
            '771a4a58500fefa77874eb3fad860491616c1c44e0d11b546930b1daae4055c7'
            '74155d07474bcf18632fb3e1947271c59783519aea5cc07ff38924b9f6cc67e3')
validpgpkeys=(38FDAD7D73CE1AE7A16200291F3845DD1432A9FB) # Georgios Kiagiadakis <gkiagia@tolabaki.gr>

prepare() {
  cd $pkgname-$pkgver
  # port to python3, patch taken from Fedora
  patch -Np1 -i ../telepathy-gabble-0.18.4-python3.patch
  # patches for gcc14, taken from Fedora
  patch -Np1 -i ../telepathy-gabble-0.18.4-aviod-errno-name-confusion.patch
  patch -Np1 -i ../telepathy-gabble-0.18.4-xmlerror-constness.patch
  
  patch -Np1 -i ../telepathy-gabble-0.18.4-libsoup-3.0.patch
  autoreconf -fiv
  cd lib/ext/wocky
  autoconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/telepathy \
    --disable-Werror --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
