# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalarm
pkgver=24.02.2
pkgrel=1
pkgdesc='Personal alarm scheduler'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/kalarm/'
license=(GPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-mime
         gcc-libs
         glibc
         kauth
         kcalendarcore
         kcalutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         kglobalaccel
         kguiaddons
         kholidays
         ki18n
         kidentitymanagement
         kio
         kitemmodels
         kmailtransport
         kmime
         knotifications
         knotifyconfig
         kstatusnotifieritem
         ktextaddons
         ktextwidgets
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libakonadi
         libcanberra
         qt6-5compat
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2eb95704ffac43a3bde942121a63a6ff3a7f59ee783fc614eb0d0216e5786ffe'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
