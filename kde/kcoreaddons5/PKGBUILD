# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kcoreaddons
pkgname=${_name}5
pkgver=5.114.0
pkgrel=1
pkgdesc='Addons to QtCore'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools qt5-doc doxygen)
groups=(kf5)
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('1ddbca49743c4d6a09d9a8b83c45799e0a18c172427f7bc23429c6a8fbd063f3'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -D_KDE4_DEFAULT_HOME_POSTFIX=4 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
