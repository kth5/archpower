# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libmediawiki
pkgver=5.37.0
pkgrel=4
pkgdesc="A KDE C++ interface for MediaWiki based web service as wikipedia.org"
arch=(x86_64 powerpc64le powerpc riscv64)
url='https://www.digikam.org/'
license=(GPL)
depends=(kcoreaddons)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        libmediawiki-5.37.0-fix-warnings.patch
        libmediawiki-5.37.0-qt-5.15.patch)
sha256sums=('da167259c49d012359ad65175c365613f0c42699fb99ac7fb9d1f1c8c48292c1'
            'SKIP'
            'e234f92233d2ef981771886b6628b72e840c304507ae26c2f9d613fddce81b23'
            '39a8dd3e05d31e1a6938afab188075c41c7f740d4077d4fb881ca18928a39294')
validpgpkeys=(7C35920F1CE2899E8EA9AAD02E7C0367B9BFA089) # Scarlett Clark <sgclark@kde.org>

prepare() {
  mkdir -p build 

  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/libmediawiki-5.37.0-fix-warnings.patch
  patch -Np1 -i ${srcdir}/libmediawiki-5.37.0-qt-5.15.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
