# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kasts
pkgver=25.08.1
pkgrel=1
pkgdesc='Kirigami-based podcast player'
url='https://apps.kde.org/kasts/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(breeze-icons
         gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kiconthemes
         kirigami
         kirigami-addons
         kwindowsystem
         libvlc
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-multimedia
         qtkeychain-qt6
         syndication
         taglib
         threadweaver
         vlc-plugins-base)
makedepends=(extra-cmake-modules
             python)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d21c7f84595992ff76f297789c73944c7815e746e2d36792d24ad0eae758303a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
