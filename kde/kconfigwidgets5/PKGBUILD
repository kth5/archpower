# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kconfigwidgets
pkgname=${_name}5
pkgver=5.114.0
pkgrel=1
pkgdesc='Widgets for KConfig'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kauth5 kcodecs5 kconfig5 kguiaddons5 ki18n5 kwidgetsaddons5)
makedepends=(extra-cmake-modules kdoctools5 doxygen qt5-tools qt5-doc)
optdepends=('perl: for preparetips5')
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('d2ca1cccf1dd65fe5762f5540e3eb02ef6b5109ef97e89fbdfcb71369bf78a9a'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
