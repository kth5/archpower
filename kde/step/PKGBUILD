# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=step
pkgver=23.08.4
pkgrel=1
pkgdesc='Interactive Physical Simulator'
url='https://apps.kde.org/step/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(libqalculate gsl knewstuff5 kplotting5 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools5 eigen qt5-tools python)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('36e52e01fca3e1ee5025f82aa33b598e0a543599a534e2aa881d8ab76762b636'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  case "${CARCH}" in
    powerpc64)
      export CFLAGS="-O2 -pipe"
      export CXXFLAGS="-O2 -pipe"
    ;;
  esac

  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
