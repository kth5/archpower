# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kcachegrind
pkgname=(kcachegrind
         kcachegrind-common
         qcachegrind)
pkgver=25.08.0
pkgrel=1
pkgdesc='Visualization of Performance Profiling Data'
url='https://apps.kde.org/kcachegrind/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
makedepends=(extra-cmake-modules
             kdoctools
             kio
             kxmlgui
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4253d8643a5e1c9261be470ac61c8f5eb2ce721036115ae204ac3aab6b38a0a4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_kcachegrind() {
  groups=(kde-applications
          kde-sdk)
  depends=(gcc-libs
           glibc
           karchive
           kcachegrind-common
           kconfig
           kconfigwidgets
           kcoreaddons
           kdbusaddons
           ki18n
           kio
           kwidgetsaddons
           kxmlgui
           qt6-base)
  optdepends=('graphviz: for plotting support')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/{bin/*tree,share/{doc,icons,locale}}
}

package_kcachegrind-common() {
  pkgdesc+=' (common files)'
  depends=(hicolor-icon-theme)
  optdepends=('perl: for dprof2calltree, memprof2calltree and op2calltree'
              'php: for pprof2calltree')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/{bin/kcachegrind,share/{kcachegrind,applications,metainfo}} # provided by kcachegrind

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "$pkgdir"/usr/bin/hotshot2calltree
}

package_qcachegrind() {
  pkgdesc+=' (pure Qt version)'
  depends=(gcc-libs
           glibc
           kcachegrind-common
           qt6-base)
  optdepends=('graphviz: for plotting support')

  install -Dm755 build/bin/qcachegrind -t "$pkgdir"/usr/bin/
  install -Dm644 $pkgbase-$pkgver/qcachegrind/qcachegrind.desktop -t "$pkgdir"/usr/share/applications/
}
