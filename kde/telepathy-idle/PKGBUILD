# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Samuel Mendes <heka.lok@gmail.com>

pkgname=telepathy-idle
pkgver=0.2.2
pkgrel=3
pkgdesc='IRC connection manager for Telepathy'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://telepathy.freedesktop.org/'
license=(LGPL-2.1-only)
depends=(
  dbus-glib
  glib2
  glibc
  telepathy-glib
)
makedepends=(
  git
  libxslt
  python
)
checkdepends=(python-dbus)
source=("git+https://gitlab.freedesktop.org/telepathy/$pkgname.git?signed#tag=$pkgname-$pkgver")
b2sums=(740d9f0292b7cdd835181b5efef9b075dc3a56f61be24f813de450043db5f376e8ecc7073b70b879b43ee91186c077af4bfb99d51e74a1de483fe15e0f71d5a8)
validpgpkeys=(AA33B0D27868E36C151780F0FE0B6D736B1195ED) # Alexandr Akulich

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/telepathy
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
