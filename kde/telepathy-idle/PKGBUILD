# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Samuel Mendes <heka.lok@gmail.com>

pkgname=telepathy-idle
pkgver=0.2.2
pkgrel=2
pkgdesc='IRC connection manager for Telepathy'
arch=(x86_64 powerpc64le powerpc)
url='https://telepathy.freedesktop.org/'
license=('LGPL-2.1-only')
groups=('telepathy')
depends=('dbus-glib' 'glib2' 'glibc' 'telepathy-glib')
makedepends=('libxslt' 'python')
checkdepends=('python-dbus')
source=("https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('8387e25e5fb0b4cbe701e5dc092d666d6510b833fd3e7e462e9170d36ec3c15f'
            'SKIP')
validpgpkeys=('AA33B0D27868E36C151780F0FE0B6D736B1195ED') # Alexandr Akulich

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/telepathy
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
