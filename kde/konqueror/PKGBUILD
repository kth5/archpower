# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=23.04.3
pkgrel=1
pkgdesc='KDE File Manager & Web Browser'
arch=(x86_64 powerpc64le)
url='https://apps.kde.org/konqueror/'
license=(LGPL)
depends=(dolphin keditbookmarks qt5-webengine kinit)
makedepends=(extra-cmake-modules kdoctools kdesu)
optdepends=('kdesu: shell command plugin')
groups=(kde-applications kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e0c614f94825ad589967bbe4e500c32b0b75e229e960ed18f64061f9a8f6d663'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DUSE_SYSTEM_DICTIONARIES=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
