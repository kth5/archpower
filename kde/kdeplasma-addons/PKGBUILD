# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons
pkgver=6.3.1
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=2
pkgdesc='All kind of addons to improve your Plasma experience'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         icu
         kauth
         kcmutils
         kconfig
         kcoreaddons
         kdbusaddons
         kdeclarative
         kglobalaccel
         kholidays
         ki18n
         kiconthemes
         kio
         kirigami
         kirigami-addons
         kitemmodels
         kjobwidgets
         knewstuff
         knotifications
         kpackage
         krunner
         kservice
         ksvg
         kunitconversion
         kwidgetsaddons
         kwin
         kwindowsystem
         kxmlgui
         libplasma
         plasma-workspace
         plasma5support
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-quick3d
         sonnet)
makedepends=(extra-cmake-modules
             networkmanager-qt)
makedepends_x86_64=(qt6-webengine)
optdepends=('networkmanager-qt: POTD wallpaper'
            'quota-tools: disk quota applet')
optdepends_x86_64=('qt6-webengine: dictionary and webbrowser applets')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('696ceb400ac73466b332971ea4e70fc0b913d70f189431028ae4bd1db201058a'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build  -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
