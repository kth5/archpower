# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: said
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=kamoso
pkgver=25.12.2
pkgrel=1
pkgdesc='A webcam recorder from KDE community'
url='https://apps.kde.org/kamoso/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glib2
         glibc
         gst-plugin-qml6
         gst-plugins-bad
         gst-plugins-base-libs
         gst-plugins-good
         gstreamer
         kconfig
         kcoreaddons
         ki18n
         kio
         kirigami
         kirigami-addons
         kitemviews
         knotifications
         purpose
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules
             glib2-devel
             kdoctools
             python)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('a3c80edf315fcfd0ef49ad1808410202f8083ca935f3876eeba03681c2c063a7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

