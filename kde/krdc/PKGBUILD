# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=krdc
pkgver=24.02.2
pkgrel=1
pkgdesc='Remote Desktop Client'
url='https://apps.kde.org/krdc/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kactivities5
         kbookmarks5
         kcmutils5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kdnssd5
         ki18n5
         kio5
         knotifications5
         knotifyconfig5
         kwidgetsaddons5
         kwallet5
         kxmlgui5
         qt5-base)
makedepends=(extra-cmake-modules
             freerdp2
             kdoctools5
             libvncserver)
optdepends=('libvncserver: VNC support'
            'libssh: VNC support'
            'freerdp2: RDP support'
            'keditbookmarks: to edit bookmarks')
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('aa4e8786480dccb5840532fe8c9188881437d8ca59a98cffcff6685e5579aff9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
