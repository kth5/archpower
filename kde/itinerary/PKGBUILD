# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=itinerary
pkgver=24.01.95
pkgrel=1
pkgdesc='Itinerary and boarding pass management application'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/itinerary/'
license=(LGPL-2.0-or-later BSD)
depends=(gcc-libs
         glibc
         kcalendarcore
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         kfilemetadata
         khealthcertificate
         kholidays
         ki18n
         kio
         kirigami
         kirigami-addons
         kitinerary
         kitemmodels
         knotifications
         kosmindoormap
         kpkpass
         kpublictransport
         kunitconversion
         kwindowsystem
         libquotient
         networkmanager-qt
         prison
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-location
         qt6-multimedia
         qt6-positioning
         zlib)
makedepends=(extra-cmake-modules
             python)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8a9094b8ef633b23fda4da1e446fe8dc0580a75509e5e86553a72ca427287984'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
