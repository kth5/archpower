# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=24.05.2
pkgrel=2
pkgdesc='Archiving Tool'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/ark/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kfilemetadata
         ki18n
         kio
         kjobwidgets
         kparts
         kpty
         kservice
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libarchive
         libzip
         qt6-base
         sh
         zlib)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('arj: ARJ format support'
            'lrzip: LRZ format support'
            'lzop: LZO format support'
            'p7zip: 7Z format support'
            'unarchiver: RAR format support'
            'unrar: RAR decompression support')
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/ark/-/commit/85c5e26f.patch)
sha256sums=('3a8f8b8330eb1ce1511cee8c12dd8225c4ef1ad72a7649e40e0ddc574cf70ee0'
            'SKIP'
            '237da69cc46228bac20fc00924e680669b9ec41fa84f89a0d58e6cc557bd11b3')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 85c5e26f.patch # Fix temp dir regression
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

