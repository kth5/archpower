# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=marble
pkgname=(marble
         marble-common
         marble-maps
         marble-qt)
pkgver=24.02.0
pkgrel=2
pkgdesc='Desktop Globe'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://apps.kde.org/marble/'
license=(GPL-2.0-or-later)
makedepends=(extra-cmake-modules
             gpsd
             kdoctools5
             knewstuff5
             kparts5
             libwlocate
             phonon-qt5
             protobuf
             qt5-serialport
             qt5-tools
             shapelib)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgbase-$pkgver.tar.xz{,.sig})
sha256sums=('9299073ca61724350f3236c9916ea81d4bd445009ec65b736660a4bf016ef96a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_CXX_STANDARD=17 \
    -DQT_PLUGINS_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_TOUCH=ON \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  cmake --build build
}

package_marble-common() {
  pkgdesc='Common libraries and plugins for Marble'
  depends=(gcc-libs
           glibc
           phonon-qt5
           protobuf
           qt5-base
           qt5-declarative
           qt5-location
           qt5-svg
           qt5-webchannel
           qt5-webengine
           zlib)
  optdepends=('gpsd: GPS based geolocation'
              'libwlocate: WLAN based geolocation'
              'qt5-serialport: APRS plugin'
              'shapelib: SHP plugin')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/share/{config.kcfg,kxmlgui5,metainfo} \
        "$pkgdir"/usr/bin \
        "$pkgdir"/usr/lib/qt/plugins/*.so \
        "$pkgdir"/usr/share/applications/{marble_geo.desktop,marble_worldwind.desktop,org.kde.marble*.desktop} \
        "$pkgdir"/usr/share/kservices5/marble_part.desktop \
        "$pkgdir"/usr/share/locale/*/LC_MESSAGES/*.mo
}

package_marble-qt() {
  pkgdesc+=' (Qt version)'
  depends=(gcc-libs
           glibc
           marble-common
           qt5-base)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-qt
}

package_marble() {
  depends=(gcc-libs
           glibc
           kconfig5
           kconfigwidgets5
           kcoreaddons5
           kcrash5
           ki18n5
           kio5
           kparts5
           kwidgetsaddons5
           kxmlgui5
           marble-common
           qt5-base)
  groups=(kde-applications
          kde-education)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-kde
  DESTDIR="$pkgdir" cmake --install build/src/plasma
  DESTDIR="$pkgdir" cmake --install build/src/plasmarunner
  DESTDIR="$pkgdir" cmake --install build/src/thumbnailer
  rm -r "$pkgdir"/usr/share/{icons,doc}
}

package_marble-maps() {
  pkgdesc='OpenStreetMap Navigation'
  depends=(gcc-libs
           glibc
           kirigami2
           marble-common
           qt5-base
           qt5-declarative)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-maps
}
