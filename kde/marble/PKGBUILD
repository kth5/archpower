# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=marble
pkgname=(marble
         marble-behaim
         marble-common
         marble-maps
         marble-qt)
pkgver=25.08.1
pkgrel=2
pkgdesc='Desktop Globe'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://apps.kde.org/marble/'
license=(GPL-2.0-or-later)
makedepends=(extra-cmake-modules
             gpsd
             kdoctools
             knewstuff
             kparts
             krunner
             libplasma
             libwlocate
             phonon-qt6
             protobuf
             qt6-serialport
             qt6-tools
             shapelib)
makedepends_x86_64=(
             qt6-webengine
)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgbase-$pkgver.tar.xz{,.sig})
sha256sums=('18cd1c221ce5b916c10d828ce084219c15e2806a376f53f9970fb6682d72f51b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_CXX_STANDARD=17 \
    -DQT_PLUGINS_DIR=lib/qt6/plugins \
    -DBUILD_QT_AND_KDE=ON \
    -DBUILD_TESTING=OFF \
    -DBUILD_TOUCH=ON \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DMOBILE=OFF
  cmake --build build
}

package_marble-common() {
  pkgdesc='Common libraries and plugins for Marble'
  depends=(abseil-cpp
           gcc-libs
           glibc
           phonon-qt6
           protobuf
           qt6-5compat
           qt6-base
           qt6-declarative
           qt6-positioning
           qt6-svg
           qt6-webchannel
           zlib)
  depends_x86_64=(
           qt6-webengine
  )
  optdepends=('gpsd: GPS based geolocation'
              'libwlocate: WLAN based geolocation'
              'qt6-serialport: APRS plugin'
              'shapelib: SHP plugin')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/share/{config.kcfg,kxmlgui5,metainfo} \
        "$pkgdir"/usr/bin \
        "$pkgdir"/usr/lib/qt6/plugins/{kf6,*.so} \
        "$pkgdir"/usr/share/applications/{marble_geo.desktop,marble_worldwind.desktop,org.kde.marble*.desktop,marble_thumbnail_*.desktop} \
        "$pkgdir"/usr/share/icons/hicolor/scalable/apps/org.kde.marble*.svg \
        "$pkgdir"/usr/share/plasma
}

package_marble-qt() {
  pkgdesc+=' (Qt version)'
  depends=(gcc-libs
           glibc
           marble-common
           qt6-base)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-qt
}

package_marble() {
  depends=(gcc-libs
           glibc
           kconfig
           kconfigwidgets
           kcoreaddons
           kcrash
           ki18n
           kio
           kparts
           kwidgetsaddons
           kxmlgui
           marble-common
           qt6-base)
  groups=(kde-applications
          kde-education)

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-kde
  DESTDIR="$pkgdir" cmake --install build/src/plasma
  DESTDIR="$pkgdir" cmake --install build/src/plasmarunner
  DESTDIR="$pkgdir" cmake --install build/src/thumbnailer
  rm -r "$pkgdir"/usr/share/{icons,doc,locale}
}

package_marble-maps() {
  pkgdesc='OpenStreetMap Navigation'
  depends=(gcc-libs
           glibc
           kconfig
           kcoreaddons
           kcrash
           ki18n
           kirigami
           kirigami-addons
           marble-common
           qt6-base
           qt6-declarative
  )
  depends_x86_64=(
           qt6-webengine
  )

  DESTDIR="$pkgdir" cmake --install build/src/apps/marble-maps
}

package_marble-behaim() {
  pkgdesc='Behaim Globe'
  depends=(gcc-libs
           glibc
           kcoreaddons
           kcrash
           ki18n
           kirigami-addons
           marble-common
           qt6-base
           qt6-declarative
  )
  depends_x86_64=(
           qt6-webengine
  )

  DESTDIR="$pkgdir" cmake --install build/src/apps/behaim
}
