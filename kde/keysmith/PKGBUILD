# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=keysmith
pkgver=25.08.0
pkgrel=1
pkgdesc='OTP client for Plasma Mobile and Desktop'
url='https://apps.kde.org/keysmith/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kdbusaddons
         ki18n
         kirigami
         kirigami-addons
         kwindowsystem
         libsodium
         openssl
         prison
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-multimedia)
makedepends=(extra-cmake-modules
             python)
groups=(kde-applications
        kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0e5f21ebffb21856e22dfd2fa961f5d14c5c565a88002a32ce1f4117bad60987'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
