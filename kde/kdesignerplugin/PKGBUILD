# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin
pkgver=5.108.0
pkgrel=1
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kconfig kcoreaddons)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
groups=(kf5-aids)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4bf9b9072aa1bd823accadfeb09409516da001d2383c55c43a94abe89f0e6d45'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
