# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kguiaddons
pkgname=${_name}5
pkgver=5.115.0
pkgrel=2
pkgdesc='Addons to QtGui'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-x11extras qt5-wayland)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc plasma-wayland-protocols)
groups=(kf5)
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('4f6dda36ea182c8f9f7a703301cea8258b094d7b9254f42ad70dedbeb73ff262'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -DBUILD_GEO_SCHEME_HANDLER=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
