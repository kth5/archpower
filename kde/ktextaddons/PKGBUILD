# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ktextaddons
pkgver=1.8.0
pkgrel=2
pkgdesc='Various text handling addons'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-or-later)
url='https://invent.kde.org/libraries/ktextaddons'
depends=(gcc-libs
         glibc
         karchive
         kcolorscheme
         kconfig
         kcoreaddons
         ki18n
         kio
         kitemviews
         ktextwidgets
         kwidgetsaddons
         qt6-base
         qt6-speech
         qtkeychain-qt6
         sonnet
         syntax-highlighting)
makedepends=(extra-cmake-modules
             qt6-tools)
optdepends=('languagetool: Grammar checking'
            'grammalecte: French grammar checking'
            'libreoffice: Use autocorrection data from LibreOffice')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('159c605d3d031bf818e164ea410150103c5f9f87cea35e2979e42d86c3318c99'
            'SKIP')
validpgpkeys=(90A774939A973FF1ECC827788FFE4352ED54BB8A) # laurent Montel <montel@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_DESIGNERPLUGIN=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
