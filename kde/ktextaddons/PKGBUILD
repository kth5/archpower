# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ktextaddons
pkgver=1.5.0
pkgrel=1
pkgdesc='Various text handling addons'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL)
url='https://invent.kde.org/libraries/ktextaddons'
depends=(kio sonnet syntax-highlighting qtkeychain-qt5 qt5-speech)
makedepends=(extra-cmake-modules doxygen qt5-tools)
optdepends=('languagetool: Grammar checking'
            'grammalecte: French grammar checking'
            'libreoffice: Use autocorrection data from LibreOffice')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('68365b3832ef258df66650250746ba26be149afae36dde3e23c7fc33303ac765')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_DESIGNERPLUGIN=ON \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
