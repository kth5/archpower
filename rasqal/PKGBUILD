# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lawrence Lee <valheru@facticius.net>

pkgname=rasqal
pkgver=0.9.33
pkgrel=7
epoch=1
pkgdesc="A library handling RDF query syntaxes, construction and execution"
license=('Apache-2.0 OR GPL-2.0-or-later OR LGPL-2.1-or-later')
url="https://librdf.org/rasqal"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(
  glibc
  libgcrypt
  mpfr
  pcre2
  sh
)
makedepends=(
  gtk-doc
  raptor
  util-linux
)
provides=(librasqal.so)
source=(
  https://download.librdf.org/source/$pkgname-$pkgver.tar.gz{,.asc}
  0001-Add-support-for-PCRE-V2-and-prefer-it.patch
)
sha512sums=('05728682797470db9e51d156012e8fde9dec1554d107372faa11cbe6cdc3356e92386f4f8de6d7c41e3100b76f9b1c6809102a913829cddbd2ff29043c04d522'
            'SKIP'
            '2d2a56251963cadea18822ba42da03cbbe58d19952420563de862d95393ba5987f811cbb409bc26981c3b5c33714a303d5947273fc3f0b569ed010785cf7d3f5')
b2sums=('87bca86b2f0dceb0801f5a34dae9ae1f87d8a7d0dc1e03fdf04998a0d4885ed76be2719d3e01489a5510a26715bb093a75ce194cc42f3cedff88f64161d6a2fb'
        'SKIP'
        'e25677586a5c5988567887811413ad3e4255a26228545fa642313b37bead61be265bd7b0705c75b0644002f78a5f837185a711ea3f332ec5a44f3bfd4929d883')
validpgpkeys=('F879F0DEDA780198DD08DC6443EC92504F71955A') # Dave Beckett <dave@dajobe.org>

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../0001-Add-support-for-PCRE-V2-and-prefer-it.patch
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --disable-static
    --enable-release
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    raptor libraptor2.so
    util-linux-libs libuuid.so
  )

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{ChangeLog*,README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
}
