# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=remmina
epoch=1
pkgver=1.4.33
pkgrel=3
pkgdesc="remote desktop client written in GTK+"
url="https://www.remmina.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
depends=('avahi' 'libgcrypt' 'libssh' 'vte3' 'libsodium' 'libayatana-appindicator' 'libsoup3')
makedepends=('cmake' 'freerdp' 'libvncserver' 'spice-gtk' 'spice-protocol'
             'harfbuzz' 'xorgproto' 'gobject-introspection' 'kwallet5'
             'webkit2gtk-4.1' 'gtk-vnc' 'ninja')
optdepends=('freerdp: RDP plugin'
            'libsecret: Secret plugin'
            'libvncserver: VNC plugin'
            'spice-gtk: Spice plugin'
#            'pyhoca-cli: X2Go plugin'
            'webkit2gtk-4.1: WWW plugin'
            'gtk-vnc: GVNC plugin'
            'kwallet5: kwallet plugin'
            'gnome-terminal: external tools')
replaces=('remmina-plugins')
provides=('remmina-plugins')
source=("$pkgname-$pkgver.tar.bz2::https://gitlab.com/Remmina/Remmina/-/archive/v.${pkgver/rc/-rc}/Remmina-v.${pkgver/rc/-rc}.tar.bz2")
sha512sums=('864281ab65bff30098cc40e572d7d10190c59393dd673e23aa0c24956fef51d60bbb048c32a675c6bf405257b6ae7812c071a7bf207fcdf0e045ef6a7294efc7')

build() {
  cmake -S Remmina-v.${pkgver/rc/-rc} -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=ON \
    -DWITH_NEWS=OFF \
    -DWITH_KF5WALLET=ON \
    -DWITH_X2GO=ON \
    -DWITH_GVNC=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
