# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=virt-manager
pkgname=(virt-install virt-manager)
pkgver=5.0.0
pkgrel=1
arch=(any)
url='https://virt-manager.org/'
license=('GPL-2.0-only')
makedepends=(
  git
  gtk-update-icon-cache
  meson
  python-docutils
  python-pycodestyle
  python-pylint
  python-pytest
  python-setuptools
)
optdepends=(
  'x11-ssh-askpass: provide password for remote machines connected via ssh tunnel'
)
source=("git+https://github.com/virt-manager/virt-manager.git#tag=v${pkgver}")
b2sums=('84564af1193f10cf530e9dd0adfc6b29b39eccf92f6ffc35550cc9fd4a9390f34e661b9dca6d89015c84beaac0d588d454a4ec942bc64859f4d50756f12f0c47')

build() {
  local meson_options=(
    -D default-hvs=qemu,lxc
  )

  arch-meson ${pkgbase} build "${meson_options[@]}"
  meson compile -C build
}

package_virt-install() {
  pkgdesc='Command line tool for creating new KVM , Xen, or Linux container guests using the libvirt hypervisor'
  depends=(
    cpio
    libisoburn
    libosinfo
    libvirt-python
    python-gobject
    python-requests
  )

  meson install -C build --destdir "${pkgdir}"

  # Split virt-manager
  [[ -d "${srcdir}"/virt-manager ]] && rm -r "${srcdir}"/virt-manager/
  mkdir -p "${srcdir}"/split/usr/{bin,share/{man/man1,virt-manager}}
  mv "${pkgdir}"/usr/bin/virt-manager "${srcdir}"/split/usr/bin/
  mv "${pkgdir}"/usr/share/{applications,glib-2.0,icons,metainfo} "${srcdir}"/split/usr/share/
  mv "${pkgdir}"/usr/share/man/man1/virt-manager.1 "${srcdir}"/split/usr/share/man/man1/
  mv "${pkgdir}"/usr/share/virt-manager/{icons,ui,virtManager} "${srcdir}"/split/usr/share/virt-manager/
}

package_virt-manager() {
  pkgdesc='Desktop user interface for managing virtual machines'
  depends=(
    "virt-install=${pkgver}"
    gtk-update-icon-cache
    gtk-vnc
    libvirt-glib
    spice-gtk
    vte3
    python-cairo
    gtksourceview4
  )

  mv -v split/* "${pkgdir}/"
}
