# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=json-glib
pkgname=(
  json-glib
  json-glib-docs
)
pkgver=1.8.0
pkgrel=2
pkgdesc="JSON library built on GLib"
url="https://gnome.pages.gitlab.gnome.org/json-glib/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
)
makedepends=(
  docbook-xsl
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
)
source=("git+https://gitlab.gnome.org/GNOME/json-glib.git?signed#tag=$pkgver-actual")
b2sums=('755b85f1deda24188d421debbf32563d47873228d2115cf86ffb46695d8dec8ed2e4f627489841d04a8625db73e40861dc29d18b1af2c5b5de5a4620d3a25ecc')
validpgpkeys=(
  53EF3DC3B63E2899271BD26322E8091EEA11BBB7 # Emmanuele Bassi <ebassi@gnome.org>
)

prepare() {
  cd json-glib
}

build() {
  local meson_options=(
    -D man=true
  )

  arch-meson json-glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_json-glib() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libjson-glib-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  rm -r "$pkgdir"/usr/{lib,share}/installed-tests
}

package_json-glib-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
