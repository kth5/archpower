# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion0.14
pkgver=0.14.1
pkgrel=2
pkgdesc="A general purpose formula parser & interpreter"
arch=(x86_64 powerpc64le)
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python')
makedepends=('boost' 'mdds14' 'spdlog')
conflicts=('libixion')
source=("https://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz")
sha256sums=('6ad1384fcf813083c6d981a16b2643c953f9bac4c2caf1ed1682921d9b69ed91')

build() {
  cd libixion-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
  make
}

check() {
  cd libixion-${pkgver}
  make -k check
}

package() {
  cd libixion-${pkgver}
  make DESTDIR="${pkgdir}" install
  # LICENSE file is missing in tarball
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
