# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=avahi
pkgver=0.8+r139+ge5b0bad
pkgrel=1
epoch=1
pkgdesc="Service Discovery for Linux using mDNS/DNS-SD (compatible with Bonjour)"
url="https://github.com/lathiat/avahi"
license=(LGPL)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(
  dbus
  expat
  gdbm
  glib2
  libcap
  libdaemon
)
makedepends=(
  doxygen
  git
  gobject-introspection
  graphviz
  gtk3
  libevent
  dbus-python
  python-gobject
  qt5-base
  xmltoman
)
optdepends=(
  'gtk3: avahi-discover, avahi-discover-standalone, bshell, bssh, bvnc'
  'libevent: libevent bindings'
  'nss-mdns: NSS support for mDNS'
  'python-dbus: avahi-bookmarks, avahi-discover'
  'python-gobject: avahi-bookmarks, avahi-discover'
  'python-twisted: avahi-bookmarks'
  'qt5-base: qt5 bindings'
)
provides=(
  libavahi-{client,common,core,glib,gobject,libevent,qt5,ui-gtk3}.so
  libdns_sd.so
)
backup=(
  etc/avahi/{hosts,avahi-daemon.conf,avahi-{autoip,dnsconf}d.action}
  usr/lib/avahi/service-types.db
)
_commit=e5b0badfc725b7fa9e6069b6a3d5f278d1b96113  # master
source=(
  "git+https://github.com/lathiat/avahi#commit=$_commit"
  0001-Fix-avahi-browse-Invalid-service-type.patch
  0002-HACK-Install-fixes.patch
)
b2sums=('SKIP'
        '79a881a7f45b4d6061285257b07aa0f72aa69cedc636c74ec12dd031372b3560b4013f18d821e31b323fd73af5de811a1f5ba94ba0e32ce1b1bb940af3bad54f'
        'd5bbef78b13008a006d23730a86039b09b78e468961c3fbeddf42578a7d871a1ec12e46a3b85591f4d2d4eb7f64e9021f9c9bec7c2959ad523737f1a142269a7')

pkgver() {
  cd avahi
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd avahi

  # https://bugs.archlinux.org/task/71781
  # https://github.com/lathiat/avahi/issues/212
  git apply -3 ../0001-Fix-avahi-browse-Invalid-service-type.patch

  # https://bugs.archlinux.org/task/47822
  git apply -3 ../0002-HACK-Install-fixes.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --sbindir=/usr/bin
    --disable-mono
    --enable-compat-libdns_sd
    --with-autoipd-group=avahi
    --with-autoipd-user=avahi
    --with-avahi-priv-access-group=network
    --with-distro=archlinux
    --with-systemdsystemunitdir=/usr/lib/systemd/system
    with_dbus_sys=/usr/share/dbus-1/system.d
  )

  cd avahi

  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(libdbus-1.so)

  cd avahi
  make DESTDIR="$pkgdir" install

  echo 'u avahi - "Avahi mDNS/DNS-SD daemon"' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}

# vim:set sw=2 sts=-1 et:
