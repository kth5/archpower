# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=pahole
pkgname=(
  pahole
  ostra-cg
)
pkgver=1.30
pkgrel=2
epoch=1
pkgdesc="Type info tools"
url="https://git.kernel.org/pub/scm/devel/pahole/pahole.git"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-2.0-only)
depends=(
  bash
  glibc
  libelf
  zlib
)
makedepends=(
  cmake
  git
  ninja
  python
  python-matplotlib
)
source=(
  "git+$url?signed#tag=v$pkgver"
  "git+https://github.com/libbpf/libbpf"
  0001-CMakeLists.txt-Install-ostra.py-into-Python3_SITELIB.patch
)
b2sums=('69bdc8cbc211a239f5f8e50ad63a8106cc58936ebd9c4c440dbaee7c22190189fa132147e0dd2f5fb81e0f07ca1a1c7e9a0691fe985eef12aeb43ea7d94cf4b4'
        'SKIP'
        'f299bb705066a05f5390e07475cec5ab4184aa6d5e1e290c0fb73d50a6e60d5e4729eba59748c1ead388338e2010220a3054db737415f7b2e4bdf7362f4a4745')
validpgpkeys=(
  2DBF5BAA46FB4DED338A335BD65016F35352AA40  # Arnaldo Carvalho de Melo <acme@kernel.org>
)

prepare() {
  cd pahole

  # Fix linux 6.15 almost doubling in size
  git cherry-pick -n 042d73962d35fdd1466e056f1ea14590b1cdbb9b

  # https://bugs.archlinux.org/task/70013
  git apply -3 ../0001-CMakeLists.txt-Install-ostra.py-into-Python3_SITELIB.patch

  git submodule init
  git submodule set-url lib/bpf "$srcdir/libbpf"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D GIT_SUBMODULE=OFF
    -D LIBBPF_EMBEDDED=ON
  )

  cmake -S pahole -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_pahole() {
  optdepends=('ostra-cg: Generate call graphs from encoded traces')
  provides=(libdwarves{,_emit,_reorganize}.so)

  DESTDIR="$pkgdir" cmake --install build

  _pick ostra "$pkgdir"/usr/{bin/ostra-cg,lib/python*}
}

package_ostra-cg() {
  pkgdesc="Generate call graphs from encoded traces"
  depends=(
    pahole
    python
    python-matplotlib
  )

  mv ostra/* "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim:set sw=2 sts=-1 et:
