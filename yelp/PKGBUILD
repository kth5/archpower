# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=yelp
pkgver=42.3
pkgrel=1
pkgdesc="Get help with GNOME"
url="https://apps.gnome.org/Yelp"
license=(GPL-2.0-or-later)
arch=(x86_64 powerpc powerpc64 powerpc64le espresso)
depends=(
  bzip2
  dconf
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libhandy
  libxml2
  libxslt
  sqlite
  webkit2gtk-4.1
  xz
  yelp-xsl
)
makedepends=(
  git
  glib2-devel
  itstool
  meson
)
optdepends=('man-db: View manual pages')
provides=(libyelp.so)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/yelp.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('e571f5ab96e2b5ce78c5cdc9c835441e1bc10bcfc81d65c5d7b82209a50094662c4fe07f2eb732c60e15fa4172d7141026d4cecbe47897003ad5989239a02ed1')

prepare() {
  cd yelp
}

build() {
  arch-meson yelp build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
