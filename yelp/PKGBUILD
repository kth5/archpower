# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=yelp
pkgver=49.0
pkgrel=1
pkgdesc="Get help with GNOME"
url="https://apps.gnome.org/Yelp"
license=(GPL-2.0-or-later)
arch=(x86_64 powerpc powerpc64 powerpc64le espresso)
depends=(
  bzip2
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libxml2
  libxslt
  sqlite
  webkitgtk-6.0
  xz
  yelp-xsl
)
makedepends=(
  git
  glib2-devel
  itstool
  meson
)
optdepends=('man-db: View manual pages')
provides=(libyelp-1.so)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/yelp.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('d890d410f15d6927a2392acff57a20bdcc970c9bf088e77cedbd0364d0ab3efcedc2ca38e6cb8037b0f768ba0bf5082e34a57da0b923451bf618d54fb9cb7c9c')

prepare() {
  cd yelp
}

build() {
  arch-meson yelp build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
