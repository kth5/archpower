# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.34.0
pkgrel=1
pkgdesc="Documentation tool for public library API"
url="https://www.gtk.org/gtk-doc/"
arch=(any)
license=(
  GFDL-1.1-or-later
  GPL-2.0-or-later
)
depends=(
  docbook-xml
  docbook-xsl
  glib2-docs
  python-lxml
  python-pygments
)
makedepends=(
  dblatex
  git
  meson
  yelp-tools
)
checkdepends=(
  bc
  gtk3
  python-parameterized
)
optdepends=('dblatex: PDF support')
_commit=b7c8317bf023b5116250b4c5381737d5b9786976  # tags/1.34.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk-doc.git#commit=$_commit"
  0001-Fix-tests.patch
)
b2sums=('SKIP'
        'aec43a320f2c687a8c3529eb0b396dcf318b08fec8dcae06d3c2ab0ddeed04e1d324bac158cc15a50ddae768b42a815742a86c24917b295f3c3f6f2d7ad225c7')

pkgver() {
  cd gtk-doc
  git describe --tags | sed 's/GTK_DOC_//;s/_/\./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtk-doc

  # test-gobject-mkhtml: ../xml/object.xml:139: element include: XInclude error : could not load ../../examples/gobject.c, and no fallback was found
  git apply -3 ../0001-Fix-tests.patch
}

build() {
  arch-meson gtk-doc build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
