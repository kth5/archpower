# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.34.0
pkgrel=2
pkgdesc="Documentation tool for public library API"
url="https://www.gtk.org/gtk-doc/"
arch=(any)
license=(
  GFDL-1.1-or-later
  GPL-2.0-or-later
)
depends=(
  docbook-xml
  docbook-xsl
  glib2-docs
  python
  python-lxml
  python-pygments
  sh
)
makedepends=(
  dblatex
  git
  meson
  yelp-tools
)
checkdepends=(
  bc
  gtk3
  python-parameterized
)
optdepends=(
  'dblatex: PDF support'
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gtk-doc.git#tag=$pkgver"
  0001-Fix-tests.patch
  0002-Fix-syntax-warnings.patch
)
b2sums=('3b2b55a85e5ab4b168bb322667a7552bc3ee6c21e05309e498a1768b36a86ce267bd492ff5d1d22fc829b7625a5b93a045177f72c56e04d6563c6c9bc464ef44'
        'aec43a320f2c687a8c3529eb0b396dcf318b08fec8dcae06d3c2ab0ddeed04e1d324bac158cc15a50ddae768b42a815742a86c24917b295f3c3f6f2d7ad225c7'
        '5a096a4dfd3b1ee5bd3d60606d5714fc8baefb08f12e846b25dc5235b89f3ed22d2e20ae45c36f4cfd73c9b1ae38c097884646653e4e5ae477164321968a9532')

prepare() {
  cd gtk-doc

  # test-gobject-mkhtml: ../xml/object.xml:139: element include: XInclude error : could not load ../../examples/gobject.c, and no fallback was found
  git apply -3 ../0001-Fix-tests.patch

  # SyntaxWarning: invalid escape sequence '\s'
  git apply -3 ../0002-Fix-syntax-warnings.patch
}

build() {
  arch-meson gtk-doc build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
