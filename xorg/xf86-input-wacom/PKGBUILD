# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=xf86-input-wacom
pkgver=1.2.2
pkgrel=1
pkgdesc="X.Org Wacom tablet driver"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/linuxwacom/xf86-input-wacom"
license=('GPL-2.0-or-later')
depends=('libxi' 'libxinerama' 'libxrandr' 'systemd-libs' 'libx11' 'glibc')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=24.4' 'meson'
            'gobject-introspection'
            # for tests
            'python-libevdev' 'python-pytest' 'python-yaml' 'python-gobject' 'python-attrs')
conflicts=('xorg-server<21.1.1' 'X-ABI-XINPUT_VERSION<24' 'X-ABI-XINPUT_VERSION>=25')
source=(https://github.com/linuxwacom/xf86-input-wacom/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.bz2{,.sig})
# check at https://github.com/linuxwacom/xf86-input-wacom/releases
sha256sums=('70771033543097e6c616a39ae8bb40fd0e991a25560ed10c65b33756c3061511'
            'SKIP')
#validpgpkeys=('9A12ECCC5383CA2AF5B42CDCA6DC66911B2127D5') # Jason Gerecke <killertofu@gmail.com>
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') #  "Peter Hutterer (Who-T) <office@who-t.net>"
#validpgpkeys=('5222AA87620F928D2C16F62BDB4ABF7C3424190B') # "Aaron Armstrong Skomra <skomra@gmail.com>"
#validpgpkeys=('FBE078781106933D3DDCF93E5B4EA609784983CA') # "Jason Gerecke <jason.gerecke@wacom.com>"

build() {
  arch-meson $pkgname-$pkgver build \
    -D xorg-conf-dir=/usr/share/X11/xorg.conf.d/ \
    -D systemd-unit-dir=/usr/lib/systemd/system/ \
    -D unittests=enabled

  # Print config
  meson configure build
  ninja -C build
}

check() {
  meson test -C build # || /bin/true
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
