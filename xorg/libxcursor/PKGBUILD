# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxcursor
pkgver=1.2.3
pkgrel=1
pkgdesc="X cursor management library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.freedesktop.org/xorg/lib/libxcursor"
license=('HPND-sell-variant')
depends=('libxfixes' 'libxrender' 'default-cursors' 'libx11' 'glibc' 'xorgproto')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org//releases/individual/lib/libXcursor-${pkgver}.tar.xz{,.sig})
sha512sums=('069a1eb27a0ee1b29b251bb6c2d0688543a791d6862fad643279e86736e1c12ca6fc02b85b8611c225a9735dc00efab84672d42b547baa97304362f0c5ae0b5a'
            'SKIP')
# validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8') # "Matthieu Herrb <matthieu.herrb@laas.fr>"
# validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # "Thomas E. Dickey (self-signed w/o SHA1) <dickey@invisible-island.net>"

build() {
  cd libXcursor-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXcursor-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
