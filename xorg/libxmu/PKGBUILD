# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxmu
pkgver=1.2.1
pkgrel=1
pkgdesc="X11 miscellaneous micro-utility library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.freedesktop.org/xorg/lib/libxmu"
depends=('libxext' 'libxt' 'libx11' 'xorgproto' 'glibc')
makedepends=('xorg-util-macros')
license=('MIT-open-group AND SMLNJ AND X11 AND ISC')
source=(https://xorg.freedesktop.org//archive/individual/lib/libXmu-${pkgver}.tar.xz{,.sig})
sha512sums=('05a30e5961e00e940ad7ec4eb94ec482b5cbe808585972bf70c849333af3fddf4ae4bd87895d706507fb3a94ed0fb73ea161e0e27dc2840323a1a1af49c0e8a3'
            'SKIP')
#validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  cd libXmu-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static
  make
}

package() {
  cd libXmu-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
