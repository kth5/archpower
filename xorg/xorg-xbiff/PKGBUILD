# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xbiff
pkgver=1.0.5
pkgrel=1
pkgdesc="Watch mailboxes for new message delivery"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://xorg.freedesktop.org/"
license=('X11')
depends=('libxaw' 'libxext' 'libxmu' 'libx11' 'libxt' 'glibc')
makedepends=('xorg-util-macros' 'xbitmaps')
source=(https://xorg.freedesktop.org/archive/individual/app/xbiff-${pkgver}.tar.xz{,.sig})
sha512sums=('4dd8943daf670dffb6e108b192977f6ad313d6b0535da0c809699e05f0509aba45be81c72af341e1fef31f59d0a5f209d87441230b3fdcecb4a7a8fe7a9c0658'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "xbiff-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "xbiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
