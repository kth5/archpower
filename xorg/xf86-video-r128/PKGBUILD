# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-r128
pkgver=6.13.0
pkgrel=4.1
pkgdesc="X.org ati Rage128 video driver"
arch=(i686 x86_64 powerpc64 powerpc)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=25.2')
optdepends=('r128-dri: DRI1 support from mesa-7.11')
groups=('xorg-drivers')
install=install
source=(https://gitlab.freedesktop.org/xorg/driver/$pkgname/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.bz2
        0001-Fix-output-detection-on-non-x86-and-allow-override.patch
        0002-Disable-Int10-support-on-all-non-x86-arches.patch
        ibookg3.conf.recommendation
        imacg3.conf.recommendation)
sha256sums=('47e475dc9a2c78845e9ea953b0e628baef5abd811578690146f641b3bf30d174'
            '00ac881ba255fdc0b5961781ac232d3f30f7dc97185cb1f6df5a953bbcd2b730'
            '9ae8a32b7958559d7c4e960fc8dd1cca8b9e6e5d0b70c797b9e6040207c98fb2'
            '05336939ecb519cfaba1d20faa254b855d67ee6e566ebac1418c0d0ee5f7f725'
            '8e11aba49d86468a2ea8df95ef4269efcf2e2d92bfc1a72d5c89a3fd54c467d1')
#validpgpkeys=('6EA3F3F3B9082632A9CBE931D53A0445B47A0DAB') # Connor Behan <connor.behan@gmail.com>

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/0001-Fix-output-detection-on-non-x86-and-allow-override.patch
  patch -Np1 -i ${srcdir}/0002-Disable-Int10-support-on-all-non-x86-arches.patch
}

build() {
  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/-Wl,-z,now}
  export LDFLAGS=${LDFLAGS/,-z,now}

  cd ${pkgname}-${pkgname}-${pkgver}
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 ${srcdir}/ibookg3.conf.recommendation "${pkgdir}/etc/X11/xorg.conf.d/imacg3.conf.recommendation"
  install -m644 ${srcdir}/imacg3.conf.recommendation "${pkgdir}/etc/X11/xorg.conf.d/imacg3.conf.recommendation"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
