# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xfd
pkgver=1.1.5
pkgrel=1
pkgdesc="Displays all the characters in a font using either the X11 core protocol or libXft2"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://xorg.freedesktop.org/"
license=('MIT-open-group')
depends=('libxaw' 'fontconfig' 'libxft' 'libxrender' 'libxmu' 'libxkbfile'
         'libx11' 'libxt' 'glibc')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xfd-${pkgver}.tar.xz{,.sig})
sha512sums=('6a8838e90c4751ae357832da7856d65d2cc65f3626655e24d22b25e7623f1a47d41dea0d093c0f250ceee2651214a746b3e7bd7f152c5b1718ce28aa5f217fa4'
            'SKIP')
validpgpkeys=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xfd-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xfd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
