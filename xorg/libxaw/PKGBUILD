# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxaw
pkgver=1.0.16
pkgrel=1
pkgdesc="X11 Athena Widget library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MIT-open-group'
         'X11'
         'HPND'
         'HPND-sell-variant'
         'SMLNJ'
         'NTP')
url="https://gitlab.freedesktop.org/xorg/lib/libxaw"
depends=('libxmu' 'libxpm' 'libx11' 'libxt' 'libxext' 'xorgproto' 'glibc')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org//releases/individual/lib/libXaw-${pkgver}.tar.xz{,.sig})
sha512sums=('1cabad1a9b06dec7de601b977e844536823ee1a0045244acc42ca6b6107da9b4953736d94898a96cab40845eab77dae21e20e1fa66a3dfc4dedf575dfbca04aa'
            'SKIP')
#validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>
#validpgpkeys+=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # "Thomas E. Dickey (self-signed w/o SHA1) <dickey@invisible-island.net>"

build() {
  cd libXaw-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXaw-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
