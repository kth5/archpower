# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-appres
pkgver=1.0.7
pkgrel=1
pkgdesc="List X application resource database"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.freedesktop.org/xorg/app/appres"
license=('MIT-open-group')
depends=('libxt' 'libx11' 'glibc')
makedepends=('xorg-util-macros')
conflicts=('xorg-res-utils')
source=(https://xorg.freedesktop.org/archive/individual/app/appres-${pkgver}.tar.xz{,.sig})
sha512sums=('5948f869124dd7160ffe109b0fc7ccdca45c3b3f551a382a0bcf2c2af9e3832b0d4451e2868ca93a38de8e8e2caf1946ac72936b3256e99a32437d0ddd937a60'
            'SKIP')
validpgpkeys+=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "${srcdir}/appres-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/appres-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
