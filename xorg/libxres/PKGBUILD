# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxres
pkgver=1.2.3
pkgrel=1
pkgdesc="X11 Resource extension library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://xorg.freedesktop.org"
license=('X11')
depends=('libxext' 'libx11' 'glibc' 'xorgproto')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXres-${pkgver}.tar.xz{,.sig})
sha512sums=('385dbc87bd4e0d306a1cad6b317d8431494cd2a381766bc3f9e6b7f488ff41ee1f4f25e756421f6a2b5681976d7da0108cea6305f7a34f7105d861cb6c1ae854'
            'SKIP')
#validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # "Keith Packard <keithp@keithp.com>"
validpgpkeys+=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "libXres-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "libXres-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
