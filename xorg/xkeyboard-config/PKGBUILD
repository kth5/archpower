# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xkeyboard-config
pkgver=2.46
pkgrel=1
pkgdesc="X keyboard configuration files"
arch=(any)
license=('LicenseRef-xkeyboard-config')
url="https://gitlab.freedesktop.org/xkeyboard-config/xkeyboard-config"
makedepends=('xorg-xkbcomp' 'libxslt' 'python' 'meson') # 'git')
checkdepends=('python-pytest' 'python-pytest-xdist' 'libxkbcommon')
provides=('xkbdata')
replaces=('xkbdata')
conflicts=('xkbdata')
# https://gitlab.freedesktop.org/xkeyboard-config/xkeyboard-config
source=(https://xorg.freedesktop.org/archive/individual/data/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        MR857.patch)
sha256sums=('10c58218fb60d08fb1f7b30304deb3ba47613195aa8a08a81f1972775ccc3640'
            'SKIP'
            '19ad8efa4b0f30f88fd2091a1f29795f9814bf27ea3106d49e4fac3a0121d2d6')
validpgpkeys=('FFB4CCD275AAA422F5F9808E0661D98FC933A145') # Sergey Udaltsov <sergey.udaltsov@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}
  # fix test failure with libxkbcommon <= 1.11.x
  patch -Np1 -i ../MR857.patch  
}

build() {
  arch-meson ${pkgname}-${pkgver} build \
    -D compat-rules=true \
    -D xorg-rules-symlinks=true

  # Print config
  meson configure build

  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}
 
package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}
