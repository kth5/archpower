# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-smproxy
pkgver=1.0.8
pkgrel=1
pkgdesc="Allows X applications that do not support X11R6 session management to participate in an X11R6 session"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://xorg.freedesktop.org/"
license=('MIT-open-group')
depends=('libsm' 'libxt' 'libxmu' 'libice' 'libx11' 'glibc')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/smproxy-${pkgver}.tar.xz{,.sig})
sha512sums=('e930a01c3535050d7d580a93c0b2b7898ff8a8d7ab498215f2d035ab902989497d1fbc70f2939d04b609c980e86d0d4ba7829a98fda3261cb4b159432cd2bc8e'
            'SKIP')
# validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2') # "Alan Coopersmith <alan.coopersmith@oracle.com>"  

build() {
  cd smproxy-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd smproxy-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
