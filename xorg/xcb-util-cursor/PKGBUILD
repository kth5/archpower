# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=xcb-util-cursor
pkgver=0.1.6
pkgrel=1
pkgdesc='XCB cursor library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://cgit.freedesktop.org/xcb/util-cursor'
license=('MIT')
depends=('libxcb' 'xcb-util-renderutil' 'xcb-util-image' 'glibc')
makedepends=('xorgproto')
#makedepends=('doxygen' 'xorg-util-macros' 'graphviz') # no docs yet to build
source=(https://xorg.freedesktop.org/archive/individual/lib/xcb-util-cursor-$pkgver.tar.xz{,.sig})
sha512sums=('5cdf2f942e3aad9662456cf6b5c2be2ff1837d73348046f6d8b72aeabf005c481a5f00ee169d9ccd18878697cb1744f0b274d0caa85955979c8e4f92d2cc3f72'
            'SKIP')
validpgpkeys=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

