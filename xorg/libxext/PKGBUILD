# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxext
pkgver=1.3.7
pkgrel=1
pkgdesc="X11 miscellaneous extensions library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://gitlab.freedesktop.org/xorg/lib/libxext"
license=('LicenseRef-libxext')
depends=('libx11' 'glibc' 'xorgproto')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/releases/individual/lib/libXext-${pkgver}.tar.xz{,.sig})
sha512sums=('09cd230da472e87e4fdbc9b0f83a9181cc44af04c06fa4a7d8aa405e0f8551d3ac3a4b379249c44d97e1025b60d1c52f8ca13817eed0206e2bf3d66a55d89701'
            'SKIP')
validpgpkeys=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd libXext-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXext-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
