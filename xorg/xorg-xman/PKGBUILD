# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xman
pkgver=1.2.0
pkgrel=1
pkgdesc="Unix manual page viewer"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://xorg.freedesktop.org/"
license=('X11')
depends=('libxaw' 'libxt' 'libx11' 'glibc')
makedepends=('xorg-util-macros' 'xorgproto' 'libxmu')
source=(https://xorg.freedesktop.org/archive/individual/app/xman-${pkgver}.tar.xz{,.sig})
sha512sums=('072dba172fd30606c1e185c6e50b3b9a9ec5e430eb2ac7f0c476e1c2de998e8c0b5fbf53de20a7339d39a8990255e77fe72d3928b43d055e8463f8069389d192'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xman-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xman-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
