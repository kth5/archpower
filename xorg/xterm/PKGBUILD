# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xterm
pkgver=397
pkgrel=2
pkgdesc="X Terminal Emulator"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://invisible-island.net/xterm/"
license=('custom')
depends=('libxft' 'libxaw' 'ncurses' 'luit' 'xbitmaps' 'libutempter' 'libxkbfile')
makedepends=('git')
optdepends=('xorg-mkfontscale: font scaling')
source=(${pkgname}::git+https://github.com/ThomasDickey/xterm-snapshots.git?signed#tag=xterm-${pkgver})
sha256sums=('2ab1723c2167c26169043d20f3e2746fa55b7105b373271ae12e61691f3ab2f9')
#validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # "Thomas Dickey <dickey@invisible-island.net>"
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # "Thomas E. Dickey (self-signed w/o SHA1) <dickey@invisible-island.net>"

build() {
  cd ${pkgname}
  ./configure --prefix=/usr \
      --libdir=/etc \
      --mandir=/usr/share/man \
      --with-app-defaults=/usr/share/X11/app-defaults/ \
      --with-x \
      --disable-full-tgetent \
      --disable-imake \
      --enable-ansi-color \
      --enable-88-color \
      --enable-256-color \
      --enable-broken-osc \
      --enable-broken-st \
      --enable-load-vt-fonts \
      --enable-i18n \
      --enable-wide-chars \
      --enable-doublechars \
      --enable-warnings \
      --enable-tcap-query \
      --enable-logging \
      --enable-dabbrev \
      --enable-freetype \
      --enable-luit \
      --enable-mini-luit \
      --enable-narrowproto \
      --enable-exec-xterm \
      --enable-sixel-graphics \
      --enable-regis-graphics \
      --enable-block-select \
      --with-tty-group=tty \
      --with-utempter
  make
}

check() {
  cd ${pkgname}
  make -k check
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  chmod 0755 "${pkgdir}/usr/bin/xterm"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m755 -d "${pkgdir}"/usr/share/applications
  install -m644 "${srcdir}"/${pkgname}/{xterm,uxterm}.desktop "${pkgdir}"/usr/share/applications/
}
