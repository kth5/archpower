# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xterm
pkgver=406
pkgrel=1
pkgdesc="X Terminal Emulator"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://invisible-island.net/xterm/"
license=('MIT AND X11 AND HPND AND CC-BY-4.0')
depends=(
  'fontconfig'
  'freetype2'
  'glibc'
  'libice'
  'libutempter'
  'libx11'
  'libxaw'
  'libxext'
  'libxft'
  'libxkbfile'
  'libxmu'
  'libxpm'
  'libxt'
  'luit'
  'ncurses'
  'xbitmaps'
)
makedepends=('git')
optdepends=('xorg-mkfontscale: font scaling')
source=("$pkgname::git+https://github.com/ThomasDickey/xterm-snapshots.git?signed#tag=xterm-$pkgver")
sha256sums=('1ae19266e4b054393fd1456176159093600e4c71c58e4897fe1dbf1548b73aac')
#validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # "Thomas Dickey <dickey@invisible-island.net>"
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # "Thomas E. Dickey (self-signed w/o SHA1) <dickey@invisible-island.net>"

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --libdir=/etc \
    --mandir=/usr/share/man \
    --with-app-defaults=/usr/share/X11/app-defaults/ \
    --with-x \
    --disable-full-tgetent \
    --disable-imake \
    --enable-ansi-color \
    --enable-88-color \
    --enable-256-color \
    --enable-broken-osc \
    --enable-broken-st \
    --enable-load-vt-fonts \
    --enable-i18n \
    --enable-wide-chars \
    --enable-doublechars \
    --enable-warnings \
    --enable-tcap-query \
    --enable-logging \
    --enable-dabbrev \
    --enable-freetype \
    --enable-luit \
    --enable-mini-luit \
    --enable-narrowproto \
    --enable-exec-xterm \
    --enable-sixel-graphics \
    --enable-regis-graphics \
    --enable-block-select \
    --with-tty-group=tty \
    --with-utempter
  sed -i '/^LIBS\s*=/ s/$/ -lfreetype/' Makefile
  make
}

check() {
  cd $pkgname
  make -k check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
  install -vDm644 -t "$pkgdir/usr/share/applications" {xterm,uxterm}.desktop
}
