# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: 
# Contributor: Alad Wenter <alad@archlinux.org>

pkgname=xsettingsd
pkgver=1.0.2
pkgrel=3
pkgdesc="Provides settings to X11 applications via the XSETTINGS specification"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://codeberg.org/derat/xsettingsd"
license=('custom:BSD')
depends=('libx11' 'gcc-libs')
makedepends=('cmake' 'git')
source=(git+https://codeberg.org/derat/xsettingsd.git#tag=v${pkgver})
sha256sums=('856635233137cc75f5435960252c3cb7f48422d1fc3d8049c06104fe01215996')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
