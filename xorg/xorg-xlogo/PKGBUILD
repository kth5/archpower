# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xlogo
pkgver=1.0.7
pkgrel=1
pkgdesc="Draw [old] X logo"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://xorg.freedesktop.org/"
license=('MIT-open-group')
depends=('libsm' 'libxaw' 'libxmu' 'libxt' 'libxext' 
         'libx11' 'libxrender' 'libxft' 'glibc')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xlogo-${pkgver}.tar.xz{,.sig})
sha512sums=('dfea0d42aeec7394c487e009c22ebb24af9501fa8144079d4fe1f32931cfad6bd1eb3830a1d211b9785a9f89fe22c61d4d4b6d6b881af42f3f916ccc5eb9ce66'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xlogo-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xlogo-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
