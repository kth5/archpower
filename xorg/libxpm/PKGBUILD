# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxpm
pkgver=3.5.18
pkgrel=1
pkgdesc="X11 pixmap library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('MIT' 'X11-distribute-modifications-variant')
url="https://xorg.freedesktop.org/"
depends=('libxt' 'libxext' 'libx11' 'glibc')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXpm-${pkgver}.tar.xz{,.sig})
sha512sums=('ced907c984cb9735948da82a69e33ef09421fee2e6f24178a2cca683a5425e48e3fa9d771bf82bc80b78e6dedf485d464563d91c0395a71f13502d0f7599f40e'
            'SKIP')
#validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>
#validpgpkeys+=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # "Peter Hutterer (Who-T) <office@who-t.net>"
validpgpkeys=('3AB285232C46AE43D8E192F4DAB0F78EA6E7E2D2') # Alan Coopersmith <alan.coopersmith@oracle.com>
#validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # Matt Turner <mattst88@gmail.com>

build() {
  cd libXpm-${pkgver}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --disable-open-zfile
  make
}

check() {
  cd libXpm-${pkgver}
  # 3 test fails due to disabling compression support from 3.5.15 on
  make check || /bin/true
}

package() {
  cd libXpm-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}
