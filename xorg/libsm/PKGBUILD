# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libsm
pkgver=1.2.5
pkgrel=1
pkgdesc="X11 Session Management library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MIT' 'MIT-open-group')
url="https://xorg.freedesktop.org/"
depends=('libice' 'util-linux-libs' 'xorgproto' 'glibc')
makedepends=('xorg-util-macros' 'xtrans')
source=(${url}/releases/individual/lib/libSM-${pkgver}.tar.xz{,.sig})
sha512sums=('f2e58503a7cd144a610621104b4727878dd5ba0cdf8220e72f27a3474c9b49c01dc514c60cbdfb35a9318f307b27a601580725cef0f6b5091d4c4ea57cef8c91'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alanc@freedesktop.org>

build() {
  cd libSM-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd libSM-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
