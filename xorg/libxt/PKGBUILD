# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxt
pkgver=1.3.1
pkgrel=1
pkgdesc="X11 toolkit intrinsics library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://xorg.freedesktop.org/"
license=('MIT'
         'HPND-sell-variant'
         'SMLNJ'
         'MIT-open-group'
         'X11')
depends=('libsm' 'libx11' 'libice' 'glibc' 'xorgproto')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXt-${pkgver}.tar.xz{,.sig})
sha512sums=('c220292f60b0f53134cf9364831a32bbaa9fa6bbb3a7143e917920957b7a48c616e946042747089f29ea9d8a18ecd64de620bcaf56d82462e7107de906f5db38'
            'SKIP')
# validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>
# validpgpkeys+=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # "Thomas Dickey <dickey@invisible-island.net>"
validpgpkeys+=('3BB639E56F861FA2E86505690FDD682D974CA72A') #"Matt Turner <mattst88@gmail.com>"

build() {
  cd libXt-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXt-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
