# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-font-util
pkgver=1.4.1
pkgrel=1
pkgdesc='X.Org font utilities'
url='https://xorg.freedesktop.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('custom')
makedepends=('xorg-util-macros')
depends=('glibc')
source=("${url}"/releases/individual/font/font-util-${pkgver}.tar.xz{,.sig})
sha512sums=('cec7fc38ba64ca9c81c59a8395da94d71c75e14f19faf6457da0a1a8c3c1cf51bcaab227834f6f5a6d274ba2557555e542dbe2bd88e527c45ea196318bbc38d0'
            'SKIP')
#validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>
replaces=('font-util')
provides=('font-util')
conflicts=('font-util')
groups=('xorg-fonts' 'xorg')

build() {
  cd font-util-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-mapdir=/usr/share/fonts/util \
    --with-fontrootdir=/usr/share/fonts
  make
}

package() {
  cd font-util-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
