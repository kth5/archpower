# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=xwallpaper
pkgver=0.7.5
pkgrel=1
pkgdesc='Wallpaper setting utility for X'
url='https://github.com/stoeckmann/xwallpaper'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(ISC)
depends=(
  glibc
  libjpeg-turbo
  libjpeg.so
  libpixman-1.so
  libpng
  libseccomp
  libseccomp.so
  libxcb
  libxpm
  pixman
  xcb-util
  xcb-util-image
)
source=(https://github.com/stoeckmann/xwallpaper/releases/download/v${pkgver}/xwallpaper-${pkgver}.tar.xz{,.sig})
sha512sums=('beb1689240b97f8dff0ee40c8dbee2158e824e567fde84059a8c4e37365f0c9827eb3eb0b82f89fab8a73fba9c54617deb1bdd6567493755be51e090ea614990'
            'SKIP')
b2sums=('17e659520565cf1e52276c367dce68b4aa1515a2c8119ae6541847e535572c090a598ca0bd6cb70b15e8c5109245c176c7205bc820f5c490d15a41855a2ef780'
        'SKIP')
validpgpkeys=('CDB24BD3BDDCBCBBAE5CB6207DB470F35B0B8B18') # Tobias Stoeckmann <tobias@stoeckmann.org>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-zshcompletiondir=/usr/share/zsh/site-functions
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
