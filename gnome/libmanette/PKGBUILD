# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libmanette
pkgver=0.2.9
pkgrel=1
pkgdesc='Simple GObject game controller library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gnome.pages.gitlab.gnome.org/libmanette/'
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libevdev
  libgudev
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=$pkgver")
b2sums=('8c8ea03efb067e91984dbecb54eaed747644eb675ecb7b98691b5b52915aa88ce96343e62328096d814282e769438944b04d2456c3e978cfa094ff813231f596')

build() {
  arch-meson $pkgname build \
    -D doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
