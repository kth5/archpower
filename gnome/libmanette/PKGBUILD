# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libmanette
pkgname=(
  libmanette
  libmanette-docs
)
pkgver=0.2.13
pkgrel=1
pkgdesc="Simple GObject game controller library"
url="https://gnome.pages.gitlab.gnome.org/libmanette/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  hidapi
  libevdev
  libgudev
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/libmanette.git#tag=$pkgver")
b2sums=('ba0faf503bc96520ab94e74ea7eebc60f2679dba9ba132883259f23246f51ba920aa0bcb8d17af5dbd8f745e392f13d715e520af6f9d7d294acc79a1f910573c')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libmanette() {
  provides=(libmanette-0.2.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libmanette-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
