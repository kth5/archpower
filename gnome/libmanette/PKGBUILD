# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libmanette
pkgver=0.2.7
pkgrel=1
pkgdesc='Simple GObject game controller library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gnome.pages.gitlab.gnome.org/libmanette/'
license=('LGPL-2.1-or-later')
depends=('gcc-libs' 'glib2' 'glibc' 'libevdev' 'libgudev')
makedepends=('gobject-introspection' 'meson' 'vala')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('cddd5c02a131072c19c6cde6f2cb2cd57eae7dacb50d14c337efd980baa51a51')

build() {
  arch-meson $pkgname-$pkgver build -D doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
