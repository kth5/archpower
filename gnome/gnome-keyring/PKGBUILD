# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring
pkgver=46.2
pkgrel=1
epoch=1
pkgdesc="Stores passwords and encryption keys"
url="https://gitlab.gnome.org/GNOME/gnome-keyring"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  dconf
  gcr
  gcr-4
  glib2
  glibc
  libgcrypt
  pam
  systemd-libs
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  p11-kit
  python
)
provides=(org.freedesktop.secrets)
groups=(gnome)
install=gnome-keyring.install
source=(
  "git+$url.git#tag=$pkgver"
  0001-daemon-Add-Cinnamon-to-autostart-files.patch
)
b2sums=('42253abd7684fc4f37ab644e0cabed0b4b4549e859f1a3cc81a0a97a0ddacb6dfa29dac0a926d0901be4fd2ad01352091aa19d1a2057a0116af062b8089b1ac7'
        '584832e529ee2be98b78b128ab4271fae919a194f2d9254526943da6baccfe5eb6deb17ee03cfb54261cade4be0e93ea7fbcc91ebc77ecfacd2f25d90f9e29c4')

prepare() {
  cd $pkgname

  # Autolaunch in Cinnamon
  git apply -3 ../0001-daemon-Add-Cinnamon-to-autostart-files.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --with-pam-dir=/usr/lib/security
    --without-libcap-ng
    --disable-static
    --disable-schemas-compile
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  # Secure memory tests fail
  dbus-run-session make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
