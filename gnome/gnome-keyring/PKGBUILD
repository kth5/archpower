# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring
pkgver=42.1
pkgrel=3
epoch=1
pkgdesc="Stores passwords and encryption keys"
url="https://wiki.gnome.org/Projects/GnomeKeyring"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(
  GPL
  LGPL
)
depends=(
  gcr
  gcr-4
  openssh
  pam
)
makedepends=(
  docbook-xsl
  git
  p11-kit
  python
)
provides=(org.freedesktop.secrets)
groups=(gnome)
install=gnome-keyring.install
_commit=9d562e1e359e3ce01b61f0f52c75d38b96dece7b  # tags/42.1^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-keyring.git#commit=$_commit"
  0001-build-Use-p11_module_configs-as-default-pkcs11-confi.patch
  0002-daemon-Add-Cinnamon-to-autostart-files.patch
)
b2sums=('SKIP'
        '42befc7c259f37263e765d73686494604e4fae7c5debd420e8b2f8585fea0563b468eeccb78ba96c6e20bb4c53ede2301b067386122764c819e7cff2e0a48b19'
        '2b7a9cb1af5d8e85624b97ab24f8333bc68828b22afdd2890a800e296887d706a8dea8792b0807f277cd17b10d83e760a6d2fd631f8ae5d5ceaa1eaf8b8121b4')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix pkcs11 config dir
  git apply -3 ../0001-build-Use-p11_module_configs-as-default-pkcs11-confi.patch

  # Autolaunch in Cinnamon
  git apply -3 ../0002-daemon-Add-Cinnamon-to-autostart-files.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --with-pam-dir=/usr/lib/security
    --without-libcap-ng
    --disable-static
    --disable-schemas-compile
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  # Secure memory tests fail
  dbus-run-session make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
