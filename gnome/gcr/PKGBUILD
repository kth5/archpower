# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gcr
pkgname=(
  gcr
  gcr-docs
)
pkgver=3.41.2
pkgrel=2
pkgdesc="A library for bits of crypto UI and parsing"
url="https://gitlab.gnome.org/GNOME/gcr"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libgcrypt
  libp11-kit
  libsecret
  openssh
  pango
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  libxslt
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/gcr.git?signed#tag=$pkgver")
b2sums=('8fee923174d0b350a924994d6fada63c2deac46859f62b09cd7adca971ab47ae4c5f82b69a03e203254da695f46f05457c9a081923010e1437dec035d28f07ed')
validpgpkeys=(
  EF3226243ABEFC1A828BC5B46A2AC2B52126D946 # Dhanuka Warusadura <dhanuka@gnome.org>
  A7C626E13F9AD776776BD9CA1D8A57CF2E8D36A3 # Niels De Graef (nielsdg) <nielsdegraef@gmail.com>
)

prepare() {
  cd gcr
}

build() {
  local meson_options=(
    # ssh-agent moved to gcr-4
    -D ssh_agent=false
  )

  arch-meson gcr build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # Secure memory tests fail
  dbus-run-session meson test -C build --print-errorlogs || :
}

package_gcr() {
  provides=(
    libgck-1.so
    libgcr-{base,ui}-3.so
  )
  install=gcr.install

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gcr-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
