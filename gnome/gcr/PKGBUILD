# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gcr
pkgname=(
  gcr
  gcr-docs
)
pkgver=3.41.2
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
url="https://gitlab.gnome.org/GNOME/gcr"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  gtk3
  libgcrypt
  libsecret
  openssh
  p11-kit
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  libxslt
  meson
  vala
)
_commit=cd63e8a0378e13ba0aa15c2a68d1070fa62e6532  # tags/3.41.2^0
source=("git+https://gitlab.gnome.org/GNOME/gcr.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd gcr
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gcr
}

build() {
  local meson_options=(
    # ssh-agent moved to gcr-4
    -D ssh_agent=false
  )

  arch-meson gcr build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # Secure memory tests fail
  dbus-run-session meson test -C build --print-errorlogs || :
}

package_gcr() {
  provides=(
    libgck-1.so
    libgcr-{base,ui}-3.so
  )
  install=gcr.install

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gcr-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
