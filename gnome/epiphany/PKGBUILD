# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=49.1
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://apps.gnome.org/Epiphany"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gmp
  gstreamer
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libarchive
  libportal
  libportal-gtk4
  libsecret
  libsoup3
  libxml2
  nettle
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  blueprint-compiler
  git
  glib2-devel
  gobject-introspection
  lsb-release
  meson
  python-docutils
  startup-notification
  yelp-tools
)
checkdepends=(
  aspell
  glibc-locales
  hspell
  hunspell
  libvoikko
  nuspell
  xorg-server-xvfb
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/epiphany.git#tag=${pkgver/[a-z]/.&}")
b2sums=('8dbbf18919dd7eb8a8b00e149e7fe6c5089b8d5d222bc72a5f890635d37d6170d46b77fcfb1c32e248f534c8444dbf6d18104ff64bf27df8cb0e63ba5a4c1760')

prepare() {
  cd epiphany
}

build() {
  arch-meson epiphany build
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  export LC_COLLATE=en_US.UTF-8
  export LIBGL_ALWAYS_SOFTWARE=1
  export WEBKIT_DISABLE_SANDBOX_THIS_IS_DANGEROUS=1

  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
