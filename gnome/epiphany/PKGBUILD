# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=47.2
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://apps.gnome.org/Epiphany"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gmp
  gstreamer
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libarchive
  libportal
  libportal-gtk4
  libsecret
  libsoup3
  libxml2
  nettle
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  docbook-xml
  git
  glib2-devel
  gobject-introspection
  lsb-release
  meson
  startup-notification
  yelp-tools
)
checkdepends=(
  aspell
  glibc-locales
  hspell
  hunspell
  libvoikko
  nuspell
  xorg-server-xvfb
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/epiphany.git#tag=${pkgver/[a-z]/.&}")
b2sums=('00c44c7d54c237e96c11e00c7d3bed3e6a6f3bf9067926d2805a52ac65e46783907c6cd4268c2cf52a385561f9e97b6e1db0ec6724dabdfea38ca700b86fc864')

prepare() {
  cd epiphany
}

build() {
  arch-meson epiphany build
  meson compile -C build
}

check() {
  export \
    XDG_RUNTIME_DIR="$PWD/runtime-dir" \
    LIBGL_ALWAYS_SOFTWARE=1 \
    WEBKIT_DISABLE_SANDBOX_THIS_IS_DANGEROUS=1
    LC_COLLATE=en_US.UTF-8

  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  local meson_tests=(
    'validate-desktop'
    'validate-appdata'
    'Embed shell test'
    'Embed utils test'
    'Encodings test'
    'File helpers test'
    'History test'
    'Location entry test'
    'Migration test'
    'Search engine manager test'
    'SQLite test'
    'String test'
    'URI helpers test'
    # 'Web view test'
    'Web extension test'
  )

  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs "${meson_tests[@]}"
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
