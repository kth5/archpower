# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gspell
pkgname=(
  gspell
  gspell-docs
)
pkgver=1.14.2
pkgrel=1
pkgdesc="Spell-checking library for GTK applications"
url="https://gitlab.gnome.org/GNOME/gspell"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  enchant
  glib2
  glibc
  gtk3
  icu
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  vala
)
checkdepends=(
  aspell
  hspell
  hunspell
  hunspell-en_us
  libvoikko
  nuspell
  xorg-server-xvfb
)
source=("git+https://gitlab.gnome.org/GNOME/gspell.git#tag=$pkgver")
b2sums=('9ef4f172703eb8455122949e29a69a3b1abca2e983a2c521e67935c053d2a10c5bf9f5c45b293daf03e126a25bc6bd609f27211f388cd34c50a2563db2059ce6')

prepare() {
  cd gspell
}

build() {
  local meson_options=(
    -D install_tests=false
  )

  arch-meson gspell build "${meson_options[@]}"
  meson compile -C build
}

check() {
  echo '*:hunspell' > enchant.ordering
  ENCHANT_CONFIG_DIR="$PWD" \
    xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_gspell(){
  depends+=(
    libenchant-2.so
    libgtk-3.so
    libg{lib,object,io}-2.0.so
    libicuuc.so
    libpango-1.0.so
  )
  provides+=("libgspell-${pkgver%%.*}.so")

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_gspell-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
