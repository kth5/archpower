# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=atkmm
pkgname=(
  atkmm
  atkmm-docs
)
pkgver=2.28.4
pkgrel=1
pkgdesc="C++ bindings for ATK"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  atk
  glibmm
)
makedepends=(
  git
  glibmm-docs
  meson
  mm-common
)
options=(!emptydirs)
_commit=b8bd942ebb5b2e12c7f80a873a6500d51307456d  # tags/2.28.4^0
source=("git+https://gitlab.gnome.org/GNOME/atkmm.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd atkmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd atkmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson atkmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_atkmm() {
  depends+=(
    libglibmm-2.4.so
    libsigc-2.0.so
  )
  provides=(libatkmm-1.6.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_atkmm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
