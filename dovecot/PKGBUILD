# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>
# Contributor: Phillip Schichtel <phillip@schich.tel>

# --->>> remember to rebuild/bump the following packages TOGETHER with a new dovecot ABI:
#   +pigeonhole
#   +dovecot-fts-elastic
#   +dovecot-fts-xapian

pkgname=dovecot
pkgver=2.4.2
pkgrel=4
pkgdesc="An IMAP and POP3 server written with security primarily in mind"
url="https://dovecot.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(
  'MIT'
  'LGPL-2.1-only'
)
depends=(
  'bzip2'
  'expat'
  'gcc-libs'
  'glibc'
  'icu'
  'krb5'
  'libcap'
  'libsodium'
  'libstemmer'
  'libxcrypt'
  'lz4'
  'mariadb-libs'
  'openssl'
  'pam'
  'postgresql-libs'
  'sqlite'
  'systemd-libs'
  'zlib'
  'zstd'
)
makedepends=(
  'libldap'
  'lua53'
  'systemd'
  'xapian-core'
)
optdepends=(
  'libldap: LDAP plugin'
  'lua53: Lua auth and push support'
  'xapian-core: FTS flatcurve indexer'
)
provides=(
  'imap-server'
  'pop3-server'
)
backup=(
  'etc/dovecot/dovecot.conf'
  'etc/pam.d/dovecot'
)
install="${pkgname}.install"
options=('!emptydirs')
_json_lua_ver='0.1.2'
source=(
  "https://dovecot.org/releases/2.4/${pkgname}-${pkgver}.tar.gz"{,.sig}
  "json-v$_json_lua_ver.lua::https://raw.githubusercontent.com/rxi/json.lua/v$_json_lua_ver/json.lua"
  'dovecot.sysusers'
  'dovecot.tmpfiles'
  'dovecot.ld.so.conf'
  'dovecot.pam'
  'dovecot.install'
)
sha256sums=('2cd62e4d22b9fc1c80bd38649739950f0dbda34fbc3e62624fb6842264e93c6e'
            'SKIP'
            'b13df59b32c77db3bec7a1619280ea77ee5014e715ccffaa4876d857e3e9ab87'
            '068b16ab8afcc4f5cbced76269264088aed6d662db409b94bd5d22e816a869cc'
            '0b0625b1e66ca6a95d506fd00d6a68e70620c8ea28606e2528953ffb1806b08e'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858'
            'ad9245f5e916480edd67139603cbe52e7a868233075f900ab63a0ce58f03741a'
            '2cfaade95b24639558b28c781687b32029d6566ce3ccc3999b1ff81d94674d6c')
validpgpkeys=(
  'E643F0BDFDCD04D9FFCB6279C948525140558AC9' # Timo Sirainen <tss@iki.fi>
  '2BE74AAB3EE754DFB9C80D3318A348AEED409DA1' # Dovecot Community Edition
  'EF0882079FD4ED32BF8B23B2A1B09EF84EDC5219' # Dovecot Community Edition <dovecot-ce@dovecot.org>
)

prepare() {
  cd "${pkgname}-${pkgver}"

  # Fix path in helper script
  sed -i 's:OPENSSLCONFIG=${OPENSSLCONFIG-dovecot-openssl.cnf}:OPENSSLCONFIG=${OPENSSLCONFIG-/etc/ssl/dovecot-openssl.cnf}:' doc/mkcert.sh

  autoreconf -vfi

  # Add missing (unpackaged) check dependency
  cp -v ../json-v0.1.2.lua src/lib-lua/json.lua
}

build() {
  cd "${pkgname}-${pkgver}"

  # This uses malloc_usable_size, which is incompatible with fortification
  # level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-rundir=/run/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static \
    --with-pam \
    --with-sqlite \
    --with-pgsql \
    --with-mysql \
    --with-ssl=openssl \
    --with-ssldir=/etc/ssl \
    --with-gssapi \
    --with-ldap=plugin \
    --with-lua=plugin \
    --with-zlib \
    --with-bzlib \
    --with-lz4 \
    --with-zstd \
    --with-solr \
    --with-sodium \
    --with-libcap \
    --with-stemmer \
    --with-flatcurve \
    --with-docs

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac

  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  # system user/group dovenull - 74
  # system user/group dovecot  - 76

  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -vDm644 "${srcdir}/dovecot.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/dovecot.conf"
  install -vDm644 "${srcdir}/dovecot.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/dovecot.conf"
  install -vdm755 "${pkgdir}/etc/dovecot/conf.d"
  rm -vf "${pkgdir}/etc/dovecot/README"

  # Install mkcert helper script
  install -vDm755  doc/mkcert.sh "${pkgdir}/usr/lib/dovecot/mkcert.sh"

  # Add dovecot libdir
  install -vDm644 "${srcdir}/dovecot.ld.so.conf" "${pkgdir}/etc/ld.so.conf.d/dovecot.conf"

  # Install PAM snippet for dovecot
  install -vDm644 "${srcdir}/dovecot.pam" "${pkgdir}/etc/pam.d/dovecot"

  # License
  install -vDm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING.MIT
}
