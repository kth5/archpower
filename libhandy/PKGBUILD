# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libhandy
pkgname=(
  libhandy
  libhandy-docs
)
pkgver=1.8.3
pkgrel=1
pkgdesc="Library full of GTK+ widgets for mobile phones"
url="https://gitlab.gnome.org/GNOME/libhandy"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  gtk3
)
makedepends=(
  gi-docgen
  git
  glade
  gobject-introspection
  meson
  vala
)
checkdepends=(xorg-server-xvfb)
_commit=9b0071408ce86a3ef843806fddd723a85f6f2416  # tags/1.8.3^0
source=("git+https://gitlab.gnome.org/GNOME/libhandy.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd libhandy
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libhandy
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson libhandy build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_libhandy() {
  provides=(libhandy-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libhandy-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
