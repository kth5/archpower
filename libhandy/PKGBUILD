# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libhandy
pkgname=(
  libhandy
  libhandy-docs
)
pkgver=1.8.3
pkgrel=2
pkgdesc="GTK 3 UI elements for mobile devices"
url="https://gitlab.gnome.org/GNOME/libhandy"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  at-spi2-core
  cairo
  fribidi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  pango
)
makedepends=(
  gi-docgen
  git
  glade
  glib2-devel
  gobject-introspection
  meson
  vala
)
checkdepends=(xorg-server-xvfb)
source=("git+https://gitlab.gnome.org/GNOME/libhandy.git#tag=$pkgver")
b2sums=('d2263077f17d787be1930701b7c7d274d7c5d10358d87a8029881ebe4cc8435d0d00cbafadfb5bf8690655c6813aeea4ac2c95b38c619bbe8081fccf722ca056')

prepare() {
  cd libhandy
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson libhandy build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_libhandy() {
  provides=(libhandy-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libhandy-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
