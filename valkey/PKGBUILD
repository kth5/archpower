# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>
# Contributor: nofxx <x@<nick>.com>

pkgname=valkey
pkgver=8.1.1
pkgrel=1
pkgdesc='An in-memory database that persists on disk'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://valkey.io/'
license=(
  BSD-2-Clause
  BSD-3-Clause
)
depends=(
  glibc
  jemalloc
  openssl
  systemd-libs
)
# pkg-config fails to detect systemd libraries if systemd is not installed
makedepends=(
  systemd
)
checkdepends=(
  procps-ng
  tcl
)
conflicts=(
  redis
)
provides=(
  redis
)
backup=(
  etc/valkey/valkey.conf
  etc/valkey/sentinel.conf
)
install=valkey.install
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/valkey-io/valkey/archive/${pkgver}.tar.gz"
  valkey.service
  valkey-sentinel.service
  valkey.sysusers
  valkey.tmpfiles
  valkey.conf-sane-defaults.patch
  valkey-5.0-use-system-jemalloc.patch
  remove-deprecated-use-of-je_calloc.patch
)
sha512sums=('1f84b24839856c8d6cdbe1bca5f571fc41cda2b08f495ee80e9a0a03a8ba737f0effeedcb0508195f8d30570443cbccd3c0cf8f4d93980e57f9f8ac656379327'
            '286ce5be525166814f8ffce64fdb8ddc3abbaefe75bfc3044e8a4ee2111359fda3b35b3e548538bc05e4393bb4f5c716f549a23d069e5c1f4327b9a60594fbb9'
            'f1f8265106604c068e1d1ef4bb5755ff85af7965b40b2515adcbe65780a4b45ca9fcdda3c4e6cab52bf9483e0a1d95d11c18a15eb6e504c22591c88c71fd3039'
            'd47185f700293304b5c23caf59999fecda2d1485a28a5eeff3a2922906f0184794d3eeeeeaac2ca415b865d7c4b5d74f88e694d34eeb6d1ee3a6bedbcd6edfdd'
            '11cf6d6999329af7a9fa4bcbbcf22242b461cec0c16ad949cc6b0383703f19417092782569bf6224f94167a560de0b4ba53ec0d8522683736a14f01bc5986b28'
            '032b19af22dd96c7898aa3dcae76d63fd8566c1d35ccb069e22fd0b76612d3285cd318f26ad5994b4f761f44a23c091d5322dec975b9a5a8cc65455399576045'
            'c6972be1a89bb19d8bae4a92b6549dfb16f1779bd9e3d8f018d62fa388f52022adf347665080879745e5a9571db5461fd59d4383857031ecd74937f3d20566d4'
            'c656904ade64b2498766a420dab8e72795bb96bf23cb04e408e011197c755648a3c155e699821347f4ca9e207031493df50b4474c41534e50ec4fb0d4817c45c')

prepare() {
  # extract licenses
  sed -n '7,16p' $pkgname-$pkgver/COPYING > BSD-3-Clause.txt
  sed -n '22,31p' $pkgname-$pkgver/COPYING > BSD-2-Clause.txt

  cd $pkgname-$pkgver
  # disable defrag test since defrag is currently disabled as long as the bundled jmalloc is not used
  sed -i '/defragtest.so/d' tests/modules/Makefile
  rm tests/unit/memefficiency.tcl

  patch -Np1 < ../valkey.conf-sane-defaults.patch
  patch -Np1 < ../valkey-5.0-use-system-jemalloc.patch
  patch -Np1 < ../remove-deprecated-use-of-je_calloc.patch
}

build() {
  make BUILD_TLS=yes \
       USE_SYSTEMD=yes \
       -C $pkgname-$pkgver
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac

  make test -C $pkgname-$pkgver
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir"/usr install

  install -vDm 644 ../BSD-{2,3}-Clause.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -Dm644 -t "$pkgdir"/etc/valkey valkey.conf sentinel.conf
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system/ ../valkey.service ../valkey-sentinel.service
  install -Dm644 "$srcdir"/valkey.sysusers "$pkgdir"/usr/lib/sysusers.d/valkey.conf
  install -Dm644 "$srcdir"/valkey.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/valkey.conf
}
