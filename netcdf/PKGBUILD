# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf
pkgver=4.9.3
pkgrel=4
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=(hdf5 curl libxml2 libaec blosc bzip2 libzip zstd)
makedepends=(cmake git)
checkdepends=(unzip)
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=(!makeflags)
license=(custom)
source=(git+https://github.com/Unidata/netcdf-c#tag=v$pkgver)
sha256sums=('01e621bb0a556860bb6e580142e4c05ba1ca7ef46f115ec41f592fd1736423ab')

prepare() {
  # https://github.com/Unidata/netcdf-c/issues/2188 https://github.com/Unidata/netcdf-c/issues/2242
  sed -i "/tst_remote3/d" ${pkgname}-c/ncdap_test/CMakeLists.txt

  git -C $pkgname-c cherry-pick -n 1f1f359aeb4a31c47410d36581a141bb04d7b4a1 \
                                   cf095c623963552a571b1c7fc46a2af69be071e2 \
                                   741c4b4a909179d3fce56b60eede7ee5332af349 # Fix build with HDF5 2.0
}

build() {
  case "${CARCH}" in
    powerpc64*|x86_64)
      cmake_options=(
        -DENABLE_CDF5=ON
      )
    ;;
    powerpc)
      export CFLAGS+=' -Wno-incompatible-pointer-types'
      cmake_options=(
        -DENABLE_CDF4=OFF
        -DENABLE_CDF5=OFF
        -DNETCDF_GENERATE_NCGEN=OFF
      )
    ;;
  esac

  cmake -B build -S ${pkgname}-c \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DNETCDF_WITH_PLUGIN_DIR=/usr/lib/netcdf/plugin \
    -DENABLE_BYTERANGE=ON \
    -DENABLE_DAP_LONG_TESTS=ON \
    -DENABLE_DAP_REMOTE_TESTS=ON \
    -DENABLE_EXAMPLE_TESTS=ON \
    -DENABLE_EXTRA_TESTS=ON \
    -DENABLE_FILTER_TESTING=ON \
    -DENABLE_LARGE_FILE_TESTS=ON \
    -DENABLE_UNIT_TESTS=ON \
    -DNETCDF_ENABLE_LOGGING=ON \
    -DENABLE_PLUGIN_INSTALL=ON ${cmake_options[@]}
  make -C build
}

check() {
  case "${CARCH}" in
    x86_64) make -C build test ;;
    *) echo "tests disabled for ${CARCH}" ;;
  esac
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-c/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
