# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
pkgname=cpio
pkgver=2.15
pkgrel=2
pkgdesc="A tool to copy files into or out of a cpio or tar archive"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL-3.0-or-later')
url="https://www.gnu.org/software/cpio"
depends=('glibc')
makedepends=('git' 'rsync' 'wget')
source=(git+https://git.savannah.gnu.org/git/cpio.git?signed#tag=v${pkgver})
b2sums=('SKIP')
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732') # Sergey Poznyakoff <gray@gnu.org>

prepare() {
  cd "${pkgname}"
  ./bootstrap
}

build() {
  cd "${pkgname}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd "${pkgname}"
  make check
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/libexec
  rm -r "${pkgdir}"/usr/share/man/man8
}
