# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=whois
pkgver=5.6.4
pkgrel=1
pkgdesc="Intelligent WHOIS client"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/rfc1036/whois"
license=('GPL')
depends=('libidn2' 'libxcrypt')
makedepends=('perl' 'git' 'bash-completion')
backup=('etc/whois.conf')
source=(git+https://github.com/rfc1036/whois.git#tag=v$pkgver?signed)
sha256sums=('c7b03b66b0d9ff55409db22ca1ff1d6902e761c866b87e9f8fe3846686bcb4d2')
validpgpkeys=('6791403B68AE2690517C42EAE6FFF1E38DC968B0') # Marco d'Itri <md@linux.it>

build() {
  cd $pkgname
  make \
    CONFIG_FILE=/etc/whois.conf \
    CFLAGS="$CFLAGS $CPPFLAGS" \
    HAVE_ICONV=1
}

package() {
  cd $pkgname
  make prefix=/usr BASEDIR="$pkgdir" install
  install -Dm644 whois.conf -t "$pkgdir/etc"
}

# vim:set ts=2 sw=2 et:
