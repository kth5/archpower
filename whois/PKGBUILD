# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=whois
pkgver=5.6.1
pkgrel=1
pkgdesc="Intelligent WHOIS client"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/rfc1036/whois"
license=('GPL')
depends=('libidn2' 'libxcrypt')
makedepends=('perl' 'git' 'bash-completion')
backup=('etc/whois.conf')
source=(git+https://github.com/rfc1036/whois.git#tag=v$pkgver?signed)
sha256sums=('6d9e86db10931b94955205028c670b59e65f2db60cf11eeda690d63e4747ef4f')
validpgpkeys=('6791403B68AE2690517C42EAE6FFF1E38DC968B0') # Marco d'Itri <md@linux.it>

build() {
  cd $pkgname
  make \
    CONFIG_FILE=/etc/whois.conf \
    CFLAGS="$CFLAGS $CPPFLAGS" \
    HAVE_ICONV=1
}

package() {
  cd $pkgname
  make prefix=/usr BASEDIR="$pkgdir" install
  install -Dm644 whois.conf -t "$pkgdir/etc"
}

# vim:set ts=2 sw=2 et:
