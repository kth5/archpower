# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=spirv-headers
pkgver=1.4.328.1
pkgrel=4
epoch=1
pkgdesc="SPIR-V header files and registry"
url="https://www.khronos.org/spirv/"
arch=(any)
license=(MIT)
makedepends=(
  cmake
  git
  ninja
)
groups=(vulkan-devel)
source=("git+https://github.com/KhronosGroup/SPIRV-Headers#tag=vulkan-sdk-$pkgver")
b2sums=('7ee097dd666efb6be860d796540901bc0ca21ebc455ad0c6fc4752cd34241d3254f65428535d9fb786d13d0c60b54c9bde798496b25eb3772edc454f5efdabcb')

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_SYSCONFDIR=/etc
    -D CMAKE_SKIP_INSTALL_RPATH=ON
  )

  cmake -S SPIRV-Headers -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 SPIRV-Headers/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
