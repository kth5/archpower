# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

pkgname=ntfs-3g
pkgver=2022.10.3
pkgrel=2
pkgdesc='NTFS filesystem driver and utilities'
url='https://www.tuxera.com/community/open-source-ntfs-3g/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
depends=('util-linux' 'fuse2')
makedepends=('git')
conflicts=('ntfsprogs')
provides=('ntfsprogs')
replaces=('ntfsprogs')
source=("git+https://github.com/tuxera/ntfs-3g.git#tag=${pkgver}")
sha256sums=('2ea31198406ad58b7cf4fd163c7192b7ea21639b0f770d930ccb25b3c188112e')

prepare() {
  cd ${pkgname}
  autoreconf -fiv
  sed 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' -i {ntfsprogs,src}/Makefile.in
}

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/usr \
    --sbin=/usr/bin \
    --mandir=/usr/share/man \
    --disable-ldconfig \
    --disable-static \
    --with-fuse=external \
    --enable-xattr-mappings \
    --enable-posix-acls \
    --enable-extras \
    --enable-crypto \

  make
}

package_ntfs-3g() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" rootbindir=/usr/bin rootsbindir=/usr/bin rootlibdir=/usr/lib install
  rm "${pkgdir}"/usr/share/man/man8/ntfsfallocate.8 # uninstalled binary
  ln -s /usr/bin/ntfs-3g "${pkgdir}/usr/bin/mount.ntfs"
}
