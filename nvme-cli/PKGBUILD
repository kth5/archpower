# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Devin Cofer <ranguvar[at]ranguvar[dot]io>
# Contributor: DriverX
# Contributor: Nicolas Qui√©not <niQo @ aur>
# Contributor: Martin Schrodt <martin@schrodt.org>

pkgname=nvme-cli
pkgver=2.16
pkgrel=1
pkgdesc="NVM-Express user space tooling for Linux"
arch=(i686 x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/linux-nvme/nvme-cli"
license=('GPL-2.0-or-later')
makedepends=(
    'asciidoc'
    'git'
    'meson'
    'swig'
    'systemd'
    'xmlto'
)
depends=(
    'gcc-libs'
    'glibc'
    'json-c'
    'libnvme'
    'libsystemd'
    'util-linux'
)
install=nvme-cli.install
# checkdepends=('python2-nose' 'python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linux-nvme/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('989682ed7b250a2c7a8127e362ffc5d29f5c370127abe405be09c73216da2b97')

build() {
    local meson_options=(
        --sysconfdir /etc
        -D docs=man -D docs-build=true
        -D systemddir=lib/systemd/system/
        -D udevrulesdir=lib/udev/rules.d
        build
        "${pkgname}-${pkgver}"
    )

    arch-meson "${meson_options[@]}"
    meson compile -C build
}

# Tests require a working NVME disk.
# check() {
#   cd "${pkgname}-${pkgver}"
#   make test
# }

package() {
    meson install -C build --destdir "$pkgdir"
}
