# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Devin Cofer <ranguvar[at]ranguvar[dot]io>
# Contributor: DriverX
# Contributor: Nicolas Qui√©not <niQo @ aur>
# Contributor: Martin Schrodt <martin@schrodt.org>

pkgname=nvme-cli
pkgver=2.15
pkgrel=1
pkgdesc="NVM-Express user space tooling for Linux"
arch=(i686 x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/linux-nvme/nvme-cli"
license=('GPL-2.0-or-later')
makedepends=(
    'asciidoc'
    'git'
    'meson'
    'swig'
    'systemd'
    'xmlto'
)
depends=(
    'gcc-libs'
    'glibc'
    'json-c'
    'libnvme'
    'libsystemd'
    'util-linux'
)
install=nvme-cli.install
# checkdepends=('python2-nose' 'python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linux-nvme/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('93282c426f22dd1ea6d172dec8af043c4e9ff80189becfbbb5378fe1ca0a74ad')

build() {
	cd "${pkgname}-${pkgver}"

	# this uses malloc_usable_size, which is incompatible with fortification level 3
	# export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
	# export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

	meson setup \
	--prefix /usr \
	--sysconfdir /etc \
	--libexecdir lib \
	--sbindir bin \
	--buildtype plain \
	--auto-features enabled \
	-D b_lto=true -D b_pie=true \
	-D docs=man -D docs-build=true \
	-D systemddir=lib/systemd/system/ \
	-D udevrulesdir=lib/udev/rules.d \
	.build
}

# Tests require a working NVME disk.
# check() {
# 	cd "${pkgname}-${pkgver}"
# 	make test
# }

package() {
	cd "${pkgname}-${pkgver}"

	# this uses malloc_usable_size, which is incompatible with fortification level 3
	export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
	export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

	DESTDIR="$pkgdir" meson install -C .build
}
