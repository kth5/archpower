# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Robert Tari <robert at tari dot in>

pkgname=libayatana-appindicator
pkgver=0.5.94
pkgrel=1
pkgdesc='Ayatana Application Indicators shared library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://github.com/AyatanaIndicators/libayatana-appindicator'
license=(LGPL-2.1-or-later)
depends=(gcc-libs
         glib2
         glibc
         gtk3
         libayatana-indicator
         libdbusmenu-glib
         libdbusmenu-gtk3)
makedepends=(cmake
             git
             glib2-devel
             gobject-introspection
             vala)
source=(git+https://github.com/AyatanaIndicators/$pkgname#tag=$pkgver)
sha256sums=('bf21c9e6fb35c883183126b900beac1a6300723f0eff4baae6c83c8d995f5220')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_BINDINGS_MONO=OFF \
    -DENABLE_GTKDOC=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
