# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Robert Tari <robert at tari dot in>

pkgname=libayatana-appindicator
pkgver=0.5.93
pkgrel=1
pkgdesc='Ayatana Application Indicators shared library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/AyatanaIndicators/libayatana-appindicator'
license=(LGPL2.1 LGPL3)
depends=(gcc-libs
         glib2
         glibc
         gtk3
         libayatana-indicator
         libdbusmenu-glib
         libdbusmenu-gtk3)
makedepends=(cmake
             gobject-introspection
             vala)
source=(https://github.com/AyatanaIndicators/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('cbefed7a918a227bf71286246e237fcd3a9c8499b3eaac4897811a869409edf0')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_BINDINGS_MONO=OFF \
    -DENABLE_GTKDOC=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
