# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org> 
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=dosfstools
pkgver=4.2
pkgrel=5
pkgdesc="DOS filesystem utilities"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('glibc')
source=(https://github.com/$pkgname/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.sig}
	$pkgname-reproducible-mkfs.patch::https://github.com/dosfstools/dosfstools/commit/8da7bc93315cb0c32ad868f17808468b81fa76ec.patch
)
url="https://github.com/dosfstools/dosfstools"
license=('GPL-3.0-or-later')
validpgpkeys=('25714AECDBFDACEE1CE95FE77F6022516E869F64') # Andreas Bombe
sha256sums=('64926eebf90092dca21b14259a5301b7b98e7b1943e8a201c7d726084809b527'
            'SKIP'
            'b9953dde3d755a6e9a31a28e2a7aebccd0b5ed9bcc664893bd55290945589b5b')


prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/$pkgname-reproducible-mkfs.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib \
              --sbindir=/usr/bin --mandir=/usr/share/man \
              --docdir=/usr/share/doc/dosfstools --enable-compat-symlinks
   make
}

package () {
   cd $pkgname-$pkgver
   make DESTDIR="$pkgdir" install
}
