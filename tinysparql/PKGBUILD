# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=tinysparql
pkgname=(
  tinysparql
  tinysparql-docs
)
pkgver=3.8.2
pkgrel=2
pkgdesc="Low-footprint RDF triple store with SPARQL 1.1 interface"
url="https://tinysparql.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later)
depends=(
  avahi
  gcc-libs
  glib2
  glibc
  icu
  json-glib
  libsoup3
  libstemmer
  libxml2
  sqlite
)
makedepends=(
  asciidoc
  bash-completion
  dbus
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  python-dbus
  python-gobject
  python-tappy
  systemd
  vala
)
checkdepends=(man-db)
source=("git+https://gitlab.gnome.org/GNOME/tinysparql.git#tag=${pkgver/[a-z]/.&}")
b2sums=('85d92b4f01656512ce13f0ac1af423414a005da74d93a44baa0b07fe8c0f911ea6b4e9942448719a25ec80f9c4d68157d4895f7244b9bf878cf641262a537e9d')

build() {
  local meson_options=(
    -D tests_tap_protocol=true
  )

  arch-meson tinysparql build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs -t 3
}

package_tinysparql() {
  provides=(
    "tracker3=$pkgver"
    libtinysparql-3.0.so
  )
  replaces=('tracker3<=3.7.3-2')
  conflicts=('tracker3<=3.7.3-2')

  meson install -C build --destdir "$pkgdir"

  mkdir -p docs/usr/share
  mv {"$pkgdir",docs}/usr/share/doc
}

package_tinysparql-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
