# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc
pkgver=1.16.0
pkgrel=2
pkgdesc="Suite of VNC servers and clients based on the VNC 4 branch of TightVNC"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.tigervnc.org"
license=('GPL-2.0-only')
depends=(
  'bash'
  'ffmpeg'
  'fltk1.3'
  'gcc-libs'
  'glibc'
  'gmp'
  'gnutls'
  'hicolor-icon-theme'
  'libgl'
  'libjpeg-turbo'
  'libunwind'
  'libx11'
  'libxau'
  'libxcvt'
  'libxdamage'
  'libxdmcp'
  'libxext'
  'libxfixes'
  'libxfont2'
  'libxi'
  'libxrender'
  'libxshmfence'
  'libxtst'
  'mesa'
  'nettle'
  'pam'
  'perl'
  'pixman'
  'systemd-libs'
  'xkeyboard-config'
  'xorg-xauth'
  'xorg-xinit'
  'xorg-xkbcomp'
  'xorg-xsetroot'
  'zlib'
)
makedepends=(
  'cmake'
  'git'
  'imagemagick'
  'java-environment=8'
  #'strip-nondeterminism'
  'xorg-font-util'
  'xorg-server-src'
  'xorg-util-macros'
  'xorgproto'
  'xtrans'
)
makedepends_x86_64=(
  'nasm'
)
optdepends=(
  'java-runtime: for java-based client'
)
conflicts=('tightvnc')
backup=(
  etc/pam.d/tigervnc
  etc/tigervnc/vncserver-config-defaults
  etc/tigervnc/vncserver-config-mandatory
  etc/tigervnc/vncserver.users
)
source=(
  git+https://github.com/TigerVNC/tigervnc.git#tag=v${pkgver}
  Xsession
  more-xsessions.patch
  remove-selinux.patch
  tigervnc-1.16.0-fltk1.3.patch
)
sha256sums=('86d13ae2ec6775f7c1e61f93aff8a9036c06aa82529cc5d446d779da34210f93'
            'c9276f6ea277cf9654fb2cc3bc9dadbb2e596b5cf8ca867ee906c0080cf7f810'
            'df7d5ac0b16781ba50963833f1bdd4603f1cdfcc24b1727f563ae800508c9dc0'
            'b5da49ee5f10dd40945df9c8563066c5523a24ac0ad934c7efcb41d0847b94ed'
            '4f17f32887b7818c27c7ddb2a4da499e477f5f2f7e0f183bb473dfaafab263c4')

prepare() {
  cd ${pkgname}
  patch -p1 -i ../more-xsessions.patch
  patch -p1 -i ../remove-selinux.patch

  # use fltk1.3, as upstream is not compatible with fltk >= 1.4 yet
  patch -Np1 -i ../$pkgname-1.16.0-fltk1.3.patch

  cd unix/xserver
  cp -r /usr/src/xorg-server/* .
  patch -Np1 -i ../xserver21.patch

  autoreconf -fiv
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_SBINDIR=/usr/bin
    -D CMAKE_INSTALL_LIBEXECDIR=/usr/bin
    -D BUILD_JAVA=TRUE
    -D BUILD_VIEWER=TRUE
    -S .  # out-of-tree build not possible due to xorg-server build below
    -W no-dev
  )
  local configure_options=(
    --disable-config-hal
    --disable-config-udev
    --disable-devel-docs
    --disable-dri
    --disable-kdrive
    --disable-selective-werror
    --disable-static
    --disable-unit-tests
    --disable-xephyr
    --disable-xnest
    --disable-xorg
    --disable-xvfb
    --disable-xwin
    --enable-dri2
    --enable-dri3
    --enable-glx
    --prefix=/usr
    --with-pic
    --without-dtrace
  )

  cd $pkgname
  CFLAGS+=" -I/usr/include/fltk1.3 -L/usr/lib/fltk1.3"
  LDFLAGS+=" -L/usr/lib/fltk1.3"
  cmake "${cmake_options[@]}"
  cmake --build . --verbose

  cd unix/xserver
  ./configure "${configure_options[@]}"
  make
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" cmake --install .
  install -Dm0644 java/VncViewer.jar "${pkgdir}"/usr/share/vnc/classes/VncViewer.jar

  cd unix/xserver/hw/vnc
  make DESTDIR="$pkgdir" install
  install -Dm0755 "$srcdir"/Xsession "$pkgdir"/etc/X11/tigervnc/Xsession

  # signed jar file
  # strip-nondeterminism "$pkgdir"/usr/share/vnc/classes/VncViewer.jar
}
