# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=libsass
pkgver=3.6.5
pkgrel=3
pkgdesc='C implementation of Sass CSS preprocessor (library)'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://sass-lang.com/$pkgname/"
license=(MIT)
_archive="$pkgname-$pkgver"
source=("https://github.com/sass/$pkgname/archive/$pkgver/$_archive.tar.gz")
sha256sums=('89d8f2c46ae2b1b826b58ce7dde966a176bac41975b82e84ad46b01a55080582')

build() {
	cd "$_archive"
	export LIBSASS_VERSION="$pkgver"
	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir/" install
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
