# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgbase=libxml++2.6
pkgname=(
  libxml++2.6
  libxml++2.6-docs
)
pkgver=2.42.3
pkgrel=2
pkgdesc="C++ bindings to libxml2"
url="https://libxmlplusplus.github.io/libxmlplusplus/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
  glibmm
  libxml2
)
makedepends=(
  git
  glibmm-docs
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://github.com/libxmlplusplus/libxmlplusplus#tag=$pkgver")
b2sums=('c45ffa441a19719885e5135c374d4964102f62407df1191d86cc2448e8fa008478d70fc6d64a81aca2e516900a5e1f5ac75a2782ece924dd8a0670b0b7abff59')

prepare() {
  cd libxmlplusplus
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson libxmlplusplus build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libxml++2.6() {
  provides=(libxml++-2.6.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_libxml++2.6-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
