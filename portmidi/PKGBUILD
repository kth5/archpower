# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=portmidi
pkgver=2.0.7
pkgrel=1
epoch=1
pkgdesc="Cross platform library for interfacing with OS MIDI I/O APIs"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/portmidi/portmidi"
license=(MIT)
depends=(
  glibc
)
makedepends=(
  alsa-lib
  cmake
)
provides=(
  libportmidi.so
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('da5c8ccdbd47c7318fffc26f2fa812c0ab8365ffd6e5b6cb25260b9d2dc42ecb41a0cf1de224e28b1ca99ce3b53773f7a16e7cce5c90a89975ef6a7d03c77638')
b2sums=('84a15bc094da11625b286357c01da4a52af1958e2a3ffc9cab936881e460c4698cd5d6ae7ee8216b550e6b4c29dc75707317917ca40c66e665dabe078acfbd1e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/license.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,README}.txt -t "$pkgdir/usr/share/doc/$pkgname/"
}
