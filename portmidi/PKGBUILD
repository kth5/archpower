# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=portmidi
pkgver=2.0.6
pkgrel=1
epoch=1
pkgdesc="Cross platform library for interfacing with OS MIDI I/O APIs"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/portmidi/portmidi"
license=(MIT)
depends=(
  glibc
)
makedepends=(
  alsa-lib
  cmake
)
provides=(
  libportmidi.so
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('00d7cec97b58c074d484793b6097f4e60d061a9d680940bbcdb6670b287b78dbc099af378fb2e066c61f1c26e5060ded9c8f78c80fc03518b33e43f830e34a27')
b2sums=('5ebb3815a0b57711962836e241fe02f657420d7b95f1241f3fb1bfa8ba7897bd28acbb9c05e352a0d42be45ce73ac43d59cfed9da1b392f593ff1a41d5e01367')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    alsa-lib libasound.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/license.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,README}.txt -t "$pkgdir/usr/share/doc/$pkgname/"
}
