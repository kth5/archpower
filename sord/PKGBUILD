# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=sord
pkgname=(sord sord-docs)
pkgver=0.16.20
pkgrel=1
pkgdesc="A lightweight C library for storing RDF data in memory"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://drobilla.net/software/sord.html"
license=(ISC)
makedepends=(
  doxygen
  glibc
  meson
  pcre2
  python-sphinx
  serd
  zix
)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('b5e59896ff62cd2cac911061d32ed2ee59d39ab73f28360039f618e67e2c8bc9b793d7b92ee54099e2e69640c2e999aaa6cd2a1710b1f6ff78cacd5794c0b3e8'
            'SKIP')
b2sums=('5309993f43c32f514ed2941c4cc655042af5aa775ac958224ff9b0ef96deba12af9226e1de1e51acaf999857f51f094e9a0cd4d206c73d8abeaf5bf1081ef624'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package_sord() {
  depends=(
    glibc
    pcre2
    serd libserd-0.so
    zix libzix-0.so
  )
  provides=(libsord-0.so)
  optdepends=('sord-docs: for developer documentation')

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )
  install -vDm 644 $pkgname-$pkgver/LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_sord-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/"sord{-0,}
}
