# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=run-parts
pkgver=5.5
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=(x86_64 powerpc64le powerpc)
url='https://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
depends=('glibc')
source=("https://clint.pages.debian.net/debianutils-tarballs/artifacts/debianutils-${pkgver}.tar.gz"{,.sig})
sha256sums=('3ec744186b8d016fb8b45915a40871a51f701937d80c467118f8cee6a9f517f2'
            'SKIP')
validpgpkeys=('2100A32C46F895AF3A08783AF6D3495BB0AE9A02')

build() {
	cd "$srcdir/debianutils-${pkgver}"
	./configure --prefix=/usr
	make run-parts
}

package() {
	cd "$srcdir/debianutils-${pkgver}"
	install -D -m755 run-parts "$pkgdir/usr/bin/run-parts"
	install -D -m644 run-parts.8 "$pkgdir/usr/share/man/man8/run-parts.8"
	for l in po4a/*/run-parts.8; do
		install -D -m644  "${l}" "$pkgdir/usr/share/man/${l:5:2}/man8/run-parts.8"
	done
}
