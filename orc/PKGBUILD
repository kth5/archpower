# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=orc
pkgver=0.4.34
pkgrel=1
pkgdesc="Optimized Inner Loop Runtime Compiler"
url="https://gitlab.freedesktop.org/gstreamer/orc"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(custom:BSD)
depends=(glibc)
makedepends=(
  git
  gtk-doc
  meson
  valgrind
)
provides=(liborc{,-test}-${pkgver%.*}.so)
_commit=7d5bbada3f1c6cf34182abccf47a34d79b83fa97  # tags/0.4.34^0
source=("git+https://gitlab.freedesktop.org/gstreamer/orc.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd orc
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd orc
}

build() {
  arch-meson orc build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 orc/COPYING
}

# vim:set sw=2 sts=-1 et:
