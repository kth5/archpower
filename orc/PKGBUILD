# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=orc
pkgver=0.4.40
pkgrel=1
pkgdesc="Optimized Inner Loop Runtime Compiler"
url="https://gstreamer.freedesktop.org/modules/orc.html"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(BSD-3-Clause)
depends=(
  glibc
)
makedepends=(
  git
  gtk-doc
  meson
  valgrind
)
provides=(liborc{,-test}-${pkgver%.*}.so)
source=("git+https://gitlab.freedesktop.org/gstreamer/orc.git?signed#tag=$pkgver")
b2sums=('08cd279d7a572b078b00aac3ced56fc96d7d4472103357e74e86901c5b1edc02df768fe4b862a55faf56b5218b05c71c64d0b378f1b9a1d4c40efcc7c738de4c')
validpgpkeys=(
  D637032E45B8C6585B9456565D2EEE6F6F349D7C # Tim-Philipp MÃ¼ller <tim@centricular.com>
)

prepare() {
  cd orc
}

build() {
  arch-meson orc build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 orc/COPYING
}

# vim:set sw=2 sts=-1 et:
