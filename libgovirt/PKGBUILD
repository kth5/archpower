# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgovirt
epoch=2
pkgver=0.3.9
pkgrel=2
pkgdesc='GObject-based library to access oVirt REST API'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gitlab.gnome.org/GNOME/libgovirt'
license=(LGPL)
depends=(librest)
makedepends=(gobject-introspection meson glib2-devel)
source=("https://gitlab.gnome.org/GNOME/libgovirt/-/archive/v${pkgver}/libgovirt-v${pkgver}.tar.bz2"
        "${pkgname}-suppress-error-cast-align.patch::https://gitlab.gnome.org/GNOME/libgovirt/-/merge_requests/21.diff")
b2sums=('383660540bcde90e4406e086f8acc98af233b83cd8a8d7f3634fa1de4e6cc43ceeae2f476a16a65732c3cf5c302a2f108b9c5b1d67b39b9999d1de48af22da24'
        'c4cb09c44dd2a8410191e98a4054ddd27e9890d37166fa4c33c74c08dda025e57eed3f8140df3690fdcf8909398fa016b319e2da65058365d9af1154b74af060')

prepare() {
  cd ${pkgname}-v${pkgver}
  patch -Np1 -i ../${pkgname}-suppress-error-cast-align.patch 
}
  
build() {
  arch-meson ${pkgname}-v${pkgver} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
