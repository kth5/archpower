# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=quota-tools
pkgver=4.09
pkgrel=2.2
epoch=1
pkgdesc='Tools to manage kernel-level quotas in Linux'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://sourceforge.net/projects/linuxquota/'
license=(GPL-2.0-or-later)
depends=(dbus
         e2fsprogs
         glibc
         libldap
         libnl
         libtirpc)
makedepends=(rpcsvc-proto)
backup=(etc/quotagrpadmins
        etc/quotatab
        etc/warnquota.conf)
source=(https://downloads.sourceforge.net/sourceforge/linuxquota/quota-$pkgver.tar.gz)
sha256sums=('9cdaca154bc92afc3117f0e5f5b3208dd5f84583af1cf061c39baa0a2bb142f9')

build() {
  cd quota-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd quota-$pkgver
  make DESTDIR="$pkgdir" sbindir=/usr/bin root_sbindir=/usr/bin STRIP="" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -33 quota.c > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

# remove conflicts with glibc
  rm -r "$pkgdir"/usr/include
}
