# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=(
  glibmm
  glibmm-docs
)
pkgver=2.66.7
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  libsigc++
)
makedepends=(
  clang
  git
  meson
  mm-common
  perl-xml-parser
)
checkdepends=(glib-networking)
_commit=a33c12cb694a46f97fe46372e6a97a22bbcd601f  # tags/2.66.7^0
source=("git+https://gitlab.gnome.org/GNOME/glibmm.git#commit=$_commit")
b2sums=('4b626196a994c74a94e2b9e3876b5ec0e3050b5f7b669537e0afb3d4b4872ead17ef6ae7a3359a497ba06570acd58e9f4c831ca48fb164dec016639471894c54')

pkgver() {
  cd glibmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glibmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson glibmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_glibmm() {
  depends+=(libsigc-2.0.so)
  provides=(libglibmm-2.4.so)
  options=(!emptydirs)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_glibmm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
