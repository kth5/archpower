# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libhubbub
pkgver=0.3.8
pkgrel=1
pkgdesc='HTML5 compliant parsing library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.netsurf-browser.org/projects/hubbub/'
license=(MIT)
depends=('libparserutils>=0.2.5')
makedepends=(gperf netsurf-buildsystem)
source=("https://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
b2sums=('f3db98971246b08452df0383d960f528e4c0a0def4318bd074fbdb02a045691c6476a55b3b2a851a058785b358a839cb9a8e05abab091127f2affad04bd26936')

build() {
  make -C $pkgname-$pkgver COMPONENT_TYPE=lib-shared INCLUDEDIR=include LIBDIR=lib PREFIX=/usr
}

package() {
  make -C $pkgname-$pkgver COMPONENT_TYPE=lib-shared DESTDIR="$pkgdir" INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr install
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
