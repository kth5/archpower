# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db
pkgver=20250124
pkgrel=1
pkgdesc='Osinfo database of information about operating systems for virtualization provisioning tools'
arch=(any)
url='https://libosinfo.org/'
license=(GPL-2.0-or-later)
makedepends=(
  git
  osinfo-db-tools
)
checkdepends=(
  python-lxml
  python-pytest
)
source=("git+https://gitlab.com/libosinfo/$pkgname.git?signed#tag=v$pkgver")
b2sums=('94e2f795599cc02eaffc669b6a582abfcb10e55d32da8566b7d92ae1cfff9fcdd0d9cceb0f5244ffc7d73cf84b48ce355e9f4091e805a2161523506c7a81d78d')
validpgpkeys=(
  DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF # Daniel P. Berrange
  09B9C8FF223EF113AFA06A39EE926C2BDACC177B # Fabiano Fidêncio
  206D3B352F566F3B0E6572E997D9123DE37A484F # Victor Toso de Carvalho <me@victortoso.com>
)

prepare() {
  cd $pkgname
  sed -i "s/TODAY = .*/TODAY = $pkgver/" Makefile
}

build() {
  cd $pkgname
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir" OSINFO_DB_TARGET="--system"
}
