# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db
pkgver=20240523
pkgrel=1
pkgdesc='Osinfo database of information about operating systems for virtualization provisioning tools'
arch=(any)
url='https://libosinfo.org/'
license=('GPL-2.0-or-later')
makedepends=('git' 'osinfo-db-tools')
checkdepends=('python-lxml' 'python-pytest')
source=("git+https://gitlab.com/libosinfo/$pkgname.git?signed#tag=v$pkgver")
b2sums=('44f87d41e3d93abf56c89296b245c8ea5a4181681f0dd5860e40318521e77659fb826799b7b75eeee5754d5fd71f591ac5e2d28a1bad7d35fe71fc5dac053343')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF'  # Daniel P. Berrange
              '09B9C8FF223EF113AFA06A39EE926C2BDACC177B'  # Fabiano Fidêncio
              '206D3B352F566F3B0E6572E997D9123DE37A484F') # Victor Toso de Carvalho <me@victortoso.com>

prepare() {
  cd $pkgname
  sed -i "s/TODAY = .*/TODAY = $pkgver/" Makefile
}

build() {
  cd $pkgname
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir" OSINFO_DB_TARGET="--system"
}
