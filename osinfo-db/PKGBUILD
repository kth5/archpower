# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db
pkgver=20250606
pkgrel=1
pkgdesc='Osinfo database of information about operating systems for virtualization provisioning tools'
arch=(any)
url='https://libosinfo.org/'
license=(GPL-2.0-or-later)
makedepends=(
  git
  osinfo-db-tools
)
checkdepends=(
  python-lxml
  python-pytest
)
source=("git+https://gitlab.com/libosinfo/$pkgname.git?signed#tag=v$pkgver")
b2sums=('cc311d4791fabbd6abd5728d27ea7bb3d768af9c25c638bfea28340aefcc80377341f527d205cd5a90e280b50a6a1eac01fe15a8385c89d2a0076824a5ac5460')
validpgpkeys=(
  DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF # Daniel P. Berrange
  09B9C8FF223EF113AFA06A39EE926C2BDACC177B # Fabiano Fidêncio
  206D3B352F566F3B0E6572E997D9123DE37A484F # Victor Toso de Carvalho <me@victortoso.com>
)

prepare() {
  cd $pkgname
  sed -i "s/TODAY = .*/TODAY = $pkgver/" Makefile
}

build() {
  cd $pkgname
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir" OSINFO_DB_TARGET="--system"
}
