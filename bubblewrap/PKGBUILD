# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=bubblewrap
pkgname=(
  bubblewrap
  bubblewrap-suid
)
pkgver=0.9.0
pkgrel=1
pkgdesc='Unprivileged sandboxing tool'
url='https://github.com/containers/bubblewrap'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  libcap
)
makedepends=(
  bash-completion
  docbook-xsl
  git
  libxslt
  meson
)
_commit=8e51677abd7e3338e4952370bf7d902e37d8cbb6  # tags/v0.9.0^0
source=("git+$url#commit=$_commit")
b2sums=('f843694c2c2ba08bcf1f8c1c214655a5de1eaed6fba92758874240ac21a76c85a103ded60cd7789253e73e86ce209b65cf36ef0707dcd09addee1ed92b6dc178')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  arch-meson $pkgbase build -D selinux=disabled
  meson compile -C build
}

check() {
  # Broken in our containers
  meson test -C build --print-errorlogs || :
}

package_bubblewrap() {
  meson install -C build --destdir "$pkgdir"
}

package_bubblewrap-suid() {
  pkgdesc+=" (setuid variant)"
  provides=("bubblewrap=$pkgver-$pkgrel")
  conflicts=(bubblewrap)

  meson install -C build --destdir "$pkgdir"
  chmod u+s "$pkgdir/usr/bin/bwrap"
}

# vim:set sw=2 sts=-1 et:
