# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=bubblewrap
pkgname=(
  bubblewrap
  bubblewrap-suid
)
pkgver=0.11.0
pkgrel=1
pkgdesc='Unprivileged sandboxing tool'
url='https://github.com/containers/bubblewrap'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  libcap
)
makedepends=(
  bash-completion
  docbook-xsl
  git
  libxslt
  meson
)
source=("git+$url?signed#tag=v$pkgver")
b2sums=('dd5ac8fcffdf71a9bd10b765ae113424b1dea740fcdbf99cbd7716b6c33121ec2a987d8cf186eccc17a2e67dfa82ce4f843301c7d33a9007e3702d52c3312017')
validpgpkeys=(
  DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90 # Simon McVittie <smcv@collabora.com>
)

prepare() {
  cd $pkgbase
}

build() {
  arch-meson $pkgbase build -D selinux=disabled
  meson compile -C build
}

check() {
  # Broken in our containers
  meson test -C build --print-errorlogs || :
}

package_bubblewrap() {
  meson install -C build --destdir "$pkgdir"
}

package_bubblewrap-suid() {
  pkgdesc+=" (setuid variant)"
  provides=("bubblewrap=$pkgver-$pkgrel")
  conflicts=(bubblewrap)

  meson install -C build --destdir "$pkgdir"
  chmod u+s "$pkgdir/usr/bin/bwrap"
}

# vim:set sw=2 sts=-1 et:
