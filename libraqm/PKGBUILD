# Maintainer:
# Contributor: Daniel Bermond < yahoo-com: danielbermond >

pkgname=libraqm
pkgver=0.10.1
pkgrel=2
pkgdesc='A library that encapsulates the logic for complex text layout'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/HOST-Oman/libraqm/'
license=(MIT)
depends=(freetype2
         fribidi
         glibc
         harfbuzz)
makedepends=(git
             gtk-doc
             meson)
source=(git+https://github.com/HOST-Oman/libraqm#tag=v$pkgver)
sha256sums=('3ffc828da549a7f77216236c6d9bfdb9c21cbd151adbf5dac54fa8480d9f7b03')

build() {
  meson build libraqm \
    --prefix=/usr \
    -D docs=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -D -m644 libraqm/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
