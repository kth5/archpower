# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
pkgver=0.56.18
pkgrel=1
pkgdesc='Compiler for the GObject type system'
url='https://wiki.gnome.org/Projects/Vala'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  bash
  gcc
  glib2
  glibc
  graphviz
  gtk-doc
  pkg-config
  ttf-font
)
makedepends=(
  autoconf-archive
  git
  gobject-introspection
  help2man
  libxslt
  vala
)
checkdepends=(
  dbus
  libx11
)
provides=(
  libvala{,doc}-${pkgver%.*}.so
  valadoc
)
conflicts=(valadoc)
replaces=(valadoc)
source=("git+https://gitlab.gnome.org/GNOME/vala.git?signed#tag=$pkgver")
b2sums=('8e4b2044c2a4674d1a3d75334ac55f16806a901bd4db3cda5fc86a397389edd8b67de6c464bdf7fe0d444657927b2eb0504dd0321ca698199b4c81040eb72a0f')
validpgpkeys=(
  E4884AEEDE4CC02043C3D8045DECDBA89270E723 # Rico Tzschichholz <ricotz@ubuntu.com>
)

prepare() {
  cd vala
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd vala
  ./configure --prefix=/usr || cat config.log
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd vala
  make check
}

package() {
  cd vala
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
