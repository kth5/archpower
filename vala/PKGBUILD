# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
pkgver=0.56.16
pkgrel=1
pkgdesc='Compiler for the GObject type system'
url='https://wiki.gnome.org/Projects/Vala'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  bash
  gcc
  glib2
  glibc
  graphviz
  gtk-doc
  pkg-config
  ttf-font
)
makedepends=(
  autoconf-archive
  git
  help2man
  libxslt
  vala
)
checkdepends=(
  dbus
  gobject-introspection
  libx11
)
provides=(
  libvala{,doc}-${pkgver%.*}.so
  valadoc
)
conflicts=(valadoc)
replaces=(valadoc)
_commit=b6f944c4601092cad4dbb3d1e3bbbf84fa0ee000  # tags/0.56.16^0
source=("git+https://gitlab.gnome.org/GNOME/vala.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd vala
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd vala
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd vala
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd vala
  make check
}

package() {
  cd vala
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
