# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
pkgver=0.56.18
pkgrel=5
pkgdesc='Compiler for the GObject type system'
url='https://wiki.gnome.org/Projects/Vala'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  bash
  gcc
  glib2
  glibc
  graphviz
  gtk-doc
  pkg-config
  ttf-font
)
makedepends=(
  autoconf-archive
  git
  help2man
  libxslt
  vala
)
checkdepends=(
  dbus
  gobject-introspection
  libx11
)
provides=(
  libvala{,doc}-${pkgver%.*}.so
  valadoc
)
source=(
  "git+https://gitlab.gnome.org/GNOME/vala.git?signed#tag=$pkgver"
  0001-valadoc-Fix-gvRenderData-to-match-graphviz-13.0.0.patch
)
b2sums=('8e4b2044c2a4674d1a3d75334ac55f16806a901bd4db3cda5fc86a397389edd8b67de6c464bdf7fe0d444657927b2eb0504dd0321ca698199b4c81040eb72a0f'
        'f824e8c0a67a5bc93730d9c50a9d880eae86da9e2a04575f91912355085d36c09bef12ce1f0f5556e3917f07d13bc49d819c61ee025f8b9078263c102c07e74c')
validpgpkeys=(
  E4884AEEDE4CC02043C3D8045DECDBA89270E723 # Rico Tzschichholz <ricotz@ubuntu.com>
)

prepare() {
  cd vala

  # Fix valadoc with graphviz 13.0.0
  # https://gitlab.archlinux.org/archlinux/packaging/packages/vala/-/issues/1
  # https://gitlab.gnome.org/GNOME/vala/-/issues/1621
  git apply -3 ../0001-valadoc-Fix-gvRenderData-to-match-graphviz-13.0.0.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd vala

  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -C vala check
}

package() {
  make -C vala DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
