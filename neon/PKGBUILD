# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.36.0
pkgrel=1
pkgdesc='HTTP/1.1 and WebDAV client library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://notroj.github.io/neon/'
license=(LGPL-2.0-or-later)
depends=(
  ca-certificates
  expat
  glibc
  krb5
  openssl
  zlib
)
makedepends=(
  docbook-xsl
  git
  xmlto
)
source=("git+https://github.com/notroj/neon.git#tag=$pkgver")
b2sums=(194306289b74763ee7a166448cd16af5fcb694f5ebce0a13eabee8f1490932b174eec3c4676c0c010bbe03b5dae5a3eccdf89473d702400e141dee316a26aea5)
validpgpkeys=(190555472DCC589BEF01609C608A86DF9833CC49) # Joe Orton

prepare() {
  cd $pkgname
  echo $pkgver > .version
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-shared \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
  make docs compile-gmo
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
