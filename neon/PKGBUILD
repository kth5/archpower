# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.32.5
pkgrel=2
pkgdesc='HTTP/1.1 and WebDAV client library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://notroj.github.io/neon/'
license=('LGPL2.1')
depends=('ca-certificates' 'expat' 'glibc' 'krb5' 'openssl' 'zlib')
source=("https://notroj.github.io/neon/$pkgname-$pkgver.tar.gz")
sha256sums=('4872e12f802572dedd4b02f870065814b2d5141f7dbdaf708eedab826b51a58a')
validpgpkeys=('190555472DCC589BEF01609C608A86DF9833CC49') # Joe Orton

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-shared \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
