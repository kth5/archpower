# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.34.2
pkgrel=1
pkgdesc='HTTP/1.1 and WebDAV client library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://notroj.github.io/neon/'
license=(LGPL-2.0-or-later)
depends=(
  ca-certificates
  expat
  glibc
  krb5
  openssl
  zlib
)
makedepends=(
  docbook-xsl
  git
  xmlto
)
source=("git+https://github.com/notroj/neon.git#tag=$pkgver")
b2sums=(0a4a3f7e4d12ac2eb454910ae5aaa76a489423b3c6da2b210688e8cc39e18b56c1d6f0a6e77617d2298e4f0464daa06fca971d967e2e8a2845247ce9c22167cb)
validpgpkeys=(190555472DCC589BEF01609C608A86DF9833CC49) # Joe Orton

prepare() {
  cd $pkgname
  echo $pkgver > .version
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-shared \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
  make docs compile-gmo
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
