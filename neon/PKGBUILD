# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.34.0
pkgrel=1
pkgdesc='HTTP/1.1 and WebDAV client library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://notroj.github.io/neon/'
license=(LGPL-2.0-or-later)
depends=(
  ca-certificates
  expat
  glibc
  krb5
  openssl
  zlib
)
makedepends=(
  docbook-xsl
  git
  xmlto
)
source=("git+https://github.com/notroj/neon.git#tag=$pkgver")
b2sums=(a1ab23c037157cb5f4eb7b71c86217639dc692812ac8c1fd7e906d2be67297ec631286eb0b801f633001c27a322915bbf7313e3938bba83d403c0ec44045cd9b)
validpgpkeys=(190555472DCC589BEF01609C608A86DF9833CC49) # Joe Orton

prepare() {
  cd $pkgname
  echo $pkgver > .version
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-shared \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
  make docs compile-gmo
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
