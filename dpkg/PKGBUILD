# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alad Wenter <alad (at) archlinux.info>
# Contributor: Jochen Schalanda <jochen+aur (at) schalanda.name>
# Contributor: C. Dominik BÃ³di <dominik.bodi@gmx.de>
# Contributor: Pierre Carrier <pierre@spotify.com>
# Contributor: Thomas Dziedzic <gostrc (at) gmail>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Contributor: seblu <seblu+arch (at) seblu.net>
# Contributor: squiddo <squiddo (at) intheocean.net>
# Contributor: dront78 <dront78 (at) gmail.com>
# Contributor: hugelgupf <ckoch (at) cs.nmt.edu>

pkgname=dpkg
pkgver=1.23.5
pkgrel=1
pkgdesc='The Debian Package Manager tools'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://tracker.debian.org/pkg/dpkg"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'gcc-libs'
  'glibc'
  'libmd'
  'perl'
  'xz'
  'zlib-ng'
  'zstd'
)
makedepends=(
  'git'
  'perl-io-string'
  'perl-timedate'
)
checkdepends=(
  'perl-io-string'
  'perl-test-pod'
)
options=('!emptydirs')
install=dpkg.install
source=(
  "git+https://git.dpkg.org/git/dpkg/dpkg.git?signed#tag=$pkgver"
  origin.archpower
)
b2sums=('70a5abe0c9296a7c340655ade12cc939abeab99bbee61beae4a30880a2df1047dd883c133fc9cdb1817a609e2e606f5c6eb0ef609bf2d962dd09ff897eac00ed'
        '9139a61a5cc44db7e0094b5f89b5bb172f275972e626a3a10f5d3c95eefecc28ceeb7e3d76cace3bdc7c35d3775f8982fe2ba60292bba04787adbff33a397d65')
validpgpkeys=('4F3E74F436050C10F5696574B972BF3EA4AE57A3') # Guillem Jover <guillem@hadrons.org>

prepare() {
  cd $pkgname
  # Fix zsh completions install location
  # https://gitlab.archlinux.org/archlinux/packaging/packages/dpkg/-/issues/1
  sed -i 's|zsh/vendor-completions|zsh/site-functions|' configure.ac

  ./autogen
}

build() {
  cd $pkgname
  CFLAGS+=' -ffat-lto-objects' \
    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --disable-start-stop-daemon
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -vDm644 /dev/null "$pkgdir/var/lib/dpkg/status"
  install -vDm644 /dev/null "$pkgdir/var/lib/dpkg/available"
  install -vDm644 "$srcdir/origin.archpower" "$pkgdir/etc/dpkg/origins/archpower"
  ln -vsf archlinux "$pkgdir/etc/dpkg/origins/default"
}
