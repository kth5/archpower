# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Alad Wenter <alad (at) archlinux.info>
# Contributor: Jochen Schalanda <jochen+aur (at) schalanda.name>
# Contributor: C. Dominik BÃ³di <dominik.bodi@gmx.de>
# Contributor: Pierre Carrier <pierre@spotify.com>
# Contributor: Thomas Dziedzic <gostrc (at) gmail>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Contributor: seblu <seblu+arch (at) seblu.net>
# Contributor: squiddo <squiddo (at) intheocean.net>
# Contributor: dront78 <dront78 (at) gmail.com>
# Contributor: hugelgupf <ckoch (at) cs.nmt.edu>

pkgname=dpkg
pkgver=1.22.14
pkgrel=1
pkgdesc='The Debian Package Manager tools'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://tracker.debian.org/pkg/dpkg"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'gcc-libs'
  'glibc'
  'libmd'
  'perl'
  'xz'
  'zlib'
  'zstd'
)
makedepends=(
  'git'
  'perl-io-string'
  'perl-timedate'
)
checkdepends=(
  'perl-io-string'
  'perl-test-pod'
)
options=('!emptydirs')
install=dpkg.install
source=(
  "git+https://git.dpkg.org/git/dpkg/dpkg.git?signed#tag=$pkgver"
  origin.archpower
)
sha256sums=('7237aec25b913e536d1fd38d143697e8c0698fd9041776deedc216227c43a7d8'
            '63a0675bf8f2c490c8530cc2a67f8ad8892a224e20bb0dbfeda5e0b7780f9af7')
validpgpkeys=('4F3E74F436050C10F5696574B972BF3EA4AE57A3') # Guillem Jover <guillem@hadrons.org>

prepare() {
  cd $pkgname
  # Fix zsh completions install location
  # https://gitlab.archlinux.org/archlinux/packaging/packages/dpkg/-/issues/1
  sed -i 's|zsh/vendor-completions|zsh/site-functions|' configure.ac

  ./autogen
}

build() {
  cd $pkgname
  CFLAGS+=' -ffat-lto-objects' \
    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --disable-start-stop-daemon
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -vDm644 /dev/null "$pkgdir/var/lib/dpkg/status"
  install -vDm644 /dev/null "$pkgdir/var/lib/dpkg/available"
  install -vDm644 "$srcdir/origin.archpower" "$pkgdir/etc/dpkg/origins/archpower"
  ln -vsf archlinux "$pkgdir/etc/dpkg/origins/default"
}
