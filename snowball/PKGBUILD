# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=snowball
pkgname=(
  snowball
  libstemmer
)
pkgver=3.0.1
pkgrel=1
pkgdesc="String processing language for creating stemming algorithms"
url="https://snowballstem.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(git)
source=(
  "git+https://github.com/snowballstem/snowball#tag=v$pkgver"
  0001-Make-libstemmer-a-shared-library.patch
)
b2sums=('01ee54f79f76d12bfbcf89d3b6f2b036756c704bee2232a5401aba91f62f4da01229d7efe8a738602871fb77b33e1d1753def3efee0e51178a9228ad71655296'
        '91334cdd8142db106db709fdd5d5e9dd910505f6138d2ee40c0305aed3a652e5dac840c8abcca9784a77feea00a4c5dbec310e9190a25e7991b9d373cd3c129b')

prepare() {
  cd snowball
  git apply -3 ../0001-Make-libstemmer-a-shared-library.patch
}

build() {
  cd snowball
  make
}

# No check - needs external test data

package_snowball() {
  depends+=(libstemmer)

  cd snowball

  install -D snowball stemwords -t "$pkgdir/usr/bin"

  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_libstemmer() {
  pkgdesc="Stemming library supporting several languages"
  provides=(libstemmer.so)

  cd snowball

  install -Dm644 include/libstemmer.h -t "$pkgdir/usr/include"

  install -Dm644 libstemmer.so.0 "$pkgdir/usr/lib/libstemmer.so.0.0.0"
  ln -s libstemmer.so.0.0.0 "$pkgdir/usr/lib/libstemmer.so.0"
  ln -s libstemmer.so.0 "$pkgdir/usr/lib/libstemmer.so"

  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
