# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Camille Moncelier <pix@devlife.org>

pkgname=m17n-lib
pkgver=1.8.4
pkgrel=2.1
pkgdesc='Multilingual text processing library (runtimes)'
url='https://www.nongnu.org/m17n/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
depends=('libxft' 'm17n-db' 'fribidi' 'libthai' 'libxml2' 'gd' 'libotf')
makedepends=('anthy' 'ispell')
optdepends=('anthy: to use the anthy input method'
            'ispell: to use the ispell input method')
options=('!makeflags' '!emptydirs')
validpgpkeys=('D68584F9B40A6502FD7C53015F592A4029890630')
source=("https://download.savannah.gnu.org/releases/m17n/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c6a2582c6e4f2a8c2e7a2844fa5c7eb363aad2538b052f203c710649dd421cc8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/freetype-config/pkg-config freetype2/' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --build=${CHOST}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
