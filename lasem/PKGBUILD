# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Adria Arrufat <adria.arrufat@protonmail.ch>

pkgbase=lasem
pkgname=(
  lasem
  lasem-docs
)
pkgver=0.6.0
pkgrel=3
pkgdesc='SVG and MathML rendering library'
arch=(x86_64 powerpc powerpc64 powerpc64le espresso)
url='https://github.com/LasemProject/lasem'
license=(LGPL-2.1-or-later)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  libxml2
  pango
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
)
checkdepends=(librsvg)
source=("git+https://github.com/LasemProject/lasem.git#tag=$pkgver")
b2sums=(721f335c94878ad788851d0d0e48a6beb6717bf3f7bc9980f1cac696491fd57623219112e548071b2f201d354aa450c3450009fcb325dd0810f90af94084f518)

prepare() {
  cd $pkgbase

  # Skip broken test
  rm tests/data/svg/batik/spec/filters/feImage.svg
}

build() {
  arch-meson $pkgbase build \
    -D documentation=enabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lasem() {
  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_lasem-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
