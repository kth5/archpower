# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pciutils
pkgver=3.13.0
pkgrel=2.1
pkgdesc="PCI bus configuration space access library and tools"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
license=('GPL-2.0-only')
url="https://mj.ucw.cz/sw/pciutils/"
depends=('glibc' 'hwdata' 'kmod')
makedepends=('git')
optdepends=('which: for update-pciids'
            'grep: for update-pciids'
            'curl: for update-pciids')	
source=("git+https://git.kernel.org/pub/scm/utils/pciutils/pciutils.git#tag=v$pkgver?signed")
validpgpkeys=(
              'C466A56CADA981F4297D20C31F3D0761D9B65F0B' # Martin Mares <mj@ucw.cz>
             )
b2sums=('51f41d1ec60eeea51f8534a0dd27f9b0aed4230dbea871d1161273019ee305c1f3536c81c295d13ff40edafe86ae76b0eec9a808f0fd2f90e3f5a7dd34e80ea7')

build() {
  cd "${pkgname}"
  make OPT="${CFLAGS}" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man SBINDIR=/usr/bin lib/libpci.a 
  cp lib/libpci.a "${srcdir}/"
  make clean
  make OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SBINDIR=/usr/bin SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
  cd "${pkgname}"
  make SHARED=yes PREFIX=/usr SBINDIR=/usr/bin SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
  rm -rf "${pkgdir}"/usr/share/hwdata
}

