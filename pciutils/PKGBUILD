# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pciutils
pkgver=3.14.0
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL-2.0-only')
url="https://mj.ucw.cz/sw/pciutils/"
depends=(
  glibc
  hwdata
  kmod
)
makedepends=('git')
optdepends=(
  'which: for update-pciids'
  'grep: for update-pciids'
  'curl: for update-pciids'
)	
source=(
  git+https://git.kernel.org/pub/scm/utils/pciutils/pciutils.git#tag=v$pkgver?signed
)
validpgpkeys=(
  C466A56CADA981F4297D20C31F3D0761D9B65F0B # Martin Mares <mj@ucw.cz>
)
b2sums=('cf3855b52ae27bfad231fb78500ab14030144ed874d6b18a0cc39bfe61a37b2c4138f7492947c6fd3cd9466afa375447e3fda67be7e57376fde61c46cea5d19c')

build() {
  cd "${pkgname}"
  make OPT="${CFLAGS}" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man SBINDIR=/usr/bin lib/libpci.a 
  cp lib/libpci.a "${srcdir}/"
  make clean
  make OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SBINDIR=/usr/bin SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man all
}

package() {
  cd "${pkgname}"
  make SHARED=yes PREFIX=/usr SBINDIR=/usr/bin SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
  rm -rf "${pkgdir}"/usr/share/hwdata
}

