# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=liblangtag
pkgver=0.6.7
pkgrel=1
pkgdesc="interface library to access/deal with tags for identifying languages"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://bitbucket.org/tagoh/liblangtag/wiki/Home"
license=('LGPL3' 'MPL2')
depends=('libxml2' 'glib2' 'glibc')
makedepends=('gobject-introspection')
optdepends=('gobject-introspection-runtime: use with liblangtag-gobject')
source=(https://bitbucket.org/tagoh/liblangtag/downloads/$pkgname-$pkgver.tar.bz2)
sha256sums=('5ed6bcd4ae3f3c05c912e62f216cd1a44123846147f729a49fb5668da51e030e')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
