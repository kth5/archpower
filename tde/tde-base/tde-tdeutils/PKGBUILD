# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David C. Rankin <drankinatty at gmail dot com>

_mod=tdeutils
_cat=core/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.3
pkgrel=2
pkgdesc="The TDE something - with Trinity upstream"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-base')
depends=('tde-tdebase')
makedepends=('tde-cmake-trinity' 'python3')
optdepends=()
provides=("${_mod}")
conflicts=("trinity-${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('c9299c7a0c60c1ba8d1d6a8cace6ba78')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

build() {
   msg "Creating out-of-source build directory: ${srcdir}/${_builddir}"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   ## cmake call
   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver} \
     -GNinja \
     -DCMAKE_VERBOSE_MAKEFILE=ON \
     -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
     -DBUILD_ALL=ON \
     -DWITH_DPMS=ON \
     -DWITH_XSCREENSAVER=ON

   msg "Building - ${pkgname#*-}..."
   ninja $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  DESTDIR="${pkgdir}" ninja -j1 install
}
