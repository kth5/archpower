# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=tdepim
_cat=core/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.4
pkgrel=1
pkgdesc="Trinity Desktop PIM Components"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-extra')
depends=(
  'boost'
  'boost-libs'
  'gnokii'
  'libxss'
  'tde-tdelibs'
  'tde-libcaldav'
  'tde-libcarddav'
)
makedepends=('tde-cmake-trinity')
provides=("${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('2aa7a4a862885862b05ffd9ee477b85d')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

build() {
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p ${srcdir}/build
  cd ${srcdir}/build

  msg "Starting cmake..."
  cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver} \
    -GNinja \
    -DCMAKE_C_FLAGS="-fpermissive" \
    -DCMAKE_CXX_FLAGS="-fpermissive" \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX=$TDEDIR \
    -DWITH_ALL_OPTIONS=ON \
    -DBUILD_ALL=ON

  msg "Building - ${pkgname#*-}..."
  ninja $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  DESTDIR="${pkgdir}" ninja -j1 install
  # Removing /opt/trinity/share/icons/default.tde to prevent a conflict
  rm -rf ${pkgdir}/opt/trinity/share/icons/default.tde
}
