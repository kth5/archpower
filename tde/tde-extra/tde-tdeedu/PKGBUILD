# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: e1z0 <justinas at eofnet dot lt>

_mod=tdeedu
_cat=core/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.4
pkgrel=1
pkgdesc="Educational utilities for the TDE Desktop"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-extra')
depends=('tde-tdelibs')
makedepends=('tde-cmake-trinity' 'boost' 'boost-libs' 'doxygen' 'python3' 'texlive-bin')
optdepends=()
provides=("${_mod}")
conflicts=("trinity-${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('6eeeec9a04028fecb186933596f194ca')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

build() {
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   export PKG_CONFIG_PATH=${TDEDIR}/lib/pkgconfig:${PKG_CONFIG_PATH}

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver} \
     -GNinja \
     -DCMAKE_VERBOSE_MAKEFILE=ON \
     -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
     -DBUILD_ALL=ON \
     -DWITH_ALL_OPTIONS=ON \
     -DWITH_OCAML_SOLVER=OFF \
     -DWITH_LIBUSB=OFF

   msg "Building - ${pkgname#*-}..."
   ninja $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  DESTDIR="${pkgdir}" ninja -j1 install
}
