# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=xdg-desktop-portal-tde
_cat=applications/system/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod/tde-}"
pkgver=14.1.5
pkgrel=1
pkgdesc="Trinity Desktop Enviroment $_cat$_mod"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
provides=("${_mod}")
groups=('tde-complete')
depends=(
  'tde-tdelibs'
  'xdg-desktop-portal'
)
makedepends=(
  'tde-cmake-trinity'
  'dbus-1-tqt'
)
optdepends=()
conflicts=("trinity-${_kdemod}" "kdemod3-${_kdemod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!strip' '!debug')
install=

source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('f4257468572545250408a7439f437f78')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$TQTDIR" ] || TQTDIR=${TDEDIR}/tqt3

build() {
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  export PKG_CONFIG_PATH=${TDEDIR}/lib/pkgconfig:${PKG_CONFIG_PATH}

  msg "Starting cmake..."
  cmake ${srcdir}/${_mod}-trinity-${pkgver} \
    -GNinja \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DSYSCONF_INSTALL_DIR=${TDEDIR}/etc \
    -DBUILD_ALL=ON \
    -DWITH_ALL_OPTIONS=ON

  msg "Building - ${pkgname}..."
  ninja $NUMJOBS
}

package() {
  msg "Packaging - ${pkgname}-${pkgver}"
  cd "$srcdir/build"
  DESTDIR="$pkgdir" ninja -j1 install
}
