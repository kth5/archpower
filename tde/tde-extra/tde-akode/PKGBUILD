# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Sl√°vek Banko <slavek.banko@axis.cz>

_mod=akode
_cat=dependencies/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.4
pkgrel=1
pkgdesc="akode plugin for aRts"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-extra')
depends=('alsa-lib'
	'audiofile'
	'libmad'
	'libogg'
	'libpulse'
	'libsamplerate'
	'libvorbis'
	'speex'
)
makedepends=('tde-cmake-trinity')
provides=("${_mod}")
conflicts=("kdemod3-${_mod}" "${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('4057869cbce2fb3932255860373a83b1')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

build() {
   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p build
   cd build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver}/ \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
      -DWITH_ALL_OPTIONS=ON \
      -DWITH_JACK_SINK=OFF

   msg "Building $pkgname..."
   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   DESTDIR="$pkgdir" ninja -j1 install
}
