# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: e1z0 <justinas at eofnet dot lt>

_mod=tde-style-qtcurve
_cat=applications/themes/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod/tde-}"
pkgver=14.1.5
pkgrel=1
pkgdesc="TDE Style QTCurve"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc espresso)
url="http://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-extra')
depends=('tde-tdelibs')
makedepends=('tde-cmake-trinity')
provides=("${_mod}")
conflicts=("trinity-${_kdemod}" "kdemod3-${_kdemod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!strip' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('79139a7d7c81999178cbfa5ff3f499de')
install=''

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

build() {
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  msg "Starting cmake..."
  cmake ${srcdir}/${pkgname}-trinity-${pkgver} \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DSYSCONF_INSTALL_DIR=${TDEDIR}/etc \
    -DBUILD_ALL=ON \
    -DWITH_GCC_VISIBILITY=ON

  msg "Building - ${pkgname}..."
  ninja $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  DESTDIR="${pkgdir}" ninja -j1 install
}
