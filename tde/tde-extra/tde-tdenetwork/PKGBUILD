# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=tdenetwork
_cat=core/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.4
pkgrel=1.1
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-extra')
pkgdesc="Trinity Desktop Network Components"
depends=('tde-tdelibs' 'tde-tqca' 'libgadu' 'wireless_tools' 'wpa_supplicant')
makedepends=('tde-cmake-trinity')
provides=("${_mod}")
conflicts=("trinity-${_mod}" "trinity-${_kdemod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('7b98f418541811ebcdb00afe49ae8eed')
# install=''

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$TQTDIR" ] || TQTDIR=${TDEDIR}/tqt3

build() {
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   export PKG_CONFIG_PATH=${TDEDIR}/lib/pkgconfig:${TQTDIR}/lib/pkgconfig:${PKG_CONFIG_PATH}

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver} \
      -GNinja \
      -DCMAKE_VERBOSE_MAKEFILE=ON \
      -DCMAKE_INSTALL_PREFIX=$TDEDIR \
      -DBUILD_ALL=ON \
      -DWITH_ALL_OPTIONS=ON \
      -DWITH_JINGLE=OFF \
      -DWITH_SPEEX=OFF \
      -DWITH_XMMS=OFF \
      -DWITH_SLP=OFF \
      -DBUILD_KOPETE_PROTOCOL_ALL=ON \
      -DBUILD_KOPETE_PROTOCOL_SMS=OFF \
      -DBUILD_KOPETE_PROTOCOL_MEANWHILE=OFF \
      -DBUILD_KOPETE_PLUGIN_ALL=ON

   msg "Building - ${pkgname#*-}..."
   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   DESTDIR="${pkgdir}" ninja -j1 install
}
