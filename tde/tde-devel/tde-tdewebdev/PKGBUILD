# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: e1z0 <justinas at eofnet dot lt>

_mod=tdewebdev
_cat=core/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.3
pkgrel=1
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-devel')
pkgdesc="Trinity Web Developer Tools"
depends=('tde-tdelibs')
makedepends=('tde-cmake-trinity' 'tde-tdesdk')
provides=("${_mod}")
conflicts=("trinity-${_mod}" "trinity-${_kdemod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!strip')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz"
        'bp000-kxsldbg-remove-sax1-initialization.diff')
md5sums=('88bd4804ce6daf838d0f3679939ebee0'
         '8a97f188b88a3e804b8814b32190812a')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

prepare() {
  msg "Apply backported patches"
  cd ${srcdir}/${pkgname#*-}-trinity-${pkgver} || exit 1
  patch -p1 < ${srcdir}/bp000-kxsldbg-remove-sax1-initialization.diff
}

build() {
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver} \
      -GNinja \
      -DCMAKE_VERBOSE_MAKEFILE=ON \
      -DCMAKE_INSTALL_PREFIX=$TDEDIR \
      -DBUILD_ALL=ON \
      -DWITH_ALL_OPTIONS=ON

   msg "Building - ${pkgname#*-}..."
   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   DESTDIR="${pkgdir}" ninja -j1 install
}
