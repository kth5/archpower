# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=libkexiv2
_cat=libraries/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.5
pkgrel=1
pkgdesc="The EXIV2 Library interface for TDE kipi-plugins"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-libs')
depends=('tde-tdelibs' 'exiv2')
makedepends=('tde-cmake-trinity')
optdepends=()
provides=("${_mod}")
conflicts=("trinity-${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('143bbdfdd6c12ff7334bf2557e5b619c')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$TQTDIR" ] || TQTDIR=${TDEDIR}/tqt3

build() {
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver}/ \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
      -DWITH_ALL_OPTIONS=ON

   msg "Building $pkgname..."
   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   DESTDIR="$pkgdir" ninja -j1 install
}
