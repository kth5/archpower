# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=libkipi
_cat=libraries/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.3
pkgrel=1
pkgdesc="The library for apps that want to use TDE kipi-plugins"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-libs')
depends=('tde-tdelibs')
makedepends=('tde-cmake-trinity')
optdepends=()
provides=("${_mod}")
conflicts=("trinity-${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('8117fe1eb2e72ed37953da168dd2ef3a')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$TQTDIR" ] || TQTDIR=${TDEDIR}/tqt3

build() {
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver}/ \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
      -DWITH_ALL_OPTIONS=ON

   msg "Building $pkgname..."
   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   DESTDIR="$pkgdir" ninja -j1 install
}
