# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Sl√°vek Banko <slavek.banko@axis.cz>

_mod=tde-cmake-trinity
_cat=dependencies/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod/tde-}"
pkgver=14.1.3
pkgrel=1
pkgdesc="TDE CMake modules"
arch=(any)
url="https://scm.trinitydesktop.org/scm/git/tde-cmake"
license=('GPL')
provides=("${_mod}")
groups=('tde-core')
depends=(
  'cmake'
  'perl'
  'ninja'
  'pkgconfig'
)
makedepends=()
optdepends=()
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
install=

source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-${pkgver}.tar.xz")
md5sums=('2b7f154dc87a5c623cea43e3a4ec3b4e')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$TQTDIR" ] || TQTDIR=${TDEDIR}/tqt3

build() {
  msg "Creating out-of-source build directory: ${srcdir}/build"
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  msg "Starting cmake..."
  cmake ${srcdir}/${_mod}-${pkgver} \
    -GNinja \
    -DCMAKE_VERBOSE_MAKEFILE=ON

  msg "Building - ${pkgname}..."
  ninja $NUMJOBS
}

package() {
  msg "Packaging - ${pkgname}-${pkgver}"
  cd "$srcdir/build"
  DESTDIR="$pkgdir" ninja -j1 install
}
