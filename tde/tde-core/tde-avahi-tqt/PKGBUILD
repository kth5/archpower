# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=avahi-tqt
_cat=dependencies/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.5
pkgrel=1
pkgdesc="Trinity Avahi-TQt"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-core')
depends=('tde-tqtinterface' 'avahi' 'nss-mdns')
# nss-mdns should probably move to tdelibs depends
makedepends=('tde-cmake-trinity')
provides=("${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!strip' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('8fb0e6c388efa1a0d4269ba8a46e82c8')

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

build() {
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver}/ \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
      -DWITH_ALL_OPTIONS=ON

   msg "Building - $pkgname..."
   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname"
   cd ${srcdir}/build
   DESTDIR="$pkgdir" ninja -j1 install
}
