# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=tqtinterface
_cat=dependencies/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.5
pkgrel=1
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL2')
groups=('tde-core')
pkgdesc="TQt interface for the TDE desktop"
depends=('tde-tqt3' 'glu')
makedepends=('tde-cmake-trinity')
provides=("${_mod}")
conflicts=("trinity-${_kdemod}" "kdemod3-${_kdemod}")
replaces=("trinity-${_mod}")
# makeopts=('libtool' '!emptydirs' '!strip')
options=('staticlibs' 'libtool' '!emptydirs' '!strip' '!debug')

source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('e971775205076bf61d542a21b19df5f0')

install="trinity-${_mod}.install"

_prefix=/usr
[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity
[ -n "$TQTDIR" ] || TQTDIR=${TDEDIR}/tqt3

build() {
   cd $srcdir
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p build
   cd build

   export PKG_CONFIG_PATH=${TDEDIR}/lib/pkgconfig:${TQTDIR}/lib/pkgconfig:${PKG_CONFIG_PATH}

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver}/ \
     -GNinja \
     -DCMAKE_INSTALL_PREFIX=${_prefix} \
     -DQT_PREFIX_DIR=${TQTDIR} \
     -DTQT_INCLUDE_DIR=${TQTDIR}/include \
     -DMOC_EXECUTABLE=${TQTDIR}/bin/moc

   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   DESTDIR="${pkgdir}" ninja -j1 install

   # Create and install ld.so.conf.d file
   install -d -m755 $pkgdir/etc/ld.so.conf.d/
   echo ${TDEDIR}/lib > $pkgdir/etc/ld.so.conf.d/trinity.conf
   echo ${TDEDIR}/lib/trinity >> $pkgdir/etc/ld.so.conf.d/trinity.conf
}
