# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

_mod=arts
_cat=dependencies/
_kdemod="${_mod/tde/kde}"

pkgname="tde-${_mod}"
pkgver=14.1.4
pkgrel=1
pkgdesc="Trinity aRts Sound System and libraries"
arch=(i686 x86_64 aarch64 armv7h powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://scm.trinitydesktop.org/scm/git/${_mod}"
license=('GPL')
groups=('tde-core')
depends=('alsa-lib'
	'audiofile'
	'glib2'
	'libmad'
	'libogg'
	'libvorbis'
	'tde-tqtinterface')
makedepends=('tde-cmake-trinity')
optdepends=('audio-convert: A script with an easy to use interface to convert audio files: wav, mp3, ogg, flac, aac, mpc, ape and wma'
	'lame: A high quality MPEG Audio Layer III (MP3) encoder'
	'libmp3splt: Library for splitting mp3 and ogg files without decoding'
	'mpd: Music daemon that plays MP3, FLAC, and Ogg Vorbis files'
	'vorbis-tools: Extra tools for Ogg-Vorbis')
provides=("${_mod}")
conflicts=("kdemod3-${_mod}" "${_mod}")
replaces=("trinity-${_mod}")
options=('staticlibs' 'libtool' '!emptydirs' '!debug')
source=("https://mirror.ppa.trinitydesktop.org/trinity/releases/R${pkgver}/main/${_cat}${_mod}-trinity-${pkgver}.tar.xz")
md5sums=('d65926e76fb4af4bc5a6769388457cab')
install="tde-${_mod}.install"

[ -n "$TDEDIR" ] || TDEDIR=/opt/trinity

build() {
   msg "Creating out-of-source build directory: ${srcdir}/build"
   mkdir -p ${srcdir}/build
   cd ${srcdir}/build

   msg "Starting cmake..."
   cmake ${srcdir}/${pkgname#*-}-trinity-${pkgver}/ \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
      -DWITH_ALL_OPTIONS=ON \
      -DWITH_ESOUND=OFF \
      -DWITH_JACK=OFF

   msg "Building $pkgname..."
   ninja $NUMJOBS
}

package() {
   msg "Packaging - $pkgname-$pkgver"
   cd ${srcdir}/build
   DESTDIR="$pkgdir" ninja -j1 install
}
