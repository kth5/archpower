# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db-tools
pkgver=1.11.0
pkgrel=2
pkgdesc='Tools for managing the osinfo database'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://libosinfo.org/'
license=('GPL-2.0-or-later')
depends=('gcc-libs' 'glib2' 'glibc' 'json-glib' 'libarchive' 'libsoup3' 'libxml2')
makedepends=('meson')
checkdepends=('python-pytest' 'python-requests')
source=("https://releases.pagure.org/libosinfo/$pkgname-$pkgver.tar.xz"{,.asc}
        'osinfo-db-tools-libxml2-2.12.patch')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF'  # Daniel P. Berrange
              '09B9C8FF223EF113AFA06A39EE926C2BDACC177B'  # Fabiano Fidêncio
              '206D3B352F566F3B0E6572E997D9123DE37A484F') # Victor Toso de Carvalho <me@victortoso.com>
sha256sums=('8ba6d31bb5ef07056e38879e070671afbcfec0eb41a87f9950450bbb831b0a1d'
            'SKIP'
            '5088a4d8e141d1ced604e93498f65b1d1aeb0c1f29db9098dad89231ceeb2b5d')

prepare() {
  # Fix build with libxml2 2.12
  # https://gitlab.com/libosinfo/osinfo-db-tools/-/merge_requests/80
  patch -d $pkgname-$pkgver -Np1 -i ../osinfo-db-tools-libxml2-2.12.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
