# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db-tools
pkgver=1.12.0
pkgrel=1
pkgdesc='Tools for managing the osinfo database'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://libosinfo.org/'
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  json-glib
  libarchive
  libsoup3
  libxml2
)
makedepends=(
  git
  meson
)
checkdepends=(
  python-pytest
  python-requests
)
source=("git+https://gitlab.com/libosinfo/$pkgname.git?signed#tag=v$pkgver")
b2sums=(d95c4ab2190637f7cae280b403684d7379cacbcc25c2ff42706bc29da61cd5b3415b0236203d53c91000f00580252572da3d15a883487ce9f594ddcc449fa7d0)
validpgpkeys=(
  DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF # Daniel P. Berrange
  09B9C8FF223EF113AFA06A39EE926C2BDACC177B # Fabiano Fidêncio
  206D3B352F566F3B0E6572E997D9123DE37A484F # Victor Toso de Carvalho <me@victortoso.com>
)

prepare() {
  cd $pkgname
  sed -i 's/-Werror//' meson.build
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
