# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=arcticfox
pkgver=39.2
pkgrel=1
pkgdesc='A fork of Palemoon with TenFourFox enhancements'
arch=(powerpc)
license=(MPL2)
url='https://github.com/wicknix/Arctic-Fox'
depends=(gtk2 libxcb libxt pulseaudio alsa-lib)
makedepends=(zip unzip autoconf2.13 python2 xorgproto python2-psutil python2-setuptools git)
provides=('browser')
source=("git+https://github.com/wicknix/Arctic-Fox.git#tag=v${pkgver}"
        mozconfig
        powerpc-disable-malloc-static-sizes.patch)
sha256sums=('SKIP'
            '7b6941025b6305cf5ec6e937c8f2ca3f368e712b81a0d1807f20772403fe9733'
            '91e85670d39c9b58bb0deae45906b86c0fffdc6b440fa1602e3d334cccb6323a')

prepare() {
  cd Arctic-Fox

  cp ${srcdir}/mozconfig .mozconfig
  [ ! -z "${MAKEFLAGS}" ] &&
    echo "mk_add_options MOZ_MAKE_FLAGS=\"${MAKEFLAGS}\"" >> .mozconfig

  patch -Np1 -i ${srcdir}/powerpc-disable-malloc-static-sizes.patch
}

build() {
  cd Arctic-Fox 

  python2 mach build
}

package() {
  cd Arctic-Fox
  DESTDIR=${pkgdir} python2 mach install
}

# vim: ts=2 sw=2 et:
