# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=composefs
pkgver=1.0.7
pkgrel=1
pkgdesc="A file system for mounting container images"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/containers/composefs"
license=(
  LGPL-2.1-or-later
  'BSD-2-Clause OR GPL-2.0-or-later'
  'GPL-2.0-only OR Apache-2.0'
  GPL-3.0-or-later
)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  fuse3
  go-md2man
  meson
)
checkdepends=(
  python
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('a21e3622dffa1baae96c4b46c393ba3a8672abd4e4f41fcce1ec05ab4ecbb4c9703302a549dae9d2398531deed2142f92ee0b2662b13400821ad29fd76be4689')
b2sums=('a163026ef57ef6f30407db0e43b68bf7e08af3ea28bcccad309799a05b43ff25dad1d3891667bccb6192f2112c367992acd29bde8a69648e6d937f6755acfbe0')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  case "${CARCH}" in
    riscv64) return 0 ;;
  esac
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname-$pkgver/BSD-2-Clause.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
