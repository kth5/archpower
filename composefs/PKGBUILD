# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=composefs
pkgver=1.0.4
pkgrel=1
pkgdesc="A file system for mounting container images"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/containers/composefs"
license=(
  LGPL-2.1-or-later
  'BSD-2-Clause OR GPL-2.0-or-later'
  'GPL-2.0-only OR Apache-2.0'
  GPL-3.0-or-later
)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  fuse3
  go-md2man
)
checkdepends=(
  python
)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
  $pkgname-1.0.4-composefs-info-test.patch::https://github.com/containers/composefs/commit/f7465b3a57935d96451b392b07aa3a1dafb56e7b.patch
)
sha512sums=('bf286ab1b0e10c7268ff703d960a45c5af29536be5e2a64cdd942a20ecacf2a2d4e880c720cb5b0dbda4c790edfe870fcc907c187153568c2c972f72ff0c0abb'
            '4560ed053eb74b62c1758cae9cb999e82e7ec34188f2aa7a4e99fe412ac2eb83014bc795e1c3601533c8487dc8ac23091fdd88da33f20d3dfee90c44df2df227')
b2sums=('def8a5b92ac0cc4273f2832a36a9d8b793de72945fd295f725922cb0d4d7a2b8518c6d749c494c998cc98a3ea01062c8a8cbdff618a43b5520823921ca801b35'
        '29d414bfbd4c8d0aa9265b2cf99d59a16ae7f22e44fcd5dcf68b96c20f3f5d3e48ab78b6cf867f6671625ea8474cc3a8b4ccdf62093819db9545b50f103b0346')

prepare() {
  # fix issue with wrong compose-info location in tests: https://github.com/containers/composefs/issues/305
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.0.4-composefs-info-test.patch

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --enable-man
    --prefix=/usr
    --sbindir=/usr/bin
    --with-fuse
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/BSD-2-Clause.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
