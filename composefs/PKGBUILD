# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=composefs
pkgver=1.0.3
pkgrel=1
pkgdesc="A file system for mounting container images"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/containers/composefs"
license=(
  LGPL-2.1-or-later
  'BSD-2-Clause OR GPL-2.0-or-later'
  'GPL-2.0-only OR Apache-2.0'
  GPL-3.0-or-later
)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(
  fuse3
  go-md2man
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('b09c6034f83bd50171ce9b09e279534d1df87ea2c062098f7cf8663e4e4fab8450b75ebd05b7272c4ca2b08d2f5b22146ad9a5ff0fd8258277373ec691976354')
b2sums=('3dfec7dc91651abf2d0ec4a1e90fd5cf35ea90e25e2ea8cc37408a570eae19ff6adcf79a89aa2c5e0a78722c0dfb21443e5f3d6cf4610d5fbc40b410f6a1e44b')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --enable-man
    --prefix=/usr
    --sbindir=/usr/bin
    --with-fuse
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/BSD-2-Clause.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
