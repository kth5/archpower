# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=links
pkgver=2.30
pkgrel=1
pkgdesc='A text WWW browser, similar to Lynx'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://links.twibright.com/'
license=('GPL')
depends=('brotli' 'libbrotlidec.so' 'bzip2' 'libbz2.so' 'gpm' 'libgpm.so'
         'libevent' 'openssl' 'xz' 'zstd' 'libzstd.so')
makedepends=('libtiff' 'libpng' 'libxt' 'librsvg')
optdepends=('libx11: for using xlinks'
	    'libtiff: for using xlinks'
	    'libpng: for using xlinks'
	    'librsvg: for using xlinks')
source=("http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2"
        'links.desktop')
sha256sums=('c4631c6b5a11527cdc3cb7872fc23b7f2b25c2b021d596be410dadb40315f166'
            'f96bf2638e9c309bfdb857bd62a732b980231b3a683cd585ec872b249c2c1b19')

_configure_options=(
  --prefix=/usr
  --mandir=/usr/share/man
  --disable-javascript
)

prepare() {
  cd "${pkgname}-${pkgver}"

  sed -i '/AC_PROG_CC/a AC_PROG_CXX/' configure.in
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"

  ( cd intl
    ./gen-intl
    ./synclang )

  ./configure \
    "${_configure_options[@]}" \
    --enable-graphics \
    --with-x \
    --with-fb
  make
  mv links xlinks

  ./configure \
    "${_configure_options[@]}" \
    --disable-graphics \
    --without-x \
    --without-fb
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m0755 xlinks "${pkgdir}/usr/bin/xlinks"
  ln -s links.1.gz "${pkgdir}/usr/share/man/man1/xlinks.1.gz"

  install -D -m0644 "${srcdir}/links.desktop" "${pkgdir}/usr/share/applications/links.desktop"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -m0644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm links-48x48.xpm "${pkgdir}/usr/share/pixmaps/"

  install -d "${pkgdir}/usr/share/doc/links/calibration"
  install -m0644 doc/links_cal/* "${pkgdir}/usr/share/doc/links/calibration/"
}
