# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=enchant
pkgver=2.6.3
pkgrel=1
pkgdesc="A wrapper library for generic spell checking"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://abiword.github.io/enchant/"
license=('LGPL')
depends=('glib2')
makedepends=('aspell' 'hunspell' 'hspell' 'nuspell' 'libvoikko')
checkdepends=('unittestpp')
optdepends=('aspell: for aspell based spell checking support'
            'hunspell: for hunspell based spell checking support'
            'libvoikko: for libvoikko based spell checking support'
            'hspell: for hspell based spell checking support'
            'nuspell: for nuspell based spell checking support')
provides=('libenchant-2.so') # enchant_voikko.so enchant_nuspell.so enchant_hunspell.so enchant_hspell.so enchant_aspell.so)
source=("https://github.com/AbiWord/enchant/releases/download/v$pkgver/enchant-$pkgver.tar.gz")
sha512sums=('cfc6afbfbed7bee64b892f087f26b9bf941290f613c877f4ce114415f78e78bea0314046f58a50f2d3392c65434dde4a8bbd30cac9a7b5bbc87dce477f70ca55')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static \
    --enable-relocatable
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
