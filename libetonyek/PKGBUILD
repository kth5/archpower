# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Yue

pkgbase=libetonyek
pkgname=(libetonyek libetonyek-doc)
pkgver=0.1.12
pkgrel=2
pkgdesc='Library and a set of tools for reading and converting Apple Keynote presentations'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://wiki.documentfoundation.org/DLP/Libraries/libetonyek"
license=('MPL-2.0')
makedepends=(
  'boost' 'cppunit' 'doxygen' 'gcc-libs' 'glibc' 'glm' 'gperf' 'liblangtag'
  'librevenge' 'libxml2' 'mdds' 'zlib'
)
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('b9fa82fbeb8cb7a701101060e4f3e1e4ef7c38f574b2859d3ecbe43604c21f83')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --with-mdds=2.1
  make
}

check() {
  cd $pkgbase-$pkgver
  make check
}

package_libetonyek() {
  depends=('libxml2' 'zlib' 'librevenge' 'liblangtag' 'gcc-libs' 'glibc')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-exec install-pkgconfDATA
  make -C inc/libetonyek DESTDIR="$pkgdir" install
}

package_libetonyek-doc() {
  pkgdesc+=" - documentation"

  cd $pkgbase-$pkgver
  make -C docs/doxygen/ DESTDIR="$pkgdir" install
}
