# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=hfsutils
pkgver=3.2.6
pkgrel=2
pkgdesc="User space utils for create and check Apple HFS/HFS+ filesystem"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('openssl')
makedepends=('sh')
license=('GPL')
url="http://www.opensource.apple.com/"
source=("ftp://ftp.mars.org/pub/hfs/${pkgname}-${pkgver}.tar.gz"
        0001-Fix-build-system-issues.patch
        0002-Fix-FTBFS-with-gcc-3.4.patch
        0003-Add-support-for-files-larger-than-2GB.patch
        0004-Add-DUSE_INTERP_RESULT-to-DEFINES-in-Makefile.in.patch
        0005-Fix-missing-inclusion-of-string.h-in-hpwd.c.patch
        0006-hfsutils-3.2.6_p15-drop-manual-autoconf.patch
        0007-hfsutils-3.2.6-fix-tcl-8.6.patch)
sha256sums=('bc9d22d6d252b920ec9cdf18e00b7655a6189b3f34f42e58d5bb152957289840'
            '692140eccf92e5f9848a9290be7ae2ba302e163f1569e12d614c87dfb2b17a7b'
            '29484411cc305fdbe79596b755a7c86c3421e3916ddf558b628b6edcfd5e457d'
            '65063aad0620d5c6db76cbbb61a9a8414fa1a8a3d6e8d7bfb7874eb5e75cf5fb'
            'e9c854d332b03dc30bd53d235a59758009705197ae9e3f301b8c4195e8036d3c'
            'b9649f166787f950e52c6f54ac5abb70c9330f78ad2244a63fd19631bfb2da73'
            '4be7a0119ea2ad7da0f8a31208a08d12637ec0eb5e91d4904199faa83f278bb2'
            '4a0db9c817c1dc015543d3acaaf1d8e007f450a7208ed2509b79faeea6add091')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/0001-Fix-build-system-issues.patch
  patch -Np1 -i ${srcdir}/0002-Fix-FTBFS-with-gcc-3.4.patch
  patch -Np1 -i ${srcdir}/0003-Add-support-for-files-larger-than-2GB.patch
  patch -Np1 -i ${srcdir}/0004-Add-DUSE_INTERP_RESULT-to-DEFINES-in-Makefile.in.patch
  patch -Np1 -i ${srcdir}/0005-Fix-missing-inclusion-of-string.h-in-hpwd.c.patch
  patch -Np1 -i ${srcdir}/0006-hfsutils-3.2.6_p15-drop-manual-autoconf.patch
  patch -Np1 -i ${srcdir}/0007-hfsutils-3.2.6-fix-tcl-8.6.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  CFLAGS+=' -Wno-implicit-int' \
  ./configure --prefix=${pkgdir}/usr || cat config.log
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  mkdir -p ${pkgdir}/usr/{bin,man/man1,share/man}
  make install

  mv ${pkgdir}/usr/man/* ${pkgdir}/usr/share/man/
  rm -rf ${pkgdir}/usr/man
}
