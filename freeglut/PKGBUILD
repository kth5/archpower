# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=freeglut
pkgver=3.8.0
pkgrel=1
pkgdesc="Free OpenGL Utility Toolkit"
url="https://freeglut.sourceforge.net/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(X11)
depends=(
  glibc
  glu # Required by headers
  libgl
  libx11
  libxi
  libxrandr
  libxxf86vm
)
makedepends=(
  cmake
  git
  mesa
  ninja
)
provides=(glut)
conflicts=(glut)
source=("git+https://github.com/FreeGLUTProject/freeglut#tag=v${pkgver/[a-z]/-&}")
b2sums=('2681b876d8867c6fb1f5a4242068434575c0c5ea14ce62b4754f1656bb5297821a95ce24eb14f326acccc929675211e908e6c702796a6d8c72b0f96e6503fbc0')

prepare() {
  cd freeglut
}

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D FREEGLUT_BUILD_STATIC_LIBS=OFF
  )
  cmake -S freeglut -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  ln -s glut.pc "$pkgdir/usr/lib/pkgconfig/freeglut.pc"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" freeglut/COPYING
}

# vim:set sw=2 sts=-1 et:
