# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Christian Hesse <eworm@archlinux.org>

pkgname=libnghttp3
pkgver=1.15.0
pkgrel=1
pkgdesc='HTTP/3 library written in C'
url='https://github.com/ngtcp2/nghttp3'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('MIT')
depends=('glibc')
makedepends=('git')
provides=('libnghttp3.so')
validpgpkeys=('F4F3B91474D1EB29889BD0EF7E8403D5D673C366') # Tatsuhiro Tsujikawa <tatsuhiro.t@gmail.com>
source=("git+https://github.com/ngtcp2/nghttp3.git#tag=v${pkgver}?signed"
        'git+https://github.com/ngtcp2/munit.git'
        'git+https://github.com/ngtcp2/sfparse.git')
sha256sums=('490ce6f743bd9515351020a1182d7e220879ce40ee695d0b019a10eb3a7359bc'
            'SKIP'
            'SKIP')

prepare() {
  cd nghttp3/

  git config --file=.gitmodules submodule.tests/munit.url ../munit/
  git config --file=.gitmodules submodule.lib/sfparse.url ../sfparse/

  git submodule init
  git -c protocol.file.allow=always submodule update

  autoreconf -i
}

build() {
  cd nghttp3/

  ./configure \
    --prefix=/usr
  make
}

check() {
  cd nghttp3/

  make check
}

package() {
  cd nghttp3/

  make DESTDIR="${pkgdir}" install
  install -D -m0644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
