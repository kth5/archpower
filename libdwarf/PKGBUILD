# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
epoch=1
pkgver=2.2.0
pkgrel=1
pkgdesc='A library for handling DWARF Debugging Information Format'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.prevanders.net/dwarf.html'
license=('LGPL-2.1-only' 'GPL-2.0-only' 'BSD-2-Clause' 'BSD-3-Clause' 'LicenseRef-libdwarf-public-domain')
depends=('glibc' 'elfutils' 'zlib' 'zstd')
makedepends=('git' 'meson')
checkdepends=('python')
provides=('libdwarf.so')
options=('staticlibs')
source=("$pkgname::git+https://github.com/davea42/libdwarf-code#tag=libdwarf-$pkgver")
sha512sums=('085029907198afe7d4cfb2f3e0324b6c8e70fbf8c0fa534bfa7a1f352945f6c75927a35974a70bcace97d9aaad8491c688c9fec3b7df53afa1b85d465281b2b6')
b2sums=('991574ae46ceda5de83a406efaa1e8985ce8b23c6ee5062aae72a931489e60b4cf7e57be4efa490e9c204e6dc694fac9857f6791d7156d32e42d5496bc74b292')

build() {
  CFLAGS+=' -ffat-lto-objects'

  arch-meson "$pkgname" build -Ddwarfexample=true

  meson compile -C build
}

check() {
  meson test -C build -j1
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README NEWS

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    COPYING src/lib/libdwarf/LIBDWARFCOPYRIGHT \
    src/bin/dwarfdump/DWARFDUMPCOPYRIGHT
}
