# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
epoch=1
pkgver=2.0.0
pkgrel=1
pkgdesc='A library for handling DWARF Debugging Information Format'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.prevanders.net/dwarf.html'
license=('LGPL-2.1-only' 'GPL-2.0-only' 'BSD-2-Clause' 'BSD-3-Clause' 'LicenseRef-libdwarf-public-domain')
depends=('glibc' 'elfutils' 'zlib' 'zstd')
makedepends=('git' 'meson')
checkdepends=('python')
provides=('libdwarf.so')
options=('staticlibs')
source=("$pkgname::git+https://github.com/davea42/libdwarf-code#tag=libdwarf-$pkgver")
sha512sums=('94d1a1be4db8082130451f5d50e8734e041cbbb6ae16bf5f32668c1431a7cb9b6859a9e1d0b4b9beff95fb36b8aad434ba5ed21be5f7ffa59a0db7707cbbc01e')
b2sums=('d3ff335909c2eb58abc442dd6b134f530e11ff41be54a7e0128df8be7256609a587a25545a5954c13d0072b024bd3c591188b73760c08807107cf78939e1f04d')

build() {
  CFLAGS+=' -ffat-lto-objects'

  arch-meson "$pkgname" build -Ddwarfexample=true

  meson compile -C build
}

check() {
  meson test -C build -j1
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README NEWS

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    COPYING src/lib/libdwarf/LIBDWARFCOPYRIGHT \
    src/bin/dwarfdump/DWARFDUMPCOPYRIGHT
}
