# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
epoch=1
pkgver=2.3.0
pkgrel=1
pkgdesc='A library for handling DWARF Debugging Information Format'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.prevanders.net/dwarf.html'
license=('LGPL-2.1-only' 'GPL-2.0-only' 'BSD-2-Clause' 'BSD-3-Clause' 'LicenseRef-libdwarf-public-domain')
depends=('glibc' 'elfutils' 'zlib' 'zstd')
makedepends=('git' 'meson')
checkdepends=('python')
provides=('libdwarf.so')
options=('staticlibs')
source=("$pkgname::git+https://github.com/davea42/libdwarf-code#tag=libdwarf-$pkgver")
sha512sums=('dab97d58647f3c8d7cfb489da00d434872645c3c8daec4b5854dc284a9aec02d7384524f200417c6ec6366c27a79c708c7846ca8816837a37190e02c8a14e8c6')
b2sums=('172f68704ebed049e8377f0c79ba0fa54e9898aa80013eb7a20a883e33bd09a7d4c433801298547fca574d80991eb223300c85960fc3877d9d02da5a2d9dbb65')

build() {
  CFLAGS+=' -ffat-lto-objects'

  arch-meson "$pkgname" build -Ddwarfexample=true

  meson compile -C build
}

check() {
  meson test -C build -j1
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README NEWS

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    COPYING src/lib/libdwarf/LIBDWARFCOPYRIGHT \
    src/bin/dwarfdump/DWARFDUMPCOPYRIGHT
}
