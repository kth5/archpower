# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=libngtcp2
pkgver=1.6.0
pkgrel=1
pkgdesc='Implementation of IETF QUIC protocol'
url='https://github.com/ngtcp2/ngtcp2'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MIT')
depends=('glibc')
makedepends=('brotli')
provides=('libngtcp2.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ngtcp2/ngtcp2/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('6bd3dbf2943500223b850602c73ce74fd6863c435e3a77fb03d93ba0a3d1d21a')
b2sums=('88744229a9779781535c68dfd4e7637055afc195582b427054d265ceff01733e2e09f45cd35fe55b33aba39420c6d551bc5dfea1dcd78b257c2d3fa2ff95a65a')

prepare() {
  cd ngtcp2-${pkgver}
  autoreconf -i
}

build() {
  cd ngtcp2-${pkgver}
  # add --with-openssl after quic was released in openssl mainline
  ./configure \
    --prefix=/usr \
    --with-libbrotlienc \
    --with-libbrotlidec
  make
}

package() {
  cd ngtcp2-${pkgver}/lib
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
