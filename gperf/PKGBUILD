# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gperf
pkgver=3.3
pkgrel=1
pkgdesc="Perfect hash function generator"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.gnu.org/software/gperf/"
license=('GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
)
source=("https://ftp.gnu.org/pub/gnu/gperf/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('fd87e0aba7e43ae054837afd6cd4db03a3f2693deb3619085e6ed9d8d9604ad8'
            'SKIP')
options=('!makeflags')
validpgpkeys=(
  'EDEB87A500CC0A211677FBFD93C08C88471097CD' # Marcel Schaible <marcel.schaible@studium.fernuni-hagen.de>
  'E0FFBD975397F77A32AB76ECB6301D9E1BBEAC08' # Bruno Haible (Free Software Development) <bruno@clisp.org>
)

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --docdir=/usr/share/gperf/doc/
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
