# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gperf
pkgver=3.1
pkgrel=5.3
pkgdesc="Perfect hash function generator"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://www.gnu.org/software/gperf/"
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'glibc')
makedepends=('texinfo')
source=("https://ftp.gnu.org/pub/gnu/gperf/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('588546b945bba4b70b6a3a616e80b4ab466e3f33024a352fc2198112cdbb3ae2'
            'SKIP')
options=('!makeflags')
validpgpkeys=('EDEB87A500CC0A211677FBFD93C08C88471097CD') # Marcel Schaible <marcel.schaible@studium.fernuni-hagen.de>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --docdir=/usr/share/gperf/doc/
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
