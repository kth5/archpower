# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=irssi
pkgver=1.4.4
pkgrel=3
pkgdesc='Modular text mode IRC client with Perl scripting'
url='https://irssi.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
depends=('glibc' 'glib2' 'openssl' 'libotr' 'perl' 'ncurses' 'libncursesw.so')
makedepends=('meson' 'ninja')
optdepends=('perl-libwww: for the scriptassist script')
replaces=('irssi-otr')
backup=('etc/irssi.conf')
source=(https://github.com/irssi/irssi/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc}
        https://github.com/irssi/irssi/commit/da49ec62e6cc949d3e5359b88abbd0b038d3e23a.patch)
sha512sums=('da28ac7a527be301d0615d6d733e4cf4e09bb6d4f5c70bc33ff70e22439a01f197bb5d91b4432ca74d3ac2dbb3235f30d53efc63a4279de8664923c2ccdbdbea'
            'SKIP'
            '241813e418accea665d8688ab6c42654f0352fceb3e4c2444a465d08010c84fe774cffbd6b92c3635532db13badf2f637e6193c3b0c69c998ac8b5a88975c7b2')
validpgpkeys=('7EE65E3082A5FB06AC7C368D00CCB587DDBEF0E1') # The Irssi project <staff@irssi.org>

prepare() {
  # FS#79325
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../da49ec62e6cc949d3e5359b88abbd0b038d3e23a.patch
}

build() {
  cd ${pkgname}-${pkgver}
  arch-meson Build \
    -Dwith-perl-lib=vendor \
    -Dwith-proxy=yes \
    -Dwith-otr=yes \
    -Dwith-perl=yes \
    -Denable-true-color=yes
  ninja -C Build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C Build install
  install -Dm 644 irssi.conf "${pkgdir}"/etc/irssi.conf
}

# vim: ts=2 sw=2 et:
