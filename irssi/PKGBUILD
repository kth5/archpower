# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=irssi
pkgver=1.4.5
pkgrel=1
pkgdesc='Modular text mode IRC client with Perl scripting'
url='https://irssi.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL')
depends=('glibc' 'glib2' 'openssl' 'libotr' 'perl' 'ncurses' 'libncursesw.so')
makedepends=('meson' 'ninja')
optdepends=('perl-libwww: for the scriptassist script')
replaces=('irssi-otr')
backup=('etc/irssi.conf')
source=(https://github.com/irssi/irssi/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc})
sha512sums=('5d80107ad625feb6d2b3ac807f517c5cfeae58986d3fbfe53ea82f2dee70396f5092abdc6ef137c5651e4515e081591079b37f0e428f69990db493cfe14e4f36'
            'SKIP')
validpgpkeys=('7EE65E3082A5FB06AC7C368D00CCB587DDBEF0E1') # The Irssi project <staff@irssi.org>

build() {
  cd ${pkgname}-${pkgver}
  arch-meson Build \
    -Dwith-perl-lib=vendor \
    -Dwith-proxy=yes \
    -Dwith-otr=yes \
    -Dwith-perl=yes \
    -Denable-true-color=yes
  ninja -C Build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C Build install
  install -Dm 644 irssi.conf "${pkgdir}"/etc/irssi.conf
}

# vim: ts=2 sw=2 et:
