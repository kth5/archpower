# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=0.9.5
pkgrel=1
pkgdesc='Fast and neat VNC server library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://github.com/any1/neatvnc
license=(custom:ISC)
depends=(
  glibc
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  zlib
)
makedepends=(
  git
  meson
  ninja
)
provides=(libneatvnc.so)
_tag=36ef59a83291368d72f471700702a8b6a76f763b
source=(git+https://github.com/any1/neatvnc.git#tag=${_tag})
b2sums=('8ab4ba7b8a66182c3c63296b7066c856644041b18aff35bf40f1e948ea3ab97eec6388e4567692bcc61d4a46c6ca89c9db4031592d89eed512d3839280bc89f6')

pkgver() {
  cd neatvnc
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
