# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=0.9.4
pkgrel=1
pkgdesc='Fast and neat VNC server library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url=https://github.com/any1/neatvnc
license=(custom:ISC)
depends=(
  glibc
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  zlib
)
makedepends=(
  git
  meson
  ninja
)
provides=(libneatvnc.so)
_tag=50fddde8ba4ead044ad61c3302fefbaad2a03965
source=(git+https://github.com/any1/neatvnc.git#tag=${_tag})
b2sums=('31a5e297ca4af1407978e0ac7a695a79b34953f61f0b228f43073e624b5623f1b891dea195d4b4744e7ef8198b72278801fd6d0faf9b8b658ee74518393eac7e')

pkgver() {
  cd neatvnc
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
