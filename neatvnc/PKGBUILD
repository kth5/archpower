# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=0.8.1
pkgrel=3
pkgdesc='Fast and neat VNC server library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url=https://github.com/any1/neatvnc
license=(custom:ISC)
depends=(
  glibc
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  zlib
)
makedepends=(
  git
  meson
  ninja
)
provides=(libneatvnc.so)
_tag=07081567ab21a2b099ceb41ae8cab872a31cbb9a
source=(git+https://github.com/any1/neatvnc.git#tag=${_tag}
        ffmpeg-7.1.patch)
b2sums=('e5739d474e1d1610eed391625776a02c2bd1de93bde13bb88962bb8dec4e38aa8ff3db70065d0d29966dedbab10a2959fb83f631ee216650d2e199414a67c5c7'
        '90ca045278e0bf54b900252836e6d76692ee7d99bc073403a784dfb270dd65aca9384d030558c27097044294a277c523cbbd260417e397635bcac5b33ca0e6b9')

pkgver() {
  cd neatvnc
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd neatvnc
  patch -p1 -i ../ffmpeg-7.1.patch
}

build() {
  arch-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
