# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Gilbert Kennen <gilbert firewatcher org>

pkgname=elixir
pkgver=1.15.0
pkgrel=1

pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="https://elixir-lang.org"
license=('Apache' 'custom:EPL')

arch=(any)

depends=('erlang-nox')
checkdepends=('git')

source=("$pkgname-$pkgver.tar.gz::https://github.com/elixir-lang/elixir/archive/v$pkgver.tar.gz"
        https://github.com/elixir-lang/elixir/commit/73b65eca5af294a8afbed5bc73c178da18f0055c.patch)
sha256sums=('0f4df7574a5f300b5c66f54906222cd46dac0df7233ded165bc8e80fd9ffeb7a'
            'b0b0b4dc662da103de35f63c29d7805fd7fef6d7996c15edcd53c26c22fe9739')

prepare() {
  cd elixir-"$pkgver"
  # https://github.com/elixir-lang/elixir/issues/12677
  patch -Np1 < "../73b65eca5af294a8afbed5bc73c178da18f0055c.patch"
}

build() {
  cd elixir-"$pkgver"
  make
}

check() {
  cd elixir-"$pkgver"
  ERL_EPMD_PORT=5369 make test

  # The elixir test suite starts up epmd and then doesn't kill it again afterwards.
  epmd -port 5369 -kill
}

package() {
  cd elixir-"$pkgver"
  mkdir -p "$pkgdir"/usr/share/licenses/"$pkgname"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
