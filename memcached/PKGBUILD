# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.6.39
pkgrel=1
pkgdesc='Distributed memory object caching system'
url='https://memcached.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('BSD-3-Clause')
depends=(
  'glibc'
  'libevent'
  'libseccomp'
  'libseccomp.so'
)
makedepends=('git')
optdepends=('perl: for memcached-tool usage')
source=("git+https://github.com/memcached/memcached.git#tag=${pkgver}"
        memcached.service.patch
        memcached.tmpfiles
        memcached.sysusers
        0001-tests-enable-drop_privileges-by-default-for-better-s.patch
        0001-seccomp-extend-syscalls-tested-on-Linux-6.1-with-gli.patch)
sha512sums=('1cf9e9c0f1ab53cec0d6526203762d6988b52e82648354f270baa625eb490eb0b94236a012afffea647f1536590f3572050ff59321c7ccdc7ede5a18a18e77c6'
            'dd21f49062054c601469f9abc9ee39a0f06c604e8805e7bc9bf2cfa65429962f93e7ac67764257c578fc15b1e75394956f350162c99b084c76a6e14d9331f31f'
            '960705ff74d25afed477e0b2a5872a3a4fb49ed3105a351f0d0224abc947778f9dbda81e80be94ab636da4a8411a9dd56a8fd4513e5b86a3096a14fa67f1548b'
            'fe99150ffd9de464c96e6bad802d17bad0f2a57477f7f05e63a5fc7f74a7414833b3b787e73908edf981075ea405d1114a89e5a9a753d9c09eb1181210143cf6'
            '5da2a4fb0cdaefe9527dec67e01d881161c49a1593f11ddccf28fb55cbc25fe841ea8eca8a50387e2fef5bc304f12a5c9af4909543eafc0f8865dea39f9e8355'
            '8d2518507cf843202dc90c10f6fce3c3fc9b41c7aaf10ff0b862c31d30bbbec7de92f9be0327ea3e58e77e29ae2a12b2f47370502bbddc04324f394cfdd7d4a8')

prepare() {
  cd ${pkgname}
  patch -Np1 < ../0001-seccomp-extend-syscalls-tested-on-Linux-6.1-with-gli.patch
  patch -Np1 < ../0001-tests-enable-drop_privileges-by-default-for-better-s.patch
  patch -Np1 < ../memcached.service.patch
  sed -e 's/^##safer##//g' -i scripts/*.service

  ./autogen.sh
}

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/usr \
    --enable-seccomp
  make
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd ${pkgname}
  make test
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install

  install -vDm 755 scripts/memcached-tool -t "${pkgdir}/usr/bin"
  install -vDm 644 scripts/memcached-tool.1 -t "${pkgdir}/usr/share/man/man1"

  install -vDm 644 scripts/*.service -t "${pkgdir}/usr/lib/systemd/system"
  install -vDm 644 ../memcached.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/memcached.conf"
  install -vDm 644 ../memcached.sysusers "${pkgdir}/usr/lib/sysusers.d/memcached.conf"

  install -vDm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

# vim: ts=2 sw=2 et:
