# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=fop
pkgver=2.11
pkgrel=1
pkgdesc='XSL-FO to PDF formatter'
arch=(any)
url='https://xmlgraphics.apache.org/fop/'
license=(Apache-2.0)
depends=(java-avalon-framework 'java-batik=1.17' java-commons-io java-commons-logging java-xmlgraphics-commons)
makedepends=(apache-ant jdk8-openjdk maven)
source=("https://dlcdn.apache.org/xmlgraphics/$pkgname/source/$pkgname-$pkgver-src.tar.gz"
        fop.conf)
b2sums=('880324d21ffa4ce08da4425e21a2440e9fbad47ccf5aa5f465ef599132349d4c6d461ec35ca89806d172224ee670a5032325a3b939622e0dc7dde42a706ca15a'
        '928ced5f2591d2537829d74ae94c6f59e1be2f12eddf44e2f7fe5fd06606e94a5911396d6ef8596137449524c9d0d13ef8896bc4fb5e58096bd9948708879857')
backup=(etc/fop.conf)

prepare() {
  cd $pkgname-$pkgver/$pkgname
  cp -f "$srcdir/fop.conf" fop.conf
  pacman -Ql java-batik | grep \.jar | sed 's/java-batik /CLASSPATH="$CLASSPATH:/g;s/$/"/g' >> fop.conf
}

build() {
  export JAVA_HOME=/usr/lib/jvm/java-8-openjdk
  cd $pkgname-$pkgver
  ant -f $pkgname/build.xml package
}

package() {
  cd $pkgname-$pkgver/$pkgname
  install -Dm755 -t "$pkgdir/usr/bin" $pkgname
  install -Dm644 -t "$pkgdir/usr/share/java/fop" build/fop.jar
  install -Dm644 -t "$pkgdir/etc" fop.conf
}
