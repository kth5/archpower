# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=fop
pkgver=2.11
pkgrel=2
pkgdesc='XSL-FO to PDF formatter'
arch=(any)
url='https://xmlgraphics.apache.org/fop/'
license=(Apache-2.0)
depends=(java-avalon-framework 'java-batik=1.17' java-commons-io java-commons-logging java-xmlgraphics-commons)
makedepends=(apache-ant git jdk8-openjdk maven)
source=("$pkgname::git+https://github.com/apache/xmlgraphics-fop#tag=${pkgver//./_}"
        fop.conf)
b2sums=('9c43bdad973804b53293cda1fe9305cefd8515c7e5a115d4d4d079c056e1f7991845db146495a162a9ce9e4aee24239c655d39858601db95939e7782232a958d'
        '928ced5f2591d2537829d74ae94c6f59e1be2f12eddf44e2f7fe5fd06606e94a5911396d6ef8596137449524c9d0d13ef8896bc4fb5e58096bd9948708879857')
backup=(etc/fop.conf)

prepare() {
  cd $pkgname/$pkgname
  cp -f "$srcdir/fop.conf" fop.conf
  pacman -Ql java-batik | grep \.jar | sed 's/java-batik /CLASSPATH="$CLASSPATH:/g;s/$/"/g' >> fop.conf
}

build() {
  export JAVA_HOME=/usr/lib/jvm/java-8-openjdk
  cd $pkgname
  ant -f $pkgname/build.xml package
}

package() {
  cd $pkgname/$pkgname
  install -Dm755 -t "$pkgdir/usr/bin" $pkgname
  install -Dm644 -t "$pkgdir/usr/share/java/fop" build/fop.jar
  install -Dm644 -t "$pkgdir/etc" fop.conf
}
