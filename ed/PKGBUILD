# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=ed
pkgver=1.20.2
pkgrel=2
pkgdesc='A POSIX-compliant line-oriented text editor'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-only)
url="https://www.gnu.org/software/ed/ed.html"
depends=(glibc
         sh)
options=(!emptydirs)
_archive="$pkgname-$pkgver"
source=("https://ftp.gnu.org/gnu/ed/$_archive.tar.lz"{,.sig})
sha256sums=('65fec7318f48c2ca17f334ac0f4703defe62037bb13cc23920de077b5fa24523'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742') # Antonio Diaz Diaz

build() {
	cd "$_archive"
	./configure \
		--prefix=/\
		--bindir=/usr/bin \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		"CPPFLAGS=$CPPFLAGS" \
		"CFLAGS=$CFLAGS" \
		"LDFLAGS=$LDFLAGS"
	make
}

check() {
	make -C "$_archive" check
}

package() {
	make -C "$_archive" "DESTDIR=$pkgdir" install
}
