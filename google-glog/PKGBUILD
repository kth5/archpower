# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ian Yang <doit.ian@gmail.com>

pkgname=google-glog
pkgver=0.7.1
pkgrel=1
pkgdesc="Logging library for C++"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(BSD-3-Clause)
url="https://github.com/google/glog"
depends=(
  gcc-libs
  gflags
  glibc
)
makedepends=(cmake)
provides=(libglog.so)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('00e4a87e87b7e7612f519a41e491f16623b12423620006f59f5688bfd8d13b08')

build() {
  case "${CARCH}" in
    powerpc|riscv64) _cmake_args+=(-DCMAKE_CXX_FLAGS="-latomic") ;;
  esac

  cd glog-$pkgver
  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_PKGCONFIG=ON ${_cmake_args[@]}
  cmake --build build
}

check() {
  cd glog-$pkgver
  ctest --test-dir build --output-on-failure -E stacktrace
}

package() {
  cd glog-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
