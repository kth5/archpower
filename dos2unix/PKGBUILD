# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski
# Contributor: Renato Garcia <fgar.renatoATgmailDOTcom>
# Contributor: Gerson E. Ruotolo <gersonruotolo@globo.com>

pkgname=dos2unix
pkgver=7.5.2
pkgrel=1
pkgdesc='Text file format converter'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('glibc')
makedepends=('perl')
conflicts=('hd2u')
source=(https://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('F8F1BEA490496A09CCA328CC38C1F572B12725BE')
sha256sums=('264742446608442eb48f96c20af6da303cb3a92b364e72cb7e24f88239c4bf3a'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  make LDFLAGS_USER="${LDFLAGS}"
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
