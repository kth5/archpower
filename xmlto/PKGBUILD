# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>
# Contributor: Robert Stoffers <rob1@ubuntu.com>

pkgname=xmlto
pkgver=0.0.29
pkgrel=1.1
pkgdesc="Convert xml to many other formats"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://pagure.io/xmlto/"
license=('GPL')
depends=('libxslt')
makedepends=('docbook-xsl')
source=("https://pagure.io/xmlto/archive/${pkgver}/xmlto-${pkgver}.tar.gz")
md5sums=('556f2642cdcd005749bd4c08bc621c37')
sha256sums=('40504db68718385a4eaa9154a28f59e51e59d006d1aa14f5bc9d6fded1d6017a')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
  autoreconf -i
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  ./configure BASH=/bin/bash --prefix=/usr \
                             --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
