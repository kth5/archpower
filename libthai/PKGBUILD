# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Chaiwat Suttipongsakul <cwt114@gmail.com>

pkgname=libthai
pkgver=0.1.30
pkgrel=1
pkgdesc='Thai language support library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://linux.thai.net/projects/libthai'
license=(LGPL-2.1-or-later)
depends=(
  glibc
  libdatrie
)
makedepends=(
  autoconf-archive
  doxygen
  git
)
source=("git+https://github.com/tlwg/libthai.git#tag=v$pkgver")
b2sums=(b098f6dfdfc23b77e9b56861cdb71897e7d77a9c5d2196119192ff80c592b78bf580006b56134641b5e3882a833f983ac471a19991fc94b6cb165377a828ff07)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
