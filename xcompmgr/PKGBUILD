# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Leslie P. Polzer <leslie.polzer@gmx.net>

pkgname=xcompmgr
pkgver=1.1.9
pkgrel=3
pkgdesc="Composite window effect manager for X.org"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://xorg.freedesktop.org/"
license=('custom')
makedepends=('git' 'xorg-util-macros')
depends=('libxcomposite' 'libxdamage' 'libxrender' 'libxext')
source=("git+https://gitlab.freedesktop.org/xorg/app/xcompmgr.git?signed#tag=${pkgname}-${pkgver}")
sha512sums=('119bc124f7a148dded0e31bb64257116fdf8f017b1aefe3d5879345c753080302980fffe26356b2f7dd830b2ae5285c552e9f628a5f72cc54a2ed637d40d2b8d')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

prepare() {
  cd ${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
