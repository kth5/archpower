# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Leslie P. Polzer <leslie.polzer@gmx.net>

pkgname=xcompmgr
pkgver=1.1.10
pkgrel=2
pkgdesc="Composite window effect manager for X.org"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://xorg.freedesktop.org/"
license=('MIT')
makedepends=('git' 'xorg-util-macros')
depends=('libxcomposite' 'libxdamage' 'libxrender' 'libxext' 'libxfixes' 'libx11' 'glibc')
source=("git+https://gitlab.freedesktop.org/xorg/app/xcompmgr.git?signed#tag=${pkgname}-${pkgver}")
sha512sums=('33018f2475eec0661472e9cb322921cb12b2dc112da40f402b61e90631edc97a273f18b869e8ee27e6194874d2e7dc70620454e192ca7f89bf31d331899b8182')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

prepare() {
  cd ${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
