# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=mujs
pkgver=1.3.8
pkgrel=1
pkgdesc='An embeddable Javascript interpreter in C'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://mujs.com/'
license=('ISC')
depends=(
    'glibc'
    'readline')
makedepends=('python')
source=("${pkgname}-${pkgver}.tar.gz"::"https://codeberg.org/ccxvii/mujs/archive/${pkgver}.tar.gz"
        '010-mujs-use-arch-flags.patch')
noextract=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('10e8548033924ada0828a821777371861ea3e127d472e8423ad87426118291d9'
            '7f08c4f14e957c609c45bf81264dd69144aa13622cb2730b92ad03d930851f13')

prepare() {
    mkdir "${pkgname}-${pkgver}"
    bsdtar -x -f "${pkgname}-${pkgver}.tar.gz" -C "${pkgname}-${pkgver}" --strip-components='1'
    
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-mujs-use-arch-flags.patch"
}

build() {
    make -C "${pkgname}-${pkgver}" prefix='/usr' release
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" prefix='/usr' install-shared
    install -d -m755 "${pkgdir}/usr/share/doc"
    install -D -m644 "${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/mujs/LICENSE"
    cp -dr --no-preserve='ownership' "${pkgname}-${pkgver}/docs" "${pkgdir}/usr/share/doc/${pkgname}"
}
