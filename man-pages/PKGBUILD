# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=man-pages
pkgver=6.13
_posixver=2017-a
pkgrel=1
pkgdesc="Linux man pages"
arch=(any)
license=(BSD-2-Clause
         BSD-3-Clause
         BSD-4.3TAHOE
         BSD-4-Clause-UC
         GPL-1.0-or-later
         GPL-2.0-only
         GPL-2.0-or-later
         GPL-3.0-or-later
         LGPL-3.0-or-later
         LGPL-3.0-linking-exception
         LicenseRef-Public-Domain
         LicenseRef-UltraPermissive
         Linux-man-pages-1-para
         Linux-man-pages-copyleft
         Linux-man-pages-copyleft-2-para
         Linux-man-pages-copyleft-var
         MIT
         Spencer-94)
url="https://www.kernel.org/doc/man-pages/"
depends=('sh')
makedepends=('man2html' 'git')
# https://git.kernel.org/pub/scm/docs/man-pages/man-pages.git/
source=(https://www.kernel.org/pub/linux/docs/man-pages/$pkgname-$pkgver.tar.{xz,sign}
        https://www.kernel.org/pub/linux/docs/man-pages/man-pages-posix/$pkgname-posix-${_posixver}.tar.{xz,sign})
# https://www.kernel.org/pub/linux/docs/man-pages/sha256sums.asc
sha256sums=('a2c8a0c2efe8a978ce51ce800461eb9e8931f12cc7ba4b7faa3082b69ba7f12c'
            'SKIP'
            'ce67bb25b5048b20dad772e405a83f4bc70faf051afa289361c81f9660318bc3'
            'SKIP')
validpgpkeys=('E522595B52EDA4E6BFCCCB5E856199113A35CE5E') # Michael Kerrisk (Linux man-pages maintainer) <mtk.manpages@gmail.com>
# + for posix tarball
validpgpkeys+=('A9348594CE31283A826FBDD8D57633D441E25BB5') # Alejandro Colomar Andres <alx.manpages@gmail.com>

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  # included in shadow
  rm man5/passwd.5
  rm man3/getspnam.3
  # included in tzdata
  rm man5/tzfile.5 man8/{tzselect,zdump,zic}.8
  # included in libxcrypt
  rm man3/crypt*.3
  # nscd is dead
  rm man5/nscd.conf.5 man8/nscd.8
}

# make -R is required until gnu_make 4.5.x will be released

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  # install man-pages
  make DESTDIR="${pkgdir}" prefix=/usr install -R

  # install posix pages
  pushd "${srcdir}"/$pkgname-posix-${_posixver%-*}
  make DESTDIR="${pkgdir}" install -R
  popd
  
  # posix pages have a custom license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}"/$pkgname-$pkgver/LICENSES/*  "${pkgdir}/usr/share/licenses/${pkgname}"/
  install -m644 "${srcdir}"/$pkgname-posix-${_posixver%-*}/POSIX-COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/POSIX-COPYRIGHT"
}
