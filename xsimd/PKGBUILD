# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander Susha <isushik94@gmail.com>
# Contributor: Fredrik Tegenfeldt <tegenf@gmail.com>

pkgname=xsimd
pkgver=13.0.0
pkgrel=2
pkgdesc='QuantStack tools library - Multi-dimensional arrays with broadcasting and lazy computing'
arch=(any)
url='https://github.com/QuantStack/xsimd'
license=(BSD-3-Clause)
depends=()
makedepends=(cmake
             git
             gtest)
source=(git+https://github.com/QuantStack/$pkgname#tag=$pkgver)
sha512sums=('5c9f4cb16d0a2e835ebea58e15f290f8aab4f1d5bf7ae79500ad521fabfeb33892062431e7faa9b106c294de9e4f2041af228383c38d9a8f070dd7ec3c6f9c0b')

prepare() {
  cd $pkgname
# Backport fixes for Krita
  git cherry-pick -n 80a59235e3ffa51659aaa06f002bfd088b77023c
  git cherry-pick -n 96edf0340492fa9c080f5182b38358ca85baef5e
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
