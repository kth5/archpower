# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=11.86
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.monkeysaudio.com/index.html'
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs')
makedepends=('cmake' 'python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('3f5c543b140ec88ff97589e9a70c0604bb0140858c6306fea9866678b2deb84ce22d5d720718ff46175e1c25fb626f94e5b4711b4c0988c8a16f5d24aa3004cf'
            '058cc796121e8d14b3862a63fa7c9afec728b57817a585cf194365f4a21e2f300b39666036a6ddf9fb6a245867e74d06fb3a6fd5d460abe9a10700ea4a493ce7')
b2sums=('137c59b9494fea24ff69a388ae55c28ecc3b2393c01ed46e9a185e8d1bf46982763380f8bf713d593aad57770e5bceeb2ce8efcc71aa0837db32493f4aafd4e4'
        '1cc1d61e4f64ce30a0de198133251b3da8b77087020c1a7cfd4716a5a7ffdc738e05a0665ea215126950be30f0d8ecac2d54770a16f4492d70731191fc79f702')

build() {
  cmake \
    -S . \
    -B build \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX='/usr'

  cmake --build build

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
