# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=liborcus0.14
pkgver=0.14.1
pkgrel=4
pkgdesc="File import filter library for spreadsheet documents."
arch=(x86_64 powerpc64le)
url="https://gitlab.com/orcus/orcus/blob/master/README.md"
license=('MPL')
depends=('libixion0.14')
makedepends=('boost' 'mdds14')
conflicts=('liborcus')
source=(https://kohei.us/files/orcus/src/liborcus-${pkgver}.tar.xz)
sha256sums=('f2d1dc840fa6a5358d956a7cb746c36b857d92190de0ac5b40b2841ed408daab')

build() {
    cd liborcus-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd liborcus-${pkgver}
    make check
}

package() {
    cd liborcus-${pkgver}
    make DESTDIR="$pkgdir" install
}
