# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Santiago Torres-Arias <santiago in el archlinux organization>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Javier Ti√° <javier dot tia at gmail dot com>

pkgname=doctest
pkgver=2.4.11
pkgrel=1
pkgdesc='The lightest feature rich C++ single header testing framework'
arch=(any)
url='https://github.com/doctest/doctest'
license=('MIT')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('632ed2c05a7f53fa961381497bf8069093f0d6628c5f26286161fbd32a560186')

build() {
  cd ${pkgname}-${pkgver}
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cd ${pkgname}-${pkgver}
  ctest --test-dir build --output-on-failure
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" cmake --install build
  install -vDm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.txt
}

# vim:set ts=2 sw=2 et:
