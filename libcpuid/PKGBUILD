# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: X0rg

pkgname=libcpuid
pkgver=0.6.4
pkgrel=2
pkgdesc='A small C library for x86 CPU detection and feature extraction'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://libcpuid.sourceforge.net'
license=('BSD')
depends=('glibc')
makedepends=(
  'git'
  'cmake'
  'ninja'
  'doxygen'
  'python'
  'graphviz'
)
provides=('libcpuid.so')
_commit=02237e67f5cbff348c63ad5338a70d75909a23b1  # tags/v0.6.4
source=("$pkgname::git+https://github.com/anrieff/libcpuid#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
	cmake \
    -S "$pkgname" \
    -B build \
    -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D LIBCPUID_TESTS=ON

	cmake --build build
}

check() {
  cd build

  ninja consistency test-old
}

package() {
	DESTDIR="$pkgdir" cmake --install build

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/COPYING"
}
