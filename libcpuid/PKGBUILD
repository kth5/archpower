# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: X0rg

pkgname=libcpuid
pkgver=0.8.1
pkgrel=1
pkgdesc='A small C library for x86 CPU detection and feature extraction'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='http://libcpuid.sourceforge.net'
license=('BSD-2-Clause')
depends=('glibc')
makedepends=(
  'git'
  'cmake'
  'ninja'
  'doxygen'
  'python'
  'graphviz'
)
provides=('libcpuid.so')
source=("$pkgname::git+https://github.com/anrieff/libcpuid#tag=v$pkgver")
sha512sums=('ad463068217856946412435ba1f4fc4827e3fa0ce0dea312a7cecafeaaf3ed55b9b3217241b59d2e4445cced4fdb751e3b07d0da04d6eb8ee0e04985c3630caf')
b2sums=('641e424c6b748aa4977952efd4f2bc56a3185f43758a8fa621c2f7fc106a8c5dffa17c6e2806ba375ba0f6efe045efbfa1b37afc9c772d11c39401cc22c9ed26')

build() {
  cd "$pkgname"

	cmake \
    -S . \
    -B build \
    -G Ninja \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D LIBCPUID_SHARED=ON \
    -D LIBCPUID_ENABLE_TESTS=ON

	cmake --build build
}

check() {
  cd "$pkgname/build"

  ninja consistency
}

package() {
  cd "$pkgname"

	DESTDIR="$pkgdir" cmake --install build

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
