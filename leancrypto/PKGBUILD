# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org> 
# Contributor: Pan Lanlan <abbypan@gmail.com>

pkgname=leancrypto
pkgver=1.3.0
pkgrel=1
pkgdesc="Lean cryptographic library usable for bare-metal environments"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('GPL-2.0-or-later'
         'LicenseRef-BSDvariant'
         'LicenseRef-leancrypto')
url="https://leancrypto.org"
depends=('glibc')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/smuellerDD/leancrypto/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('53b51936d77304e82cc6aa34a0a65eec3327ca1165342180694c5aa6a7d745c8')

build() {
  case "${CARCH}" in
    riscv64) _meson_options=(-Denable_selftests=disabled -Ddisable-asm=true) ;;
  esac

  arch-meson $pkgname-$pkgver build

  # Print config
  meson configure build ${_meson_options[@]}

  meson compile -C build
}

check() {
  case "${CARCH}" in
    powerpc*|riscv64) return 0 ;;
  esac
 
  # may take pretty long
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname-$pkgver/LICENSE{,.bsd,.gplv2}
}

