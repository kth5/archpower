# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org> 
# Contributor: Pan Lanlan <abbypan@gmail.com>

pkgname=leancrypto
pkgver=1.2.0
pkgrel=2
pkgdesc="Lean cryptographic library usable for bare-metal environments"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL-2.0-or-later'
         'LicenseRef-BSDvariant'
         'LicenseRef-leancrypto')
url="https://leancrypto.org"
depends=('glibc')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/smuellerDD/leancrypto/archive/refs/tags/v$pkgver.tar.gz"
        nodlopen.patch
        fix-tests.patch)
sha256sums=('247481cac4cedbf4b9e1c689b7726592015352a11cd22625013185d01cda2c15'
            '09dacc1b5f56e6c1619793dd5f9f4885ceb75bfd96d4cb0100f9bf84bbb27e31'
            '5619418bb4cf66293dbcedc6f74101c83768d088ff02e5e0520ea0f24242067f')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-tests.patch
  patch -Np1 -i ../nodlopen.patch
}

build() {
  arch-meson $pkgname-$pkgver build

  # Print config
  meson configure build

  meson compile -C build
}

check() {
  # may take pretty long
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname-$pkgver/LICENSE{,.bsd,.gplv2}
}

