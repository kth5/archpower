# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libexif
pkgver=0.6.25
pkgrel=1
pkgdesc='Library to parse an EXIF file and read the data from those tags'
url='https://github.com/libexif/libexif'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('LGPL')
depends=('glibc')
provides=('libexif.so')
source=($url/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha512sums=('8dbf56c85f18157affde0e8500b847424ec508e7dfd40ff9f8d85816d45eccc4b70ebca891550a10824641bbd63f69050f9e24be82bcbfa9e8acda2f123ce848'
            'SKIP')
b2sums=('a0f8a80194694fab295dcd9c72088055b83534b281cb5bdac67a781dfb56fab0e354e32a520eb31d9f4dd604524355d69ba62012056f5706051e8382e4d898b8'
        'SKIP')
validpgpkeys=('7C4AFD61D8AAE7570796A5172209D6902F969C95') # Marcus Meissner

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
