# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=netavark
pkgver=1.17.1
pkgrel=1
pkgdesc="Container network stack"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/containers/netavark"
license=(Apache-2.0)
depends=(
  aardvark-dns
  gcc-libs
  glibc
)
makedepends=(
  cargo
  git
  mandown
  protobuf
)
provides=(container-network-stack=2)
source=(git+$url#tag=v$pkgver)
sha512sums=('1afbb2a336318eb27a2d64b4d147474062d7ae68dc949582349752ff780294b988ae6518d752ef02f06cb21d0befa70665a63c738e71da8cdba0fb4da6e919b8')
b2sums=('e239b18efb5006130a575d16f6226f8cdbebd46825835e9166cda52e004650de20992386c377b32d6525b5af2545531e84c7e3907b65395d2278bdfad9166f8f')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  # generate man page directly as docs target is broken: https://github.com/containers/netavark/issues/524
  mandown docs/$pkgname.1.md > $pkgname.1
  # create service file manually as we are not using the Makefile
  sed "s|@@NETAVARK@@|/usr/lib/podman/netavark|" contrib/systemd/system/netavark-dhcp-proxy.service.in > contrib/systemd/system/netavark-dhcp-proxy.service
  sed "s|@@NETAVARK@@|/usr/lib/podman/netavark|" contrib/systemd/system/netavark-firewalld-reload.service.in > contrib/systemd/system/netavark-firewalld-reload.service
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 $pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 contrib/systemd/system/*.{service,socket} -t "$pkgdir/usr/lib/systemd/system/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
