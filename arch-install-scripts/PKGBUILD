# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=arch-install-scripts
pkgver=29
pkgrel=1
pkgdesc="Scripts to aid in installing Arch Linux"
arch=(any)
url="https://gitlab.archlinux.org/archlinux/arch-install-scripts"
license=('GPL-2.0-only')
depends=('awk' 'bash' 'coreutils' 'grep' 'pacman' 'util-linux')
makedepends=('asciidoc' 'git')
source=("git+$url.git#tag=v${pkgver}?signed")
validpgpkeys=(
  'C100346676634E80C940FB9E9C02FF419FECBE16'  # Morten Linderud
)
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed 's/^v//'
}

build() {
  make -C "$pkgname"
}

check() {
  make -C "$pkgname" check
}

package() {
  make -C "$pkgname" PREFIX=/usr DESTDIR="$pkgdir" install
}
