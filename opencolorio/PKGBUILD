# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dumitru Ursu <dima@ceata.org>

pkgname=opencolorio
pkgver=2.4.2
pkgrel=1
pkgdesc="A color management framework for visual effects and animation"
arch=(x86_64 powerpc64le)
url="https://opencolorio.org"
license=('BSD-3-Clause')
depends=('freeglut' 'glew' 'lcms2' 'expat' 'yaml-cpp' 'imath' 'pystring' 'minizip-ng' 'openexr')
makedepends=('cmake' 'python' 'pybind11' 'ninja')
checkdepends=('openimageio')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz)
sha512sums=('d626007d7a7ae26f4cf2fa8e5675963af9127f500f824548ccc4e659ddb2cd275b988822b4f66e0170971426dc330d106e281cdae63a5bd141b9aadaa874a746')

build() {
  cd OpenColorIO-$pkgver

  CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_INSTALL_RPATH=YES \
    -Wno-dev
  ninja -C build
}

check() {
  cd OpenColorIO-$pkgver/build

  # Currently broken
  # ctest -E test_gpu
}

package() {
  cd OpenColorIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
