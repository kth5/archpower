# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=mpfr
_pkgver=4.2.0
_patchver=12
pkgrel=3
pkgver=${_pkgver}.p${_patchver}
#pkgver=$_pkgver
pkgdesc='Multiple-precision floating-point library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.mpfr.org/'
license=(GPL3 LGPL3)
depends=(glibc gmp)
source=(https://ftp.gnu.org/gnu/mpfr/mpfr-${_pkgver}.tar.xz{,.sig}
        patches.diff) # wget https://www.mpfr.org/mpfr-${_pkgver}/allpatches -O patches.diff
sha512sums=('58e843125884ca58837ae5159cd4092af09e8f21931a2efd19c15de057c9d1dc0753ae95c592e2ce59a727fbc491af776db8b00a055320413cdcf2033b90505c'
            'SKIP'
            'c3de1907648cbf1a87d292bc017f1f651ed4449eb28459187821199016e0f12e7cb3b45d6163c9a4361d3b7499f5b6f771b271edcfa93399f3c28c1180b3b387')
b2sums=('dd88ae3a6a910ad8faeb791b49c3b1085de5d0d4c49c637c124bf4d9bc79fb621d0d1d69e07d0642b9b678f6d355a5830d083dfd189e1e91d0e04c970c10bd64'
        'SKIP'
        '0c8e3da9057defe4289a9665f27ec03d2312d58bf6b2a108d089097d6b22fbc5b9fe92b1cb8607c3bd53f8b93d82c5db6a467a0d7881ed8e893560a24b33e061')
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D'
              'A534BE3F83E241D918280AEB5831D11A0D4DB02A') # Vincent Lefevre <vincent@vinc17.net>

prepare() {
  cd $pkgname-${_pkgver}

  patch -p1 -i ../patches.diff
  autoreconf -fiv
}

build() {
  cd $pkgname-${_pkgver}
  ./configure --prefix=/usr \
    --enable-thread-safe \
    --enable-shared
  make
}

check() {
  cd $pkgname-${_pkgver}
  make check
  make check-exported-symbols
}

package() {
  cd $pkgname-${_pkgver}
  make DESTDIR="$pkgdir" install
}
