# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Hipp <thomashipp at gmail dot com>

pkgname=raft
pkgver=0.18.1
pkgrel=1
pkgdesc="C implementation of the Raft consensus protocol"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/canonical/raft"
license=('LGPL3')
depends=('libuv')
options=(strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/canonical/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('43e851352d99e6958c2a23805e25de8a726939e227e50ce596394e2306efc3b7')

build() {
	cd "${pkgname}-${pkgver}"
	autoreconf -i
	./configure --prefix=/usr
	make
}

check() {
	cd "${pkgname}-${pkgver}"
	make check || true
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir/" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	rm -rf "${pkgdir}/usr/bin"
}
