# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=opencollada
pkgver=1.6.68
pkgrel=3.2
epoch=1
pkgdesc="Stream based reader and writer library for COLLADA files"
arch=(x86_64 powerpc64le)
url="https://github.com/KhronosGroup/OpenCOLLADA"
license=('GPL')
depends=('libxml2' 'pcre')
makedepends=('git' 'cmake')
source=('opencollada.conf'
        'pcre.patch'
        "git+https://github.com/KhronosGroup/OpenCOLLADA.git#tag=v${pkgver}"
        "${pkgname}-${pkgver}-gcc13-fix.patch::https://patch-diff.githubusercontent.com/raw/KhronosGroup/OpenCOLLADA/pull/656.patch")
sha256sums=('2fc9656a2b881ca4528416daa91fc525adaa97d73e96a18b41aa7856270eba1f'
            'f37a7cbadcaa06b70133f76c509c61bdcf13715bc13aed11b5db83ef594813b3'
            'b201b99935d817b18bd1588cce824d6e891102d1d8dbe272cbd9a4ce91ec4f8e'
            '9741a73c9759d12ebb52258b83905ffc06f801e710beaeb224443b58da13f8b4')

prepare() {
  cd OpenCOLLADA

  patch -Np1 -i "$srcdir"/pcre.patch
  patch -Np1 -i "$srcdir"/${pkgname}-${pkgver}-gcc13-fix.patch
}

build() {
  cd OpenCOLLADA

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SHARED=ON \
    -DUSE_STATIC=OFF
  make
}

package() {
  cd OpenCOLLADA/build

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/opencollada.conf "$pkgdir"/etc/ld.so.conf.d/opencollada.conf
}

# vim:set ts=2 sw=2 et:
