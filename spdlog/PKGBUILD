# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Luca Weiss
# Contributor: Michael Yang

pkgname=spdlog
pkgver=1.15.0
pkgrel=2
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/gabime/spdlog'
license=('MIT')
depends=(
    'gcc-libs'
    'glibc'
    'libfmt.so'
)
makedepends=(
    'cmake'
    'catch2'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/gabime/spdlog/archive/v$pkgver.tar.gz"
    "spdlog_fmt_external.patch"
    "fix_update_to_string_view_function_for_fmt_11.1.patch::https://github.com/gabime/spdlog/commit/276ee5f5c0eb13626bd367b006ace5eae9526d8a.patch"
)
provides=(
    'libspdlog.so'
)
sha256sums=('9962648c9b4f1a7bbc76fd8d9172555bad1871fdb14ff4f842ef87949682caa5'
            'a0eb34b7c6920f0db2587460071f53372663c191cdfe34bf5ea2704c309c745f'
            '7491e085df3f456b14d3df99629282e2d82105e2fe5d75024fb8f230affddd19')

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 < ../spdlog_fmt_external.patch
    patch -p1 < ../fix_update_to_string_view_function_for_fmt_11.1.patch
}

build() {
    cmake -B build -S "$pkgname-$pkgver" \
        -DSPDLOG_BUILD_BENCH=OFF \
        -DSPDLOG_FMT_EXTERNAL=ON \
        -DSPDLOG_BUILD_SHARED=ON \
        -DSPDLOG_BUILD_TESTS=ON \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -Wno-dev
    cmake --build build
}

check() {
    cmake --build build --target test
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -vDm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
