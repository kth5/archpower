# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Luca Weiss
# Contributor: Michael Yang

pkgname=spdlog
pkgver=1.17.0
pkgrel=1
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/gabime/spdlog'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'libfmt.so'
)
makedepends=(
  'cmake'
  'catch2'
)
provides=('libspdlog.so')
source=(
  "$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "$pkgname-fmt-external.patch"
)
b2sums=('e8cab0d0de646c71c7494f173ecc356dfee13d5d97a1f208f6141e3923b65bdb9b6e637d8143fc1797c2b123fe94398e21207cd811c95dfc366e5ff3884e5826'
        '686be3f61e4c578c53660e0bad6afe1918472f5b46322f207d3fde2cca3a3bc480c10891eeaa7b85e3c4d3370c43a6da85376a93eea411333a1147eb853bddc8')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../$pkgname-fmt-external.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DSPDLOG_BUILD_BENCH=OFF \
    -DSPDLOG_FMT_EXTERNAL=ON \
    -DSPDLOG_BUILD_SHARED=ON \
    -DSPDLOG_BUILD_TESTS=ON \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -Wno-dev
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname/" $pkgname-$pkgver/LICENSE
}
