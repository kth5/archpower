# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Luca Weiss
# Contributor: Michael Yang

pkgname=spdlog
pkgver=1.16.0
pkgrel=1
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/gabime/spdlog'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'libfmt.so'
)
makedepends=(
  'cmake'
  'catch2'
)
provides=('libspdlog.so')
source=(
  "$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
  "spdlog_fmt_external.patch"
)
sha256sums=('8741753e488a78dd0d0024c980e1fb5b5c85888447e309d9cb9d949bdb52aa3e'
            'a0eb34b7c6920f0db2587460071f53372663c191cdfe34bf5ea2704c309c745f')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < ../spdlog_fmt_external.patch
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -DSPDLOG_BUILD_BENCH=OFF \
    -DSPDLOG_FMT_EXTERNAL=ON \
    -DSPDLOG_BUILD_SHARED=ON \
    -DSPDLOG_BUILD_TESTS=ON \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -Wno-dev
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
