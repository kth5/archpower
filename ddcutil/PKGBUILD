# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=2.1.0
pkgrel=4
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('GPL2')
depends=('glib2' 'i2c-tools' 'libusb' 'libdrm' 'jansson')
makedepends=('systemd')
source=(https://github.com/rockowitz/ddcutil/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/rockowitz/ddcutil/commit/7f157f62.patch
        https://github.com/rockowitz/ddcutil/commit/8fd63cfd.patch)
sha512sums=('d48f87427050caf8e38f6a4b0dd71c1639101762444a712d7a7d97302ebc0dc4fd7c4a5cb188b251c6a6a369b298a43dbc7d91e0eca8cdb6f51c1ae6ae7f1f4b'
            '782e4205121a2e8ae84721f4ffdcc8c39e518531d11d79f2e4f0e08508614d488bccf0ce3985ec5231fe6ff625041dab56ed82c77ddca2c03645fd4c52486865'
            'e377f7fafa6c52cb30a34021b4d73fc3f52d142071418785c34c7fa324ae9106dc601a0246d75dbb1372b390d8c10c43633603b5ede0555b50d300b59e7e23d2')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../7f157f62.patch # Fix assert
  patch -p1 -i ../8fd63cfd.patch # Fix another assert
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
