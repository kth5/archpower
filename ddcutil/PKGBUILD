# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=2.2.3
pkgrel=1
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
depends=('glib2' 'i2c-tools' 'libusb' 'libdrm' 'jansson' 'libx11' 'libxext' 'libxrandr')
makedepends=('git' 'systemd')
source=(git+https://github.com/rockowitz/ddcutil.git#tag=v$pkgver)
sha512sums=('1679daddb4dd1f4a4acc7d8c3297e9fcb305fec4b0f5b87921d880074ccb5a35ebd7a3ea7c4e614d3c493b625711c02aa499f991a1924003a792e703169dc8c8')

prepare() {
  cd ddcutil
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ddcutil
  ./configure --prefix=/usr --disable-build-timestamp
  make
}

package() {
  cd ddcutil
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
