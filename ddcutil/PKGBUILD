# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=2.2.1
pkgrel=1
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
depends=('glib2' 'i2c-tools' 'libusb' 'libdrm' 'jansson' 'libx11' 'libxext' 'libxrandr')
makedepends=('git' 'systemd')
source=(git+https://github.com/rockowitz/ddcutil.git#tag=v$pkgver)
sha512sums=('e299765eec72ef2567da69555fe3254885532bf6df9c7df53e790779b644992313dedbbd8fbf19e1e75d841333c6e39c14a8896ccaccf0918542572f2b1ae430')

prepare() {
  cd ddcutil
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ddcutil
  ./configure --prefix=/usr --disable-build-timestamp
  make
}

package() {
  cd ddcutil
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
