# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.10.0
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/opensvc/multipath-tools'
license=('GPL-2.0-or-later' 'GPL-3.0-or-later' 'LGPL-2.0-or-later' 'LGPL-2.1-or-later')
depends=('glibc' 'gcc-libs' 'libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs' 'util-linux-libs')
makedepends=('git' 'systemd')
source=("multipath-tools::git+https://github.com/opensvc/multipath-tools#tag=${pkgver}")
sha256sums=('52e5ac6c5fd51b4605faf3fe18f90fdf76124e00b622897d9ba0d24263b52353')

_make_opts=(
  prefix='/usr/'
  bindir='/usr/bin'
  etc_prefix=""
  configfile='/etc/multipath.conf'
  statedir='/etc/multipath'
  LIB='lib'
)


build() {
  cd "${pkgname}"

  make \
    "${_make_opts[@]}" \
    FORTIFY_OPT=''
}

package() {
  cd "${pkgname}"

  make -j1 \
    "${_make_opts[@]}" \
    DESTDIR="${pkgdir}" \
    install

  install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README*
}
