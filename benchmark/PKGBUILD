# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Philipp Cla√üen <philipp.classen@posteo.de>

pkgname=benchmark
pkgver=1.9.2
pkgrel=1
pkgdesc="A microbenchmark support library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/google/benchmark"
license=('Apache-2.0')
depends=('gcc-libs')
makedepends=('cmake')

source=("https://github.com/google/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('409075176168dc46bbb81b74c1b4b6900385b5d16bfc181d678afb060d928bd3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p build
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"

  cmake \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DBENCHMARK_ENABLE_LTO=ON \
    -DBENCHMARK_ENABLE_GTEST_TESTS=OFF \
    ..

  make VERBOSE=1
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
}
