# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=gpgme
pkgver=2.0.0
pkgrel=1
pkgdesc='C wrapper library for GnuPG'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.gnupg.org/related_software/gpgme/'
license=(
  GPL-2.0-or-later
  LGPL-2.0-or-later
  LGPL-2.1-or-later
  MIT)
depends=(
  'gcc-libs'
  'glib2'
  'glibc'
  'libassuan'
  'libgpg-error'
  'gnupg>=2')
makedepends=('git')
provides=('libgpgme.so')
options=('!emptydirs')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA'  # Werner Koch (dist signing 2020)
              'AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD') # Niibe Yutaka (GnuPG Release Key)
source=("git+https://dev.gnupg.org/source/gpgme.git#tag=${pkgname}-${pkgver}?signed")
sha256sums=('cb4574411c7145d7b91d9b7bacff11e94c4994713a6ff5de908de9ebc5c30ff9')

prepare() {
  cd ${pkgname}

  sed -i 's/-unknown//' autogen.sh
  autoreconf -fi
}

build() {
  cd ${pkgname}

  ./configure \
    --prefix=/usr \
    --disable-fd-passing \
    --disable-static \
    --disable-gpgsm-test

  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}

  # this test fails with gnupg (FS#66572)
  sed -i 's#"t-keylist-secret",##' tests/json/t-json.c

  make check
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install

  install -vDm 644 LICENSES "$pkgdir/usr/share/licenses/$pkgname/MIT.txt"
}
