# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm-2.48
pkgname=(
  pangomm-2.48
  pangomm-2.48-docs
)
pkgver=2.52.0
pkgrel=1
pkgdesc="C++ bindings for Pango"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  cairomm-1.16
  glibmm-2.68
  pango
)
makedepends=(
  cairomm-1.16-docs
  git
  glibmm-2.68-docs
  meson
  mm-common
)
options=(!emptydirs)
_commit=a767b1b98003ae3d7defa838af789616395d2602  # tags/2.52.0^0
source=("git+https://gitlab.gnome.org/GNOME/pangomm.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd pangomm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pangomm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson pangomm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_pangomm-2.48() {
  depends+=(
    libcairomm-1.16.so
    libglibmm-2.68.so
    libsigc-3.0.so
  )
  provides=(libpangomm-2.48.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_pangomm-2.48-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
