# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=openexr
pkgver=3.3.3
pkgrel=1
pkgdesc='A high dynamic-range image file format library'
url='https://www.openexr.com/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(BSD-3-Clause)
depends=(gcc-libs
         glibc
         imath
         libdeflate)
makedepends=(cmake
             git)
source=(git+https://github.com/openexr/openexr#tag=v$pkgver
        openexr-3.3.3-32bit-atomics.patch)
sha256sums=('b23d696cea2ed567161ab2172b56a1500084b9608a9a15a37e5bea5c28f2b7a4'
            'e88b851dafb7ecb34b8dce94b8f935cab6b3e1821a56289f6ea1ae0650361bdd')

prepare() {
  cd $pkgname
  patch -Np1 -i ${srcdir}/openexr-3.3.3-32bit-atomics.patch
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
