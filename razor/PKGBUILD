# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bert Peters <bertptrs@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=razor
pkgver=2.86
pkgrel=3
pkgdesc="A distributed, collaborative, spam detection and filtering network"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://metacpan.org/dist/Razor2-Client-Agent"
license=('Artistic-2.0')
depends=('perl-net-dns' 'perl-digest-sha1' 'perl-uri' 'perl-digest-nilsimsa' 'perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/T/TO/TODDR/Razor2-Client-Agent-${pkgver}.tar.gz")
sha256sums=('5e062e02ebb65e24b708e7eefa5300c43d6f657bf20d08fec4ca8a0a3b94845f')

build() {
  cd Razor2-Client-Agent-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Razor2-Client-Agent-${pkgver}
  make test
}

package() {
  cd Razor2-Client-Agent-${pkgver}
  make DESTDIR="${pkgdir}" install
}
