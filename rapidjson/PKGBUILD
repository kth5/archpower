# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Rebischke <chris.rebischke at archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Yunhui Fu <yhfudev@gmail.com>

pkgname=rapidjson
pkgver=1.1.0
pkgrel=6
pkgdesc="Fast JSON parser/generator for C++ with both SAX/DOM style API"
arch=(any)
url="https://github.com/miloyip/rapidjson"
license=('MIT')
makedepends=('cmake')
checkdepends=('gtest' 'valgrind')
source=(https://github.com/miloyip/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/Tencent/rapidjson/commit/3b2441b8.patch)
sha512sums=('2e82a4bddcd6c4669541f5945c2d240fb1b4fdd6e239200246d3dd50ce98733f0a4f6d3daa56f865d8c88779c036099c52a9ae85d47ad263686b68a88d832dff'
            '5002ff20a65b7d057411e39adf7f5a29eddff818d20579900b655df4d838b984a1b68f488232e1990b592943a70943619d924da1c4e1d2ce0d3ef65bc40f75d6')

prepare() {
  cd $pkgname-$pkgver
  find -name CMakeLists.txt | xargs sed -e 's|-march=native||' -i # Don't employ native optimization
  find -name CMakeLists.txt | xargs sed -e 's|-Werror||' -i # Don't use -Werror
  patch -p1 -i ../3b2441b8.patch # Fix build with GCC 14
}

build () {
  cd $pkgname-$pkgver

  mkdir -p build
  cd build

  cmake \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DRAPIDJSON_HAS_STDSTRING=ON \
      -DRAPIDJSON_BUILD_CXX11=ON \
      -DRAPIDJSON_ENABLE_INSTRUMENTATION_OPT=OFF \
      -DDOC_INSTALL_DIR=/usr/share/doc/${pkgname} \
      ..

  make
}

check() {
  cd $pkgname-$pkgver/build
  ctest -j 2 -V
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir" make -C build install

  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

# vim: ts=2 sw=2 et:
