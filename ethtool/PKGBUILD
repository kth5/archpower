# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Martin Kemp <mdkemp@elys.com>

pkgname=ethtool
pkgver=6.5
pkgrel=1
epoch=1
pkgdesc="Utility for controlling network drivers and hardware"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.kernel.org/pub/software/network/ethtool/"
license=('GPL')
depends=('glibc' 'libmnl')
source=(https://www.kernel.org/pub/software/network/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('814171ea4b8026b081c0741dbbf32e6968311483ecf64711232faec2ac70a14c'
            'SKIP')
validpgpkeys=(CE4A4D080F0D304F23B9EBDD972D5BF4DC613806  # John W. Linville <linville@kernel.org>
              D2CB120AB45957B721CD9596F4554567B91DE934) # Michal Kubecek <mkubecek@suse.cz>

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin
    make
}

check() {
    cd $pkgname-$pkgver
    make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
