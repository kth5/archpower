# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=namcap
pkgver=3.6.0
pkgrel=3
pkgdesc='A Pacman package analyzer'
arch=(any)
url="https://gitlab.archlinux.org/pacman/$pkgname"
license=(GPL-2.0-or-later)
depends=(binutils
         elfutils
         licenses
         pkgconf
         pyalpm
         python
         python-license-expression
         python-pyelftools)
checkdepends=(python-pytest
              systemd)
makedepends=(python-{build,installer,wheel}
            python-setuptools
    	    git)
source=("git+https://gitlab.archlinux.org/pacman/namcap.git#tag=${pkgver}")
sha256sums=('SKIP')
validpgpkeys=(9F377DDB6D3153A48EB3EB1E63CC496475267693  # caleb@alerque.com
              CCB34EBBB9541EF3F7B366C1D4A753468A5A5B67) # alerque@archlinux.org

build() {
  cd namcap
  python -m build -wn
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd namcap
  env PARSE_PKGBUILD_PATH="$PWD" PATH="$PWD/scripts:$PATH" pytest
}

package() {
  cd namcap
  python -m installer -d "$pkgdir" dist/*.whl
  local site_packages="$(python -c "import site; print(site.getsitepackages()[0])")"
  mv "$pkgdir/"{"$site_packages/usr/share",usr}
  rmdir "$pkgdir/$site_packages/usr"
}
