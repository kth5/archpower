# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=tk
pkgver=9.0.1
pkgrel=1
pkgdesc='A windowing toolkit for use with tcl'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://tcl.sourceforge.net/"
license=(TCL)
depends=(tcl
         libxss
         libxft)
makedepends=(zip)
options=(staticlibs)
_archive="$pkgname$pkgver"
source=("https://downloads.sourceforge.net/sourceforge/tcl/$_archive-src.tar.gz")
sha256sums=('d6f01a4d598bfc6398be9584e1bab828c907b0758db4bbb351a1429106aec527')

build() {
	cd "$_archive/unix"
	./configure \
		ZIP_PROG=zip \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-threads \
		--disable-rpath \
		--enable-64bit
	make
}

check() {
	cd "$_archive/unix"
	#  make test
}

package() {
	cd "$_archive/unix"
	make INSTALL_ROOT="$pkgdir" install install-private-headers
	ln -sf wish${pkgver%.*} "$pkgdir/usr/bin/wish"
	ln -sf libtk${pkgver%.*}.so "$pkgdir/usr/lib/libtk.so"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" license.terms

	# install private headers (FS#14388, FS#47616)
	cd ..
	for dir in compat generic generic/ttk unix; do
		install -dm755 "$pkgdir/usr/include/tk-private/$dir"
		install -m644 -t "$pkgdir/usr/include/tk-private/$dir" $dir/*.h
	done

	# remove buildroot traces
	sed -e "s#$srcdir/$_archive/unix#/usr/lib#" \
		-e "s#$srcdir/$_archive#/usr/include#" \
		-i "$pkgdir/usr/lib/tkConfig.sh"

	# remove unrequired execute permissions (FS#75042)
	chmod -x "$pkgdir/usr/lib/libtkstub.a"
}
