# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.80.2
pkgrel=2
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  gobject-introspection-runtime
  js115
  libffi
  libgirepository
  libsysprof-capture
  libx11
  readline
)
makedepends=(
  dbus
  git
  gobject-introspection
  meson
)
checkdepends=(
  gtk3
  gtk4
  xorg-server-xvfb
)
provides=(libgjs.so)
source=("git+https://gitlab.gnome.org/GNOME/gjs.git?signed#tag=$pkgver")
b2sums=('40fc19481e1c8ad8dc6c7d122cd8b71254f3b76e2483fd7bbc13a2cbfafecb3d1fbadbb375f343850332065e4f901b182e10c2e26c0269660addc33d2781b8ae')
validpgpkeys=(
  53C0524AD3AE115F69C47D2D0E9D857756977391 # Philip Chimento (Signing Key for GNOME Releases) <philip.chimento@gmail.com>
)

prepare() {
  cd gjs
}

build() {
  local meson_options=(
    -D installed_tests=false
  )

  arch-meson gjs build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libreadline.so)
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
