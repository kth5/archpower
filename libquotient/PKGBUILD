# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor Sefa Eyeoglu <contact@scrumplex.net>

pkgname=libquotient
pkgver=0.9.2
pkgrel=1
pkgdesc='A Qt library to write cross-platform clients for Matrix'
license=(LGPL-2.1-or-later)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://quotient-im.github.io/libQuotient/'
depends=(gcc-libs
         glibc
         libolm
         openssl
         qt6-base
         qtkeychain-qt6)
makedepends=(cmake
             git)
source=(git+https://github.com/quotient-im/libQuotient#tag=$pkgver)
sha256sums=('b103fefbaec809c8003f5e9577171a5d44d7744dd562ecf7870fa29d6ea56550')

build() {
  cmake -B build -S libQuotient \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
