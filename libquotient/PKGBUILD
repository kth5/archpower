# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor Sefa Eyeoglu <contact@scrumplex.net>

pkgname=libquotient
pkgver=0.8.2
pkgrel=1
pkgdesc='A Qt library to write cross-platform clients for Matrix'
license=(LGPL-2.1-or-later)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://matrix.org/docs/projects/sdk/quotient'
depends=(gcc-libs
         glibc
         libolm
         openssl
         qt6-base
         qtkeychain-qt6)
makedepends=(cmake)
source=(https://github.com/quotient-im/libQuotient/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('12ff2fa8b80a934b9dd88fa3416a4b88e94bc0e18a8df0dcebfc90614dd2f5c9')

build() {
  cmake -B build -S libQuotient-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_WITH_QT6=ON \
    -DQuotient_ENABLE_E2EE=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
