# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor Sefa Eyeoglu <contact@scrumplex.net>

pkgname=libquotient
pkgver=0.9.0
pkgrel=1
pkgdesc='A Qt library to write cross-platform clients for Matrix'
license=(LGPL-2.1-or-later)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://matrix.org/docs/projects/sdk/quotient'
depends=(gcc-libs
         glibc
         libolm
         openssl
         qt6-base
         qtkeychain-qt6)
makedepends=(cmake
             git)
source=(git+https://github.com/quotient-im/libQuotient#tag=$pkgver)
sha256sums=('bf146df936d7763d7c9dddefee4b3a4fc0298fb8f394cc0d11be21fca5d8584f')

build() {
  cmake -B build -S libQuotient \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_WITH_QT6=ON \
    -DQuotient_ENABLE_E2EE=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
