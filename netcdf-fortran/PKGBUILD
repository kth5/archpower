# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf-fortran
pkgver=4.6.1
pkgrel=3
pkgdesc="NetCDF fortran bindings"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/Unidata/netcdf-fortran"
license=(custom)
depends=(gcc-libs netcdf curl)
makedepends=(cmake gcc-fortran git doxygen)
checkdepends=(valgrind)
options=(!makeflags staticlibs)
source=(git+${url}#tag=v${pkgver})
sha256sums=('1bd7f7dbae3cc4e535742fd5a17a41be655df1c4063c0fd00b75cc105a10d814')

prepare() {
  cd $pkgname
# netcdf 4.9.3 compatibility
  git cherry-pick -n 7eff7deca464bcf195f0b37a1f0dba1786d1a19b \
                     eca8233d3e8c2a3588d6a79a660a8b5d4f1288fc
}

build() {
  cmake -B build -S ${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_FILTER_TEST=ON \
    -DENABLE_LARGE_FILE_TESTS=ON \
    -DTEST_WITH_VALGRIND=ON
  make -C build
}

check() {
  make -C build test
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Remove unwanted artifacts https://github.com/Unidata/netcdf-fortran/issues/338
  rmdir "${pkgdir}"/usr/include/CMakeFiles/{netcdff{,_c}.dir,}
  rm "${pkgdir}"/usr/lib/objects-RelWithDebInfo/{nf_,f}test_c/f03lib.c.o
  rmdir "${pkgdir}"/usr/lib/objects-RelWithDebInfo/{{nf_,f}test_c,}
  rm "${pkgdir}"/usr/lib/objects-Release/netcdff_c/nf_{lib,v2compat}.c.o
  rmdir "${pkgdir}"/usr/lib/objects-Release/{netcdff_c,}
}
