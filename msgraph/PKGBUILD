# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgbase=msgraph
pkgname=(
  msgraph
  msgraph-docs
)
pkgver=0.2.1
pkgrel=1
pkgdesc="A shared library for accessing MS Graph API"
url="https://gitlab.gnome.org/jbrummer/msgraph"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gobject-introspection-runtime
  json-glib
  libgoa
  librest
  libsoup3
  uhttpmock
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
)
_commit=9f3e53a7f38fa178e4c78caf271f9b93b3cf4c9b  # tags/0.2.1^0
source=("git+https://gitlab.gnome.org/jbrummer/msgraph.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd msgraph
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd msgraph
}

build() {
  arch-meson msgraph build
  meson compile -C build
}

check() {
  meson test -vC build --print-errorlogs
}

package_msgraph() {
  meson install -C build --destdir "$pkgdir"
  
  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_msgraph-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
