# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=msgraph
pkgname=(
  msgraph
  msgraph-docs
)
pkgver=0.2.2
pkgrel=1
pkgdesc="A shared library for accessing MS Graph API"
url="https://gitlab.gnome.org/GNOME/msgraph"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  json-glib
  libgoa
  librest
  libsoup3
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  uhttpmock
)
source=("git+https://gitlab.gnome.org/GNOME/msgraph.git#tag=$pkgver")
b2sums=('10e795148d2f2a497d0ebce1d3b9cdd62d02c0549f091179c4d07d373e868588148563f49b13395dc4cffd06ea7bfac08f60e64ff34bfc9215e82aebf484a909')

prepare() {
  cd msgraph
}

build() {
  arch-meson msgraph build
  meson compile -C build
}

check() {
  meson test -vC build --print-errorlogs
}

package_msgraph() {
  meson install -C build --destdir "$pkgdir"
  
  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_msgraph-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
