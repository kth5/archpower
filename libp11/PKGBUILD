# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: kevku <kevku@msn.com>
# Contributor: Rene PÃ¤rts <rene87@hot.ee>

pkgname=libp11
pkgver=0.4.14
pkgrel=2
pkgdesc="A library implementing a small layer on top of the PKCS11 API"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/OpenSC/libp11/wiki"
license=('LGPL-2.1-or-later')
depends=('openssl')
source=("https://github.com/OpenSC/${pkgname}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'util-h.patch::https://github.com/OpenSC/libp11/commit/fd8cbb43e9d98764d3f3b861a435cd0441cb7a58.patch')
sha256sums=('652ae2ac0732ec1eb998e8a99409eec6a00d5b47717f973b6bfb6c50f7a0ac60'
            'SKIP'
            '6642ad35457ed74c52bba157632fb79b3bbafe5b804bfe260954830a60cb2e1a')
validpgpkeys=('AC915EA30645D9D3D4DAE4FEB1048932DD3AAAA3')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../util-h.patch
}

build() {
  cd "$pkgname-$pkgver"
  automake
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
