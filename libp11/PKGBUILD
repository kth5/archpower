# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: kevku <kevku@msn.com>
# Contributor: Rene PÃ¤rts <rene87@hot.ee>

pkgname=libp11
pkgver=0.4.13
pkgrel=1.1
pkgdesc="A library implementing a small layer on top of the PKCS11 API"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
url="https://github.com/OpenSC/libp11/wiki"
license=('LGPL-2.1-or-later')
depends=('openssl')
source=("https://github.com/OpenSC/${pkgname}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('d25dd9cff1b623e12d51b6d2c100e26063582d25c9a6f57c99d41f2da9567086'
            'SKIP')
validpgpkeys=('AC915EA30645D9D3D4DAE4FEB1048932DD3AAAA3')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
