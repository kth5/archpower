# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: kevku <kevku@msn.com>
# Contributor: Rene PÃ¤rts <rene87@hot.ee>

pkgname=libp11
pkgver=0.4.12
pkgrel=3.1
pkgdesc="A library implementing a small layer on top of the PKCS11 API"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
url="https://github.com/OpenSC/libp11/wiki"
license=('LGPL-2.1-or-later')
depends=('openssl')
source=("https://github.com/OpenSC/${pkgname}/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "libp11-0.4.12-openssl-3.1.patch")
sha256sums=('1e1a2533b3fcc45fde4da64c9c00261b1047f14c3f911377ebd1b147b3321cfd'
            'SKIP'
            '8c06ef235508730c0f0e0fb7f928a3bd008804520a146ea058944b8172a29ac9')
validpgpkeys=('AC915EA30645D9D3D4DAE4FEB1048932DD3AAAA3')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../libp11-0.4.12-openssl-3.1.patch

  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
