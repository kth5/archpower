# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=rrdtool
pkgver=1.9.0
pkgrel=7
pkgdesc="Data logging and graphing application"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.rrdtool.org"
license=('GPL' 'custom')
depends=('libxml2' 'pango' 'ttf-dejavu')
makedepends=('intltool' 'ruby' 'python-setuptools' 'tcl' 'lua51' 'systemd')
optdepends=('perl: to use corresponding binding'
            'tcl: to use corresponding binding'
            'python: to use corresponding binding'
            'ruby: to use corresponding binding'
            'lua51: to use corresponding binding')
options=('!emptydirs' '!makeflags')
source=(https://github.com/oetiker/rrdtool-1.x/releases/download/v$pkgver/rrdtool-$pkgver.tar.gz)
sha256sums=('5e65385e51f4a7c4b42aa09566396c20e7e1a0a30c272d569ed029a81656e56b')

prepare() {
  cd ${pkgname}-${pkgver}

  # fix FS#28521 make ruby install to vendor_ruby instead of site_ruby
  sed -e 's/$(RUBY) ${abs_srcdir}\/ruby\/extconf.rb/& --vendor/' -i bindings/Makefile.am

  # https://github.com/oetiker/rrdtool-1.x/issues/1135
  autoreconf -vi
}

build() {
  cd ${pkgname}-${pkgver}
  LUA=/usr/bin/lua5.1 \
    LUA_CFLAGS="-I/usr/include/lua5.1 -llua5.1" LUA_INSTALL_CMOD="/usr/lib/lua/5.1" \
    CFLAGS+=" -Wno-incompatible-pointer-types" \
    ./configure --prefix=/usr --localstatedir=/var --disable-rpath \
    --enable-perl --enable-perl-site-install --with-perl-options='INSTALLDIRS=vendor' \
    --enable-ruby --enable-ruby-site-install --enable-python \
    --enable-lua --enable-lua-site-install --enable-tcl --disable-libwrap
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" includedir=/usr/include install
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
