# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.18.1
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=(any)
license=('GPL')
url="https://www.gnu.org/software/automake"
depends=('perl' 'bash')
makedepends=('autoconf' 'git')
checkdepends=('dejagnu' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope'
              'expect' 'ncompress' 'gettext' 'lzip' 'zip' 'sharutils' 'help2man'
              # disable TeX tests for now, lots of them fail and need upstream
              # fixes for current texlive release
              #'texlive-bin' 'texinfo'
              'python' 'python-virtualenv')
source=("git+https://git.savannah.gnu.org/git/automake.git#tag=v${pkgver}?signed")
sha512sums=('d22a02a3395fd6e866a68ef1fc6550fabf6d2b505f047d6aaba39685e90f97fa59dbbd788270495e6ea2ad605240fa38ad122c97203446e3fd2e32a242b16e9f')
validpgpkeys=('E1622F96D2BB4E58018EEF9860F906016E407573'   # Stefano Lattarini
              'F2A38D7EEB2B66405761070D0ADEE10094604D37'   # Mathieu Lirzin
              '155D3FC500C834486D1EEA677FD9FCCB000BEEEE'   # Jim Meyering
              '17D3311B14BC0F248267BF020716748A30D155AD')  # Karl Berry

prepare() {
	cd "${pkgname}"
	./bootstrap
}

build() {
	cd "${pkgname}"
	./configure --build=$CHOST --prefix=/usr
	make
}

check() {
	case "${CARCH}" in
		powerpc) return 0 ;;
	esac

	cd "${pkgname}"
	make check
}

package() {
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
}
