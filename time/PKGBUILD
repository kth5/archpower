# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: damir <damir@archlinux.org>

pkgname=time
pkgver=1.9
pkgrel=6
pkgdesc="Utility for monitoring a program's use of system resources"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.gnu.org/software/time/'
license=('GPL-3.0-only')
depends=('glibc')
source=("https://ftp.gnu.org/gnu/time/time-$pkgver.tar.gz"{,.sig})
sha512sums=('5c6dabbbe71e9103a47b892b86bb914c1704122d4fe7dff1e2cbd28503297163118d295077d8e062b035d673a1f91c36f8a45c7383f374fd766942b32bde4406'
            'SKIP')
b2sums=('b48c2def2f7aaa79ddb8838ed9e103dd7b1b573b6084d1481838cac86eb7930b3a231469936ad23d5096e576ea728e821defb27fc8b3f9187809e3f5c01b7c57'
        'SKIP')
validpgpkeys=('F576AAAC1B0FF849792D8CB129A794FD2272BC86') # "Assaf Gordon <assafgordon@gmail.com>"

build() {
  cd "$pkgname-$pkgver"

  # FTBFS with gcc15
  # https://savannah.gnu.org/bugs/index.php?66450
  CFLAGS+=' -std=gnu17'

  ./configure \
    --prefix=/usr \
    --infodir=/usr/share/info

  make
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"

  make exec_prefix="$pkgdir/usr" infodir="$pkgdir/usr/share/info" install
}
