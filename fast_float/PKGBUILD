# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Ã©clairevoyant
# Contributor: endlesseden <eden at rose dot place>

pkgname=fast_float
pkgver=6.1.6
pkgrel=2
pkgdesc='Fast and exact implementation of the C++ from_chars functions for float and double types'
arch=(any)
url="https://github.com/fastfloat/$pkgname"
license=('Apache-2.0 OR BSL-1.0 OR MIT')
makedepends=(
  'cmake'
  'doctest'
  'git'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('eb9833f7bff6e6086db1525fcc71231c5cc8f47985aa4c71078d896afb5fea20c41bfdb55de6c1e8c9cfa6c9b842331f340115590683e1d9e6a3ba40f4148870')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DFASTFLOAT_TEST=ON \
    -DSYSTEM_DOCTEST=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
}
