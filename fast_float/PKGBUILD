# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Ã©clairevoyant
# Contributor: endlesseden <eden at rose dot place>

pkgname=fast_float
pkgver=7.0.0
pkgrel=1
pkgdesc='Fast and exact implementation of the C++ from_chars functions for float and double types'
arch=(any)
url="https://github.com/fastfloat/$pkgname"
license=('Apache-2.0 OR BSL-1.0 OR MIT')
makedepends=(
  'cmake'
  'doctest'
  'git'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('7334a74952aba9629e0cd0f3b49679ed8f9c84436d3e9b10438d09416341343efc34acc0f87283a20fff5866d1e5787f456ac8a37f0a918823369419378ddb53')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DFASTFLOAT_TEST=ON \
    -DSYSTEM_DOCTEST=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname-$pkgver/LICENSE-MIT
}
