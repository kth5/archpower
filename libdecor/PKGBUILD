# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>
pkgdesc='Client-side decorations library for Wayland clients'
pkgname=libdecor
pkgver=0.2.1
pkgrel=1
url="https://gitlab.freedesktop.org/libdecor/libdecor"
license=('MIT')
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('wayland' 'pango' 'dbus' 'gtk3')
makedepends=('cmake' 'ninja' 'meson' 'wayland-protocols')
source=("https://gitlab.freedesktop.org/libdecor/libdecor/-/archive/${pkgver}/libdecor-${pkgver}.tar.gz")
sha512sums=('eb3d612c967f6c400562e400775cd38fce8ef433087f3cd6a72300aa083e483a8d3e1fd198d7bc1fbb2ca8a045eed5b146e0bc23352e048a9f49e719cf57faa7')

build() {
  arch-meson -Ddemo=false build "${pkgname}-${pkgver}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
