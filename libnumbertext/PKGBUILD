# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libnumbertext
pkgver=1.0.11
pkgrel=2
pkgdesc="Number to number name and money text conversion library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/Numbertext/libnumbertext"
#The entire source code is dual license LGPLv3+ or BSD, except for
#the data files hr.sor, sr.sor and sh.sor which are dual license
#CC-BY-SA or LGPLv3+
license=('LGPL-3.0-or-later OR BSD-3-Clause' 'LGPL-3.0-or-later OR CC-BY-SA-3.0')
depends=('gcc-libs' 'glibc')
source=(${pkgname}-${pkgver}::https://github.com/Numbertext/libnumbertext/archive/$pkgver.tar.gz)
sha256sums=('80aad7cab123edc614f904d9f145d1d15cf465084a1a15fca2117525dc746034')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # add dual license file
  install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 COPYING
}
