# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: feydaykyn <feydaykyn@yahoo.fr>

pkgname=leveldb
pkgver=1.23
pkgrel=4
pkgdesc="A fast and lightweight key/value database library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/google/leveldb/'
license=('BSD-3-Clause')
depends=('snappy')
makedepends=('cmake' 'gmock' 'benchmark')
source=("https://github.com/google/leveldb/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-leveldb-unbundle-gtest-and-benchmark.patch'
        '020-leveldb-do-not-disable-rtti.patch')
sha256sums=('9a37f8a6174f09bd622bc723b55881dc541cd50747cbd08831c2a82d620f6d76'
            '5625cd79df19ef4444a05a222b51be20c91da28d15389b929c6d5475f5fabbd1'
            '2046e9c93ce1df09715b432d7205dbdd7036049a079663d5c973a7d67eaab02d')

prepare() {
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-leveldb-unbundle-gtest-and-benchmark.patch"
    
    # don't disable RTTI, needed for ceph
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/020-leveldb-do-not-disable-rtti.patch"
}

build() {
    cmake -B build -S "${pkgname}-${pkgver}" \
        -G 'Unix Makefiles' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCMAKE_CXX_STANDARD:STRING='17' \
        -DBUILD_SHARED_LIBS:BOOL='1' \
        -Wno-dev
    cmake --build build
}

check() {
    ctest --test-dir build --output-on-failure
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    
    # documentation
    install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
    cp -dr --no-preserve='ownership' "${pkgname}-${pkgver}/doc"/* "${pkgdir}/usr/share/doc/${pkgname}"
    
    # license
    install -D -m644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
