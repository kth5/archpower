# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgbase=webkit2gtk
pkgname=(
  webkit2gtk
  webkit2gtk-docs
)
pkgver=2.46.6
pkgrel=2
pkgdesc="Web content engine for GTK"
url="https://webkitgtk.org"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(
  # :sort ui /\v^\s*['"]?/
  'AFL-2.0 OR GPL-2.0-or-later'
  Apache-2.0
  'Apache-2.0 WITH LLVM-exception'
  BSD-2-Clause
  BSD-2-Clause-Views
  BSD-3-Clause
  BSD-Source-Code
  BSL-1.0
  bzip2-1.0.6
  GPL-2.0-only
  'GPL-3.0-only WITH Autoconf-exception-3.0'
  'GPL-3.0-or-later WITH Bison-exception-2.2'
  ICU
  ISC
  LGPL-2.1-only
  LGPL-2.1-or-later
  MIT
  MPL-1.1
  MPL-2.0
  NCSA
  'NCSA OR MIT'
  OFL-1.1
  SunPro
  Unicode-TOU
)
depends=(
  at-spi2-core
  atk
  bubblewrap
  cairo
  enchant
  fontconfig
  freetype2
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  gtk3
  harfbuzz
  harfbuzz-icu
  hyphen
  icu
  lcms2
  libavif
  libdrm
  libegl
  libepoxy
  libgcrypt
  libgl
  libgles
  libjpeg-turbo
  libjxl
  libmanette
  libpng
  libseccomp
  libsecret
  libsoup
  libsystemd
  libtasn1
  libwebp
  libx11
  libxml2
  libxslt
  mesa
  openjpeg2
  pango
  sqlite
  wayland
  woff2
  xdg-dbus-proxy
  zlib
)
makedepends=(
  cmake
  gi-docgen
  glib2-devel
  gobject-introspection
  gperf
  gst-plugins-bad
  ninja
  python
  ruby
  ruby-stdlib
  systemd
  unifdef
  wayland-protocols
)
options=(
  # https://gitlab.archlinux.org/archlinux/packaging/packages/webkit2gtk-4.1/-/issues/1
  # https://bugs.webkit.org/show_bug.cgi?id=278090
  !lto
)
source=(
  $url/releases/webkitgtk-$pkgver.tar.xz{,.asc}
  reduce-memory-overheads.patch
  fix-ftbfs-riscv64.patch
)
sha256sums=('f2b31de693220ba9bab76ce6ddfe5b0bfab2515cb2b0a70f3c54d4050766c32b'
            'SKIP'
            '13df7f0b0a61866e767f280bbe77667126ae7de4ad4f463c682040da6366a864'
            'b42d36c275e5fbd6da8e0761381d9eac13b2d103a366a61d9d450421948385a3')
b2sums=('9428b17c4dd605667ba189da1587f6114eb175b319155f9c6e55c71f2b6a638778434dd0b5b32a5ee8320d1f69b622a5b0aac530d6e75ee3c17ba406ebfab7da'
        'SKIP'
        'cc16462ce22df999b47ef477d4272ef3df2fbbfe5cdc585f3ce47e56cde65a1d7dd1105e6e7bbff41695f79b6394bc3e403e53a669bb7f456c733ed216372c9b'
        'cd80efecf12982e9e9335032a433e094614c8b95edce7114d37c6d651ee1f5bb3e331c3911d7901055cd59ca3ed5de38d14e15fab5488d3248bf5899623b2f8c')
validpgpkeys=(
  # https://www.webkitgtk.org/verifying.html
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B # Adrián Pérez de Castro <aperez@igalia.com>
  013A0127AC9C65B34FFA62526C1009B693975393 # Carlos Garcia Campos <cgarcia@igalia.com>
)

prepare() {
  cd webkitgtk-$pkgver
  patch -Np1 -i ../reduce-memory-overheads.patch
  patch -Np1 -i ../fix-ftbfs-riscv64.patch
}

build() {
  local cmake_options=(
    -DPORT=GTK
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DCMAKE_INSTALL_LIBEXECDIR=lib
    -DCMAKE_SKIP_RPATH=ON
    -DUSE_GTK4=OFF
    -DUSE_LIBBACKTRACE=OFF
    -DUSE_SOUP2=ON
    -DENABLE_DOCUMENTATION=ON
    -DENABLE_MINIBROWSER=ON
  )

  case "${CARCH}" in
    powerpc64*)
      CFLAGS+=' -mminimal-toc'
      CXXFLAGS+=' -mminimal-toc'
    ;;
    powerpc)
      cmake_options+=(
        -DENABLE_WEBGL=OFF
        -DFORCE_32BIT=ON
      )
      export LDFLAGS+=" -Wl,--no-keep-memory"
    ;;
    riscv64)
      export LDFLAGS+=" -Wl,--no-keep-memory"
    ;;
    x86_64)
      # GCC with LTO fails to link libjavascriptcoregtk
      #     /usr/bin/ld: /tmp/ccXxyWZV.ltrans0.ltrans.o: in function `ipint_table_size_validate':
      #     <artificial>:(.text+0x49f0f): undefined reference to `ipint_extern_table_size'
      #     /usr/bin/ld: /tmp/ccXxyWZV.ltrans0.ltrans.o: in function `ipint_table_fill_validate':
      #     <artificial>:(.text+0x4a019): undefined reference to `ipint_extern_table_fill'
      #     collect2: error: ld returned 1 exit status
      export CC=clang CXX=clang++
      LDFLAGS+=" -fuse-ld=lld"

      # JITted code crashes when CET is used
      CFLAGS+=' -fcf-protection=none'
      CXXFLAGS+=' -fcf-protection=none'
    ;;
  esac

  # Produce minimal debug info: 4.3 GB of debug data makes the
  # build too slow and is too much to package for debuginfod
  CFLAGS+=' -g1'
  CXXFLAGS+=' -g1'

  cmake -S webkitgtk-$pkgver -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package_webkit2gtk() {
  provides+=(
    libjavascriptcoregtk-4.0.so
    libwebkit2gtk-4.0.so
  )
  optdepends=(
    'geoclue: Geolocation support'
    'gst-libav: nonfree media decoding'
    'gst-plugins-bad: media decoding'
    'gst-plugins-good: media decoding'
  )

  DESTDIR="$pkgdir" cmake --install build

  rm -r "$pkgdir/usr/bin"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc

  cd webkitgtk-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_webkit2gtk-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
