# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# POWER Contributor: Sadoon AlBader <sadoon@soulserv.xyz>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=webkit2gtk
pkgver=2.34.2
pkgrel=1
pkgdesc="Web content engine for GTK"
url="https://webkitgtk.org"
arch=(x86_64 powerpc64le powerpc)
license=(custom)
depends=(cairo fontconfig freetype2 libgcrypt glib2 gtk3 harfbuzz harfbuzz-icu
         icu libjpeg libsoup libxml2 zlib libpng sqlite atk libwebp at-spi2-core
         libegl libgl libgles libwpe wpebackend-fdo libxslt libsecret libtasn1
         enchant libx11 libxext libice libxt wayland libnotify hyphen openjpeg2
         woff2 libsystemd bubblewrap libseccomp xdg-dbus-proxy gstreamer
         gst-plugins-base-libs libmanette)
makedepends=(cmake ninja gtk-doc python ruby gobject-introspection
             wayland-protocols systemd gst-plugins-bad gperf)
optdepends=('geoclue: Geolocation support'
            'gst-plugins-good: media decoding'
            'gst-plugins-bad: media decoding'
            'gst-libav: nonfree media decoding')
source=($url/releases/webkitgtk-$pkgver.tar.xz{,.asc})
sha256sums=('584677d6e7cae12e27cdcc8e05b4cf73b54849a24afc3d7a40cec91016deff00'
            'SKIP')
validpgpkeys=('D7FCF61CF9A2DEAB31D81BD3F3D322D0EC4582C3'  # Carlos Garcia Campos <cgarcia@igalia.com>
              '5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

prepare() {
  cd webkitgtk-$pkgver

  sed -i -e '/foreach my.*(keys/s/(keys/(sort keys/' \
            Source/WebCore/bindings/scripts/CodeGenerator.pm \
            Source/WebCore/bindings/scripts/CodeGeneratorJS.pm \
            Source/WebCore/css/makeprop.pl \
            Source/cmake/tools/scripts/version-stamp.pl
}

build() {
  # WebKitCCache.cmake set this variable
  # include_file_mtime was not enabled to avoid a race,
  # but this is run under our control
  export CCACHE_SLOPPINESS=time_macros,include_file_mtime

  case "${CARCH}" in
    powerpc)
      LD_FLAGS+=' -Wl,--no-keep-memory'
      CFLAGS="${CFLAGS/-O?/-Os}"
      CXXFLAGS="${CXXFLAGS/-O?/-Os}"
      EXTRA_CMAKE_FLAGS="-DENABLE_JIT=OFF
                         -DUSE_SYSTEMD=OFF
                         -DPORT=GTK -DUSE_LD_GOLD=OFF
                         -DUSE_SYSTEM_MALLOC=ON
                         -DCMAKE_CXX_FLAGS_RELEASE=-DNDEBUG
                         -DCMAKE_CXX_FLAGS_DEBUG=-DNDEBUG
                         -DCMAKE_CXX_FLAGS_RELWITHDEBINFO=-DNDEBUG
                         -DCMAKE_C_FLAGS_RELEASE=-DNDEBUG
                         -DCMAKE_C_FLAGS_DEBUG=-DNDEBUG
                         -DCMAKE_C_FLAGS_RELWITHDEBINFO=-DNDEBUG
                         -DCMAKE_LINKER=${CHOST}-gcc"
    ;;
  esac

  cmake -S webkitgtk-$pkgver -B build -G Ninja \
    -DPORT=GTK \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DENABLE_GTKDOC=ON \
    -DENABLE_MINIBROWSER=ON \
    -DENABLE_BUBBLEWRAP_SANDBOX=ON \
    -DUSE_SOUP2=ON ${EXTRA_CMAKE_FLAGS}
  cmake --build build ${MAKEFLAGS}
}

package() {
  depends+=(libwpe-1.0.so libWPEBackend-fdo-1.0.so)
  provides+=(libjavascriptcoregtk-4.0.so libwebkit2gtk-4.0.so)

  DESTDIR="$pkgdir" cmake --install build

  cd webkitgtk-$pkgver
  find Source -name 'COPYING*' -or -name 'LICENSE*' -print0 | sort -z |
    while IFS= read -d $'\0' -r _f; do
      echo "### $_f ###"
      cat "$_f"
      echo
    done |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set sw=2 et:
