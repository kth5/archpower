# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=gettext
pkgver=0.23
pkgrel=1
pkgdesc="GNU internationalization library"
url="https://www.gnu.org/software/gettext/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
license=(
  GFDL-1.2-only
  GPL-2.0-only
  GPL-2.0-or-later
  LGPL-2.0-only
)
depends=(
  acl
  gcc-libs
  libunistring
  libxml2
  sh
)
makedepends=(
  emacs
)
optdepends=('git: for autopoint infrastructure updates')
options=(!docs)
source=(
  https://ftp.gnu.org/pub/gnu/gettext/$pkgname-$pkgver.tar.gz{,.sig}
  gettext-0.23-libxml2.patch
  )
b2sums=('32828852ff105c66d4034f8a689f3e2a007b8770daa72f7f99fec3fb5ee2be7a930fb534389edf59436c8f41b98122efab24931d0e8bea3a903d24b4d9a34bfb'
        'SKIP'
        'c93e039ac8fd150a2d210343886cdaa13308af091ed9f97b93d4137c52d51e84ec32f78961b1ee5a100f2e7f4c61ef47c7ad9282e28a406a89d11ef18c57510b')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871'  # Daiki Ueno
              '68D94D8AAEEAD48AE7DC5B904F494A942E4616C2'
              '9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D') # Bruno Haible (Open Source Development)

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../gettext-0.23-libxml2.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-csharp \
    --enable-nls \
    --with-xz \
    --without-included-gettext \
    --without-included-libunistring
  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' \
      -i gettext-{tools,runtime,runtime/libasprintf}/libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
