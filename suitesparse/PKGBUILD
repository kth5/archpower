# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=suitesparse
pkgver=7.2.0
pkgrel=3
pkgdesc='A collection of sparse matrix libraries'
url='http://faculty.cse.tamu.edu/davis/suitesparse.html'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(lapack mpfr)
makedepends=(gcc-fortran cmake lapack)
replaces=('suitesparse64<7.1.0')
license=(GPL)
source=(https://github.com/DrTimothyAldenDavis/SuiteSparse/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/DrTimothyAldenDavis/SuiteSparse/commit/5ffb460f.patch
        https://github.com/DrTimothyAldenDavis/SuiteSparse/commit/3344f13c.patch
        ppc32.patch)
sha256sums=('ce63c34f566d0aeae3c85fdc5b72d293f7e834d10ec0a0417b4c0823ce6c0474'
            '0c3aa16c082d78ee59600dd9800a6eaac517c77e0723fb92d2dc457c3c82822b'
            'ce6640789b8d0cee620c8ac33313805e3e11346a421adb476136cada57d4ab4a'
            'fe530b269842452aae6a76f3930b5355464a56ca1747b1bb8861f9751915557b')

prepare() {
  cd SuiteSparse-$pkgver
# Fix 32bit powerpc
  patch -p1 -i ../ppc32.patch
# Fix linking
  patch -p1 -i ../5ffb460f.patch
# Fix cmake files with None build type
  patch -p1 -i ../3344f13c.patch
}

build() {
  cd SuiteSparse-$pkgver
  CMAKE_OPTIONS="-DBLA_VENDOR=Generic \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=None \
  -DNSTATIC=ON" \
  make
}

package() {
  cd SuiteSparse-$pkgver
  DESTDIR="$pkgdir" make install
}
