# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

pkgname=texi2html
pkgver=5.0
pkgrel=8
pkgdesc="Converts texinfo documents to HTML"
arch=(any)
url="https://www.nongnu.org/texi2html/"
# GPLv2+ is for the code
# OFSFDL (Old FSF Documentation License) for the documentation
# CC-BY-SA or GPLv2 for the images
license=('GPL-2.0-or-later'
         'LicenseRef-OFSFDL'
         'CC-BY-SA-3.0 OR GPL-2.0-only')
depends=('perl')
source=(https://download.savannah.gnu.org/releases/texi2html/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('e8a98b0ee20c495a6ab894398a065ef580272dbd5a15b1b19e8bd1bc89d9f9fa'
            'SKIP')
validpgpkeys=('9804BBF566E198FCA15D5C215EC2A77F36ECC523') # "Patrice Dumas (Pertusus) <pertusus@free.fr>"

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --build=${CHOST}
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir"  install
  install -D -m644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -D -m644 images/{COPYING,COPYING.Attribution-ShareAlike,GPL,README} \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/images/"
}
