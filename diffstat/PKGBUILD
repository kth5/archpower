# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Matthias-Christian Ott <matthias.christian.at.tiscali.de>

pkgname=diffstat
pkgver=1.66
pkgrel=1
pkgdesc="Display a histogram of diff changes"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://invisible-island.net/diffstat"
depends=('glibc')
license=('MIT')
source=("https://invisible-island.net/archives/$pkgname/$pkgname-$pkgver.tgz"{,.asc})
sha256sums=('f54531bbe32e8e0fa461f018b41e3af516b632080172f361f05e50367ecbb69e'
            'SKIP')
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # Thomas Dickey <dickey@invisible-island.net>"

build () {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 package/debian/copyright "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
