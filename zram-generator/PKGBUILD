# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergey Shatunov <me@prok.pw>

pkgname=zram-generator
pkgver=1.2.1
pkgrel=1
pkgdesc="Systemd unit generator for zram devices"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/systemd/zram-generator"
license=('MIT')
depends=(systemd)
makedepends=(git rust ruby-ronn-ng)
source=("git+https://github.com/systemd/zram-generator.git#tag=v$pkgver")
sha256sums=('1433dddf34fec21f81c07e4c1431f35a2742a258c13a45a6be22aaa13c65d3d4')

build() {
  cd zram-generator
  make CARGOFLAGS="--target-dir=target" build man
}

check() {
  cd zram-generator
  make CARGOFLAGS="--target-dir=target" check
}

package() {
  cd zram-generator

  make CARGOFLAGS="--target-dir=target" DESTDIR="$pkgdir" install
  install -Dpm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
