# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=mac-fdisk
pkgver=0.1
pkgrel=20.1
pkgdesc="Partition table editor for Apple partition tables"
arch=(powerpc64le powerpc64 powerpc)
url="https://github.com/glaubitz/mac-fdisk-debian"
license=('GPL')
depends=(glibc)
makedepends=(git)
_commit='bda743065fa2c75a83fec60166bc2e317059ef7a'
source=("${pkgname}::git+https://github.com/glaubitz/mac-fdisk-debian#commit=${_commit}"
    largerthan2gb.patch
    big_pt.patch
    mac-fdisk-0.1-headers.patch
    mac-fdisk-0.1_p16-ppc-inline.patch
    mac-fdisk-0.1_p16-ppc64.patch
    mac-fdisk-0.1_p18-2tb.patch
    mac-fdisk-0.1_p18-lseek64.patch
    mac-fdisk-sbin-to-bin.patch)
sha256sums=('SKIP'
        '0132bb4185d5ec13a4f2220579763c83e6472eeba1c30f6972ba8e3039ffa0b3'
        '217659768cea64ac99133f3b67307c2687f6d8682f1dfe0669706a48a9fcb2ef'
        '02fa96e89dabe0eeaeb219ecbf88ec553639f7b84d58e7e058d589daf2cdff0f'
        'c10a17d83dcecf195ba862b41a61a7b529834dfa5cfcefdf472592fccf552cc0'
        '18ad277e06b9f67397a2d91b288fb67a303d4edc995300c8c6e5d614619b7858'
        '6fc10f883a83d4ed15ce1d9fe31458e7ca88aad5a7196d2d39f2cf106c69976f'
        'a7c45115ab1d4ba7c1505f1c8ff5f5d2c166a85b6b831b833d9138958fe4e72d'
        '06e54b18011c7219caa7f11c1ff3e774edea60078c760775559ea4fd0e5aaa83')

prepare() {
  cd "${pkgname}"
  patch -Np1 -i ${srcdir}/largerthan2gb.patch
  patch -Np1 -i ${srcdir}/big_pt.patch
  patch -Np1 -i ${srcdir}/mac-fdisk-0.1-headers.patch
  patch -Np1 -i ${srcdir}/mac-fdisk-0.1_p16-ppc-inline.patch
  patch -Np1 -i ${srcdir}/mac-fdisk-0.1_p16-ppc64.patch
  patch -Np1 -i ${srcdir}/mac-fdisk-0.1_p18-2tb.patch
  patch -Np1 -i ${srcdir}/mac-fdisk-0.1_p18-lseek64.patch
  patch -Np1 -i ${srcdir}/mac-fdisk-sbin-to-bin.patch
}

build() {
  cd "${pkgname}"
  CFLAGS="${CFLAGS/-Werror=format-security/}"
  make CFLAGS+=' -fcommon'
}

package() {
  cd "${pkgname}"
  mkdir -p "${pkgdir}/usr/bin"
  make DESTDIR="${pkgdir}/usr" install
}
