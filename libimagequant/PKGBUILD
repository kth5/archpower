# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libimagequant
pkgver=4.3.0
pkgrel=1
pkgdesc="Library for high-quality conversion of RGBA images to 8-bit indexed-color (palette) images"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://pngquant.org/lib/"
license=('BSD')
depends=('gcc-libs')
makedepends=('cargo' 'cargo-c')
source=("https://github.com/ImageOptim/libimagequant/archive/$pkgver/libimagequant-$pkgver.tar.gz")
sha256sums=('7f590ed400def273381ac066f46b9f9e8b3e0b1985a5b7e82ca7a65541a6681b')

build() {
    cd "$srcdir/$pkgname-$pkgver/imagequant-sys"
    cargo cbuild --release --prefix=/usr
}

check() {
    cd "$srcdir/$pkgname-$pkgver/imagequant-sys"
    cargo ctest --release
}

package() {
    cd "$srcdir/$pkgname-$pkgver/imagequant-sys"
    cargo cinstall --destdir="$pkgdir" --prefix=/usr --release
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
