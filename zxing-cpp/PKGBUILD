# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Maxime Arthaud <maxime@arthaud.me>

pkgname=zxing-cpp
pkgver=2.3.0
pkgrel=4
pkgdesc='An open-source, multi-format linear/matrix barcode image processing library implemented in C++'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/zxing-cpp/zxing-cpp'
license=(Apache-2.0)
depends=(gcc-libs
         glibc)
makedepends=(cmake
             git)
checkdepends=(gtest)
source=(git+https://github.com/zxing-cpp/zxing-cpp#tag=v$pkgver)
sha256sums=('1042010b960c29e6d7c5cd99e8c2a887cf84febb918abeb0405bcf12aaf8543c')

prepare() {
  cd $pkgname
  git cherry-pick -n 82806f5f92173b8cb4e1e9bee13a2d07a33fb69f # Fix improper use of NDEBUG
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DZXING_EXAMPLES=OFF \
    -DZXING_UNIT_TESTS=ON \
    -DZXING_C_API=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
