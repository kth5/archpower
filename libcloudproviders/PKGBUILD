# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fina Wilke <fina@felinira.net>

pkgbase=libcloudproviders
pkgname=(
  libcloudproviders
  libcloudproviders-docs
)
pkgver=0.4.0
pkgrel=1
pkgdesc="DBus API that allows cloud storage sync clients to expose their services"
url="https://gitlab.gnome.org/GNOME/libcloudproviders"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-3.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('0f81499be9382d841bd3f94e7b2469616b34622de3c1355907c28cf7e130c089d5de2dcb5ca3e0ac3f9872f304b4e7ea12c6862abbfa6de2365f4900b6e5905b')
validpgpkeys=(
  AD1483F8A95118F7F0C8AF4A965D4777208E7691 # Corentin NoÃ«l <corentin.noel@collabora.com>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D documentation=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs --no-suite doc
}

package_libcloudproviders() {
  provides=(libcloudproviders.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libcloudproviders-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
