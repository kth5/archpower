# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm-2.68
pkgname=(
  glibmm-2.68
  glibmm-2.68-docs
)
pkgver=2.80.0
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  libsigc++-3.0
)
makedepends=(
  clang
  git
  meson
  mm-common
  perl-xml-parser
)
checkdepends=(glib-networking)
_commit=55b6b756d9cdf444d86655dc2db8d183b6fc94a2  # tags/2.80.0^0
source=("git+https://gitlab.gnome.org/GNOME/glibmm.git#commit=$_commit")
b2sums=('d20971b13d4477f0b4dbf0fb4569e3b49e354c6e8c75be0ac3d3f158d02219a49b32c983df1c50c54047f986a80947841131d0b7c20ee9c6bc11361c6bdcb7f2')

pkgver() {
  cd glibmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glibmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson glibmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_glibmm-2.68() {
  depends+=(libsigc-3.0.so)
  provides=(libglibmm-2.68.so)
  options=(!emptydirs)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_glibmm-2.68-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
