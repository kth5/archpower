# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm-2.68
pkgname=(
  glibmm-2.68
  glibmm-2.68-docs
)
pkgver=2.82.0
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="https://www.gtkmm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libsigc++-3.0
)
makedepends=(
  clang
  git
  meson
  mm-common
  perl-xml-parser
)
checkdepends=(glib-networking)
options=(!emptydirs)
source=("git+https://gitlab.gnome.org/GNOME/glibmm.git#tag=$pkgver")
b2sums=('164d68ae63678d1b89d6358a77235e0e0c5996766831669faad122752fbc2c5d17769a9b22c70d2a7827ff3139d21d2e3531eff0745fc4b61636c9206eb1dd5b')

prepare() {
  cd glibmm
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson glibmm build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_glibmm-2.68() {
  depends+=(libsigc-3.0.so)
  provides=(libglibmm-2.68.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_glibmm-2.68-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
