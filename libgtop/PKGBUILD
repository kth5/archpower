# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgtop
pkgver=2.41.3
pkgrel=1
pkgdesc='Library for collecting system monitoring data'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gitlab.gnome.org/GNOME/libgtop'
license=('GPL-2.0-or-later')
depends=('glib2' 'glibc' 'libxau')
makedepends=('gobject-introspection' 'gtk-doc')
provides=('libgtop-2.0.so')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('775676df958e2ea2452f7568f28b2ea581063d312773dd5c0b7624c1b9b2da8c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
