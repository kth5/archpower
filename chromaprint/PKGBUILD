# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Wieland Hoffmann <the_mineo@web.de>

pkgname=chromaprint
pkgver=1.6.0
pkgrel=2
pkgdesc="Library for extracting fingerprints from any audio source"
url="https://acoustid.org/chromaprint"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('LGPL-2.1-or-later' 'MIT')
depends=(gcc-libs
         glibc)
makedepends=(cmake
             ffmpeg
             git
             gtest)
provides=('libchromaprint.so')
# upstream signs with DSA key: https://github.com/acoustid/chromaprint/issues/81
source=(git+https://github.com/acoustid/chromaprint#tag=v$pkgver
        chromaprint-use-system-gtest.patch)
sha512sums=('8025bc06d175efef6969f35f48cb9186e8ef89525076a71563675127c597f2b715843907f290614e47d6fd34979968be7dea64a79b574a80a348e114584e83ef'
            '2466d8b84fb72c3d6e2e53c2ec9619533774be75fbfb002c63d2d530775bf98189951c8467a652882280d77a8ef7f22488e2ab4ae1b5ddd70a4914143d9bc3a6')
b2sums=('fd9a03b69b0424e6789e95958f49848c2b143752eef18897bb40085d6d22feae90cfebe4ef01d170b8af0930940624730c045b440f05b2794a227c39884ac75b'
        '79b9b5d4f208e69322ec89b803e873c5d1412ce8fda83a3bae2a93d7036a24388a995820af2ec67f9485fe189d7780b81e15244c4589c62cf3757b2296161c47')

prepare() {
  patch -d $pkgname -p1 < chromaprint-use-system-gtest.patch
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_TESTS=ON \
        -DBUILD_TOOLS=ON \
        -Wno-dev \
        -B build \
        -S $pkgname
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  depends+=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'libswresample.so')
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname/{NEWS.txt,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
