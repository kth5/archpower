# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=libgudev
pkgver=238
pkgrel=3
pkgdesc="GObject bindings for libudev"
url="https://gitlab.gnome.org/GNOME/libgudev"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libsystemd
)
makedepends=(
  git
  glib2-devel
  glibc-locales
  gobject-introspection
  gtk-doc
  meson
  systemd
  umockdev
  vala
)
source=("git+$url.git#tag=$pkgver")
b2sums=('6da9512d87154fbd62aae044a39abd84bee451f5a21203180062c369ebedf2ee1bc7672408cf806918111397bc74be58314e6c3155e638137603d11012a4355f')

prepare() {
  cd libgudev
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson libgudev build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libg{lib,object}-2.0.so
    libudev.so
  )
  provides+=(libgudev-1.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
