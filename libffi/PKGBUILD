# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.4.8
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
url='https://sourceware.org/libffi/'
_url="https://github.com/libffi/libffi"
license=(MIT)
depends=(glibc)
checkdepends=(dejagnu)
provides=(libffi.so)
source=($pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('cbb7f0b3b095dc506387ec1e35b891bfb4891d05b90a495bc69a10f2293f80ff')
b2sums=('e01f46e0048c1805dd8a7c293c482aaa2906c0c94b76f3624ebbdccd3a7775f322c832329d270c0aaf620b5a3d7f12b498864af478955cdcbf079fc5d31443a7')

prepare() {
  # remove broken m4 stuff: https://github.com/libffi/libffi/issues/853
  rm -v $pkgname-$pkgver/m4/ax_enable_builddir.m4

  cd $pkgname-$pkgver
  autoreconf -fiv

}

build() {
  local configure_options=(
    # remove --disable-exec-static-tramp once ghc and gobject-introspection
    # work fine with it enabled (https://github.com/libffi/libffi/pull/647)
    --disable-exec-static-tramp
    --disable-multi-os-directory
    --disable-static
    --enable-pax_emutramp
    --prefix=/usr
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}

# vim:set ts=2 sw=2 et:
