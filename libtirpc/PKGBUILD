# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=1.3.6
pkgrel=2
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://git.linux-nfs.org/?p=steved/libtirpc.git;a=summary"
license=('SISSL' 'BSD-3-Clause')
depends=('krb5' 'glibc')
backup=('etc/netconfig')
# git tree: git://linux-nfs.org/~steved/libtirpc
source=(https://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2
        gcc15.patch)
sha1sums=('03352908461ad2122e5be4a678893aaa2ad2ac45'
          '25312e435324dc51a3afee435def5a73147ef4dc')
sha512sums=('f3ccc45f9de8d517ccc10dd4b68b844bbe71a2cb61ca5819995d97319353f7e24cfee205e09dddf65cf47605314176aee161091d7379b6113ee8d1f5077c93d0'
            'f6a5545c078fa572a86cecd6dfe9d85da104f909762acadb5f7f59117bc45cc9104f4e6823ac2efaa7182eb12ba5e48098183a5126afccaa2517526007dbd587')

prepare() {
  cd ${pkgname}-${pkgver}
  # upstream fixes
  patch -Np1 -i ../gcc15.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconf=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
