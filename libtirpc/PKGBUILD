# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=1.3.7
pkgrel=1
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://git.linux-nfs.org/?p=steved/libtirpc.git;a=summary"
license=('SISSL' 'BSD-3-Clause')
depends=('krb5' 'glibc')
backup=('etc/netconfig')
# git tree: git://linux-nfs.org/~steved/libtirpc
source=(https://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('885aaa0b9c348d06dd8a9e2ae0a1a7343711eb1b')
sha512sums=('50c0153bc72a3f9578eb7a9dc123ea531df83c455f19b03d2bc59563d8453fcd278a025eef05865d1218b2c9358a9152ee069ce0e4d11dfea4ca0a8a24221fad')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconf=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
