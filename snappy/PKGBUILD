# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Antony Male <antony dot male at geemail dot com>>

pkgname=snappy
pkgver=1.2.2
pkgrel=1
pkgdesc='A fast compressor/decompressor library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/google/snappy"
license=('BSD-3-Clause')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'clang'
  'cmake'
  'gtest'
)
checkdepends=('zlib')
provides=('libsnappy.so')
source=(
  "$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
  snappy.pc.in
  cmake_add_pkgconfig.patch
  use_system_gtest.patch
)
sha256sums=('90f74bc1fbf78a6c56b3c4a082a05103b3a56bb17bca1a27e052ea11723292dc'
            '134f06ca0584a1026538d0fb972fc141c008390ecae1806184b721eca1abbc75'
            '2a204d7d35509ffc290d80c641fcb7e8488c67ca550aa1695493cff12c53156b'
            '50b31b45511e8907f1319305c102f37631b0fbe509f862b612e70073383658e4')

prepare() {
  cd $pkgname-$pkgver
  cp ../snappy.pc.in .
  patch -Np1 < ../cmake_add_pkgconfig.patch # https://bugs.archlinux.org/task/71246
  patch -Np1 < ../use_system_gtest.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -W no-dev \
    -DCMAKE_CXX_STANDARD=23 \
    -DBUILD_SHARED_LIBS=ON \
    -DSNAPPY_BUILD_BENCHMARKS=OFF
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  cmake --build build --target test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
