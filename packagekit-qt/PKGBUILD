# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=packagekit-qt
pkgname=(packagekit-qt6)
pkgdesc='Qt bindings for PackageKit'
pkgver=1.1.4
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='http://www.packagekit.org/'
license=(LGPL-2.1-only)
depends=(gcc-libs
         glibc
         packagekit
         qt6-base)
makedepends=(cmake
             git)
source=(git+https://github.com/PackageKit/PackageKit-Qt#tag=v$pkgver?signed)
sha256sums=('5a765c5f0183fee918b5f2ee0ba72e59c748ef255a2eef0bf9b0530f67401dfa')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17'  # Richard Hughes <richard@hughsie.com>
              'D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB'  # Matthias Klumpp <matthias@tenstral.net>
              '70C26659D184ABC01FFAF45228DDEDC6E4480AD0') # Daniel Nicoletti <dantti12@gmail.com>

build() {
  cmake -B build -S PackageKit-Qt \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package_packagekit-qt6() {
  DESTDIR="$pkgdir" cmake --install build
}
