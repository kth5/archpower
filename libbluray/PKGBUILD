# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=1.4.0
pkgrel=2
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=https://www.videolan.org/developers/libbluray.html
license=(LGPL-2.1-only)
depends=(
  fontconfig
  glibc
  libfreetype.so
  libxml2
)
makedepends=(
  apache-ant
  git
  java-environment=17
  meson
  ninja
)
optdepends=('java-runtime: BD-J library')
provides=(libbluray.so)
_tag=9f07fbb2077be7a40b062bcf2463a9941c2a3b13
source=(
  git+https://code.videolan.org/videolan/libbluray.git#tag=${_tag}
  git+https://code.videolan.org/videolan/libudfread.git
)
b2sums=('12f30033f1d4efc24acd1886de75347de3301e708b2fdae48cdce1d29064af6235302dd33f5bc0ee8d0fa4adf61c05590818b26a15c7dfb76c188dfb59eed128'
        'SKIP')

pkgver() {
  cd libbluray
  git describe --tags
}

prepare() {
  cd libbluray
  for submodule in contrib/libudfread; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

build() {
  arch-meson libbluray build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
