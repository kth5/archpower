# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=1.3.1
pkgrel=1
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=(x86_64 powerpc64le powerpc riscv64)
url=https://www.videolan.org/developers/libbluray.html
license=(LGPL2.1)
depends=(
  fontconfig
  libfreetype.so
  libxml2
)
makedepends=(
  apache-ant
  git
  java-environment
)
optdepends=('java-runtime: BD-J library')
provides=(libbluray.so)
_tag=0540b1c1c53f9813f99233208c5ccccd87004f98
source=(
  git+https://code.videolan.org/videolan/libbluray.git#tag=${_tag}
  git+https://code.videolan.org/videolan/libudfread.git
)
b2sums=(
  SKIP
  SKIP
)

pkgver() {
  cd libbluray

  git describe --tags
}

prepare() {
  cd libbluray

# Fix build with recent java
  git cherry-pick -n 8f26777b1ce124ff761f80ef52d6be10bcea323e

  for submodule in contrib/libudfread; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git submodule update ${submodule}
  done

  autoreconf -fiv
}

build() {
  cd libbluray

  ./configure \
    --prefix=/usr
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libbluray install
}

# vim: ts=2 sw=2 et:
