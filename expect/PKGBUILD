# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=expect
pkgver=5.45.4
pkgrel=2
pkgdesc='A tool for automating interactive applications'
arch=(x86_64 powerpc64le)
url='https://www.nist.gov/el/msid/expect.cfm'
license=(custom)
depends=(tcl)
source=(https://downloads.sourceforge.net/project/expect/Expect/$pkgver/expect${pkgver}.tar.gz)
sha256sums=('49a7da83b0bdd9f46d04a04deec19c7767bb9a323e40c4781f89caf760b92c34')

build() {
  cd expect${pkgver}
  ./configure --build=$CHOST --prefix=/usr --mandir=/usr/share/man 
  make
}

check() {
  cd expect${pkgver}
  make test
}

package() {
  cd expect${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm644 README "$pkgdir/usr/share/licenses/$pkgname/README"
}
