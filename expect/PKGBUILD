# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=expect
pkgver=5.45.4
pkgrel=5
pkgdesc='A tool for automating interactive applications'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.nist.gov/el/msid/expect.cfm'
license=(custom)
depends=(tcl)
source=(https://downloads.sourceforge.net/project/expect/Expect/$pkgver/expect${pkgver}.tar.gz
	expect-configure-c99.patch
	expect-c99.patch
	expect-5.45.4-covscan-fixes.patch
	expect-5.45-format-security.patch)
sha256sums=('49a7da83b0bdd9f46d04a04deec19c7767bb9a323e40c4781f89caf760b92c34'
            '26532d2eca40f97d5461eda3705f04d5d636e759268fe8b7b39ce4d7245aa622'
            '5f218d18310052de3a05f1d795f7bb44fc9f058d26069a19e743194d35c49388'
            '60caefbec8190300e9e975bd5328281ab640ee7c74023956a79414c80b79d1d3'
            'b141e1a18186aaedb48ad503f34848413819f7a55789d86d04f14e8ae4cacc56')

prepare() {
  cd expect${pkgver}
  patch -Np0 -i ${srcdir}/expect-5.45-format-security.patch
  patch -Np1 -i ${srcdir}/expect-c99.patch
  patch -Np1 -i ${srcdir}/expect-5.45.4-covscan-fixes.patch
  patch -Np1 -i ${srcdir}/expect-configure-c99.patch

  autoreconf -i
}

build() {
  cd expect${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --build=${CHOST}
  make
}

check() {
  cd expect${pkgver}
  make test
}

package() {
  cd expect${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm644 README "$pkgdir/usr/share/licenses/$pkgname/README"

  # avoid file conflict with the more powerful mkpasswd from the whois package
  mv -v "$pkgdir"/usr/bin/mkpasswd{,-expect}
  mv -v "$pkgdir"/usr/share/man/man1/mkpasswd{,-expect}.1
}
