# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrián Pérez de Castro <aperez@igalia.com>

pkgname=lowdown
pkgver=2.0.2
pkgrel=1
pkgdesc='A simple Markdown translator'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://kristaps.bsd.lv/lowdown'
license=('ISC')
depends=('glibc' 'libmd')
makedepends=('git' 'bmake')
provides=('liblowdown.so')
source=("$pkgname::git+https://github.com/kristapsdz/lowdown#tag=$(echo $pkgver | sed -e 's/^/VERSION_/' -e 's/\./_/g')")
sha512sums=('286a3dc516bc8049ba1a6cf90fe3c78ed9a053617148010947238dd47ac7812f97b96a4ee2dc7ae5d9898138d4df0c6a18e01962f9e9e2628f7156dfecc9814a')
b2sums=('9891f9ffdf78922811f61620a7fcf9dd71d546026002d0bf5e9f179efff8fc63dfc002ce2dc7e02a053cb0c5225b91f16f2d950a995e84453311e4e2cd759064')

build () {
  cd "$pkgname"

	./configure \
    PREFIX=/usr \
    MANDIR=/usr/share/man

  # ensure LDFLAGS is passed correctly
  sed -i "s/^LDFLAGS.*/LDFLAGS = $LDFLAGS/" Makefile.configure

	bmake
}

check () {
	bmake -C "$pkgname" regress
}

package () {
  cd "$pkgname"

  # package
	bmake DESTDIR="$pkgdir" \
    install \
    install_lib_common \
    install_shared

  # symlink unversioned to versioned shared library
  local LIBVER=$(grep "^LIBVER" Makefile | sed "s/.*= //")
  ln -sf "/usr/lib/liblowdown.so.$LIBVER" "$pkgdir/usr/lib/liblowdown.so"

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
