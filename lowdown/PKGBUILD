# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrián Pérez de Castro <aperez@igalia.com>

pkgname=lowdown
pkgver=2.0.4
pkgrel=1
pkgdesc='A simple Markdown translator'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://kristaps.bsd.lv/lowdown'
license=(ISC)
depends=(glibc libmd)
makedepends=(git bmake)
provides=(liblowdown.so)
source=("$pkgname::git+https://github.com/kristapsdz/lowdown#tag=$(echo $pkgver | sed -e 's/^/VERSION_/' -e 's/\./_/g')")
sha512sums=('d30350b64b49722c27f6da31bb4a4306bc2b9329ed07d2e02e9c9b6ce2f7c281a4c72a6bec2efa383803c50fe5d834687f9f7435538229d4fcbacd7927749f42')
b2sums=('3307166d40f95247bb6e8ebccee83d0877234ba2fce7e9b80d216ab1229dd21c26e61b3ce399740b8449c6dd647df784d7be8d2a367d647988a690dadfad2906')

build () {
  cd "$pkgname"

	./configure \
    PREFIX=/usr \
    MANDIR=/usr/share/man

  # ensure LDFLAGS is passed correctly
  sed -i "s/^LDFLAGS.*/LDFLAGS = $LDFLAGS/" Makefile.configure

	bmake
}

check () {
	bmake -C "$pkgname" regress
}

package () {
  cd "$pkgname"

  # package
	bmake DESTDIR="$pkgdir" \
    install \
    install_lib_common \
    install_shared

  # symlink unversioned to versioned shared library
  local LIBVER=$(grep "^LIBVER" Makefile | sed "s/.*= //")
  ln -sf "/usr/lib/liblowdown.so.$LIBVER" "$pkgdir/usr/lib/liblowdown.so"

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
