# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=pugixml
pkgver=1.15
pkgrel=2
pkgdesc="Light-weight, simple and fast XML parser for C++ with XPath support"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://pugixml.org"
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(cmake)
source=(https://github.com/zeux/pugixml/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('94f3e33d225a6cc4cf57b353704a5e1bfea989fecf4616958db85be1d2a73ef1b7bb0667998576134b820d7c0e9ea30691caad91e67aca7c26f558ed3b31ebf8')

build() {
  local cmake_options=(
    -B build
    -S $pkgname-$pkgver
    -W no-dev
    -DCMAKE_BUILD_TYPE=None
    -DBUILD_SHARED_LIBS=ON
    -DCMAKE_INSTALL_PREFIX=/usr
  )
  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
