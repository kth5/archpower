# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Storm Dragon <stormdragon2976@gmail.com>
# Contributor: Aaron 'venisonslurpee' Laursen <venisonslurpee@gmail.com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
# Contributor: lh <jarryson@gmail.com>
# Contributor: Sebastian Schwarz <seschwar@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Xilon <xilon@gmail.com>

pkgname=xmms2
pkgver=0.9.6
pkgrel=2
pkgdesc='X-platform Music Multiplexing System 2'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/xmms2/wiki/wiki'
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  glib2
  glibc
  readline
)
optdepends=(
  'alsa-lib: ALSA output'
  'avahi: announce xmms2d via bonjour/mDNS/zeroconf'
  'curl: play HTTP streams'
  'faad2: AAC support'
  'ffmpeg: WMA, avcodec & avformat support'
  'fftw: visualization'
  'flac: FLAC support'
  'fluidsynth: MIDI support'
  'jack: JACK output'
  'libao: libao output'
  'libcdio-paranoia: CDDA support'
  'libdiscid: CDDA support'
  'libgme: support for various video game music formats'
  'libmad: MP3 support'
  'libmms: play MMS streams'
  'libmodplug: MOD support'
  'libmpcdec: Musepack support'
  'libpulse: PulseAudio output'
  'libsamplerate: vocoder support'
  'libshout: Icecast output'
  'libsndfile: sampled sound support'
  'libvorbis: Ogg Vorbis support'
  'libxml2: XSPF and podcast support'
  'mpg123: alternative MP3 support'
  'opusfile: Opus support'
  'perl: Perl bindings'
  'python: Python bindings'
  'ruby: Ruby bindings'
  'smbclient: direct CIFS/SMB access'
  'speex: Speex support'
  'sqlite: for sqlite2s4'
  'wavpack: WavPack support'
)
makedepends=(
  "${optdepends[@]%%:*}"
  boost
  cython
  git
  perl-pod-parser
  waf
)
source=(
  "xmms2::git+https://github.com/xmms2/xmms2-devel.git?signed#tag=$pkgver"
  git+https://github.com/xmms2/xmms2-tutorial.git
  git+https://github.com/xmms2/s4.git
  tmpfiles.conf
  sysusers.conf
  system.service
  user.service
)
b2sums=(
  265d1090260e50c49b0f54f9e108f4da7449c85f33b048806e0cf661f89d8a5253f44e741e59f9a0f4d8702f4fca8b7b0c9c7b60fa98fc7a1a083da20e53e1d0
  SKIP
  SKIP
  2042a966321a8581efbd981edcaf680054c8f91df6b8373e117e51cd4d1d208ba43da723f33995dee941b7e3fc44c46f76b4dcd7a70dc07f9a2eb7a27a159639
  58e3d33add4dae998dfe034267f216745467005fc44e646bbb228ac6885f6cf77c5964da299c050a695fb260700aafa988be26ce6dea96580ab8f1fda45de593
  e7f14c27fbe8113feba2d2c6c1d4afb7605c43c63004fdbbf18abefea08d303e7d59db30e315206b722bdf044add00492538bd3b03ef01730dd120916aa78004
  96d2174b8321176bd94753c87cda79012bc30f024b449472b41be6fd1577c1c8e84f1d2df2282d0cf6ce8e5fce45e9e3f12ccd0515958a23c9ebdd22860fc98d
)
validpgpkeys=(62197C11C7C25A61C448E95644FE231F3F3926E4) # Sergei Trofimovich <slyfox@gentoo.org>

prepare() {
  cd $pkgname
  git submodule init
  git submodule set-url doc/tutorial "$srcdir/xmms2-tutorial"
  git submodule set-url src/lib/s4 "$srcdir/s4"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $pkgname
  export LINKFLAGS="$LDFLAGS"
  ./waf configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin --without-ldconfig \
    --with-ruby-archdir=`ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]'` \
    --with-ruby-libdir=`ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]'` \
    --with-perl-archdir=`perl -V:installvendorarch | cut -f2 -d\'` \
    --with-optionals=launcher,xmmsclient++,xmmsclient++-glib,perl,ruby,nycli,pixmaps,et,mdns,medialib-updater,sqlite2s4,python
  ./waf build
}

package() {
  cd $pkgname
  ./waf --destdir="$pkgdir" install

  install -Dm644 ../sysusers.conf "$pkgdir/usr/lib/sysusers.d/xmms2.conf"
  install -Dm644 ../tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/xmms2.conf"
  install -Dm644 ../system.service "$pkgdir/usr/lib/systemd/system/xmms2d.service"
  install -Dm644 ../user.service "$pkgdir/usr/lib/systemd/user/xmms2d.service"
}
