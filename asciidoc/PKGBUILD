# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Jaroslaw Rosiek <philosoph@interia.pl>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=asciidoc
pkgver=10.2.0
pkgrel=6
pkgdesc='Text document format for short documents, articles, books and UNIX man pages'
arch=(any)
url='https://asciidoc-py.github.io'
_url='https://github.com/asciidoc-py/asciidoc-py'
license=(GPL-2.0-or-later)
depends=(docbook-xsl
         libxslt
         python)
checkdepends=(dblatex
              graphviz
              lilypond
              python-pytest{,-mock}
              source-highlight)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
optdepends=('graphviz: graphviz-filter'
            'lilypond: music-filter'
            'imagemagick: music-filter'
            'source-highlight: source-highlight-filter'
            'dblatex: pdf generation'
            'fop: alternative pdf generation'
            'w3m: text generation'
            'lynx: alternative text generation')
source=("$_url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"
        "suppress-python-warnings.patch::$_url/commit/cf72db316e3281249c6f69c5591a0092b231b53d.patch")
sha256sums=('237b2ba5c35c0ae7ccd4cd44ebf1d87c20b2695dae01798954416d492ef7fa0e'
            '26c4f502e92b975c9c1c6b86ff85e62fda2e3a7ddf3efddaa0bdbb9c88f2aeb8')
b2sums=('ed5db8fd1432b255e9c03e17805c26c76a42392c2ebeaf839b8c0aaaf38cd1a36d79b645ca4474503181200924df91218eb0997dd82335a30d6a0a1b35eb5730'
        '3934a6cd8dd5758219cc75eb5b94e545e7e093c413beb3a8b1d1bbb6984e260b5afafea2688783b4845757707303b83d5ccb5b51d21c9dc9acdfa06e77738854')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i \
    -e '/^build: /a \\tpython3 -m build -wn' \
    -e '/pip install/{s#pip install --root#installer -d#;s#\.$#dist/*.whl#}' \
    Makefile.in
  # https://gitlab.archlinux.org/archlinux/packaging/packages/asciidoc/-/issues/2
  patch -p1 -i ../suppress-python-warnings.patch
  # https://github.com/asciidoc-py/asciidoc-py/issues/234
  autoconf
}

build() {
  cd ${pkgname}-${pkgver}
  # https://github.com/asciidoc-py/asciidoc-py/issues/234#issuecomment-1045970138
  ./configure \
      --docdir /usr/share/doc/$pkgname \
      --prefix /usr
  make build
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" docs
  install -Dm0644 -t "$pkgdir/usr/share/man/man1/" doc/*.1
  # ascidocapi is deprecated, but still included in 10.x on a "provisional" basis
  local _platlib="$(python -c 'import sysconfig; print(sysconfig.get_paths()["platlib"])')"
  install -Dm0644 -t "${pkgdir}/${_platlib}/${pkgname}/" asciidoc/api.py
}
