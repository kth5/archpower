# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libdex
pkgname=(
  libdex
  libdex-docs
)
pkgver=0.6.1
pkgrel=1
pkgdesc="A library supporting 'Deferred Execution'"
url="https://gitlab.gnome.org/GNOME/libdex"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  liburing
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  libsoup3
  libsysprof-capture
  meson
  vala
)
source=("git+$url.git#tag=$pkgver")
b2sums=('86e693b7e4be5d967631023b2f7464c757889a6fbb01b12c57fed74b5b88a949609db009c491a3577ef0868b65625aba0ef8d9aae459ca1bdf0fbafb195a6914')

prepare() {
  cd libdex
}

build() {
  local meson_options=(
    -D docs=true
    -D sysprof=true
  )

  arch-meson libdex build "${meson_options[@]}"
  meson compile -C build
}

check() (
  meson test -C build --print-errorlogs
)

package_libdex() {
  provides=(libdex-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libdex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
