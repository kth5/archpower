# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libdex
pkgname=(
  libdex
  libdex-docs
)
pkgver=1.0.0
pkgrel=1
pkgdesc="A library supporting 'Deferred Execution'"
url="https://gitlab.gnome.org/GNOME/libdex"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  liburing
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  libsoup3
  libsysprof-capture
  meson
  vala
)
source=("git+$url.git#tag=$pkgver")
b2sums=('7562688231fc1d82a6e1d4da901cb58912c36305477f09cc72aab6d9f001741b475042b91f66b841d2cf6ed6c9f425e21b3b997d9a55f17f3131ccfc0457a9e9')

prepare() {
  cd libdex
}

build() {
  local meson_options=(
    -D docs=true
    -D sysprof=true
  )

  arch-meson libdex build "${meson_options[@]}"
  meson compile -C build
}

check() (
  meson test -C build --print-errorlogs
)

package_libdex() {
  provides=(libdex-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libdex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
