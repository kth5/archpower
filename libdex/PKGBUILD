# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libdex
pkgname=(
  libdex
  libdex-docs
)
pkgver=0.8.1
pkgrel=1
pkgdesc="A library supporting 'Deferred Execution'"
url="https://gitlab.gnome.org/GNOME/libdex"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  liburing
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  libsoup3
  libsysprof-capture
  meson
  vala
)
source=("git+$url.git#tag=$pkgver")
b2sums=('4a34d924e7dc75bfd4138ac864a0cdf4f36aca139c4045f92b4dbe7c06d640ba73b4379902d3c25c9193f9a6c2ac3a30bc52868a74638ddb0e105b34bfd16a58')

prepare() {
  cd libdex
}

build() {
  local meson_options=(
    -D docs=true
    -D sysprof=true
  )

  arch-meson libdex build "${meson_options[@]}"
  meson compile -C build
}

check() (
  meson test -C build --print-errorlogs
)

package_libdex() {
  provides=(libdex-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libdex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
