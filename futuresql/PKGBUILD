# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=futuresql
pkgver=0.1.1
pkgrel=3
pkgdesc='Non-blocking Qt database framework'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://api.kde.org/futuresql/html/index.html'
license=(LGPL-2.1-or-later)
depends=(gcc-libs
         glibc
         qt6-base)
makedepends=(extra-cmake-modules)
conflicts=(futuresql-qt6)
provides=(futuresql-qt6)
replaces=(futuresql-qt6)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e44ed8d5a9618b3ca7ba2983ed9c5f7572e6e0a5b199f94868834b71ccbebd43'
            'SKIP')
validpgpkeys=(C3D7CAFBF442353F95F69F4AA81E075ABEC80A7E) # Jonah Br√ºchert <jbb@kaidan.im>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DQT_MAJOR_VERSION=6 \
    -DBUILD_TESTING=OFF \
    -DCMAKE_SKIP_RPATH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
