# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Roman Cheplyaka <roma@ro-che.info>

pkgname=libedit
pkgver=20240808_3.1
_pkgver=${pkgver/_/-}
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
url='https://thrysoee.dk/editline/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(BSD-3-Clause)
depends=(
  glibc
  ncurses
  libncursesw.so
)
source=(${url}/${pkgname}-${_pkgver}.tar.gz)
sha256sums=('5f0573349d77c4a48967191cdd6634dd7aa5f6398c6a57fe037cc02696d6099f')
b2sums=('c10a53d155a7dfbffdaa177aa41206c4bdca54efe8debd7df8cc3ddeaacf574e3cdd69bce9d30801c533f92907e757a92011fde989ee3a39ec91a73b9f80aa41')

build() {
  cd ${pkgname}-${_pkgver}
  ./configure \
    --prefix=/usr
  make V=1
}

package() {
  provides=(libedit.so)

  cd ${pkgname}-${_pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm 644 ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set sw=2 sts=-1 et:
