# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgbase='packagekit'
pkgname=('packagekit' 'libpackagekit-glib')
pkgver=1.3.2
pkgrel=2.1
pkgdesc='A system designed to make installation and updates of packages easier'
arch=(powerpc64le powerpc64 powerpc espresso)
url='https://www.freedesktop.org/software/PackageKit/'
license=('GPL-2.0-or-later')
makedepends=('git'
             'bash-completion'
             'glib2-devel'
             'gobject-introspection'
             'intltool'
             'meson'
             'polkit'
             'python-setuptools'
             'sqlite'
             'vala')
options=('!emptydirs')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17'  # Richard Hughes <richard@hughsie.com>
              'EC60AABDF42AAE8FB062640480858FA38F62AF74'  # Kalev Lember <klember@redhat.com>
              'D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB') # Matthias Klumpp <matthias@tenstral.net>
source=("git+https://github.com/PackageKit/PackageKit.git#tag=v${pkgver}?signed"
        archpower.patch)
sha256sums=('3a4674c2eb55b0bc9760bfbefdf0e6f6340cae72f4b20cd592d1396dc3086cde'
            'a986af7a4663d679031b3592a11207697cb0de8decb5573c1f56255af681bb53')

prepare() {
	cd PackageKit
	# Backport pkcon get-update-detail segfault https://github.com/PackageKit/PackageKit/pull/907
	git cherry-pick -n ca55974b867e3d52facd3a8ae485157bcfbcfc2b

        patch -Np1 -i ${srcdir}/archpower.patch
}

build() {
        local _meson_options=(
                -Dcron=false
                -Dgstreamer_plugin=false
                -Dgtk_doc=false
                -Dgtk_module=false
                -Dpackaging_backend=alpm
                -Dsystemd=true
        )

        arch-meson 'PackageKit' build "${_meson_options[@]}"

        meson compile -C build
}

package_packagekit() {
        depends=('libpackagekit-glib' 'pacman' 'libalpm.so' 'polkit' 'sqlite')
        optdepends=('bash-completion: command completion in bash')
        backup=('var/lib/PackageKit/transactions.db'
                'etc/PackageKit/alpm.d/pacman.conf'
                'etc/PackageKit/alpm.d/repos.list')

        meson install -C build --destdir "$pkgdir"

        # move away for libpackagekit-glib
        mkdir -p libpackagekit/usr/{lib,share}
        mv "$pkgdir"/usr/include/ libpackagekit/usr/
        mv "$pkgdir"/usr/lib/{girepository-1.0,libpackagekit-glib2.so*,pkgconfig} libpackagekit/usr/lib/
        mv "$pkgdir"/usr/share/{gir-1.0,vala}/ libpackagekit/usr/share/
}

package_libpackagekit-glib() {
        pkgdesc='GLib library for accessing PackageKit'
        depends=('glib2')
        provides=('libpackagekit-glib2.so')

        mv libpackagekit/usr/ "$pkgdir"/
}
