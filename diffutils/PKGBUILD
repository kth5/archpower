# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=diffutils
pkgver=3.11
pkgrel=2
pkgdesc='Utility programs used for creating patch files'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.gnu.org/software/diffutils'
license=('GPL-3.0-or-later')
depends=('glibc' 'bash')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
         e9f8e6a439fd607adbdd846ab93267dc367b5c79.patch
         6ce0ebd033c395265c262ae3aab6477a49d4c2f1.patch)
b2sums=('e3d78548d1fbee060a9e0a7ee93f4aebaa80a9ff15867f13e2f1d09672e2b1e8dcf01f264d5184c441bc81b0b1a797210cbf4a1f9b8ba061994066e710e79fc1'
        'SKIP'
        '87113297d7b30c76f203cbdd9ceeaeba44be582ac51eb21dea2d4ee462cfe0c55786468493262a6a49e9e14662a8b5105dcd79db58a239f9f06ee2ab3ddf45df'
        '90bba4b6d4bb971e44dd244509ab018eecf1ab2330fdda7750ad322f54e8f5ff01f2ba8e983c68783a785fd9a7e12956fea4e7ddb1f950afa614a8e66f9bf49a')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
