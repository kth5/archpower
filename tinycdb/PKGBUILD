# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=tinycdb
pkgver=0.81
pkgrel=1
pkgdesc='Very fast and simple package for creating and reading constant data bases'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.corpit.ru/mjt/tinycdb.html"
license=(MIT)
depends=(glibc)
provides=(libcdb.so)
# we don't want libcdb.a and would need to modify the Makefile otherwise
options=(!staticlibs)
source=(https://www.corpit.ru/mjt/tinycdb/$pkgname-$pkgver.tar.gz)
sha512sums=('56d0422700835d7b0460c32c41e6d5f30b8b833cefea775eb7f2b5592d744942c1109eac1501421b02f196d15e551111bb2accc71f2fd32ef4b8d658c635340e')
b2sums=('6b5b1167bd7e58a02b9a3a1ff2d58adb0e522ef65a13384051d9a037358ed7586f7d567e543099adec6322f9c60d38525fed586488510215bf72d12a60c67684')

build() {
  local make_options=(
    CFLAGS="$CFLAGS"
    LDFLAGS="$LDFLAGS"
    all
    shared
    -C $pkgname-$pkgver
  )

  make "${make_options[@]}"
}

package() {
  local make_options=(
    DESTDIR="$pkgdir"
    prefix=/usr
    mandir=/usr/share/man
    INSTALLPROG=cdb-shared
    install-{all,sharedlib}
    -C $pkgname-$pkgver
  )

  make "${make_options[@]}"
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/NEWS -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/libcdb.pc -t "$pkgdir/usr/lib/pkgconfig/"
}
