# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=alembic
pkgver=1.8.8
pkgrel=3
pkgdesc="An open framework for storing and sharing scene data"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://www.alembic.io/"
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs' 'imath' 'hdf5')
makedepends=('cmake' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('02b7bf5782e83efb08a8653f130b02565fa997e857dbd8d0523e1b218ff58d929fbf9690db0980e8101a31f01a67341b6000af8794538890ef7d759fe0289e2f')

build() {
  cd "${pkgname}-${pkgver}"

  case "${CARCH}" in
    powerpc) export CXXFLAGS=' -latomic' ;;
  esac

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON

  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
