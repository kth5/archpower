# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=alembic
pkgver=1.8.10
pkgrel=2
pkgdesc="An open framework for storing and sharing scene data"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://www.alembic.io/"
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs' 'imath' 'hdf5')
makedepends=('cmake' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('0be292e9d75560891d38129eb2672c55475b29b33f265dd51396dfa7b530c92ac6dfdb05a4432ad1b6919e2c151068560eb04efb8d9b0c2a14f77ab489d3542c')

build() {
  cd "${pkgname}-${pkgver}"

  case "${CARCH}" in
    powerpc) export CXXFLAGS=' -latomic' ;;
  esac

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON

  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
