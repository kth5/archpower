# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=alembic
pkgver=1.8.3
pkgrel=3.2
pkgdesc="An open framework for storing and sharing scene data"
arch=(x86_64 powerpc64le powerpc)
url="http://www.alembic.io/"
license=('BSD')
depends=('imath' 'boost-libs' 'hdf5')
makedepends=('cmake' 'boost' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('0049c72d93e66e12d704d27e7ba36cd9c718667f2ce4f7baa1bee1613ed88ba53abea98f457e14f7f2144cb353810a4108d26c7dd1a1543ec2af576272f19036')

build() {
  cd "${pkgname}-${pkgver}"

  [ "${CARCH}" == 'powerpc' ] && EXTRA_CMAKE_FLAGS='-DCMAKE_CXX_FLAGS="-latomic"'

  cmake . \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON ${EXTRA_CMAKE_FLAGS}

  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  DESTDIR="${pkgdir}/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
