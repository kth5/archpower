# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=alembic
pkgver=1.8.9
pkgrel=1
pkgdesc="An open framework for storing and sharing scene data"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://www.alembic.io/"
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs' 'imath' 'hdf5')
makedepends=('cmake' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('beb139f2fe04afacf61f5dab41cc96fbaf7dde993f5118fda3e48586da0a3a3d7221525650e45c19cd3a7d6222cfb6240168f34e72f2dd521e36d09b7ce8dcd3')

build() {
  cd "${pkgname}-${pkgver}"

  case "${CARCH}" in
    powerpc) export CXXFLAGS=' -latomic' ;;
  esac

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON

  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
