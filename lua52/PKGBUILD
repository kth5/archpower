# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lua52
pkgver=5.2.4
pkgrel=8
pkgdesc='Powerful lightweight programming language designed for extending applications'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!emptydirs')
source=(https://www.lua.org/ftp/lua-$pkgver.tar.gz
        liblua.so.patch
        lua.pc
        license-from-upstream)
sha512sums=('cd77148aba4b707b6c159758b5e8444e04f968092eb98f6b4c405b2fb647e709370d5a8dcf604176101d3407e196a7433b5dcdce4fe9605c76191d3649d61a8c'
            'c764337befe23f98f46252055d7aa67acdbab8831e894962ea0650a1865a9a128058af67bce6b751baf2a499c530129a01a111fafee4a6bdcdaec9bbfc416585'
            'a3ae5f5c63a27956a2c69464cf966512be7404aef72d49b0b3b17e35999ceed04ec7be12ef19269f4cac908141b98dd5d16df9c238823fe0c1e068212914657a'
            'bef221ae96e72d11785ec608ce96fa688ce4e12fecaaadc8c3e38d7d153922f4fdd2cf06a47e9f8840a165d42d9aa31f44c322f5c6c8402e8e31475de4e5893b')

prepare() {
  cd lua-$pkgver
  patch -p1 -i ../liblua.so.patch

  sed "s/%VER%/${pkgver%.*}/g;s/%REL%/$pkgver/g" ../lua.pc > lua.pc
  sed -e 's:llua:llua5.2:' -e 's:/include:/include/lua5.2:' -i lua.pc
  sed -r -e '/^LUA_(SO|A|T)=/ s/lua/lua5.2/' -e '/^LUAC_T=/ s/luac/luac5.2/' -i src/Makefile
}

build() {
  cd lua-$pkgver

  make MYCFLAGS="$CFLAGS -fPIC" MYLDFLAGS="$LDFLAGS" linux
}

package() {
  cd lua-$pkgver

  make \
    TO_BIN='lua5.2 luac5.2' \
    TO_LIB="liblua5.2.so liblua5.2.so.5.2 liblua5.2.so.$pkgver" \
    INSTALL_DATA='cp -d' \
    INSTALL_TOP="$pkgdir"/usr \
    INSTALL_INC="$pkgdir"/usr/include/lua5.2 \
    INSTALL_MAN="$pkgdir"/usr/share/man/man1 \
    install

  install -Dm644 lua.pc "$pkgdir"/usr/lib/pkgconfig/lua52.pc
  ln -sf lua52.pc "$pkgdir"/usr/lib/pkgconfig/lua5.2.pc
  ln -sf lua52.pc "$pkgdir"/usr/lib/pkgconfig/lua-5.2.pc

  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -m644 doc/*.{gif,png,css,html} "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 ../license-from-upstream "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  ln -s liblua5.2.so "$pkgdir"/usr/lib/liblua.so.5.2
  ln -s liblua5.2.so "$pkgdir"/usr/lib/liblua.so.$pkgver

  cd "$pkgdir"/usr/share/man/man1
  mv lua.1 lua5.2.1
  mv luac.1 luac5.2.1
}
