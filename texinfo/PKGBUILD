# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=7.2
pkgrel=1
pkgdesc='GNU documentation system for on-line information and printed output'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://www.gnu.org/software/texinfo/'
license=(GPL3)
depends=(ncurses gzip perl sh)
optdepends=('perl-archive-zip: EPUB file output via texi2any')
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-install.hook
        texinfo-remove.hook)
sha256sums=('0329d7788fbef113fa82cb80889ca197a344ce0df7646fe000974c5d714363a6'
            'SKIP'
            'beb0ff50bd8e8ca1d6e1c01e6a50352f9d25937c62822cf767a7b3d8f7374a9d'
            '913ca8aac84386399b0a83ed0f6b04b5e6322da62f5c1d7ed31e1050ed37c1a9')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 texinfo-{install,remove}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
