# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Immae <ismael.bouya@normalesup.org>

pkgname=editline
pkgver=1.17.1
pkgrel=4
pkgdesc='A readline() replacement for UNIX without termcap (ncurses)'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='http://troglobit.com/editline.html'
license=(Spencer-94)
depends=(glibc)
makedepends=(git)
provides=(libeditline.so)
install="$pkgname.install"
source=("$pkgname::git+https://github.com/troglobit/editline#tag=$pkgver")
sha512sums=('63d51f65112448a9ed71ab2dcc37c25c345176ca28ff3bead5020ff82f442fe7221dae935bc0e62d254aeb4edabbc86cf66b3b7016511b758297ea13ca040efe')
b2sums=('94ad085b15facbe50d40ab498d4247efd6e26d53a1bb3580c15d72a4be74c04b81b45babe46336275d11e952de93d4843b91b0216c0b5711d5739ff8cde99a06')

build () {
  cd "$pkgname"

  # Fix FTBFS (remove on next release)
  git cherry-pick --no-commit \
    f444a316f5178b8e20fe31e7b2d979e651da077e \
    f53bebdbe98c4dbfc9cc959032a071415324dde2

  ./autogen.sh

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
  
  # avoid libedit conflict
  mv "$pkgdir"/usr/share/man/man3/editline{,-troglobit}.3

  # license
  install -Dm0644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"

  # additional documentation
  cp -vrt "$pkgdir/usr/share/doc/$pkgname" docs
}
