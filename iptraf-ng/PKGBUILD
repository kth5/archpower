# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: sh0 <mee@sh0.org>

pkgname=iptraf-ng
pkgver=1.2.2
pkgrel=1
pkgdesc='Console-based network monitoring utility'
url='https://github.com/iptraf-ng/iptraf-ng'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
depends=('ncurses')
license=('GPL2')
replaces=('iptraf')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('275a345ffd3ab9578c4d159c3a8667326224b4a58b2e7787c4db518b81973d87c04b4b6c5275a721858d01a9b44a0200f8defc06c6f88655aa38d4fa6146ea1d')
b2sums=('c2ad1821080fcebaef3500aaefcf9a8679c46c09a6851b9cef40fcc6d3b7607c65954ac884e1db4ddf702c62de68bb58ca9cf338c0642bc0ef9e944ca400e4c4')

build() {
  cd ${pkgname}-${pkgver}
  make CFLAGS="${CFLAGS} -std=gnu99 -D_GNU_SOURCE" \
    LDFLAGS="${LDFLAGS}" \
    CPPFLAGS="${CPPFLAGS}" \
    sbindir_relative=bin \
    prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr sbindir_relative=bin install
  install -Dm 644 iptraf-ng-logrotate.conf -t "${pkgdir}/etc/logrotate.d"
}

# vim: ts=2 sw=2 et:
