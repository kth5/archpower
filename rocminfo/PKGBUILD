# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Torsten Keßler <tpkessler at archlinux dot org>
# Contributor: Markus Näther <naetherm@cs.uni-freiburg.de>
# Contributor: Jakub Okoński <jakub@okonski.org>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail.com>
# Contributor: acxz <akashpatel2008 at yahoo dot com>

pkgname=rocminfo
pkgver=5.7.1
pkgrel=1
pkgdesc='ROCm Application for Reporting System Info '
arch=(x86_64 powerpc64le powerpc64 riscv64)
url='https://github.com/RadeonOpenCompute/rocminfo'
license=('custom:NCSAOSL')
depends=('pciutils' 'python' 'hsa-rocr')
makedepends=('rocm-cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/rocm-$pkgver.tar.gz"
        rocminfo_fix-cxx-flags.patch::https://github.com/RadeonOpenCompute/rocminfo/pull/63.diff)
sha256sums=('642dc2ec4254b3c30c43064e6690861486db820b25f4906ec78bdb47e68dcd0b'
            '423baa1e51a14987f472286f86eabe00533ccaa3ccfd294d153545f9abc61880')
_dirname="$(basename "$url")-$(basename "${source[0]}" .tar.gz)"

prepare() {                                                                                                                                                                                 
  patch -d "$_dirname" -Np1 -i ${srcdir}/rocminfo_fix-cxx-flags.patch
}

build() {
  # ROCRTST_BLD_TYPE=Release fixes a build error regarding _FORTIFY_SOURCE=2
  cmake \
    -Wno-dev \
    -B build \
    -S "$_dirname" \
    -DCMAKE_PREFIX_PATH=/opt/rocm \
    -DROCRTST_BLD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/rocm \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_dirname/License.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
