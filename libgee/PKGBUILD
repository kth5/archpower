# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libgee
pkgver=0.20.8
pkgrel=1
pkgdesc="A collection library providing GObject-based interfaces and classes for commonly used data structures"
url="https://gitlab.gnome.org/GNOME/libgee"
license=(LGPL-2.1-or-later)
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(
  glib2
  glibc
)
makedepends=(
  autoconf-archive
  git
  gobject-introspection
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/libgee.git#tag=$pkgver")
b2sums=('b1b7007d60f1a8e5ef56ad968121cd14ca9fc01b71f3d7e64db9fc8bbd39f7d87aac59a9b04bb3d5963bb9ac8e4b592e68cc49a4dbaee40494af7ba03094c05b')

prepare() {
  cd libgee
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libgee
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd libgee
  # generates a bazillion traps - make sure systemd's core_pattern
  # is deactivated, or you'll DoS the journal and the system
  #make check
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libgee-0.8.so)

  cd libgee
  make DESTDIR="$pkgdir" install
}
