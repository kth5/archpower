# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=usb_modeswitch
pkgver=2.6.2.20251207
pkgrel=1
_pkgver=$(echo $pkgver | cut -d. -f1-3)
_pkgdata=$(echo $pkgver | cut -d. -f4)
pkgdesc="Activating switchable USB devices on Linux."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL')
depends=('libusb' 'tcl')
makedepends=('gcc' 'make' 'systemd')
backup=("etc/$pkgname.conf")
source=("http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-${_pkgver}.tar.bz2"
	"http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-data-${_pkgdata}.tar.bz2"
	"usb-modeswitch-${_pkgver}-device_reference.txt::http://www.draisberghof.de/usb_modeswitch/device_reference.txt")
sha256sums=('f7abd337784a9d1bd39cb8a587518aff6f2a43d916145eafd80b1b8b7146db66'
            '0bb12d64aee5e467c31af61a53fb828ff7aa59c54a82ca85eeede4c5690bfa66'
            '846b45a82a31c9755ab86d9c051e706aff445e1e793575b7ae195a4edeb4d594')

prepare() {
  cd "$srcdir"/usb-modeswitch-${_pkgver}/
  sed -i 's|/usr/sbin/usb_mode|/usr/bin/usb_mode|g' usb_modeswitch{.conf,_dispatcher.tcl,.sh}
}

build() {
  cd "$srcdir"/usb-modeswitch-${_pkgver}/
  make clean
  make SBINDIR=/usr/bin
}

package() {
  cd "$srcdir"/usb-modeswitch-${_pkgver}/

  mkdir -p "$pkgdir"/usr/lib/udev
  mkdir -p "$pkgdir"/etc
  mkdir -p "$pkgdir"/usr/share/man/man1
  mkdir -p "$pkgdir"/usr/lib/systemd/system

  make \
    DESTDIR="$pkgdir" \
    UDEVDIR="$pkgdir"/usr/lib/udev \
    SBINDIR="$pkgdir"/usr/bin \
    SYSDIR="$pkgdir"/usr/lib/systemd/system \
    install

  cp "$srcdir"/usb-modeswitch-${_pkgver}-device_reference.txt "$pkgdir"/etc/usb_modeswitch.setup

  cd "$srcdir"/usb-modeswitch-data-${_pkgdata}
  make DESTDIR="$pkgdir" install RULESDIR="$pkgdir"/usr/lib/udev/rules.d
}
