# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=mdds
pkgver=3.1.0
pkgrel=1
pkgdesc="A collection of multi-dimensional data structures and indexing algorithms"
arch=(any)
url="https://gitlab.com/mdds/mdds"
license=('MIT')
checkdepends=('boost')
#source=(https://gitlab.com/mdds/mdds/repository/archive.tar.gz?ref=${pkgver})
source=(https://gitlab.com/api/v4/projects/mdds%2Fmdds/packages/generic/source/${pkgver}/mdds-${pkgver}.tar.xz)
# checksums listed there - https://gitlab.com/mdds/mdds/-/releases
sha256sums=('ca295f4c86968a7eb1f5d503ff7ae180e6605210cfb26d01658be42048c0924c')

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh --prefix=/usr
  # make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
