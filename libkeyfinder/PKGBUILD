# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libkeyfinder
pkgver=2.2.8
pkgrel=1
pkgdesc="Musical key detection for digital audio"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://mixxxdj.github.io/libkeyfinder/"
_url="https://github.com/mixxxdj/libkeyfinder"
license=(GPL-3.0-only)
depends=(
  fftw
  gcc-libs
  glibc
)
makedepends=(
  catch2
  cmake
)
source=($_url/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('31d86715172b62dd72b122a8d480db4598731b87ca58522ad797116acfcbc53b8ecf8fe7eb2b129857b5044b27d32dda9e03927e0a27f8edcdc5d6ce607a76eb')
b2sums=('9341a6d93742516ba213503414c359123b2313134a603ed621a6c7e99e77dbd6f38ad986758d9736c7083fec505c6a1495541a38d825c0d0015e86fa9baa3b3c')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  local ctest_flags=(
    --test-dir build
    --output-on-failure
  )
  ctest "${ctest_flags[@]}"
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
