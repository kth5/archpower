# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib2geom
pkgver=1.4
pkgrel=1
pkgdesc='Easy to use 2D geometry library in C++'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://gitlab.com/inkscape/lib2geom'
license=('GPL')
depends=('glib2' 'gsl' 'double-conversion' 'cairo' 'ragel')
makedepends=('cmake' 'boost' 'gtest')
source=("https://gitlab.com/inkscape/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('717a87ce7f2192271a789c1273ae7b432856c522647b9fe33bb4dd662942b3ea')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -D2GEOM_BOOST_PYTHON=NO \
    -D2GEOM_BUILD_SHARED=YES \
    -DCMAKE_BUILD_TYPE=Release
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}/build/"

  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build/"

  make DESTDIR="${pkgdir}/" install
}
