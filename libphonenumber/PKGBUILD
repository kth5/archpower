# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber
pkgver=8.13.20
pkgrel=1.1
epoch=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(Apache)
depends=(
  abseil-cpp
  icu
  protobuf
)
makedepends=(
  cmake
  git
  gtest
  jre-openjdk-headless
)
_commit=6d7589a49a91bfc15bfb757a516ded67ade2c1ed  # tags/v8.13.20^0
source=(
  "git+$url#commit=$_commit"
  protobuf-targets.patch
)
b2sums=('SKIP'
        'f80dbf7a2654a1d53e2c8452415bc99a26701edbe593ce1b86fdb7ba3fe920888ce07aad74bc33ff0e6335bc0345700318fdeec7f113f29c77a82a9495966317')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Use official protobuf cmake target instead of
  # custom one, fixes build with protobuf 23
  git apply -3 ../protobuf-targets.patch

  # Fix build with abseil-cpp 2023
  sed -e 's|CMAKE_CXX_STANDARD 11|CMAKE_CXX_STANDARD 17|' \
      -i tools/cpp/CMakeLists.txt
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_CXX_STANDARD=17
    -DUSE_BOOST=OFF
    -DUSE_STDMUTEX=ON
  )

  # Greatly reduce size of libgeocoding's relocation tables
  # https://glandium.org/blog/?p=4297
  LDFLAGS+=" -Wl,-z,pack-relative-relocs"

  cmake -S $pkgname/cpp -B build "${cmake_options[@]}"
  cmake --build build
}

check() {
  cmake --build build --target tests
}

package() {
  depends+=(libicu{uc,i18n}.so libprotobuf.so)
  provides+=(libgeocoding.so libphonenumber.so)

  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
