# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber
pkgver=8.13.33
pkgrel=1
epoch=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('Apache-2.0 AND BSD-3-Clause')
depends=(
  abseil-cpp
  icu
  protobuf
)
makedepends=(
  cmake
  git
  gtest
)
makedepends_powerpc=(jre-openjdk-headless)
makedepends_powerpc64=(jre-openjdk-headless)
makedepends_powerpc64le=(jre-openjdk-headless)
makedepends_riscv64=(jre11-openjdk-headless)
makedepends_x86_64=(jre-openjdk-headless)
_commit=5e620bb2637fac7911244fcb53206b2ea8fd65cb  # tags/v8.13.33^0
source=(
  "git+$url#commit=$_commit"
  protobuf-targets.diff
)
b2sums=('6f8ffdbeda0b53c09f6906f411c2e19fa177729076f896cb5ded6ee9c502f9bda02c28851b8c3d66ed4682a41e263be5ffc9fa28a10b4353dc48b36d299bedcd'
        '911187934b234eeda64e64513daa2ad7ce5bbb56e91921cad49334286c147acafdaf7343545b3f896ab26ead774902b9f17ace93c8a49ae4b0e9763c0009781e')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Use official protobuf cmake target instead of
  # custom one, fixes build with protobuf 23
  git apply -3 ../protobuf-targets.diff
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_CXX_STANDARD=17
    -DUSE_BOOST=OFF
    -DUSE_STDMUTEX=ON
  )

  cmake -S $pkgname/cpp -B build "${cmake_options[@]}"
  cmake --build build
}

check() {
  cmake --build build --target tests
}

package() {
  depends+=(
    libicu{uc,i18n}.so
    libprotobuf.so
  )
  provides+=(
    libgeocoding.so
    libphonenumber.so
  )

  DESTDIR="$pkgdir" cmake --install build

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/LICENSE.Chromium
}

# vim:set sw=2 sts=-1 et:
