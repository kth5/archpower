# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=fltk1.3
_name=fltk
pkgver=1.3.11
pkgrel=3
pkgdesc="Graphical user interface toolkit for X - version 1.3"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('LGPL-2.0-only WITH FLTK-exception')
url="https://www.fltk.org/"
makedepends=(
  alsa-lib
  cmake
  doxygen
  mesa
)
depends=(
  fontconfig
  gcc-libs
  glibc
  glu
  hicolor-icon-theme
  libglvnd
  libjpeg-turbo
  libpng
  libx11
  libxcursor
  libxext
  libxfixes
  libxft
  libxinerama
  libxrender
  sh
)
provides=(
  libfltk_images.so
  libfltk_gl.so
  libfltk_forms.so
  libfltk.so
)
# options=(staticlibs)
source=(
  https://github.com/$_name/$_name/archive/release-$pkgver/$_name-release-$pkgver.tar.gz
  $pkgname-1.3.11-integration.patch
)
sha512sums=('b18ff6322349af4416a37d28c4f42ebe355260786ed42bdd54dcc20dc92db1a38a8db74e6d637fdff8f320bdd51e2515c0fa939d30679c5f22ea99fb32c97204'
            '5e4e36d832afc3725e6230861e067dc9c8f4137c8e4bbc963b581bab62dec0c8b5ce9b34a96738adf3e73090e1c0842b7d325ce172817245f67d9244388540b6')
b2sums=('13f5a208f6fe19e4df3184be1143042d6469b5518f2771d60c8bba543129660c9b3d7ed5cb547cf364c91d6abd1d5d4f538dede0bfdf57db26ae9f4a8e4d069a'
        'be4379116a9b6f01e792b3f1d6d9ae60a0ec498650c4d3fd263363511c34640881aeedacf930fddf7fc6e605952a58d82a6fe453597455ea43822e4e25f3115d')

prepare() {
  patch -Np1 -d $_name-release-$pkgver -i ../$pkgname-1.3.11-integration.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_INCLUDEDIR=include/fltk1.3
    -D FLTK_BUILD_FLUID=OFF
    -D FLTK_BUILD_TEST=OFF
    -D FLTK_FLUID_EXECUTABLE=$pkgname
    -D FLTK_LIBDIR=/usr/lib/fltk1.3
    -D OPTION_BUILD_SHARED_LIBS=ON
    -S $_name-release-$pkgver
    -W no-dev
  )

  # fat-lto-objects is required for non-mangled .a files in fltk
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    libfontconfig.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-release-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"

  # expose relevant symlinks and shared object files in the global library directory
  ln -sv libfltk.so.$pkgver "$pkgdir/usr/lib/libfltk.so.1.3"
  ln -sv $pkgname/libfltk.so.$pkgver "$pkgdir/usr/lib/libfltk.so.$pkgver"
  ln -sv libfltk_forms.so.$pkgver "$pkgdir/usr/lib/libfltk_forms.so.1.3"
  ln -sv $pkgname/libfltk_forms.so.$pkgver "$pkgdir/usr/lib/libfltk_forms.so.$pkgver"
  ln -sv libfltk_gl.so.$pkgver "$pkgdir/usr/lib/libfltk_gl.so.1.3"
  ln -sv $pkgname/libfltk_gl.so.$pkgver "$pkgdir/usr/lib/libfltk_gl.so.$pkgver"
  ln -sv libfltk_images.so.$pkgver "$pkgdir/usr/lib/libfltk_images.so.1.3"
  ln -sv $pkgname/libfltk_images.so.$pkgver "$pkgdir/usr/lib/libfltk_images.so.$pkgver"
}
