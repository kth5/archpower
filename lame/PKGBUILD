# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=lame
pkgver=3.100
pkgrel=6
pkgdesc='A high quality MPEG Audio Layer III (MP3) encoder'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url=http://lame.sourceforge.net/
license=(LGPL-2.0-only)
depends=(
  glibc
  ncurses
)
makedepends_x86_64=(nasm)
provides=(libmp3lame.so)
source=(
  https://downloads.sourceforge.net/lame/lame-${pkgver}.tar.gz
  lame.pc.in
)
sha256sums=('ddfe36cab873794038ae2c1210557ad34857a4b6bdc515785d1da9e175b1da1e'
            '5b746f51caeb202ae4fe3f09fc2db0b38096598d9496945d7fdfdd23c5868e93')
b2sums=('6954d30cfd3951ea07762ba5dd7ff27038b78455f423099a225ebf748abddf9970e444456ca5a6179bd381e2205b32293392cb757c203901674860710fe2c183'
        '1a56722d730f0dff06fe756b089239d28781ec0300cb90fb014d25a7f92d40febff6ba18b621b6cbc4af7babfea90c7382e34787271b1cf2d82d239416837776')

prepare() {
  sed -e "s/VERSION/${pkgver}/" lame.pc.in > lame.pc
}

build() {
  cd $pkgname-$pkgver

  case "${CARCH}" in
    x86_64) configure_flags=(--enable-nasm) ;;
    *)      configure_flags=(--disable-nasm) ;;
  esac


  ./configure \
    --build=${CHOST} \
    --prefix='/usr' \
    --enable-mp3rtp \
    --enable-shared ${configure_flags[@]}
  make
}

check() {
  make test -C lame-${pkgver}
}

package() {
  make DESTDIR="${pkgdir}" install -C lame-${pkgver}
  install -Dm 644 lame.pc -t "${pkgdir}"/usr/lib/pkgconfig/
}
# vim: ts=2 sw=2 et:
