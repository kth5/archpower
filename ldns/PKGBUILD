# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: mathieui <mathieui@mathieui.net>
# Contributor: jiribb <jiribb@gmail.com>

pkgname=ldns
pkgver=1.9.0
pkgrel=1
pkgdesc='Fast DNS library supporting recent RFCs'
url='https://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
depends=('openssl' 'dnssec-anchors')
provides=('drill' 'libldns.so')
optdepends=('libpcap: ldns-dpa tool')
makedepends=('git' 'libpcap')
validpgpkeys=('DC34EE5DB2417BCC151E5100E5F8F8212F77A498') # Willem Toorop
source=("git+https://github.com/NLnetLabs/ldns#tag=${pkgver}?signed")
sha256sums=('c2d7ff1a4a21a37f2e1ac38f3bb88c06907c1639ac524cbb6d82eedfe8caf420')

prepare() {
  cd "${pkgname}"

  autoreconf -fi
  libtoolize -c --install
}

build() {
  cd "${pkgname}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-rpath \
    --with-drill \
    --with-examples \
    --with-trust-anchor=/etc/trusted-key.key
  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install
  install -D -m0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
