# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: mathieui <mathieui@mathieui.net>
# Contributor: jiribb <jiribb@gmail.com>

pkgname=ldns
pkgver=1.8.4
pkgrel=1
pkgdesc='Fast DNS library supporting recent RFCs'
url='https://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('openssl' 'dnssec-anchors')
provides=('drill' 'libldns.so')
optdepends=('libpcap: ldns-dpa tool')
makedepends=('libpcap')
source=("https://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('838b907594baaff1cd767e95466a7745998ae64bc74be038dccc62e2de2e4247'
            'SKIP')
validpgpkeys=('DC34EE5DB2417BCC151E5100E5F8F8212F77A498') # Willem Toorop

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-rpath \
		--with-drill \
		--with-examples \
		--with-trust-anchor=/etc/trusted-key.key \

	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
