# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-validation-layers
pkgver=1.4.313.0
pkgrel=1
pkgdesc="Vulkan Validation Layers"
url="https://www.vulkan.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  spirv-tools
)
makedepends=(
  cmake
  git
  libxrandr
  ninja
  python-lxml
  spirv-headers
  vulkan-headers
  vulkan-icd-loader
  vulkan-utility-libraries
  wayland
)
options=(
  # https://github.com/KhronosGroup/Vulkan-ValidationLayers/issues/5994
  !lto
)
groups=(vulkan-devel)
source=("git+https://github.com/KhronosGroup/Vulkan-ValidationLayers#tag=vulkan-sdk-$pkgver")
b2sums=('c549cb272c91ef6b83bea5672e411be03efbda29d55e5aceeac5389f2453ef9b3ae0694bbeb3c50f1d29fd9cafc8dd3599f03652cda72010f8bd84a14fc4faa0')

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_SYSCONFDIR=/etc
    -D CMAKE_SKIP_INSTALL_RPATH=ON
  )

  cmake -S Vulkan-ValidationLayers -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  mkdir -p "$pkgdir/usr/share/doc"
  cp -a Vulkan-ValidationLayers/docs "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set sw=2 sts=-1 et:
