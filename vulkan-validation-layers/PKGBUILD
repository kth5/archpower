# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-validation-layers
pkgver=1.4.328.1
pkgrel=1
pkgdesc="Vulkan Validation Layers"
url="https://www.vulkan.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
  spirv-tools
)
makedepends=(
  cmake
  git
  libxrandr
  ninja
  python-lxml
  spirv-headers
  vulkan-headers
  vulkan-icd-loader
  vulkan-utility-libraries
  wayland
)
options=(
  # https://github.com/KhronosGroup/Vulkan-ValidationLayers/issues/5994
  !lto
)
groups=(vulkan-devel)
source=("git+https://github.com/KhronosGroup/Vulkan-ValidationLayers#tag=vulkan-sdk-$pkgver")
b2sums=('a2e928bb2483511dc37b093428ce07b1db6eee0e4740673570e2a558d2a34c496c127813b88df3cdcaa05f86bdb6a768c028fd4389d6b19d1b09e56010836276')

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_SYSCONFDIR=/etc
    -D CMAKE_SKIP_INSTALL_RPATH=ON
  )

  cmake -S Vulkan-ValidationLayers -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  mkdir -p "$pkgdir/usr/share/doc"
  cp -a Vulkan-ValidationLayers/docs "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set sw=2 sts=-1 et:
