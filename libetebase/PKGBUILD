# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libetebase
pkgver=0.5.7
pkgrel=1
pkgdesc='Etebase C library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/etesync/libetebase'
license=(BSD-3-Clause)
depends=(gcc-libs
         glibc
         libsodium
         openssl)
makedepends=(git
             rust)
source=(git+https://github.com/etesync/libetebase#tag=v$pkgver)
sha256sums=('ae2626e555f601a5427a156cbe2f6429eb04fafe7896c2aff5d44e085dcf5a84')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  env CARGO_INCREMENTAL=0 SODIUM_USE_PKG_CONFIG= cargo build --release --locked --offline
  make pkgconfig
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
