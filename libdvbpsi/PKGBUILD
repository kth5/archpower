# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=libdvbpsi
pkgver=1.3.3
pkgrel=3
epoch=1
pkgdesc='Library designed for decoding and generation of MPEG TS and DVB PSI tables'
url='https://www.videolan.org/developers/libdvbpsi.html'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('LGPL2.1')
depends=('glibc')
provides=('libdvbpsi.so')
source=(https://code.videolan.org/videolan/libdvbpsi/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('30bc8ce28754d6488b47f71e7e3adbb39e0d8927f7a8371fd146dbf42bb022fd2091bbf802c4554856de8c6a65a0773b0258e95a9c6b77e347fcf1b2e27261ee')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
