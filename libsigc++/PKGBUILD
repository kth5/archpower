# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=libsigc++
pkgname=(
  libsigc++
  libsigc++-docs
)
pkgver=2.12.1
pkgrel=2
pkgdesc="Callback Framework for C++"
url="https://libsigcplusplus.github.io/libsigcplusplus/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://github.com/libsigcplusplus/libsigcplusplus#tag=$pkgver")
b2sums=('ae2a3775bbf10f438dd08413b28d9e206ee11c5b487fcf767f107843727adb086a360f605b643082c9b8a513527e4ac958004ff65fed2fbbb1b5d637dd40dc5e')

prepare() {
  cd libsigcplusplus
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson libsigcplusplus build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

# Do not remove the space before the () or commitpkg will
# accidentally to run this function on the system (!!!) 
package_libsigc++ () {
  provides=(
    "libsigc++2.0=$pkgver"
    libsigc-2.0.so
  )
  conflicts=(libsigc++2.0)
  replaces=(libsigc++2.0)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_libsigc++-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  provides=("libsigc++2.0-docs=$pkgver")
  conflicts=(libsigc++2.0-docs)
  replaces=(libsigc++2.0-docs)
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
