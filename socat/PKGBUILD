# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat
pkgver=1.8.0.2
pkgrel=1
pkgdesc='Multipurpose relay'
url='http://www.dest-unreach.org/socat/'
license=('GPL-2.0-only')
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(
  'glibc'
  'openssl' 'libssl.so'
  'readline' 'libreadline.so'
)
checkdepends=(
  'bash'
  'net-tools'
  'procps-ng'
)
source=("http://www.dest-unreach.org/socat/download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e9498367cb765d44bb06be9709c950f436b30bf7071a224a0fee2522f9cbb417')
b2sums=('ed2ce58d385dab52af5f7e528710f739746e4934a8b94e153aaf8ad92516d3bbdaebf6d083b8ba242812dbd7064ff2bde0dbfadc565c71d449e670fe5ba3075c')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # TODO: one of the tests always fails, but lets keep
  # it for manual sanity check
  make test||true
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
