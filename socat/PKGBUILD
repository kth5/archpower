# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat
pkgver=1.8.0.3
pkgrel=1
pkgdesc='Multipurpose relay'
url='http://www.dest-unreach.org/socat/'
license=('GPL-2.0-only')
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
depends=(
  'glibc'
  'openssl' 'libssl.so'
  'readline' 'libreadline.so'
)
checkdepends=(
  'bash'
  'net-tools'
  'procps-ng'
)
source=("http://www.dest-unreach.org/socat/download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('a9f9eb6cfb9aa6b1b4b8fe260edbac3f2c743f294db1e362b932eb3feca37ba4')
b2sums=('39c2a9971df59ab418e07c77f4d36b6ffac268037d6ba876d1a58ca0a2fecf1784ce65cdc9435825c687736d358cbb24c635b7b9db5c606f9cbda266031c0858')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # TODO: one of the tests always fails, but lets keep
  # it for manual sanity check
  make test||true
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
