# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat
pkgver=1.8.1.0
pkgrel=1
pkgdesc='Multipurpose relay'
url='http://www.dest-unreach.org/socat/'
license=('GPL-2.0-only')
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
depends=(
  'glibc'
  'openssl' 'libssl.so'
  'readline' 'libreadline.so'
)
checkdepends=(
  'bash'
  'net-tools'
  'procps-ng'
)
source=("http://www.dest-unreach.org/socat/download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9a884880b1b00dfb2ffc6959197b1554b200af731018174cd048115dc28ef239')
b2sums=('de994acadd7f1ae2ca78858ebfe4ea96415a60dae110520026d97976693a46f772b3ae97fd08599ba6d344e1667cfe9b01ad74c729603f0bbaee0eaf335e0dd0')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # TODO: one of the tests always fails, but lets keep
  # it for manual sanity check
  make test||true
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
