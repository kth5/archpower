# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.49.0
pkgrel=1
pkgdesc="Configuration database system"
url="https://wiki.gnome.org/Projects/dconf"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  bash
  gcc-libs
  glib2
  glibc
)
makedepends=(
  bash-completion
  dbus
  git
  glib2-devel
  gtk-doc
  meson
  python
  systemd
  vala
)
provides=(libdconf.so)
install=dconf.install
source=(
  "git+https://gitlab.gnome.org/GNOME/dconf.git#tag=$pkgver"
  dconf-update.hook
  dconf-update.script
)
b2sums=('8be63a8eb2d7c2f3067b051577ad80793c4e0c93784e738fe81d5d6a79407a683861237ae27631554ad5dd2421bb1a10cb2fd21c36cebd9ea57e74ae7581ab29'
        '5c8da042897f79d17751b02761b8b7d6f9ee3e3a8252c8cd4ca48e5ac7718131385f0a585078465e556f09ca94444d5357e44f9d07dfcb67c0bda063ff7875c3'
        '3d6ea18385db710a0ce669abdc38861cab5ffd5f412789621ded3ade0b8d8c75834137f6d4e18955d13200d972ec985ab2be83447fe7a0a7d26b73218a5a84a4')

prepare() {
  cd dconf
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson dconf build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  install -Dm644 dconf-update.hook -t "$pkgdir/usr/share/libalpm/hooks"
  install -D dconf-update.script "$pkgdir/usr/share/libalpm/scripts/dconf-update"

  # Prevent removal of /etc/dconf/db/ when other
  # packages installing files there get uninstalled
  install -Dm644 /dev/null "$pkgdir/etc/dconf/db/.placeholder"
}

# vim:set sw=2 sts=-1 et:
