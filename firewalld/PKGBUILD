# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=2.2.0
pkgrel=1
pkgdesc="Firewall daemon with D-Bus interface"
arch=(any)
url="https://firewalld.org"
license=('GPL-2.0-or-later')
depends=('dbus-python' 'glib2' 'hicolor-icon-theme'
	 'nftables' 'python-capng' 'python-gobject')
makedepends=('docbook-xsl' 'git' 'intltool' 'podman')
optdepends=('bash-completion: bash completion'
            'gtk3: firewall-config'
	    'libnotify: firewall-applet'
	    'networkmanager: NetworkManager support'
	    'polkit: privileged actions'
	    'python-pyqt6: firewall-applet')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
source=("git+https://github.com/firewalld/firewalld.git#tag=v${pkgver}"
        'firewalld-sysconfigdir.patch')
sha256sums=('81fb1223c3358b555f31cd01033875dea9685599f196d54dc8a9f17ed681c4ae'
            '3b2e00f67680c2e620804eb28620d7370b4096851bcb5f6fec22460a21941ad9')

prepare() {
	cd "${pkgname}"
	patch -Np1 -i ../firewalld-sysconfigdir.patch
	NOCONFIGURE=true ./autogen.sh
}

build() {
	cd "${pkgname}"
	./configure \
	  --prefix=/usr \
	  --localstatedir=/var \
	  --sbindir=/usr/bin \
	  --sysconfdir=/etc \
	  --disable-schemas-compile \
	  --disable-sysconfig
	make
}

package() {
	make DESTDIR="${pkgdir}" -C "${pkgname}" install
	python -m compileall -d /usr/lib "${pkgdir}/usr/lib"
	python -O -m compileall -d /usr/lib "${pkgdir}/usr/lib"
}
