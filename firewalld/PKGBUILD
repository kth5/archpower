# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=2.1.2
pkgrel=3
pkgdesc="Firewall daemon with D-Bus interface"
arch=(any)
url="https://firewalld.org"
license=('GPL-2.0-or-later')
depends=('dbus-python' 'glib2' 'hicolor-icon-theme'
	 'nftables' 'python-capng' 'python-gobject')
makedepends=('docbook-xsl' 'git' 'intltool' 'podman')
optdepends=('bash-completion: bash completion'
            'gtk3: firewall-config'
	    'libnotify: firewall-applet'
	    'networkmanager: NetworkManager support'
	    'polkit: privileged actions'
	    'python-pyqt5: firewall-applet qt5 backend'
	    'python-pyqt6: firewall-applet qt6 backend')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
source=("git+https://github.com/firewalld/firewalld.git#tag=v${pkgver}"
        'firewalld-sysconfigdir.patch')
sha256sums=('94236f4b882365cb3a7685a2b83340034956f2cc4dc7936399020ae9d328a51c'
            '3b2e00f67680c2e620804eb28620d7370b4096851bcb5f6fec22460a21941ad9')

prepare() {
	cd "${pkgname}"
	patch -Np1 -i ../firewalld-sysconfigdir.patch
	NOCONFIGURE=true ./autogen.sh
}

build() {
	cd "${pkgname}"
	./configure \
	  --prefix=/usr \
	  --localstatedir=/var \
	  --sbindir=/usr/bin \
	  --sysconfdir=/etc \
	  --disable-schemas-compile \
	  --disable-sysconfig
	make
}

package() {
	make DESTDIR="${pkgdir}" -C "${pkgname}" install
	python -m compileall -d /usr/lib "${pkgdir}/usr/lib"
	python -O -m compileall -d /usr/lib "${pkgdir}/usr/lib"
}
