# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Michael Baehr <usemike@spamblocked.com>

pkgname=ffcall
pkgver=2.4
pkgrel=2.1
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.gnu.org/software/libffcall/"
license=('GPL2')
depends=('glibc')
options=('!makeflags' 'staticlibs')
source=(https://ftp.gnu.org/gnu/libffcall/libffcall-${pkgver}.tar.gz
        ffcall-2.4-vacall-riscv-pic.patch)
sha256sums=('8ef69921dbdc06bc5bb90513622637a7b83a71f31f5ba377be9d8fd8f57912c2'
            'ebb7ff00393fc17c319308cdd83d33bffe206cf12fe430abe3cf56ad49dfaa8f')

prepare() {
  cd ${srcdir}/
  patch -Np0 -i ${srcdir}/ffcall-2.4-vacall-riscv-pic.patch
}

build() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  [ "$CARCH" = "x86_64" ] && CONFIGFLAG="--with-pic"
  CFLAGS+=" -ffat-lto-objects" \
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  make
}

check() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/lib${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  make DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
