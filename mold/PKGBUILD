# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=2.37.1
pkgrel=1
pkgdesc='A Modern Linker'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://github.com/rui314/mold'
license=('MIT')
# xxhash is bundled
depends=(
  glibc
  gcc-libs
  libblake3
  mimalloc
  tbb
  zlib
  zstd
)
makedepends=(
  cmake
  git
  mold
  python
)
checkdepends=(
  clang
  libdwarf
)
options=(!debug)
source=("${pkgname}::git+${url}.git#tag=v${pkgver}")
sha512sums=('c5e79c95f3cbcc5d11468a2c859d4f4d0c8cf88362dcfacde206b1551882414af46edec5ce6f2273c0414771081b2bfe66dfab3e888981586d4fe08c6b7105e3')
b2sums=('b7db5df949021a0a990f71f755448cb87d9ce32f4cedbc976b779ce81e3ebbc2db683566ec8f915581b2740cc40d3b887af7e48da2a7eb8fe94cca01368dde2d')

build() {
  export LDFLAGS="${CFLAGS/-Wl,--no-warn-rwx-segments -Wl,--no-warn-execstack/}"

  case "${CARCH}" in
    powerpc64)
      _cmake_options=(
        -D MOLD_USE_MOLD=OFF
      )
    ;;
    *)
      _cmake_options=(
        -D MOLD_LTO=ON
        -D MOLD_USE_MOLD=ON
      )
    ;;
  esac

  cmake \
  -S "${pkgname}" \
  -B build \
  -D CMAKE_BUILD_TYPE='None' \
  -D CMAKE_INSTALL_PREFIX='/usr' \
  -D CMAKE_INSTALL_LIBEXECDIR='lib' \
  -D MOLD_USE_SYSTEM_MIMALLOC=ON \
  -D MOLD_USE_SYSTEM_TBB=ON ${_cmake_options[@]}

  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 "${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/mold/LICENSE
}
