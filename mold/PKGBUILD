# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=2.40.2
pkgrel=1
pkgdesc='A Modern Linker'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/rui314/mold'
license=('MIT')
# xxhash is bundled
depends=(
  glibc
  gcc-libs
  libblake3
  mimalloc
  tbb
  zlib
  zstd
)
makedepends=(
  cmake
  git
  mold
  python
)
checkdepends=(
  clang
  libdwarf
)
options=(!debug)
source=("${pkgname}::git+${url}.git#tag=v${pkgver}")
sha512sums=('416587b2c2b0d744e9e3aca11f04586d21ad594366a9cc0b9c2886509761a2182188a0f3f622ec20618dc5fd450ac05c6ad9e2be5e5623013e6628b725af56fb')
b2sums=('32eef979194eeae8e710a6e5c46f8ec58ac89de36407f32510decc0134febc18403e77f52ef426ec95e378f90fc32f0e30717e2d13d8006011313e417324c840')

build() {
  export LDFLAGS="${CFLAGS/-Wl,--no-warn-rwx-segments -Wl,--no-warn-execstack/}"

  case "${CARCH}" in
    powerpc64)
      _cmake_options=(
        -D MOLD_USE_MOLD=OFF
      )
    ;;
    *)
      _cmake_options=(
        -D MOLD_LTO=ON
        -D MOLD_USE_MOLD=ON
      )
    ;;
  esac

  cmake \
  -S "${pkgname}" \
  -B build \
  -D CMAKE_BUILD_TYPE='None' \
  -D CMAKE_INSTALL_PREFIX='/usr' \
  -D CMAKE_INSTALL_LIBEXECDIR='lib' \
  -D MOLD_USE_SYSTEM_MIMALLOC=ON \
  -D MOLD_USE_SYSTEM_TBB=ON ${_cmake_options[@]}

  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc|powerpc64) return 0 ;;
  esac
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 "${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/mold/LICENSE
}
