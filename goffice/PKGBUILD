# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=goffice
pkgver=0.10.59
pkgrel=1
pkgdesc='A GLib/GTK set of document-centric objects and utilities library'
arch=(x86_64 powerpc powerpc64 powerpc64le)
url='https://gitlab.gnome.org/GNOME/goffice'
license=('GPL-2.0-only OR GPL-3.0-only')
depends=(
  at-spi2-core
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  lasem
  libgsf
  librsvg
  libspectre
  libxml2
  libxslt
  pango
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  intltool
)
source=("git+https://gitlab.gnome.org/GNOME/goffice.git#tag=GOFFICE_${pkgver//./_}")
b2sums=('7be38d701af477ccbf67fadb8e7d85daab66524799c492fb0d03acc25b0d4e06c2687b61eb45701bd9cfb39f9054c5b1b2e5f4d67d720340aedc7febc98e1a1a')

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-introspection \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}


check() {
  cd $pkgname
  case ${CARCH} in
    powerpc|powerpc64|powerpc64le)
      echo 'Skipping checks'
      echo 'See https://bugzilla.gnome.org/show_bug.cgi?id=748504'
    ;;
    *) make check ;;
  esac
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
