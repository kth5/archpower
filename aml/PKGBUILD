# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=aml
pkgver=0.3.0
pkgrel=3
pkgdesc='Another Main Loop'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url=https://github.com/any1/aml
license=(custom:ISC)
depends=(glibc)
makedepends=(
  git
  meson
  ninja
)
provides=(libaml.so)
_tag=b83f3576ce4187d9285f06e9066ef43a691464d4
source=(git+https://github.com/any1/aml.git#tag=${_tag})
b2sums=('f0ad91e34596bf0a69a7087a8268d3b43367c85272ee91f5ed66fb002f12c732d587b02e31e73d645304b6fc19ebaa4dae1c03fb6758d2f1680c46f300148233')

pkgver() {
  cd aml
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson aml build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 aml/COPYING -t "${pkgdir}"/usr/share/licenses/aml
}

# vim: ts=2 sw=2 et:
