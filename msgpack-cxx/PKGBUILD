# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname='msgpack-cxx'
pkgver=7.0.0
pkgrel=1
pkgdesc='An efficient object serialization library for C++'
arch=(any)
url='https://msgpack.org/'
license=('BSL-1.0')
depends=('boost')
makedepends=(
  'cmake'
  'doxygen'
  'graphviz'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/msgpack/msgpack-c/archive/cpp-$pkgver.tar.gz")
b2sums=('d75677e220a2fa67cd7ccd76dc6c081ddd8368259dc63e1b2ccafc088d1b3c0682da982ddfc54bfa283de96572c83ec9129cbea676302a277a51b3db5e76421b')

build() {
  local cmake_args=(
    -Wno-dev
    -S "msgpack-c-cpp-$pkgver"
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D MSGPACK_CXX17=ON
    -D MSGPACK_BUILD_EXAMPLES=OFF
    -D MSGPACK_BUILD_TESTS=ON
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

check() {
  make -C build test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
