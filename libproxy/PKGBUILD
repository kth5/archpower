# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libproxy
pkgname=(
  libproxy
  libproxy-docs
)
pkgver=0.5.2
pkgrel=2
pkgdesc="Automatic proxy configuration management library"
url="https://libproxy.github.io/libproxy/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL)
depends=(
  curl
  duktape
  glib2
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  gsettings-desktop-schemas
  meson
  vala
)
_commit=e4f467b05c8adf25e5a2d5464914fc7e92bb7990  # tags/0.5.2^0
source=(
  "git+https://github.com/libproxy/libproxy#commit=$_commit"
  0001-Fix-crash-querying-URLs-without-a-host.patch
)
b2sums=('SKIP'
        '32672271cce8d52c6051e51d5ccaf6186bcc26a582cd7e746e53ccc5c632c2f3019a1abfdc2c08ba9bf32a62af87ad027fce0a54b48c4abfdc8c4b7c700c7007')

pkgver() {
  cd libproxy
  git describe --tags | sed 's/^libproxy-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libproxy

  # https://bugs.archlinux.org/task/78881
  git apply -3 ../0001-Fix-crash-querying-URLs-without-a-host.patch
}

build() {
  local meson_options=(
  )

  arch-meson libproxy build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libproxy() {
  provides=(libproxy.so)
  conflicts=('libproxy-webkit<0.5.0-1')

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libproxy-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
