# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=gumbo-parser
pkgver=0.13.1
pkgrel=1
pkgdesc="HTML5 parsing library in pure C99"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://codeberg.org/gumbo-parser/gumbo-parser/"
license=(Apache-2.0)
depends=(glibc)
checkdepends=(gtest)
provides=(libgumbo.so)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('1a054d1e53d556641a6666537247411a77b0c18ef6ad5df23e30d2131676ef81')

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname
  make -k check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
