# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=gumbo-parser
pkgver=0.13.0
pkgrel=1
pkgdesc="HTML5 parsing library in pure C99"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://codeberg.org/gumbo-parser/gumbo-parser/"
license=(Apache-2.0)
depends=(glibc)
checkdepends=(gtest)
provides=(libgumbo.so)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('7ad2ee259f35e8951233e4c9ad80968fb880f20d8202cb9c48f0b65f67d38e61')

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname
  make -k check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
