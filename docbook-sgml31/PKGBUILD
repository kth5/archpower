# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: Andreas B. Wagner <AndreasBWagner@gmail.com>

pkgname=docbook-sgml31
pkgver=3.1
pkgrel=6
pkgdesc="Legacy docbook-sgml"
arch=(any)
url='https://www.docbook.org/sgml/'
license=(LicenseRef-$pkgname)
depends=('sgml-common')
install=$pkgname.install
source=("https://www.docbook.org/sgml/${pkgver}/docbk31.zip"
        LICENSE)
sha256sums=('20261d2771b9a052abfa3d8fab1aa62be05791a010281c566f9073bf0e644538'
            '4fe5a7c521db026ce926c148c051f495b0966e3c34443030116aa34381d6cec8')

build() {
  cd "$srcdir"
  # This command removes the ENT definitions from the catalog file.
  # This command replaces the DTDDECL catalog entry, which is not supported by Linux SGML tools, 
  # with the SGMLDECL catalog entry.
  sed -i -e '/ISO 8879/d' \
         -e 's|DTDDECL "-//OASIS//DTD DocBook V3.1//EN"|SGMLDECL|g' \
         docbook.cat
}

package() {
  cd "$srcdir"

  local DTDDIR="usr/share/sgml/docbook-sgml-${pkgver}"

  install -dm755 "$pkgdir/$DTDDIR"
  install -m644 docbook.cat "$pkgdir/$DTDDIR/catalog"
  install -m644 *.dtd *.mod *.dcl "$pkgdir/$DTDDIR"
  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

