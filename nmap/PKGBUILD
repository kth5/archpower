# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=nmap
pkgver=7.97
pkgrel=1
pkgdesc='Utility for network discovery and security auditing'
url='https://nmap.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LicenseRef-Nmap-Public-Source-License-Version-0.95)
depends=(
  gcc-libs
  glibc
  libpcap
  libssh2
  libssh2.so
  lua
  openssl
  pcre2
  zlib
)
source=(
  "https://nmap.org/dist/${pkgname}-${pkgver}.tar.bz2"
  "https://nmap.org/dist/sigs/${pkgname}-${pkgver}.tar.bz2.asc"
)
sha256sums=('af98f27925c670c257dd96a9ddf2724e06cb79b2fd1e0d08c9206316be1645c0'
            'SKIP')
b2sums=('affeca9f28a4ccb67b3f7154a08e587c5f80e292fc8e442f469812518e435b54d1ab125dbfd52981dc4730c41475d384092352715a2131379a7d032bd476298e'
        'SKIP')
validpgpkeys=(
  '436D66AB9A798425FDA0E3F801AF9F036B9355D0' # Nmap Project Signing Key (http://www.insecure.org/)
)

prepare() {
  cd "${pkgname}-${pkgver}"
  # ensure we build devendored deps
  rm -rf liblua libpcap libpcre macosx mwin32 libssh2 libz
  # fix build
  sed -e '/strlcat/d' -i libdnet-stripped/acconfig.h
  autoreconf -fiv -I /usr/share/gettext/m4
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --with-libpcap=/usr \
    --with-libpcre=/usr \
    --with-zlib=/usr \
    --with-libssh2=/usr \
    --with-liblua=/usr \
    --without-ndiff \
    --without-zenmap
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md docs/nmap.usage.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE docs/3rd-party-licenses.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
