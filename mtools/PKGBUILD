# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.49
pkgrel=1
epoch=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.gnu.org/software/mtools/"
license=(
  GPL-3.0-or-later
)
depends=(
  bash
  glibc
)
makedepends=(
  libx11
  texinfo
)
optdepends=(
  'libx11: required by floppyd'
  'libxau: required by floppyd'
)
backup=(etc/$pkgname.conf)
source=(https://ftp.gnu.org/gnu/mtools/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('be1c7068fb96d791bffc21b8b907f6ed6545011424fcf46bce5bc92d6544017f6c810f1aa9ab8c466708ca6002c10a078c298a0d528f21b50ef0d85b53da65f5'
            'SKIP')
b2sums=('4147fb151392a920f691ca543bd5d6fd0be83fd1b59cd610747eeb2a9c19633e17f15aa0a85659433e70e802bf6ccb6ec07f4d1357f7417d58e238476d68db19'
        'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839') # Alain KNAFF <alain@knaff.lu>

prepare() {
  # comment entry in default config
  sed -e '/^SAMPLE FILE$/s:^:# :' -i $pkgname-$pkgver/mtools.conf
}

build() {
  local configure_options=(
    --prefix=/usr
    --mandir=/usr/share/man
    --infodir=/usr/share/info
    --sysconfdir=/etc
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/mtools.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname-$pkgver/{README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname"
}
