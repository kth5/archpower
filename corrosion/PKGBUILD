# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=corrosion
pkgver=0.5.0
pkgrel=1
pkgdesc='Tool for integrating Rust into an existing CMake project'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/corrosion-rs/corrosion'
license=(MIT)
depends=(cmake
         rust)
makedepends=(git)
source=(git+https://github.com/corrosion-rs/corrosion#tag=v$pkgver)
sha256sums=('cf931bf263af47dd4ff1616e51f2700e6505c034c08b93165c8309f47589365b')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCORROSION_BUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
