# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.12.0
pkgrel=1
pkgdesc='Library for applications dealing with netlink sockets'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://github.com/thom311/libnl/'
license=('LGPL-2.1-only')
depends=('glibc' 'gcc-libs')
makedepends=('git')
provides=('libnl-3.so' 'libnl-cli-3.so' 'libnl-genl-3.so' 'libnl-idiag-3.so'
          'libnl-nf-3.so' 'libnl-route-3.so' 'libnl-xfrm-3.so')
backup=('etc/libnl/classid'
        'etc/libnl/pktloc')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728'  # Thomas Haller <thaller@redhat.com>
              '67DA3FAEBAE276BA58FC6CE314F18A98993AECD5') # Thomas Haller <thom311@gmail.com>
source=("git+https://github.com/thom311/libnl.git#tag=libnl${pkgver//./_}?signed")
sha256sums=('d741b8f94ef4a64b953603efce660a2eec4eb0cb6cd7faeabf75c8af99f0bcab')

prepare() {
  cd "${pkgname}"
  ./autogen.sh
}

build() {
  cd "${pkgname}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

check() {
  make -C "${pkgname}" check
}

package() {
  make -C "${pkgname}" DESTDIR="${pkgdir}" install
}
