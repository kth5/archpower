# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libcddb
pkgver=1.3.2
pkgrel=9
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
pkgdesc="Library that implements the different protocols (CDDBP, HTTP, SMTP) to access data on a CDDB server (https://gnudb.org)"
url="https://sourceforge.net/projects/libcddb/"
license=('LGPL-2.0-or-later')
depends=('glibc')
source=(
  "https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
  "$pkgname-switch-default-server-to-gnudb.org.patch"
  "$pkgname-pointer-types.patch::https://gitlab.alpinelinux.org/alpine/aports/-/raw/master/community/libcddb/pointer-types.patch"
  "$pkgname-autotools-fix-undefinded-gettex-macros.patch"
)
sha512sums=('0e07e7d37f2013164d75b530a5528f54847271fd20f3b7bedb192a05d1f61dcf006d10dc2927efe155a01eddcc57b940bc31d8ac88d5dfc4f1a09771caa84e0a'
            'b3418d4aabba772f92d9aca860b5e1a80084db4ff4b1b83904b1984d1b64f72bcb947b414b7726495137eac9c9a17dbbdf7e662fa976eb18af3e1b02890f3594'
            '81c27b487c0288233c2f82bc899b14f5f7b6be20ef69f0ce7b73ca5189ea3f84284f0e8423c2c5facd16a0005eec47dd8d5bf5e39a45ff95ecfc6ed9f41aea84'
            'b9abdeee1202f24535726f67bb64246a0cceb5e43b2d361e9fa896c67018233430fe5acf60554470431686e762db38f452766cd688768bba65b996eef1db2a6a')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../$pkgname-switch-default-server-to-gnudb.org.patch
  patch -Np1 < ../$pkgname-pointer-types.patch  # Fix FTBFS with GCC 14
  patch -Np1 < ../$pkgname-autotools-fix-undefinded-gettex-macros.patch
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
