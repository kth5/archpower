# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=cifs-utils
pkgver=7.4
pkgrel=1
pkgdesc="CIFS filesystem user-space tools"
arch=(powerpc64le powerpc64 powerpc espresso)
url="https://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL-3.0-or-later')
depends=(
  keyutils
  krb5
  pam
  libcap-ng
  libwbclient  
  talloc  
)
makedepends=(
  git
  python-docutils
)
optdepends=('python: for smb2-quota and smbinfo script')
source=("git+https://git.samba.org/cifs-utils.git?signed#tag=${pkgname}-${pkgver}")
validpgpkeys=(
    C699981A31F338706C817650DF5BA9D30642D5A0 #cifs-utils Distribution Verification Key <cifs-utils@samba.org>
    E9FB2EF297488ED584FC3A4B8A2CBD7007724F51 # Steve French <smfrench@gmail.com 
    F2D066D1E8C2D888248F7A7C627A8E85F6858D07 # Pavel Shilovsky <pshilovsky@samba.org>
)
b2sums=('c44d65dfaee435add0512275904189df51eb48d712cdb40c542c10767db35e13ff0d45247ea062b9704341117ad9a9c376d8f5867c47da9f29baa032213079fa')

prepare() {
  cd "${srcdir}/${pkgname}"
  # Fix install to honor DESTDIR
  sed -e 's|cd \$(ROOTSBINDIR)|cd $(DESTDIR)$(ROOTSBINDIR)|g' -i Makefile.am
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" ROOTSBINDIR=/usr/bin install
  mkdir -p "${pkgdir}"/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.idmap.conf "${pkgdir}"/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.spnego.conf "${pkgdir}"/etc/request-key.d
  # set mount.cifs uid, to enable none root mounting form fstab
  chmod +s "${pkgdir}"/usr/bin/mount.cifs
  # fix idmap-plugin #42052
  mkdir -p "${pkgdir}"/etc/cifs-utils
  ln -s /usr/lib/cifs-utils/idmapwb.so "${pkgdir}"/etc/cifs-utils/idmap-plugin
}

