# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mmtf-cpp
pkgver=1.1.0
pkgrel=2
pkgdesc='The pure C++ implementation of the MMTF API, decoder and encoder'
arch=(any)
url='http://mmtf.rcsb.org/'
license=(MIT)
depends=(msgpack-cxx)
makedepends=(cmake
             git)
source=(git+https://github.com/rcsb/mmtf-cpp#tag=v$pkgver)
sha256sums=('da7160c3015f1aae4ec3062e53e5c0bddbda993f58944a6c010fff9336ec16b7')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
