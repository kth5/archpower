# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>

pkgname=tpm2-abrmd
pkgver=3.0.0
pkgrel=2
pkgdesc='Trusted Platform Module 2.0 Access Broker and Resource Management Daemon'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://github.com/tpm2-software/tpm2-abrmd'
license=('BSD-2-Clause')
depends=(
  'glib2'
  'glibc'
  'libtss2-mu.so'
  'libtss2-rc.so'
  'libtss2-sys.so'
  'libtss2-tctildr.so'
  'tpm2-tss'
)
makedepends=('glib2-devel')
checkdepends=(
  'cmocka'
  'iproute2'
  'swtpm'
)
options=('!lto')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('65a39cd0518dfbcbd5835ac6c6628444d412e36471702f2cd1f1077d788330a477778e6403899ea2fa0497b643e5db9ca3e746845492eff78c4c78d8dd8f9e7d'
            'SKIP')
validpgpkeys=(
  '42007E876F248E04A3F2FE25AE4548D043DEC7C3' # Philip Tricca <philip.b.tricca@intel.com>
  '5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1' # William Roberts (Bill Roberts) <william.c.roberts@intel.com>
)

build() {
  cd "$pkgname-$pkgver"
  # shellcheck disable=SC2046
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-dbuspolicydir=/usr/share/dbus-1/system.d \
    $( ((CHECKFUNC)) && echo --enable-unit --enable-integration)
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -vr "$pkgdir/usr/lib/systemd/system-preset"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
