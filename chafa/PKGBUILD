# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.14.2
pkgrel=2.1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://hpjansson.org/chafa/"
depends=('libavif' 'libjxl' 'librsvg' 'libwebp' 'libxslt')
makedepends=('git' 'gtk-doc')
provides=('libchafa.so')
license=("LGPL-3.0-or-later")
source=(git+https://github.com/hpjansson/chafa.git#tag=$pkgver?signed)
sha512sums=('73e90cc7334ecb1caed433ddba88c5fad41081583143bab3224ebf7c27d1146cb040bf454b8d071c6278324bce169c946d53e055133181c28d0e20e74db6b261')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --enable-man --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  make -C tools/completions PREFIX="$pkgdir"/usr/share install-zsh-completion
}
