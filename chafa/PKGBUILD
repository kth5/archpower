# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.14.3
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://hpjansson.org/chafa/"
depends=('libavif' 'libjxl' 'librsvg' 'libwebp' 'libxslt')
makedepends=('git' 'gtk-doc')
provides=('libchafa.so')
license=("LGPL-3.0-or-later")
source=(git+https://github.com/hpjansson/chafa.git#tag=$pkgver?signed)
sha512sums=('2501e62ba16976e9d4cb2a3b8dd5af227bfa116f9b6dd8bbcc853a048061083a8610e642bd7b654793db7d162f3f1e91c3f931824c1ac2806590ecbc5f24d45b')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --enable-man --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  make -C tools/completions PREFIX="$pkgdir"/usr/share install-zsh-completion
}
