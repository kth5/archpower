# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.14.0
pkgrel=3
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://hpjansson.org/chafa/"
depends=('libavif' 'librsvg' 'libwebp' 'libxslt')
makedepends=('gtk-doc')
provides=('libchafa.so')
license=("LGPL-3.0-or-later")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.asc})
sha512sums=('f205bf5b8e31b05a227484a2445eb1a06b0b3abe2f27483b899340d89a22c0cd188e9b56a4217e09ac313af6e71fa75b5a480b66e85464422fd46049ec480522'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

prepare() {
  cd $pkgname-$pkgver
  # https://github.com/hpjansson/chafa/pull/189
  sed -i '/install-zsh-completion/a install-zsh-completion:' tools/completions/Makefile.in
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  make -C tools/completions PREFIX="$pkgdir"/usr/share install-zsh-completion
}
