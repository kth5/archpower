# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.16.2
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://hpjansson.org/chafa/"
depends=('libavif' 'libjxl' 'librsvg' 'libwebp' 'libxslt')
makedepends=('git' 'gtk-doc')
provides=('libchafa.so')
license=("LGPL-3.0-or-later")
source=(git+https://github.com/hpjansson/chafa.git#tag=$pkgver?signed)
sha512sums=('3d619553357243fc330808a6e3cab7a449a8e969ccd0a21e2d96d2f8552a017a8e4bb729288a913494f16d4cf85bd3b08f36af25d4d6585a55a0e26c08984398')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --enable-man --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  make -C tools/completions PREFIX="$pkgdir"/usr/share install-zsh-completion
}
