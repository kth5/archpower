# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libosinfo
pkgver=1.11.0
pkgrel=3
pkgdesc='GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://libosinfo.org/'
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=('gcc-libs' 'glib2' 'glibc' 'hwdata' 'libsoup3' 'libxml2' 'libxslt' 'osinfo-db')
makedepends=('git' 'glib2-devel' 'gobject-introspection' 'meson' 'vala')
source=("git+https://gitlab.com/libosinfo/$pkgname.git?signed#tag=v$pkgver")
b2sums=('db8f3bd42b2b165c0d46e58fc4e6f15159381e5499130ea692374a9951ca316702b31a44a7d38304f1f76de06a5fa92b4fec68fd479b18706d03e29e509f9c11')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF'  # Daniel P. Berrange
              '09B9C8FF223EF113AFA06A39EE926C2BDACC177B'  # Fabiano Fidêncio
              '206D3B352F566F3B0E6572E997D9123DE37A484F') # Victor Toso de Carvalho <me@victortoso.com>

prepare() {
  cd $pkgname

  # Fix build with libxml2 2.12.0
  git cherry-pick -n 5bbdd06503456784c5ffa22409e8bab50470d673

  sed -i 's/-Werror//' meson.build
}

build() {
  local meson_options=(
    -D with-usb-ids-path=/usr/share/hwdata/usb.ids
    -D with-pci-ids-path=/usr/share/hwdata/pci.ids
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
