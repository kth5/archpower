# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libinstpatch
pkgver=1.1.7
pkgrel=2
pkgdesc="Instrument file software library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/swami/libinstpatch"
license=(LGPL-2.1-only)
depends=(glibc)
makedepends=(cmake glib2 libsndfile python glib2-devel)
provides=(libinstpatch-1.0.so)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
b2sums=('7e78b0f868cd805a3633eeecbbd0b70b1d7f344372a96b0c08b1beabf5596a2d768db30499e802ab53ce22fa05e457c26b2d24d5f29285040807dc0d54a0d10e')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_INSTALL_LIBDIR=/usr/lib
    -D CMAKE_BUILD_TYPE=None
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    glib2 libglib-2.0.so libgobject-2.0.so
    libsndfile libsndfile.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README.md,TODO.tasks} -t "$pkgdir/usr/share/doc/$pkgname/"
}
