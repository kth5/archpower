# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>

pkgname=rocm-device-libs
pkgver=6.0.2
pkgrel=1
pkgdesc='ROCm Device Libraries'
arch=(x86_64 powerpc64le powerpc64 riscv64)
url='https://github.com/ROCm/ROCm-Device-Libs'
license=('NCSA')
makedepends=('rocm-core' 'rocm-cmake' 'rocm-llvm')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/rocm-$pkgver.tar.gz"
        "${pkgname}-rm-gfx700.patch")
sha256sums=('c6d88b9b46e39d5d21bd5a0c1eba887ec473a370b1ed0cebd1d2e910eedc5837'
            '0b38ab6d8b1a52bac198007af098d6ec5008a7359a4ee174283e6c69c8b1a868')
_dirname="$(basename "$url")-$(basename "${source[0]}" .tar.gz)"

prepare() {
    cd "$_dirname"
    patch -Np1 -i "$srcdir/$pkgname-rm-gfx700.patch"
}

build() {
    local cmake_args=(
        -Wno-dev
        -S "$_dirname"
        -B build
        -D CMAKE_C_COMPILER=/opt/rocm/llvm/bin/clang
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX=/opt/rocm
        -D LLVM_DIR=/opt/rocm/llvm/lib/cmake/llvm
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

check() {
    cmake --build build --target test
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 "$_dirname/LICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
