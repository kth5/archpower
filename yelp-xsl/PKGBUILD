# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=yelp-xsl
pkgver=42.4
pkgrel=1
pkgdesc="Document transformations from Yelp"
url="https://gitlab.gnome.org/GNOME/yelp-xsl"
arch=(any)
license=(
  BSD-3-Clause
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
makedepends=(
  git
  itstool
  libxslt
  meson
)
source=(
  "git+$url.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('efa5b673926b3f3f5021c652ad62d9562042fbbcf8a82dffc1b9bdfe336382c7c5f25c61673bce427b1a92e7d9c6878c130e0a8199fd7a11b315aec626b6ac16')

prepare() {
  cd yelp-xsl
}

build() {
  local meson_options=(
    -D dita=true
  )

  arch-meson yelp-xsl build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 yelp-xsl/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
