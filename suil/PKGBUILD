# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=suil
pkgname=(suil{,-docs})
pkgver=0.10.24
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://drobilla.net/software/suil.html"
license=(
  0BSD
  ISC
)
makedepends=(
  doxygen
  gtk3
  lv2
  meson
  python-sphinx
  python-sphinx-lv2-theme
  python-sphinxygen
  qt5-base
  qt5-x11extras
  qt6-base
)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('313b4ee49d43e2dc5a0955f47859fe15e88bc9687808eda93d581b16f84698c2cb4e8208f7339b2bbfb31bfc46588914f8a67405d2bb53014e3919d08653134d'
            'SKIP')
b2sums=('7a13edeefb750a4112266b8ce8c92a77c6cabc2982d8ea4e219f34960bc6926b0684b0bf21cb829ef8fbea530a676b54e7e4d8dae0e34d649654cc69f126f1da'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local meson_options=(
    -D cocoa=disabled
    -D gtk2=disabled
  )

  arch-meson $pkgname-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build
}

package_suil() {
  depends=(
    gcc-libs
    glibc
    glib2
    gtk3
    libx11
    lv2
    qt5-base
    qt5-x11extras
    qt6-base
  )
  provides=(libsuil-0.so)

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgbase-docs usr/share/doc
  )
  install -vDm 644 $pkgbase-$pkgver/LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_suil-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/$pkgbase"{-0,}
  find "$pkgdir" -type f -iname "*.buildinfo" -delete
  install -vDm 644 $pkgbase-$pkgver/LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
