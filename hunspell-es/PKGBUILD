# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hunspell-es
pkgname=(hunspell-es_any hyphen-es mythes-es)
pkgver=2.9
pkgrel=1
epoch=1
arch=(any)
url='https://github.com/sbosio/rla-es'
license=(LGPL-3.0-or-later)
makedepends_x86_64=(qt6-webengine)
_countries=(
  'AR "Argentina"'
  'BO "Bolivia"'
  'CL "Chile"'
  'CO "Colombia"'
  'CR "Costa Rica"'
  'CU "Cuba"'
  'DO "Dominican Republic"'
  'EC "Ecuador"'
  'ES "Spain"'
  'GT "Guatemala"'
  'HN "Honduras"'
  'MX "Mexico"'
  'NI "Nicaragua"'
  'PA "Panama"'
  'PE "Peru"'
  'PR "Puerto Rico"'
  'PY "Paraguay"'
  'SV "El Salvador"'
  'UY "Uruguay"'
  'VE "Venezuela"'
)

source=(https://github.com/sbosio/rla-es/releases/download/v$pkgver/es.oxt)
noextract=(es.oxt)

for _co in "${_countries[@]}"; do
  _code=${_co%% *}
  noextract+=(es_$_code.oxt)
  source+=("https://github.com/sbosio/rla-es/releases/download/v$pkgver/es_$_code.oxt")
  _pkgname=hunspell-es_${_code,,}
  pkgname+=($_pkgname)
  eval "package_$_pkgname() {
    _package $_co
  }"
done

sha256sums=('b08a1a0e3e044697f63a67184f591f7e2c37bbb53bbfbb4780bcbd84929d6e8c'
            'e1793f41a686e86b9c59ede1d53046dcbdc43e449ae7640ea340c88dd0e78f5c'
            '4593bd3d386402d356209966176f6151a2a69de302bd9ac1b3d3a68a18310ff7'
            'efe92c2939622c6a5793e5d3dde5c617b8cb86a6d0aae6058f6be6ab2fc23a2e'
            'd47ff4cf5cab98577d9a1913e226e31bc8ade9334c15fd389b97ddc0341a2901'
            '60d5ce5a47cf78284bf60ac7aef7024d62bec3d89360f2254ca40ec649c56863'
            'bb488f6601a5c131cd52afa5c8de2c02c6f442d7e742bace2d5c69a5baeb18be'
            '98836c4a19430d88dcb78ed8d74061e1f19c28e8d9378e357779bb4f0ee44d5e'
            '33c723d36f447a8c5893f8ffdc00784d156dd5fe75fbf8bba88879472de24ba2'
            '3eea87836b24b6004aa1ee6fd285b6c71774b0996aec70d096d75d5526efb4ac'
            '273f152ce66c204c76478a9451f1062247c9043663fb73335eca17ee9b725464'
            'd08fb5aae91bde07ee7ae26c721bde76621d71ba1767c5c5f12094a88dd5edbe'
            '8cb7b24f5797eb4749648e7808ce3fcaa80bc668add7894242a4edf2be4a6726'
            'de46657a0ee6d696c16935a3b118fe99419845b2ab9181c14b493f3afe65f22c'
            '3a4d0ad8b746c4a088cb6ea0adc1b1ed8f653965486503c1c76bd44f427d1a22'
            '5ed93708bb2524ae10308665aa079641dbb69d6e1bfd647b62f3b9905d542fca'
            'aa0a90d1329854b45b4bf42ad83048628d5a34cee80168eaf417c1a4832d929f'
            'eee4316f9fa65f8085235fd0025cc329870bbd7911fb607712d7f6be0c46a291'
            '0534c4ccb69346d7e9ffa209a56114ccc74909ebfc3e2f12cb63d60aa701f9bb'
            '9aee79535df6eaca101222ed538f9ad04c20056270c814d73700c985b8bb73b6'
            '65515d99ea66308b89bb238f7975c7a3acee1c060e6eaeee45865b1be8d5d38e')

_package() {
  pkgdesc="Spanish ($2) hunspell dictionary"
  optdepends=('hunspell: the spell checking libraries and apps')
  provides=('hunspell-dictionary')
  conflicts=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es_$1.oxt

  install -dm755 "$pkgdir"/usr/share/hunspell
  install -Dm644 es_$1.dic es_$1.aff "$pkgdir/usr/share/hunspell"

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir/usr/share/myspell/dicts"
    for _file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $_file) .
    done
  popd

# Install webengine dictionaries
  if [ "${CARCH}" == "x86_64" ]; then
  install -d "$pkgdir"/usr/share/qt{,6}/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    /usr/lib/qt6/qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  ln -rs "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic} "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  done
  fi

  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hunspell-es_any() {
  pkgdesc="Spanish (generic) hunspell dictionary"
  optdepends=('hunspell: the spell checking libraries and apps')
  provides=('hunspell-dictionary')
  conflicts=(hunspell-es)
  replaces=(hunspell-es)

  LANG=en_US.utf8 bsdtar -xf es.oxt

  install -Dm644 es.dic es.aff -t "$pkgdir"/usr/share/hunspell

  install -d "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
  
# Install webengine dictionaries
  if [ "${CARCH}" == "x86_64" ]; then
  install -d "$pkgdir"/usr/share/qt{,6}/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    /usr/lib/qt6/qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  ln -rs "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic} "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  done
  fi

  install -Dm644 README.txt -t "$pkgdir/usr/share/doc/$pkgname"
}

package_hyphen-es() {
  pkgdesc="Spanish hyphenation rules"
  depends=(hyphen)

  install -Dm644 hyph_es.dic -t "$pkgdir"/usr/share/hyphen/

  pushd "$pkgdir"/usr/share/hyphen/
  for _co in "${_countries[@]}"; do
    ln -s hyph_es.dic hyph_es_${_co%% *}.dic
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
  
  install -Dm644 README_hyph_es.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}

package_mythes-es() {
  pkgdesc="Spanish thesaurus"
  depends=(libmythes)

  LANG=en_US.utf8 bsdtar -xf es.oxt
  install -Dm644 th_es_v2.* -t "$pkgdir"/usr/share/mythes

  pushd "$pkgdir"/usr/share/mythes/
  for _co in "${_countries[@]}"; do
    ln -s th_es_v2.idx "th_es_"${_co%% *}"_v2.idx" || true
    ln -s th_es_v2.dat "th_es_"${_co%% *}"_v2.dat" || true
  done
  popd

  install -d "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
  for file in "$pkgdir"/usr/share/mythes/*; do
    ln -sv /usr/share/mythes/$(basename $file) .
  done
  popd
  
  install -Dm644 README_th_es.txt -t "$pkgdir"/usr/share/doc/$pkgname/
}
