# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Jiachen Yang <farseerfc@gmail.com>
# Contributor: apophys <admin AT kubikmilan DOT sk>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Renzo Carbonara <gnuk0001/gmail.com>

pkgname=lzip
pkgver=1.25
pkgrel=1
pkgdesc="A lossless file compressor based on the LZMA algorithm"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.nongnu.org/lzip/lzip.html"
license=('GPL3')
depends=('gcc-libs')
source=("https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('09418a6d8fb83f5113f5bd856e09703df5d37bae0308c668d0f346e3d3f0a56f'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742')  # Antonio Diaz Diaz <antonio@gnu.org>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr CXXFLAGS="$CXXFLAGS" LDFLAGS="$LDFLAGS"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install{,-man}
}
