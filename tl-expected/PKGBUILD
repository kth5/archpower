# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Viktor Schneider <info[at]vjs[dot]io>
pkgname=tl-expected
pkgver=1.3.1
pkgrel=1
pkgdesc="C++11/14/17 std::expected with functional-style extensions"
arch=(any)
url="https://github.com/TartanLlama/expected"
license=('CC0-1.0')
makedepends=('cmake' 'git' 'ninja')
source=("expected-v${pkgver}.tar.gz::https://github.com/TartanLlama/expected/archive/v${pkgver}.tar.gz")
sha256sums=('9a04f4f472fbb5c30bf60402f1ca626c4a76987f867978d0b8a35d7ab3fb8fe7')

build() {
  cd "expected-$pkgver"

  cmake \
      -B build \
      -G Ninja \
      -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  cd "expected-$pkgver"

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
