# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=zenity
pkgver=4.2.0
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
url="https://gitlab.gnome.org/GNOME/zenity"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  git
  glib2-devel
  help2man
  meson
  util-linux
  yelp-tools
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('9f477379d80e050c71d13738c9d931dd8e0aea5eeb3f98475187208b266a990585c129e56adb26356e819e9a9e32c280bdd788aeebfbd9925b7d0b96f806977b')
validpgpkeys=(
  98FF7532A4639119B8FB4F15E520923A38618D6C # Logan Rathbone (GNOME) <larathbone@gnome.org>
)

prepare() {
  cd zenity
}

build() {
  arch-meson zenity build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  hardlink -c "$pkgdir/usr/share/help"
}

# vim:set sw=2 sts=-1 et:
