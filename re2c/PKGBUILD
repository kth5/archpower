# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: nut543 <kfs1@online.no>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=re2c
pkgver=4.4
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
pkgdesc='A tool for generating C-based recognizers from regular expressions'
url='https://re2c.org'
license=(LicenseRef-re2c)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cmake
  python
)
source=("https://github.com/skvadrik/re2c/archive/$pkgver/re2c-$pkgver.tar.gz")
b2sums=('6cd791c3c51cc8651cd48b2fb52998c25cc6497c3257893ec70ec003e40f9c76893d00fa81f83b4d975f110de8e58418af63112f337969cd9b36059b11115cc9')

build() {
  cd $pkgname-$pkgver
  # CMAKE_BUILD_TYPE=None explicitly disallowed in cmake/Re2cBuildType.cmake
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
