# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:
# Contributor: nut543 <kfs1@online.no>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=re2c
pkgver=4.0.1
pkgrel=1
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=(gcc-libs)
makedepends=(cmake python)
pkgdesc='A tool for generating C-based recognizers from regular expressions'
url='https://re2c.org'
license=('public domain')
source=(re2c-$pkgver.tar.gz::https://github.com/skvadrik/re2c/archive/$pkgver.tar.gz)
sha256sums=('f8a0192af88cedd1cf2e1f5f7859118018971b87836fcaae230da0b2ffc60af1')

build() {
  cd $pkgname-$pkgver
  cmake -B build -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  cmake --build build --target check
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
