# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgbase=gupnp
pkgname=(
  gupnp
  gupnp-docs
)
pkgver=1.6.5
pkgrel=1
epoch=1
pkgdesc="An object-oriented UPNP framework"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL)
depends=(
  glib2
  gssdp
  libxml2
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  vala
)
_commit=04fc6512bcf26a405ede293592bd69c733f3191b  # tags/gupnp-1.6.5^0
source=("git+https://gitlab.gnome.org/GNOME/gupnp.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd gupnp
  git describe --tags | sed 's/^gupnp-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gupnp
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson gupnp build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}


package_gupnp() {
  depends+=(libg{lib,object,io,module}-2.0.so libgssdp-1.6.so libsoup-3.0.so)
  optdepends=('python: gupnp-binding-tool')
  provides=(libgupnp-1.6.so)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"
  _pick docs usr/share/doc
}

package_gupnp-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
