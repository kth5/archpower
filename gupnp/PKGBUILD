# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgbase=gupnp
pkgname=(
  gupnp
  gupnp-docs
)
pkgver=1.6.8
pkgrel=1
epoch=1
pkgdesc="GObject-based UPNP framework"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gssdp
  libxml2
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/gupnp.git?signed#tag=gupnp-$pkgver")
b2sums=('a4bb800ed8079c7a646101809fc6227283b0061ad09f4910acbd939446b0ca03e3a268fde40ed40393fd080a9643ae299e0b8d2e433214c0387ad883e04c3bec')
validpgpkeys=(
  AC9CD4E32D7C7F6357BA8ADD10F6E970175D29E1 # Jens Georg <mail@jensge.org>
)

prepare() {
  cd gupnp
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson gupnp build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gupnp() {
  depends+=(
    libg{lib,object,io,module}-2.0.so
    libgssdp-1.6.so
    libsoup-3.0.so
  )
  optdepends=('python: gupnp-binding-tool')
  provides=(libgupnp-1.6.so)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"
  _pick docs usr/share/doc
}

package_gupnp-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
