# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>

_upstream_name=license-list-data
_upstream_version=3.22
pkgname=licenses
pkgver=20231215
pkgrel=1
pkgdesc="A set of common license files"
arch=(any)
license=(custom:none)
url="https://www.archlinux.org/"
makedepends=(
  jq
)
source=(
  $_upstream_name-$_upstream_version.tar.gz::https://github.com/spdx/$_upstream_name/archive/refs/tags/v$_upstream_version.tar.gz
  https://www.gnu.org/licenses/agpl-3.0.txt
  https://www.gnu.org/licenses/fdl-1.2.txt
  https://www.gnu.org/licenses/fdl-1.3.txt
  https://www.gnu.org/licenses/gpl-2.0.txt
  https://www.gnu.org/licenses/gpl-3.0.txt
  https://www.gnu.org/licenses/lgpl-2.1.txt
  https://www.gnu.org/licenses/lgpl-3.0.txt
)
sha512sums=('b213fe66699770d75a4c994a01a5c08325751423516c2fb871088a47e7e7ce605736064f5ce3a63dc1d3c462271832db7bc464ac968df1888f9823787964c786'
            '3568a76677f3d55dbbc7fc33c4073b4714cc6f68d92cd842a9ca2b230e8bd2140e59c3fc3cce34be34ffed78d75ab6e9d991ba59bf7c4addde7fb96648d3375d'
            '22d2414762188840be68198978b9ece122813d1cac522632b8fa8315749335b43b1b58a6475fec0ef593a7bc568368526a90e199999808a118a7c6bb55906ac1'
            '22d46818d3998ad841f537af4de7c50440dd918099fb6c5d4ab324cd71dc03066bfe1b67210a5efde77abd1d97b88da3c3dbfc39a24ae7248ee2d64e7f0fe6bb'
            'aee80b1f9f7f4a8a00dcf6e6ce6c41988dcaedc4de19d9d04460cbfb05d99829ffe8f9d038468eabbfba4d65b38e8dbef5ecf5eb8a1b891d9839cda6c48ee957'
            'd361e5e8201481c6346ee6a886592c51265112be550d5224f1a7a6e116255c2f1ab8788df579d9b8372ed7bfd19bac4b6e70e00b472642966ab5b319b99a2686'
            '1bca76c9f2f559a7851c278650125cd4f44a7ae4a96ceee6a6ba81d34d28fe7d6125c5ee459fef729b6a2a0eba3075c0841c8a156b3a26f66194f77f7d49151c'
            '6a21e9f6706dacb99162dd4c70ec704e3fdf283b93ca2cc1521e08e55e0727db4c7384d027d54739f100cd26c8d3d5be717715c6b21086a9bb22efb893c34fcf')
b2sums=('751d4a31a74b1f0c6831b2e2ad7b361fddca6f9e71f58f57b2dc4fdf13f3ad2a63c652d0333e97d25c1748b7ced62db0b799cc5421c7ed4e117b46fa0869bf3c'
        'b6829320f725e3e45c4807ef5deb4738a691fb3ab146d8531b81fdbccd8376a826c8ec76165985cdf37d534f68e395652c96841ba7636c4bd34c49b7c7b3a9ec'
        'a580eceb4f7514964ef00e8c5130b9568fb2cf99d9bef0222662fa994af7b1634c1034c846eac9d63c21b67c4f94f91b79d144b1d3b4a856ca10243548407b84'
        '85947e91ba1da3f15c1d2c55f592aaaa1ede964ca8f48c9a68799c7a405a8f43b927ce9bac65c893e3b4c7925e879bdfcc852e46f03ae04e48b036fdff6c29f7'
        '1e1e108a77d8676eb68467cdfbf95684f0bad991bfcc24e7bd3d1eec58d746f14d20798c3bcf64e9bdd1ff4a9c5ec02ccb4c06331bcf251a9d1d0f28b6875296'
        '74915e048cf8b5207abf603136e7d5fcf5b8ad512cce78a2ebe3c88fc3150155893bf9824e6ed6a86414bbe4511a6bd4a42e8ec643c63353dc8eea4a44a021cd'
        'b15551a6f0f0923dfb15912db9f6976898bc3d60ab3f5505e48d5e252341bb0855f4c7f23d66e9ce8809a23187b5f2ea08c99c8eb2c3d4d0407466c222cfdcca'
        '4f97bb19f3d6aa5af71bec5a070b4317c0742069d23f591092e435ff8805788f7279524a1bb8a10e67a1013e0391e4a719bb8d89ccdae728b4eb134d7697a3c8')

prepare() {
  local license
  touch known_spdx_license_exceptions.txt known_spdx_license_identifiers.txt

  # create list of known, non-deprecated SPDX license identifiers
  for license in $(jq -r '.licenses[] | select(.isDeprecatedLicenseId == false) | .licenseId' $_upstream_name-$_upstream_version/json/licenses.json); do
    printf "%s\n" "$license" >> known_spdx_license_identifiers.txt
  done

  # create list of known SPDX exception identifiers
  for license in $(jq -r '.exceptions[] | .licenseExceptionId' $_upstream_name-$_upstream_version/json/exceptions.json); do
    printf "%s\n" "$license" >> known_spdx_license_exceptions.txt
  done
}

package() {
  local license
  local standard_licenses=(
    AGPL-3.0-only
    AGPL-3.0-or-later
    Apache-2.0
    Artistic-1.0-Perl
    Artistic-2.0
    BSL-1.0
    CC0-1.0
    CC-BY-1.0
    CC-BY-2.0
    CC-BY-2.5
    CC-BY-3.0-AT
    CC-BY-3.0
    CC-BY-3.0-US
    CC-BY-4.0
    CC-BY-NC-1.0
    CC-BY-NC-2.0
    CC-BY-NC-2.5
    CC-BY-NC-3.0
    CC-BY-NC-4.0
    CC-BY-NC-ND-1.0
    CC-BY-NC-ND-2.0
    CC-BY-NC-ND-2.5
    CC-BY-NC-ND-3.0-IGO
    CC-BY-NC-ND-3.0
    CC-BY-NC-ND-4.0
    CC-BY-NC-SA-1.0
    CC-BY-NC-SA-2.0
    CC-BY-NC-SA-2.5
    CC-BY-NC-SA-3.0
    CC-BY-NC-SA-4.0
    CC-BY-ND-1.0
    CC-BY-ND-2.0
    CC-BY-ND-2.5
    CC-BY-ND-3.0
    CC-BY-ND-4.0
    CC-BY-SA-1.0
    CC-BY-SA-2.0
    CC-BY-SA-2.0-UK
    CC-BY-SA-2.1-JP
    CC-BY-SA-2.5
    CC-BY-SA-3.0-AT
    CC-BY-SA-3.0
    CC-BY-SA-4.0
    CC-PDDC
    CDDL-1.0
    CDDL-1.1
    CPL-1.0
    EPL-1.0
    EPL-2.0
    GFDL-1.1-invariants-only
    GFDL-1.1-invariants-or-later
    GFDL-1.1-no-invariants-only
    GFDL-1.1-no-invariants-or-later
    GFDL-1.1-only
    GFDL-1.1-or-later
    GFDL-1.2-invariants-only
    GFDL-1.2-invariants-or-later
    GFDL-1.2-no-invariants-only
    GFDL-1.2-no-invariants-or-later
    GFDL-1.2-only
    GFDL-1.2-or-later
    GFDL-1.3-invariants-only
    GFDL-1.3-invariants-or-later
    GFDL-1.3-no-invariants-only
    GFDL-1.3-no-invariants-or-later
    GFDL-1.3-only
    GFDL-1.3-or-later
    GPL-2.0-only
    GPL-2.0-or-later
    GPL-3.0-only
    GPL-3.0-or-later
    GPL-CC-1.0
    LGPL-2.0-only
    LGPL-2.0-or-later
    LGPL-2.1-only
    LGPL-2.1-or-later
    LGPL-3.0-only
    LGPL-3.0-or-later
    LGPLLR
    LPPL-1.0
    LPPL-1.1
    LPPL-1.2
    LPPL-1.3a
    LPPL-1.3c
    MPL-1.0
    MPL-1.1
    MPL-2.0
    PHP-3.01
    PHP-3.0
    PSF-2.0
    Ruby
    Unlicense
    W3C
    ZPL-1.1
    ZPL-2.0
    ZPL-2.1
  )
  local standard_exceptions=(
    GPL-3.0-linking-exception
    GPL-3.0-linking-source-exception
    LGPL-3.0-linking-exception
    MPL-2.0-no-copyleft-exception
  )

  for license in "${standard_licenses[@]}"; do
    install -vDm 644 "$_upstream_name-$_upstream_version/text/$license.txt" -t "$pkgdir/usr/share/licenses/spdx/"
  done
  for license in "${standard_exceptions[@]}"; do
    install -vDm 644 "$_upstream_name-$_upstream_version/text/$license.txt" -t "$pkgdir/usr/share/licenses/spdx/"
  done

  install -vDm 644 known_spdx_license_identifiers.txt -t "$pkgdir/usr/share/licenses/"
  install -vDm 644 known_spdx_license_exceptions.txt -t "$pkgdir/usr/share/licenses/"

  # NOTE: all below license files and directories are provided for **legacy** compatibility only!
  #       Once all use in relevant downstreams have been removed, they will be deleted.

  local legacy_license_dir="$pkgdir/usr/share/licenses/common"
  install -vDm 644 agpl-3.0.txt "$legacy_license_dir/"AGPL3/license.txt
  ln -s AGPL3 "$legacy_license_dir/"AGPL

  install -vdm 755 "$legacy_license_dir/Apache/"
  ln -s ../../spdx/Apache-2.0.txt "$legacy_license_dir/Apache/license.txt"
  ln -s Apache "$legacy_license_dir/"APACHE

  install -vdm 755 "$legacy_license_dir/Artistic2.0/"
  ln -s ../../spdx/Artistic-2.0.txt "$legacy_license_dir/Artistic2.0/license.txt"

  install -vdm 755 "$legacy_license_dir/Boost/"
  ln -s ../../spdx/BSL-1.0.txt "$legacy_license_dir/Boost/license.txt"

  install -vdm 755 "$legacy_license_dir/CCPL/"
  ln -s ../../spdx/CC-BY-3.0.txt "$legacy_license_dir/CCPL/cc-by-3.0.txt"
  ln -s ../../spdx/CC-BY-NC-3.0.txt "$legacy_license_dir/CCPL/cc-by-nc-3.0.txt"
  ln -s ../../spdx/CC-BY-NC-ND-3.0.txt "$legacy_license_dir/CCPL/cc-by-nc-nd-3.0.txt"
  ln -s ../../spdx/CC-BY-NC-SA-3.0.txt "$legacy_license_dir/CCPL/cc-by-nc-sa-3.0.txt"
  ln -s ../../spdx/CC-BY-ND-3.0.txt "$legacy_license_dir/CCPL/cc-by-nd-3.0.txt"
  ln -s ../../spdx/CC-BY-SA-3.0.txt "$legacy_license_dir/CCPL/cc-by-sa-3.0.txt"

  install -vdm 755 "$legacy_license_dir/CDDL/"
  ln -s ../../spdx/CDDL-1.0.txt "$legacy_license_dir/CDDL/license.txt"

  install -vdm 755 "$legacy_license_dir/CPL/"
  ln -s ../../spdx/CPL-1.0.txt "$legacy_license_dir/CPL/license.txt"

  install -vdm 755 "$legacy_license_dir/EPL/"
  ln -s ../../spdx/EPL-1.0.txt "$legacy_license_dir/EPL/license.txt"

  install -vDm 644 gpl-2.0.txt "$legacy_license_dir/"GPL2/license.txt
  install -vDm 644 gpl-3.0.txt "$legacy_license_dir/"GPL3/license.txt
  ln -s GPL2 "$legacy_license_dir/"GPL

  install -vDm 644 fdl-1.2.txt "$legacy_license_dir/"FDL1.2/license.txt
  install -vDm 644 fdl-1.3.txt "$legacy_license_dir/"FDL1.3/license.txt
  ln -s FDL1.2 "$legacy_license_dir/"FDL

  install -vDm 644 lgpl-2.1.txt "$legacy_license_dir/"LGPL2.1/license.txt
  install -vDm 644 lgpl-3.0.txt "$legacy_license_dir/"LGPL3/license.txt
  ln -s LGPL2.1 "$legacy_license_dir/"LGPL

  install -vdm 755 "$legacy_license_dir/LPPL/"
  ln -s ../../spdx/LPPL-1.3c.txt "$legacy_license_dir/LPPL/license.txt"

  install -vdm 755 "$legacy_license_dir/MPL/"
  ln -s ../../spdx/MPL-1.1.txt "$legacy_license_dir/MPL/license.txt"

  install -vdm 755 "$legacy_license_dir/MPL2/"
  ln -s ../../spdx/MPL-2.0.txt "$legacy_license_dir/MPL2/license.txt"

  install -vdm 755 "$legacy_license_dir/PerlArtistic/"
  ln -s ../../spdx/Artistic-1.0-Perl.txt "$legacy_license_dir/PerlArtistic/license.txt"

  install -vdm 755 "$legacy_license_dir/PHP/"
  ln -s ../../spdx/PHP-3.01.txt "$legacy_license_dir/PHP/license.txt"

  install -vdm 755 "$legacy_license_dir/PSF/"
  ln -s ../../spdx/PSF-2.0.txt "$legacy_license_dir/PSF/license.txt"

  install -vdm 755 "$legacy_license_dir/Ruby/"
  ln -s ../../spdx/Ruby.txt "$legacy_license_dir/Ruby/license.txt"
  ln -s Ruby "$legacy_license_dir/RUBY"

  install -vdm 755 "$legacy_license_dir/Unlicense/"
  ln -s ../../spdx/Unlicense.txt "$legacy_license_dir/Unlicense/license.txt"

  install -vdm 755 "$legacy_license_dir/W3C/"
  ln -s ../../spdx/W3C.txt "$legacy_license_dir/W3C/license.txt"

  install -vdm 755 "$legacy_license_dir/ZPL/"
  ln -s ../../spdx/ZPL-2.1.txt "$legacy_license_dir/ZPL/license.txt"
}

# vim: ts=2 sw=2 et:
