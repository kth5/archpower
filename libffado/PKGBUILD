# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.8
pkgrel=1
pkgdesc="Driver for FireWire audio devices"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://www.ffado.org/"
license=('GPL-2.0-only or GPL-3.0-only')
depends=(
  gcc-libs
  glibc
  glibmm
  hicolor-icon-theme
  libiec61883
  libraw1394
  libxml++
  python-dbus
)
makedepends=(
  alsa-lib
  dbus-c++
  jack
  libconfig
  python-pyqt5
  scons
)
optdepends=('python-pyqt5: ffado-mixer')
provides=(libffado.so)
source=(http://www.ffado.org/files/$pkgname-$pkgver.tgz)
sha512sums=('fbbcd30640c55315f2e65fc6c07afc71a77dd2c9d1b6127ee55cd17e76511ad5f0563135e52397dbf113fb0d5925021a38780f231409bdd920da52e6c1ce3acc')
b2sums=('14b83d55fc9a7b33c888b893c808f8419292fefaa2c2239b4b51c2b84070a584ea72821c8469bb8d965a9a5e6473b6fcfb5e85f4eecd2d424e3167322f8abb81')

prepare() {
  cd $pkgname-$pkgver/$pkgname
  # fix icon name, so it's coherent with XDG desktop file name
  sed -e 's/hi64-apps-ffado/ffado-mixer/g' -i support/xdg/ffado.org-ffadomixer.desktop
  # fix id, so it's coherent with XDG desktop file name
  sed -e 's/ffado.org-ffadomixer.desktop/ffado-mixer.desktop/g' -i support/xdg/ffado-mixer.appdata.xml
}

build() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local scons_options=(
    CUSTOM_ENV=True
    DEBUG=no
    MANDIR=/usr/share/man
    PREFIX=/usr
    PYPKGDIR=$site_packages
    PYTHON_INTERPRETER=/usr/bin/python
    UDEVDIR=/usr/lib/udev/rules.d
  )

  case "${CARCH}" in
    powerpc64*|riscv64) scons_options+=(DETECT_USERSPACE_ENV=false) ;;
  esac

  cd $pkgname-$pkgver/$pkgname
  scons "${scons_options[@]}"
}

package() {
  depends+=(
    alsa-lib libasound.so
    dbus-c++ libdbus-c++-1.so
    libconfig libconfig++.so
  )

  cd $pkgname-$pkgver/$pkgname
  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF=True install
  install -vDm 644 support/xdg/ffado.org-ffadomixer.desktop "$pkgdir/usr/share/applications/ffado-mixer.desktop"
  install -vDm 644 support/xdg/hi64-apps-ffado.png "$pkgdir/usr/share/icons/hicolor/64x64/apps/ffado-mixer.png"
  install -vDm 644 {AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
