# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.4
pkgrel=2
pkgdesc="Driver for FireWire audio devices"
arch=(x86_64 powerpc64le)
url="http://www.ffado.org/"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'glibmm' 'hicolor-icon-theme' 'libavc1394'
'libiec61883' 'libxml++' 'python-dbus')
makedepends=('alsa-lib' 'dbus-c++' 'jack' 'libconfig' 'python-pyqt5' 'scons')
optdepends=('python-pyqt5: ffado-mixer')
provides=('libffado.so')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        ${pkgname}-simple-power-detect.patch)
sha512sums=('065b7f9ce9bf58320c02353b63ed17b3610d0ee8a2de58de27159b2190e39c779094b01ae1123c087387e6077375e646b564b9e7717b192865b2f1f22a3acc80'
            '97df392401f32e346fbe137f14a884abd9ec16b22930d8fff8c76eeda93f3f3ae404ec459d71a02e621937e619dcec377f5e54e8520140584686bec68a9c6c22')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix icon name, so it's coherent with XDG desktop file name
  sed -e 's/hi64-apps-ffado/ffado-mixer/g' \
      -i support/xdg/ffado.org-ffadomixer.desktop
  
  patch -Np1 -i ${srcdir}/${pkgname}-simple-power-detect.patch
}

build() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "${pkgname}-${pkgver}"
  scons PREFIX=/usr \
        DEBUG=no \
        CUSTOM_ENV=True \
        MANDIR='/usr/share/man' \
        PYTHON_INTERPRETER='/usr/bin/python3' \
        PYPKGDIR="/usr/lib/python${python_version}/site-packages" \
        UDEVDIR='/usr/lib/udev/rules.d'
}

package() {
  depends+=('libasound.so' 'libconfig++.so' 'libdbus-c++-1.so' )
  cd "${pkgname}-${pkgver}"
  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # XDG
  install -vDm 644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffado-mixer.desktop"
  install -vDm 644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/icons/hicolor/64x64/apps/ffado-mixer.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
