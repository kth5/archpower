# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.9
pkgrel=1
pkgdesc="Driver for FireWire audio devices"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://www.ffado.org/"
license=('GPL-2.0-only or GPL-3.0-only')
depends=(
  gcc-libs
  glibc
  glibmm
  hicolor-icon-theme
  libiec61883
  libraw1394
  libxml++
  python-dbus
)
makedepends=(
  alsa-lib
  dbus-c++
  jack
  libconfig
  python-pyqt5
  python-setuptools
  scons
)
optdepends=('python-pyqt5: ffado-mixer')
provides=(libffado.so)
source=(
  http://www.ffado.org/files/$pkgname-$pkgver.tgz
)
sha512sums=('0a0c61cf9339ab542c85eaefbe017b7613f716ae0ed08d11a37782dd7ef5815b18256c5d1b9240f077c9f59bbf4b4878019cefbd6cab78140477bcad6a9d3e44')
b2sums=('ec8300066d47d685aa701316efa6831f05d678c509de341d1b74551bf8a3f04b59dcd80d592e2111670212d4056bd1c150cc23dbf43a797a915bf65ae441c85e')

build() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local scons_options=(
    CUSTOM_ENV=True
    DEBUG=no
    MANDIR=/usr/share/man
    PREFIX=/usr
    PYPKGDIR=$site_packages
    PYTHON_INTERPRETER=/usr/bin/python
    UDEVDIR=/usr/lib/udev/rules.d
  )

  case "${CARCH}" in
    powerpc64*|riscv64) scons_options+=(DETECT_USERSPACE_ENV=false) ;;
  esac

  cd $pkgname-$pkgver
  scons "${scons_options[@]}"
}

package() {
  depends+=(
    alsa-lib libasound.so
    dbus-c++ libdbus-c++-1.so
    libconfig libconfig++.so
  )

  cd $pkgname-$pkgver
  scons DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
