# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: KokaKiwi <admin@kokaelkiwi.eu>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgname=nanomsg
pkgver=1.2.2
pkgrel=1
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://nanomsg.org/'
license=(MIT)
depends=(glibc)
makedepends=(cmake)
source=("https://github.com/nanomsg/nanomsg/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('3ffeafa9335245a23a99827e8d389bfce5100610f44ebbe4bfaf47e8192d5939')

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
