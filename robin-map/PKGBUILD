# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=robin-map
pkgver=1.4.0
pkgrel=1
pkgdesc="C++ implementation of a fast hash map and hash set using robin hood hashing"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/Tessil/robin-map"
license=('MIT')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Tessil/robin-map/archive/v${pkgver}.tar.gz")
sha512sums=('d65831ac9d1ae1b26d26386ee06835d788d18529d1cd9132f892091377572b2f9d68aaecfce79956238d327764fea7a144ad2922ced3cbe47cda8734b2df419f')

build() {
  cd $pkgname-$pkgver

  cmake . \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir" make -C build install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
