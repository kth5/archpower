# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Contributor: Byron Clark <byron@theclarkfamily.name>

# This package contains only thrift compiler and C/C++ libraries
# for all the language binding one should use corresponding language-specific package manager:
# CPAN for perl, gem for ruby, pip for python etc... Or better to create arch packages that install
# the binding from the language specific managers python2-thrift, perl-thrift, ruby-thrift, ...

pkgname=thrift
pkgver=0.21.0
pkgrel=1
pkgdesc='Scalable cross-language services framework for IPC/RPC'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://thrift.apache.org'
license=(APACHE)
depends=(gcc-libs openssl)
makedepends=(qt5-base boost libevent emacs-nox)
optdepends=(
  'qt5-base: TQTcpServer (Qt5) support'
)
source=(https://dlcdn.apache.org/thrift/$pkgver/thrift-$pkgver.tar.gz
        fix_missing_target.patch::https://github.com/apache/thrift/commit/0f027530d7d46ae25f3ed6d6ee453ca18ca43eb6.patch)
sha256sums=('9a24f3eba9a4ca493602226c16d8c228037db3b9291c6fc4019bfe3bd39fc67c'
            '4edc5a71ab041bd958cffc95f04f2ff2eb107258d29f471b171af95c3516496a')

prepare() {
  cd thrift-$pkgver

  patch -p1 < ../fix_missing_target.patch
}

build() {
  cd thrift-$pkgver
  case "${CARCH}" in
    powerpc|riscv64) export CXXFLAGS+=' -latomic'
  esac

  ./bootstrap.sh
  ./configure --prefix=/usr \
	      --enable-plugin \
	      --disable-tests \
              --with-cpp \
              --with-c_glib \
              --with-qt5 \
              --with-libevent \
              --with-csharp \
              --without-haskell \
              --without-php \
              --without-ruby \
              --without-python \
              --without-erlang \
              --without-perl \
              --without-java \
              --without-c_sharp \
              --without-d \
              --without-php \
              --without-go \
              --without-lua \
              --without-nodejs
              
  make

  emacs -Q --batch -f batch-byte-compile contrib/thrift.el
}

check() {
  cd thrift-$pkgver
#  make check
}

package() {
  cd thrift-$pkgver
  make DESTDIR="$pkgdir" install

  install -m0644 -D contrib/thrift.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/thrift.vim
  install -m0644 -D contrib/thrift.el "$pkgdir"/usr/share/emacs/site-lisp/thrift.el
  install -m0644 -D contrib/thrift.elc "$pkgdir"/usr/share/emacs/site-lisp/thrift.elc
}
