# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=jemalloc
epoch=1
pkgver=5.2.1
pkgrel=2
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=(x86_64 powerpc64le)
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('glibc')
provides=('libjemalloc.so')
optdepends=('perl: for jeprof')
source=("https://github.com/jemalloc/jemalloc/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('34330e5ce276099e2e8950d9335db5a875689a4c6a56751ef3b1d8c537f887f6')

build() {
  cd $pkgname-$pkgver

  # configure sets -O3 so remove our -O?
  CFLAGS="${CFLAGS/-O?/}"
  CXXFLAGS="${CXXFLAGS/-O?/}"

  # jemalloc supports a runtime page size that's smaller or equal to the build
  # time one, but aborts on a larger one. If not defined, it falls back to the
  # the build system's _SC_PAGESIZE, which in many architectures can vary. Set
  # this to 4K on x86 for performance, and set it to 64K (2^16) on every other
  # architecture, which should be a reasonable maximum. See Debian #843926, as
  # well as upstream #467 for more context.
  if [ "${CARCH}" = "x86_64" ]; then
    extra_configure_flags="--with-lg-page=12"
  else
    extra_configure_flags="--with-lg-page=16"
  fi

  # 2MB (2^21) is the default, but (as of 5.1.0) is overriden by whatever is
  # found in /proc/meminfo. Avoid relying on the build system's configuration and
  # just set it statically here.
  extra_configure_flags="${extra_configure_flags} --with-lg-hugepage=21"

  ./configure \
    --enable-autogen \
    --prefix=/usr ${extra_configure_flags}
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  chmod 644 "$pkgdir/usr/lib/libjemalloc_pic.a"
}
