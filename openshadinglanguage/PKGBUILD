# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openshadinglanguage
pkgver=1.13.6.1
pkgrel=2
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64 powerpc64le)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'imath' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses' 'clang')
makedepends=('boost' 'cmake' 'python' 'llvm' 'ninja')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('9f9ca21ecaa9dbeb9cc79ad3fb904c4acf8067f6aecc16acfb58eafa2873730d245a94e8ce208c2ffa340cf01dba82768c1d55de88b96269c82458e60cc11ede')

build() {
  cd OpenShadingLanguage-$pkgver

  cmake \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=17 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
