# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openshadinglanguage
pkgver=1.14.7.0
pkgrel=5
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64 powerpc64le)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('BSD-3-Clause')
depends=('openimageio' 'imath' 'freetype2' 'libpng' 'libtiff' 'zlib'
         'ncurses' 'clang' 'qt6-base')
makedepends=('cmake' 'python' 'llvm' 'ninja' 'git')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/archive/refs/tags/v${pkgver}.tar.gz
        openshadinglanguage-1.14.6-llvm-21.1.6.patch)
sha512sums=('b92b64d2120980a330ba19d957e2d1b400684f7779ee60ca80241ab3c3f3df46e07fa1fbbfd7810b6f8f3e2e8775cd105137b7e8c4a28efde39656363b977521'
            'c7754164c48be4287a061059e22401e49fed4ac79b3112a37ac7d299bcebdacab8073946deb9052bc38d9df15a4e3ae1b8ae883ed3634960729260a8c74dd203')

prepare() {
  cd OpenShadingLanguage-$pkgver
  # compatibility with the latest llvm
  patch -Np1 < ../openshadinglanguage-1.14.6-llvm-21.1.6.patch
}

build() {
  cd OpenShadingLanguage-$pkgver

  cmake \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=17 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  # Fixup an upstream fucky (See #1)
  rm -r "$pkgdir"/usr/build-scripts
  rm -r "$pkgdir"/usr/cmake

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
