# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Evert Vorster <evorster@gmail.com>
# Contributor: Martin Sandsmark <martin.sandsmark@kde.org>

pkgname=movit
pkgver=1.7.1
pkgrel=2.2
pkgdesc="The modern video toolkit"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://movit.sesse.net/"
license=('GPL')
depends=('fftw' 'libepoxy')
makedepends=('eigen' 'gtest' 'sdl2')
_gtest_ver=1.8.1
source=(https://movit.sesse.net/$pkgname-$pkgver.tar.gz
        googletest-release-${_gtest_ver}.tar.gz::https://github.com/google/googletest/archive/refs/tags/release-${_gtest_ver}.tar.gz
        movit-1.6.3-gcc12.patch)
sha256sums=('b33073b705f0ccb6ac4942cf51151515407b40bb4e9a2dd0228c1c2cb1fbc11a'
            '9bf1fe5182a604b4135edc1a425ae356c9ad15e9b23f9f12a02e80184c3a249c'
            'f1d0fc3c4f9693e4fca73fe9d6c2c29d74a3a7e06eda4de3f11fb888bb3f9855')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/movit-1.6.3-gcc12.patch
}

build() {
  cd $pkgname-$pkgver

  case "${CARCH}" in
    powerpc64)
      export CFLAGS='-O2 -pipe'
      export CXXFLAGS='-O2 -pipe'
    ;;
  esac

  ./configure --prefix=/usr
  make GTEST_DIR=${srcdir}/googletest-release-${_gtest_ver}/googletest -j1
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
