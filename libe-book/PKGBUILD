# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=libe-book
pkgver=0.1.3
pkgrel=8
pkgdesc="Library for import of reflowable e-book formats."
arch=(x86_64 powerpc64le)
url="https://sourceforge.net/projects/libebook/"
license=('LGPL2.1' 'MPL')
depends=('librevenge' 'libxml2' 'liblangtag')
makedepends=('gperf' 'boost' 'cppunit' 'doxygen')
source=(https://sourceforge.net/projects/${pkgname//-/}/files//${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz
	libe-book-icu68.patch)
sha256sums=('7e8d8ff34f27831aca3bc6f9cc532c2f90d2057c778963b884ff3d1e34dfe1f9'
            '58609334797594891773cbb49eacff26afedd5675f99301ae8f0b2fe94288a6c')

prepare() {
  cd "${pkgname}"-${pkgver}
  patch -Np1 -i ${srcdir}/libe-book-icu68.patch
}

build() {
  cd "${pkgname}"-${pkgver}
  ./configure --prefix=/usr --build=${CHOST}
  make
}

check() {
  cd "${pkgname}"-${pkgver}
  make check
}

package() {
  cd "${pkgname}"-${pkgver}
  make DESTDIR="$pkgdir" install
}
