# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Patrick Ziegler p.ziegler96 at gmail dot comd
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Geoffrey Teale <g.teale@elsevier.com>

pkgname=shunit2
pkgver=2.1.8
pkgrel=2
pkgdesc="xUnit based unit testing for Unix shell scripts"
url="https://github.com/kward/shunit2"
arch=('any')
license=('Apache-2.0')
depends=('bash')
makedepends=('git')
source=(git+https://github.com/kward/shunit2.git#tag=v$pkgver)
sha256sums=('ef79bf3342650f446b52576d49b99e1fc53cacb051057c703a5b53060d298dde')

check() {
  cd shunit2
  SHELL=/usr/bin/bash bash ./test_runner -s /bin/bash
}

package() {
  cd shunit2
  install -dm755 "$pkgdir"/usr/share/doc/shunit2/examples
  install -t "$pkgdir"/usr/share/doc/shunit2 ./doc/*
  install -t "$pkgdir"/usr/share/doc/shunit2/examples ./examples/*

  install -Dm755 shunit2 "$pkgdir"/usr/bin/shunit2
}
