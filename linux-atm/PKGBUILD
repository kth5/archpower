# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Surain Mallawa Arachchi <mas@crc32.net>

pkgname=linux-atm
pkgver=2.5.2
pkgrel=7
pkgdesc="Drivers and tools to support ATM networking under Linux."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
depends=('glibc')
url="http://linux-atm.sourceforge.net/"
license=(GPL)
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        man-pages.patch remove-define-hacks.patch kernel-fixes.patch)
options=(!makeflags)
md5sums=('d49499368c3cf15f73a05d9bce8824a8'
         '181390736336cdb615ae32f1ae5acfa6'
         '74275ad95cef3898ef3ec52c21012e45'
         'afed8da786f2a51e09f1ae32084d5a30')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../man-pages.patch
  patch -Np1 -i ../remove-define-hacks.patch
  patch -Np1 -i ../kernel-fixes.patch

  # install firmware in /usr/lib
  sed -i "s#/lib/firmware#/usr/lib/firmware#" src/extra/Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --build=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
