# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=icewm
pkgver=4.0.0
pkgrel=1
pkgdesc="Window Manager designed for speed, usability, and consistency"
url="https://ice-wm.org/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('LGPL-2.0-only')
depends=('alsa-lib' 'imlib2' 'libsm' 'librsvg' 'libsndfile' 'libxcomposite' 'libxcursor' 'libxdamage' 'libxinerama' 'libxrandr')
makedepends=('asciidoctor' 'cmake' 'xorg-mkfontscale')
optdepends=('perl: for icewm-menu-xrandr')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ice-wm/icewm/archive/refs/tags/${pkgver}.tar.gz"
        'static-position.patch')
sha256sums=('9a2eb74449bf4c2b995fd8b85c392dda06f0da684fae34849d8f04466b2e5943'
            '829e5b02ddc7d9679512f5ce2f20990c48276d7e2f400dff40f89b0c62a1c7b3')

prepare() {
	cd "${pkgname}-${pkgver}"

	# Ensure the window is within the screen even if positioned
	# https://github.com/bbidulock/icewm/pull/809
	# https://bbs.archlinux.org/viewtopic.php?id=309422
	patch -Np1 -i ../static-position.patch
}

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-Wno-dev
	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	# Remove the directory tree created for development purposes only (e.g. the playground locale dir)
	# See https://github.com/ice-wm/icewm/commit/5d735df293e61fd94e114ef58e800d757f98d01c
	rm -rf "${pkgdir}/build"
}
