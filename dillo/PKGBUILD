# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=dillo
pkgver=3.2.0
pkgrel=2
pkgdesc="A small, fast graphical web browser built on FLTK"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.dillo.org"
license=('GPL-3.0-or-later')
depends=(
  'fltk'
  'gcc-libs'
  'glibc'
  'libjpeg-turbo'
  'libpng'
  'libwebp'
  'libx11'
  'openssl'
  'perl'
  'zlib'
)
backup=(
  etc/dillo/dillorc
  etc/dillo/domainrc
  etc/dillo/dpidrc
  etc/dillo/keysrc
)
source=("https://github.com/dillo-browser/dillo/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('ed685168397c7b333d25c21208fcf1313ffede59b44f526b7c9b6ee41ebccabd'
            'SKIP')
validpgpkeys=('32E65EC501A1B6FDF8190D293EE6BA977EB2A253') # Rodrigo Arias Mallo <rodarima@gmail.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --enable-cookies --enable-tls
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -vDm644 -t "$pkgdir/usr/share/pixmaps" icons/128x128/dillo.png
  install -vDm644 -t "$pkgdir/usr/share/applications" dillo.desktop
}
