# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=dillo
pkgver=3.1.1
pkgrel=2
pkgdesc="A small, fast graphical web browser built on FLTK"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.dillo.org"
license=('GPL-3.0-or-later')
depends=('fltk' 'libjpeg-turbo' 'libpng' 'openssl' 'perl' 'zlib')
backup=(etc/dillo/{dillorc,domainrc,dpidrc,keysrc})
validpgpkeys=('32E65EC501A1B6FDF8190D293EE6BA977EB2A253') # Rodrigo Arias Mallo <rodarima@gmail.com>
source=(https://github.com/dillo-browser/dillo/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc}
        dillo.desktop)
sha256sums=('4e8c6d1846113a0106860c2b98740331ee7cc3ae7a0e83e240d542a3b66c184a'
            'SKIP'
            'e94ead3a359c2eceb934b07af0fac4864251b9721fa7f4855331daa017b66202')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --enable-cookies --enable-tls
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 ../dillo.desktop "$pkgdir/usr/share/applications/dillo.desktop"
#  install -Dm644 ../ProgramIcon128.png "$pkgdir/usr/share/pixmaps/dillo.png"
}
