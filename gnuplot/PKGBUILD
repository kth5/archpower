# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=gnuplot
pkgname=(gnuplot
         gnuplot-demos)
pkgver=6.0.1
pkgrel=1
pkgdesc='Plotting package which outputs to X11, PostScript, PNG, GIF, and others' 
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='http://www.gnuplot.info'
license=(LicenseRef-Gnuplot)
depends=(cairo
         gcc-libs
         gd
         glib2
         glibc
         gtk3
         libcaca
         libcerf
         libwebp
         libx11
         lua
         pango
         qt6-5compat
         qt6-base
         qt6-svg
         readline
         wxwidgets-common
         wxwidgets-gtk3) 
makedepends=(emacs
             qt6-tools
             texlive-latexextra)
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz
        lua53_compat.patch)
sha256sums=('e85a660c1a2a1808ff24f7e69981ffcbac66a45c9dcf711b65610b26ea71379a'
            'bfd8a61abbf4491c74225cb9fd252619d4fc29751838bcb4c0639ffe05a00695')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 < "$srcdir"/lua53_compat.patch
}

build() {
  cd $pkgbase-$pkgver

  MAKEINFO=/usr/bin/makeinfo \
  ./configure --prefix=/usr \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --with-readline=gnu \
              --with-bitmap-terminals \
              --with-wx-single-threaded \
              --with-caca \
              --with-texdir=/usr/share/texmf/tex/latex/gnuplot
  make pkglibexecdir=/usr/bin
}

package_gnuplot() {
  cd $pkgbase-$pkgver
  make pkglibexecdir=/usr/bin DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install -C demo

  install -Dm644 Copyright -t "$pkgdir"/usr/share/licenses/$pkgname

  rm -f "$pkgdir"/usr/share/texmf-dist/ls-R
}

package_gnuplot-demos() {
  pkgdesc='Demos for gnuplot'
  depends=(gnuplot)

  install -Dm644 $pkgbase-$pkgver/demo/*.{dem,dat} -t "$pkgdir"/usr/share/gnuplot/demos
}
