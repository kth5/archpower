# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libevdev
pkgver=1.13.4
pkgrel=1
pkgdesc="Wrapper library for evdev devices"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://www.freedesktop.org/wiki/Software/libevdev/"
license=('MIT')
depends=('glibc')
makedepends=('python' 'check' 'valgrind' 'meson')
provides=('libevdev.so')
source=(https://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('7afb3f5652982d861623c8d2e61d02dca094d360e20afb7bc30eee6d6d79d8724aae700a83fd96bc8ffe8a026f1fdf347acfd8e31fa5849f4a89a7af5c6a54ab'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

build() {
  arch-meson $pkgname-$pkgver build \
    -D documentation=disabled
  meson compile -C build
}
check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  install -Dm644 $pkgname-$pkgver/COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
