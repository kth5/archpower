# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libevdev
pkgver=1.13.6
pkgrel=1
pkgdesc="Wrapper library for evdev devices"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.freedesktop.org/wiki/Software/libevdev/"
license=('MIT')
depends=('glibc')
makedepends=('python' 'check' 'valgrind' 'meson')
provides=('libevdev.so')
source=(https://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('367081bf587dd63845086714e0b51ec0f9eb107d4c32cc387cf60d3c41ad3cb31a64b1c74e25116b5bc900d6c34e82694697e942bb646f7161b0670b70d1b54d'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

build() {
  arch-meson $pkgname-$pkgver build \
    -D documentation=disabled
  meson compile -C build
}
check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  install -Dm644 $pkgname-$pkgver/COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
