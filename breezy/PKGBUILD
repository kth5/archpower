# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Adam Fontenot <adam.m.fontenot@gmail.com>

pkgname=breezy
pkgver=3.3.9
pkgrel=1
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url=https://www.breezy-vcs.org/
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  python
  python-configobj
  python-dulwich
  python-fastbencode
  python-merge3
  python-patiencediff
  python-tzlocal
  python-yaml
)
makedepends=(
  cython
  git
  python-build
  python-fastimport
  python-gpgme
  python-installer
  python-packaging
  python-paramiko
  python-setuptools
  python-setuptools-gettext
  python-setuptools-rust
  python-wheel
)
checkdepends=(
  python-subunit
  python-testscenarios
  python-testtools
)
optdepends=(
  'python-fastimport: Fastimport support'
  'python-gpgme: PGP support'
  'python-paramiko: access branches over SFTP'
)
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
source=(
  "https://launchpad.net/brz/${pkgver%.*}/$pkgver/+download/breezy-$pkgver.tar.gz"
  "https://launchpad.net/brz/${pkgver%.*}/$pkgver/+download/breezy-$pkgver.tar.gz.asc"
)
sha256sums=('c2588bf217c8a4056987ecf6599f0ad9fb8484285953b2e61905141f43c3d5d8'
            'SKIP')
validpgpkeys=('DC837EE14A7E37347E87061700806F2BD729A457') # Jelmer VernooÄ³ <jelmer@jelmer.uk>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m installer -d tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$PWD/tmp_install/$site_packages"
  export BRZ_PLUGIN_PATH=-site:-user
  "$PWD/tmp_install/usr/bin/brz" selftest \
    --parallel=fork \
    --verbose \
    -Oselftest.timeout=120
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  ln -s /usr/bin/brz "$pkgdir/usr/bin/bzr" # backwards compatibility
}

# vim: ts=2 sw=2 et:
