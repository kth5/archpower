# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adam Fontenot <adam.m.fontenot@gmail.com>

pkgname=breezy
pkgver=3.2.2
pkgrel=2
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=(x86_64 powerpc64le powerpc riscv64)
url=https://www.breezy-vcs.org/
license=(GPL2)
depends=(
  python-configobj
  python-fastbencode
  python-patiencediff
  python-six
)
makedepends=(
  cython
  git
  python-dulwich
  python-fastimport
  python-gpgme
  python-paramiko
  python-setuptools
)
checkdepends=(python-testrepository)
optdepends=(
  'python-dulwich: Git support'
  'python-fastimport: Fastimport support'
  'python-gpgme: PGP support'
  'python-paramiko: access branches over SFTP'
)
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
_tag=7ebf59679d4234f40404b6a565f172a77bef1aea
source=(git+https://github.com/breezy-team/breezy.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd breezy
  sed 's/_script_version = (3, 2, 0)/_script_version = (3, 2, 1)/' -i brz
}

pkgver() {
  cd breezy
  git describe --tags | sed 's/brz-//; s/-/./g'
}

build() {
  cd breezy
  python setup.py build
}

package() {
  cd breezy
  python setup.py install --root="${pkgdir}" --install-data=usr/share --optimize=1 --skip-build
  ln -s brz "${pkgdir}"/usr/bin/bzr # backwards compatibility
}

# vim: ts=2 sw=2 et:
