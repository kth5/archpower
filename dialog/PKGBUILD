# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.3_20260107
pkgrel=1
epoch=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://invisible-island.net/dialog/"
license=(LGPL-2.1-or-later)
depends=(
  glibc
  ncurses
  sh
)
provides=(libdialog.so)
source=(https://invisible-mirror.net/archives/$pkgname/$pkgname-${pkgver/_/-}.tgz{,.asc})
sha512sums=('ef3dd7530c42489a08c2dcb0eb6597de32d22be9a22aad000b4841dc6e4fe4fc8681c0d6b3e8cc55caa8c9027f4f182f60fe3f2caa8cce39fbe50dcbff93875b'
            'SKIP')
b2sums=('6047d566b820da45093cd9ac48d31c50b338ad8e8c408d42347885bd021843daedcdb50a7d85e2c3e61345a2ca848c727882c807f359455592c00755f3ce722c'
        'SKIP')
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # Thomas E. Dickey (self-signed w/o SHA1) <dickey@invisible-island.net>

build() {
  local configure_options=(
    --enable-nls
    --prefix=/usr
    --with-libtool
    --with-ncursesw
  )

  cd $pkgname-${pkgver/_/-}
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -C $pkgname-${pkgver/_/-} -k check
}

package() {
  make DESTDIR="$pkgdir" -C $pkgname-${pkgver/_/-} install
}

# vim:set ts=2 sw=2 et:
