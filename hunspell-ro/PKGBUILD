# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=hunspell-ro
pkgver=3.3.10
pkgrel=8
pkgdesc="Romanian dictionary for Hunspell"
arch=('any')
url="https://sourceforge.net/projects/rospell/"
license=('LGPL' 'GPL' 'MPL')
depends=('hunspell')
makedepends_x86_64=('qt6-webengine')
provides=('hunspell-dictionary')
source=(https://downloads.sourceforge.net/rospell/ro_RO.$pkgver.zip)
sha512sums=('192d53fc087fb96306c46092a2b5cac9c28528f741c686d3b5e80c11c7bfdd16e1e406cfc1c32a99d72e2db01722e539b28c716379f38a3703e6954c8f9f2170')

package() {
  install -dm755 "$pkgdir"/usr/share/hunspell

  install -D -m644 *.aff *.dic -t "$pkgdir"/usr/share/hunspell/

 # the symlinks
  install -dm755 "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

# Install webengine dictionaries
  if [ "${CARCH}" == "x86_64" ]; then
  install -d "$pkgdir"/usr/share/qt{,6}/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    /usr/lib/qt6/qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  ln -rs "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic} "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  done
  fi
}
