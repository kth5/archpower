# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgbase=openbabel
pkgname=(openbabel python-openbabel)
pkgver=3.1.1
pkgrel=12
pkgdesc='A library designed to interconvert between many file formats used in molecular modeling and computational chemistry'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://openbabel.org/'
license=(GPL-2.0-only)
depends=(gcc-libs
         glibc)
makedepends=(boost
             cmake
             coordgen
             eigen
             git
             maeparser
             python-setuptools
             rapidjson
             swig
             wxwidgets-gtk3)
source=(git+https://github.com/openbabel/openbabel#tag=openbabel-${pkgver//./-})
sha256sums=('b045f861b0544856dc5a2f5bd2943a4a8d9efc05ea4f8cf9cd5155751b214e77')

prepare() {
  cd openbabel
  git cherry-pick -n c0570bfe # Fix build with GCC 12
  git cherry-pick -n b75c392b # UB https://github.com/openbabel/openbabel/issues/2223
  sed -e '/SET CMP0042 OLD/d' -i CMakeLists.txt # Fix build with cmake 4
}

build() {
  case "${CARCH}" in
    powerpc64)
      export CFLAGS='-O2 -pipe'
      export CXXFLAGS='-O2 -pipe'
    ;;
  esac

  cmake -B build -S openbabel \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DRUN_SWIG=ON \
    -DPYTHON_BINDINGS=ON \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  cmake --build build

  # To split python bindings
  sed -i '/scripts.cmake_install.cmake/d' build/cmake_install.cmake
}

package_openbabel() {
  depends+=(cairo
            coordgen
            libxml2)
  optdepends=('wxwidgets-gtk3: GUI interface')

  DESTDIR="$pkgdir" cmake --install build
}

package_python-openbabel() {
  depends+=(openbabel
            python)

  DESTDIR="$pkgdir" cmake --install build/scripts
}
