# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=btop
pkgver=1.4.0
pkgrel=5
pkgdesc='A monitor of system resources, bpytop ported to C++'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://github.com/aristocratos/$pkgname"
license=(Apache-2.0)
depends=(gcc-libs
         glibc)
makedepends_x86_64=(rocm-smi-lib)
optdepends_x86_64=('rocm-smi-lib: AMD GPU support')
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('ac0d2371bf69d5136de7e9470c6fb286cbee2e16b4c7a6d2cd48a14796e86650')

build() {
	cd "$_archive"
	make all
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" PREFIX=/usr install
}

