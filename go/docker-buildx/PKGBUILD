# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Joakim Saario <saario.joakim@gmail.com>

pkgname=docker-buildx
pkgver=0.13.1
pkgrel=1
pkgdesc="Docker CLI plugin for extended build capabilities with BuildKit"
arch=(x86_64 powerpc64le riscv64)
url="https://github.com/docker/buildx"
license=("Apache-2.0")
makedepends=('go' 'git')
options=(!lto)
_commit=788433953af10f2a698f5c07611dddce2e08c7a0 # refs/tags/v0.13.1
source=("git+https://github.com/docker/buildx.git#commit=${_commit}")
sha512sums=('SKIP')

pkgver() {
  cd "buildx"
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd "buildx"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'
  export GO111MODULE=on
  _buildx_r=github.com/docker/buildx
  go build -mod=vendor -o docker-buildx -ldflags "-linkmode=external \
    -compressdwarf=false \
    -X $_buildx_r/version.Version=${pkgver} \
    -X $_buildx_r/version.Revision=$(git rev-parse HEAD) \
    -X $_buildx_r/version.Package=$_buildx_r" \
    ./cmd/buildx
}

check() {
  cd "buildx"
  # mkdir cli-plugins
  # cp bin/buildx cli-plugins/docker-buildx
  # export DOCKER_CONFIG="$(PWD)"
  go test ./... || true
}

package() {
  cd "buildx"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 docker-buildx "$pkgdir/usr/lib/docker/cli-plugins/docker-buildx"
}
