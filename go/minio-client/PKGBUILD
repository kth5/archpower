# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Dhananjay Balan <mail@dbalan.in>

pkgname=minio-client
pkgver=2025.04.08
_timever=T15-39-49Z
_pkgver="${pkgver//./-}${_timever//:/-}"
pkgrel=1
pkgdesc='Replacement for ls, cp, mkdir, diff and rsync commands for filesystems and object storage'
arch=(x86_64 powerpc64 powerpc64le)
url='https://minio.io/downloads/#minio-client'
license=('AGPL-3.0-or-later')
makedepends=('go' 'git')
depends=('glibc')
options=(!lto)
install=minio-client.install
source=("${pkgname}"::git+https://github.com/minio/mc.git#tag=RELEASE.${_pkgver})
sha512sums=('88489ae095c89baf70ad2d4a3a5c8bf8ddf3bc7b5303d7db77e920be47183574bfb4f9e7f1146467b1bff47f26dcf7c80feb4a01a5a8f4fe2070d4b3bb5183c6')

build() {
  cd ${pkgname}
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  case "${CARCH}" in
    powerpc64)
      LINKMODE="-linkmode=internal"
      export GOFLAGS="-trimpath -mod=readonly -modcacherw"
    ;;
    *)
      LINKMODE="-linkmode=external"
  esac

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export MC_RELEASE="RELEASE"
  GO_LDFLAGS="\
      ${LINKMODE} \
      -compressdwarf=false \
      $(go run buildscripts/gen-ldflags.go)"

  go build -ldflags "$GO_LDFLAGS" .
}

package() {
  install -Dm755 ${pkgname}/mc "$pkgdir/usr/bin/mcli"
}
# vim:set ts=2 sw=2 et:
