# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Dhananjay Balan <mail@dbalan.in>

pkgname=minio-client
pkgver=2025.03.12
_timever=T17-29-24Z
_pkgver="${pkgver//./-}${_timever//:/-}"
pkgrel=1
pkgdesc='Replacement for ls, cp, mkdir, diff and rsync commands for filesystems and object storage'
arch=(x86_64 powerpc64 powerpc64le riscv64)
url='https://minio.io/downloads/#minio-client'
license=('AGPL-3.0-or-later')
makedepends=('go' 'git')
depends=('glibc')
options=(!lto)
install=minio-client.install
source=("${pkgname}"::git+https://github.com/minio/mc.git#tag=RELEASE.${_pkgver})
sha512sums=('c6814d7a63c9dc6485d86dc76da27c9afc46fa67f5fe19f8a95a78be10b4d2dcf7abcea65a5e21c86d6c1f1cca952dc6e0e06d75a14e1912dc2814ef32bf313e')

build() {
  cd ${pkgname}
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  case "${CARCH}" in
    powerpc64)
      LINKMODE="-linkmode=internal"
      export GOFLAGS="-trimpath -mod=readonly -modcacherw"
    ;;
    *)
      LINKMODE="-linkmode=external"
  esac

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export MC_RELEASE="RELEASE"
  GO_LDFLAGS="\
      ${LINKMODE} \
      -compressdwarf=false \
      $(go run buildscripts/gen-ldflags.go)"

  go build -ldflags "$GO_LDFLAGS" .
}

package() {
  install -Dm755 ${pkgname}/mc "$pkgdir/usr/bin/mcli"
}
# vim:set ts=2 sw=2 et:
