# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Dhananjay Balan <mail@dbalan.in>

pkgname=minio-client
pkgver=2025.01.17
_timever=T23-25-50Z
_pkgver="${pkgver//./-}${_timever//:/-}"
pkgrel=1
pkgdesc='Replacement for ls, cp, mkdir, diff and rsync commands for filesystems and object storage'
arch=(x86_64 powerpc64 powerpc64le riscv64)
url='https://minio.io/downloads/#minio-client'
license=('AGPL-3.0-or-later')
makedepends=('go' 'git')
depends=('glibc')
options=(!lto)
install=minio-client.install
source=(git+https://github.com/minio/mc.git#tag=RELEASE.${_pkgver})
sha512sums=('01982f0fb09e47c2ee16114f8149cfb0570e7335355478c25d85ba34cc851deafbd77b86af854b6cd39f85cfb39b61b30a5ad1edc8c5174925129270bffb8a80')

build() {
  cd mc
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  case "${CARCH}" in
    powerpc64)
      LINKMODE="-linkmode=internal"
      export GOFLAGS="-trimpath -mod=readonly -modcacherw"
    ;;
    *)
      LINKMODE="-linkmode=external"
  esac

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export MC_RELEASE="RELEASE"
  GO_LDFLAGS="\
      ${LINKMODE} \
      -compressdwarf=false \
      $(go run buildscripts/gen-ldflags.go)"

  go build -ldflags "$GO_LDFLAGS" .
}

package() {
  install -Dm755 mc/mc "$pkgdir/usr/bin/mcli"
}
# vim:set ts=2 sw=2 et:
