# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=prometheus-node-exporter
pkgver=1.8.2
pkgrel=2

pkgdesc='Exporter for machine metrics'
url='https://github.com/prometheus/node_exporter'
arch=(x86_64 powerpc64le riscv64)
license=('Apache-2.0')

depends=('glibc')
makedepends=('go' 'git')
options=(!lto)

backup=('etc/conf.d/prometheus-node-exporter')

source=("$pkgname-$pkgver.tar.gz::https://github.com/prometheus/node_exporter/archive/v$pkgver.tar.gz"
        prometheus-node-exporter.conf
        prometheus-node-exporter.service
        prometheus-node-exporter.sysusers)

sha256sums=('f615c70be816550498dd6a505391dbed1a896705eff842628de13a1fa7654e8f'
            'ce93e2b95bfc86a8a046e2f9175408e1cbffa784fd3b65dd141fde70b5bb2585'
            'b1f4d3afde5d062cbb0d750f5c71a3f76b9666a92c3c3cb2b6faa4c8bbe8c9cb'
            'c7fd0b1793dfe7a354a28e978d3c79e7195eaf43376b9eece37e996fe0772c5c')

build() {
  cd node_exporter-$pkgver

  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
      -X github.com/prometheus/common/version.Version=$pkgver \
      -X github.com/prometheus/common/version.Revision=$pkgver \
      -X github.com/prometheus/common/version.Branch=tarball \
      -X github.com/prometheus/common/version.BuildUser=someone@builder \
      -X github.com/prometheus/common/version.BuildDate=$(date -u '+%Y%m%d-%H:%M:%S' --date=@${SOURCE_DATE_EPOCH})" \
    .
}

check() {
  cd node_exporter-$pkgver
  make test
  ./end-to-end-test.sh
}

package () {
  install -Dm644 prometheus-node-exporter.conf "$pkgdir"/etc/conf.d/prometheus-node-exporter
  install -Dm644 prometheus-node-exporter.service "$pkgdir"/usr/lib/systemd/system/prometheus-node-exporter.service
  install -Dm644 prometheus-node-exporter.sysusers "$pkgdir"/usr/lib/sysusers.d/prometheus-node-exporter.conf

  cd node_exporter-$pkgver

  install -Dm755 node_exporter "$pkgdir"/usr/bin/prometheus-node-exporter
}
