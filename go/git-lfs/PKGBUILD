# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=git-lfs
pkgver=3.6.0
pkgrel=1
pkgdesc="Git extension for versioning large files"
arch=(x86_64 aarch64 powerpc64le powerpc64 powerpc riscv64)
url="https://git-lfs.github.com"
license=('MIT')
#FIXME: gcc-go doesnt build >=3.6.0
makedepends=('go' 'ruby-ronn' 'asciidoctor')
makedepends_powerpc64=('go-tools')
makedepends_powerpc64le=('go-tools')
makedepends_riscv64=('go-tools')
makedepends_x86_64=('go-tools')
depends=('git')
source=("git+https://github.com/git-lfs/git-lfs.git#tag=v${pkgver}?signed")
validpgpkeys=('88ACE9B29196305BA9947552F1BA225C0223B187'  # brian m. carlson <sandals@crustytoothpaste.net>
              '86CD3297749375BCF8206715F54FE648088335A9') # Chris Darroch (CODE SIGNING KEY) <chrisd@apache.org>
sha256sums=('SKIP')

build(){
  cd "$pkgname"
  if [ "${CARCH}" == "powerpc64" ]; then
    LINKMODE=internal
  else
    LINKMODE=external
    BUILDMODE='-buildmode=pie'
  fi
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="${BUILDMODE} -trimpath -modcacherw -ldflags=-linkmode=${LINKMODE}"
  go mod vendor
  go generate ./commands
  go build .
  make man
}

check(){
  cd "$pkgname"
  go test -mod=vendor ./...
}

package() {
  cd "$pkgname"
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*.1
  install -Dm644 -t "$pkgdir"/usr/share/man/man5 man/man5/*.5
  ./"${pkgname}" completion bash | install -Dm644 /dev/stdin \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  ./"${pkgname}" completion fish | install -Dm644 /dev/stdin \
    "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
  ./"${pkgname}" completion zsh | install -Dm644 /dev/stdin \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
