# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Morten Linderud <foxboron@arhlinux.org>
# Contributor: SÃ©bastien "Seblu" Luttringer <seblu@arhlinux.org>

pkgname=go-md2man
pkgver=2.0.3
pkgrel=1
pkgdesc='A markdown to manpage generator'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/cpuguy83/go-md2man'
license=('MIT')
makedepends_powerpc=('gcc-go')
makedepends_powerpc64=('gcc-go')
makedepends_powerpc64le=('go')
makedepends_riscv64=('go')
makedepends_x86_64=('go')
source=($pkgname-$pkgver.tar.gz::"https://github.com/cpuguy83/go-md2man/archive/v$pkgver.tar.gz")
sha256sums=('7ca3a04bb4ab83387538235decc42a535097a05d2fb9f2266d0c47b33119501f')

build() {
  cd "$pkgname-$pkgver"
  export GOFLAGS="-buildmode=pie -mod=vendor -trimpath"
  export CGO_LDFLAGS="$LDFLAGS"
  go build -o go-md2man .
  ./go-md2man -in=go-md2man.1.md -out=go-md2man.1
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 go-md2man "$pkgdir/usr/bin/go-md2man"
  install -Dm755 go-md2man.1 "$pkgdir/usr/share/man/man1/go-md2man.1"
  install -Dm755 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
