# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
pkgname=terraform-provider-vault
pkgver=2.24.0
pkgrel=1
pkgdesc="Terraform provider to configure Hashicorp's Vault"
url="https://github.com/mrparkers/terraform-provider-vault"
arch=(x86_64 powerpc64le)
license=("MIT")
makedepends=("go" "git")
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform-provider-vault/archive/v$pkgver.tar.gz"
)
sha512sums=('61c46256925fc88f8802b259f7345956ba1073351cdac6d726e2dea239262fc45491d0de3e363db0d4cc172f1a289ee84dcdd750691fb79137adc6a2eedefec8')

build() {
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd $pkgname-$pkgver
  go build .
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
}
