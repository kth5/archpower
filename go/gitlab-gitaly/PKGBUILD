# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: loqs <bugs-archlinux@entropy-collector.net>

pkgname=gitlab-gitaly
pkgver=17.0.1
pkgrel=1
pkgdesc='Speed up Git access using caching'
arch=(x86_64 powerpc64le)
url='https://gitlab.com/gitlab-org/gitaly'
license=(MIT)
depends=(glibc
         libxml2
         libxslt
         libssh2)
makedepends=(cmake
             git
             go)
options=(!buildflags)
backup=("etc/$pkgname/config.toml")
_archive="gitaly-v$pkgver"
source=("$url/-/archive/v$pkgver/$_archive.tar.gz"
        "$pkgname-configs.patch"
        "$pkgname.service")
sha256sums=('248ec37a3d833c789ad58853f55b5d8afaaca74fe582c13cb14dc228284c9016'
            '3e312014c5e0f4f95c3ab3fc42a184b0ac6d403537508afc4b3ce9dfbaeba8ce'
            'a047c82481ccbaa3b00ba1bb769135934fa4fa939eb6506d09c77f3a7aec08cd')

prepare() {
	cd "$_archive"
	patch -p0 -i ../$pkgname-configs.patch
	# The patched config should contain no references to '/home/git', URL encoded or otherwise
	grep -vqzE 'home.{1,3}git' config.toml.example
}

build() {
	cd "$_archive"
	make V=1 BUILD_TAGS="tracer_static tracer_static_jaeger"
}

package() {
	cd "$_archive"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	mkdir -p "$pkgdir/usr/share/webapps/gitlab-gitaly"
	install -Dm0644 config.toml.example "$pkgdir/etc/$pkgname/config.toml"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm0644 -t "$pkgdir/usr/lib/systemd/system/" ../gitlab-gitaly.service
}
