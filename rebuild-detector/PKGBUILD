# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>

pkgname=rebuild-detector
pkgver=4.4.3
pkgrel=3
pkgdesc="Detects which packages need to be rebuilt"
url="https://github.com/maximbaz/rebuild-detector"
arch=(any)
license=('ISC')
depends=('binutils' 'parallel' 'pacutils' 'pacman-contrib')
makedepends=('marked-man')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
        "${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig")
b2sums=('395edda01bbfd5de2e4be4a7318bf46ff77f36326904c0cd0cbe4e7dfa2f8239c6745c29409a72c31dc78aa11c7809ec45dded1ddf952637fcc3e0934ea90873'
        'SKIP')
validpgpkeys=('56C3E775E72B0C8B1C0C1BD0B5DB77409B11B601')

build() {
	cd "${pkgname}-${pkgver}"
	make docs
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
