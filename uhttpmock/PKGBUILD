# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=uhttpmock
pkgver=0.10.0
pkgrel=2
pkgdesc="HTTP web service mocking project for projects which use libsoup"
url="https://gitlab.freedesktop.org/pwithnall/uhttpmock"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  libsoup3
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  vala
)
source=("git+https://gitlab.freedesktop.org/pwithnall/uhttpmock.git#tag=$pkgver")
b2sums=('19b3047ab71101741cf9f1ad004bc7ac2dbc7755a7aadcd75d551ef9d445ca99cf056663083e2213bd332bf02d4c45bab78172d9b8e11f67c53d44ed72f3f48c')

prepare() {
  cd $pkgname

  # HTTP/2 support - unbreak msgraph testsuite
  git cherry-pick -n 46e86c50044e07c7427233495c0c21adfdb47891
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
