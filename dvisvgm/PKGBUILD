# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=dvisvgm
pkgver=3.4.4
pkgrel=1
pkgdesc='A fast DVI, EPS, and PDF to SVG converter'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url='https://dvisvgm.de/'
license=(GPL-3.0-or-later)
depends=(brotli
         freetype2
         gcc-libs
         ghostscript
         glibc
         potrace
         texlive-bin
         woff2
         xxhash
         zlib)
optdepends=('mupdf-tools: to process PDF files')
makedepends=(autoconf-archive
             git
             python-lxml)
source=(git+https://github.com/mgieseki/dvisvgm#tag=$pkgver)
sha256sums=('d7568e7160186a370f25927506531ae721f5ba5a0309728ce7317c9c53e4c1e7')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
