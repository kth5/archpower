# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=dvisvgm
pkgver=3.1.2
pkgrel=1
pkgdesc='A fast DVI, EPS, and PDF to SVG converter'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://dvisvgm.de/'
license=(GPL3)
depends=(brotli
         freetype2
         gcc-libs
         ghostscript
         glibc
         potrace
         texlive-bin
         woff2
         zlib)
makedepends=(python)
source=(https://github.com/mgieseki/dvisvgm/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('bea783adfe931b7794a0c30d7902b82b0d4d9ad6816dcd8a095a931cd33eacf6')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
