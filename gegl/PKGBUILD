# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=gegl
pkgver=0.4.52
pkgrel=2
pkgdesc='Graph based image processing framework'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.gegl.org/'
license=('GPL-3.0-or-later OR LGPL-3.0-or-later')
depends=('babl' 'cairo' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'jasper' 'json-glib' 'lcms2'
         'lensfun' 'libjpeg-turbo' 'libpng' 'libraw' 'librsvg' 'libspiro' 'libtiff' 'libwebp'
         'openexr' 'pango' 'poppler-glib' 'suitesparse')
depends_powerpc=(luajit)
depends_powerpc64le=(luajit)
depends_riscv64=(luajit)
depends_x86_64=(luajit)
makedepends=('ffmpeg' 'git' 'gobject-introspection' 'libgexiv2' 'meson' 'python-gobject' 'sdl2'
             'vala' 'gi-docgen')
optdepends=('ffmpeg: FFmpeg Frame Loader and FFmpeg Frame Saver plugins'
            'graphviz: for gegl-introspect'
            'sdl2: SDL2 Display plugin')
source=("git+https://gitlab.gnome.org/GNOME/gegl.git#tag=GEGL_${pkgver//./_}"
        gegl-0.4.52-fix-sdl2compat-compilation.patch)
sha256sums=('a59f046433a459f6d0d674dd4cf6f897086cfd359db0cb6519a7abddd140859b'
            'b5c1690ff1be6581d0c320cf8859ff5121f6cdf3871ff313494c0af0a78ed49d')

pkgver() {
  cd "${pkgname}"
  git describe --tags | sed 's/^GEGL_//;s/_$//;s/_/./g;s/-/+/g'
}

prepare() {
  cd "${pkgname}"
  patch -Np1 < ../gegl-0.4.52-fix-sdl2compat-compilation.patch
}

build() {
  mkdir -p build
  cd build

  case "${CARCH}" in
    powerpc64)
      _meson_options=(-Dlua=disabled)
    ;;
  esac

  arch-meson ../"${pkgname}" \
    -Dworkshop=true \
    -Dmrg=disabled \
    -Dmaxflow=disabled ${_meson_options[@]}
  ninja
}

check() {
  cd build
  ninja test || :
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
