# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=c-ares
pkgver=1.34.6
pkgrel=1
pkgdesc="A C library for asynchronous DNS requests"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://c-ares.org/"
_url=https://github.com/c-ares/c-ares
license=(MIT)
depends=(glibc)
makedepends=(
  cmake
  git
)
provides=(libcares.so)
source=(
  git+$_url.git?signed#tag=v$pkgver
)
sha512sums=('31b306dde129efa6c9e063d456a0941517fbf9ed95f3e1442ace41f31f5214cdcebd4dba6113c6de317d4ce91027d06fe13347470bb08d368274068c43a0902f')
b2sums=('0389469a2732385be87c841f95f719e2c20c98d78fe1c68b7588f2241dc3f57aa2bd5bbdd3f8b468fde4fe332179449ad67c5ef0bacd0e9fe210275344be7186')
validpgpkeys=(
  27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2  # Daniel Stenberg <daniel@haxx.se>
  DA7D64E4C82C6294CB73A20E22E3D13B5411B7CA  # Brad House <brad@brad-house.com>
)

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -S $pkgname
    -W no-dev
  )
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname/{AUTHORS,{CONTRIBUTING,README,RELEASE-NOTES}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
