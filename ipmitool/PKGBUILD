# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Florian Dejonckheere <florian@floriandejonckheere.be>
# Contributor: gori

pkgname=ipmitool
pkgver=1.8.19
pkgrel=4
pkgdesc="Command-line interface to IPMI-enabled devices"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://codeberg.org/IPMITool/ipmitool"
license=('BSD')
depends=('openssl')
makedepends=('git')
source=(
  "git+https://codeberg.org/IPMItool/ipmitool.git#tag=IPMITOOL_${pkgver//./_}"
  enterprise-numbers.txt
)
sha256sums=('17dd44f0fb6c4017b5446a3fd33fc2407842eeedb4300ea065032556f1f1d03b'
            'bb440e232a5f15e83f7e0b5d72157ca6b2d2a2aef98203e6fc4cdbff34c1a8dd')

_srcdir_name="ipmitool-IPMITOOL_${pkgver//./_}"

build(){
  cd "${pkgname}"

  sed "s|http://www.iana.org/assignments/enterprise-numbers|file://${srcdir}/enterprise-numbers.txt|" -i Makefile.am

  ./bootstrap
  ./configure --prefix=/usr --sbindir=/usr/bin --with-kerneldir
  make
}

package(){
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
