# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Bert Peters <bertptrs@archlinux.org>
pkgname=simdjson
epoch=1
pkgver=3.13.0
pkgrel=1
pkgdesc="A C++ library to see how fast we can parse JSON with complete validation."
arch=(x86_64 powerpc64le powerpc64)
url="https://github.com/simdjson/simdjson"
license=('Apache-2.0')
depends=(gcc-libs)
# This package should makedepend on cxxopts to build the tools, but that doesn't
# work regardless so we skip it.
makedepends=(
  cmake
)

source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/simdjson/simdjson/archive/v$pkgver.tar.gz"
)
sha256sums=('07a1bb3587aac18fd6a10a83fe4ab09f1100ab39f0cb73baea1317826b9f9e0d')

build() {
  case "${CARCH}" in
    powerpc64)
      export CFLAGS='-O2 -pipe'
      export CXXFLAGS='-O2 -pipe'
      _cmake_options=(
        -DSIMDJSON_IMPLEMENTATION:STRING='fallback'
        -DSIMDJSON_AVX512_ALLOWED='Off'
      )
    ;;
    powerpc64le)
      _cmake_options=(
        -DSIMDJSON_IMPLEMENTATION:STRING='ppc64;fallback'
        -DSIMDJSON_AVX512_ALLOWED='Off'
      )
    ;;
  esac

  cmake -B build -S "$pkgname-$pkgver" \
    -DBUILD_SHARED_LIBS='On' \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCPM_USE_LOCAL_PACKAGES='On' \
    -DSIMDJSON_DEVELOPER_MODE='On' \
    -DSIMDJSON_ALLOW_DOWNLOADS='Off' \
    -Wno-dev ${_cmake_options[@]}

  cmake --build build
}

check() {
  local excluded_tests=""
  local ctest_flags=(
    --test-dir build
    # show the stdout and stderr when the test fails
    --output-on-failure
    # execute tests in parallel
    --parallel $(nproc)
    # exclude problematic tests
    --exclude-regex "$excluded_tests"
  )

  ctest "${ctest_flags[@]}"
}

package() {
  DESTDIR="$pkgdir/" cmake --install build
}

# vim: tabstop=2 shiftwidth=2 expandtab:
