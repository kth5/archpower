# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=nilfs-utils
pkgver=2.2.12
pkgrel=1
pkgdesc="A log-structured file system supporting continuous snapshotting (userspace utils)"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://nilfs.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
backup=('etc/nilfs_cleanerd.conf')
depends=('util-linux')
source=(http://nilfs.sourceforge.net/download/$pkgname-$pkgver.tar.bz2)
sha256sums=('fc9a8520b68928d43fffa465c3b3845cc9b7d4973f4fbde4b3c7ecf25ce52d09')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  sed -i -e 's#root_sbindir=/sbin#root_sbindir=/usr/bin#' configure
  ./configure --sbindir=/usr/bin --with-libmount
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" sbindir=/usr/bin install LDCONFIG=/bin/true
}
