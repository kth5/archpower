# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview3
pkgver=3.24.11+r28+g73e57b57
pkgrel=2
epoch=1
pkgdesc='Text widget with syntax highlighting for GNOME'
url='https://wiki.gnome.org/Projects/GtkSourceView'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later)
depends=(gtk3 libxml2)
makedepends=(autoconf-archive git glib2-devel gobject-introspection gtk-doc intltool vala yelp-tools)
checkdepends=(xorg-server-xvfb)
_commit=73e57b5787ac60776c57032e05a4cc32207f9cf6 # gnome-3-24
source=("git+https://gitlab.gnome.org/GNOME/gtksourceview.git#commit=$_commit")
b2sums=('1d5d7508b084a0066b1ae4b9e693ac402d7ff6df3b3387c74ae5f318e3cc35e5f97d70f9fec76ab47079745cf309d67a1e57b143009ec258d24c2141b72ebe5d')

pkgver() {
  cd gtksourceview
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtksourceview
  find . -name Makefile.am -type f -exec sed -i '/@CODE_COVERAGE_RULES@/d' {} +
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtksourceview
  export CFLAGS+=' -Werror -Wno-incompatible-pointer-types -w'
  ./configure \
    --disable-glade-catalog \
    --disable-gtk-doc \
    --disable-static \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-Werror
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd gtksourceview
  dbus-run-session xvfb-run -s '-nolisten local' make check
}

package() {
  make -C gtksourceview DESTDIR="$pkgdir" install
}
