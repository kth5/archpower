# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=sox
pkgver=14.6.0.4
pkgrel=1
pkgdesc="The Swiss Army knife of sound processing tools"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
url="https://codeberg.org/sox_ng/sox_ng/"
license=('GPL-2.0-only')
depends=(
  'ffmpeg'
  'file'
  'gcc-libs'
  'gsm'
  'libid3tag'
  'libltdl'
  'libpng'
  'zlib'
)
makedepends=(
  'alsa-lib'
  'autoconf-archive'
  'flac'
  'git'
  'lame'
  'libao'
  'libmad'
  'libpulse'
  'libsndfile'
  'libvorbis'
  'opencore-amr'
  'opusfile'
  'twolame'
  'wavpack'
)
checkdepends=('time')
optdepends=(
  'alsa-lib: alsa plugin'
  'flac: flac plugin'
  'lame: mp3 plugin'
  'libao: ao plugin'
  'libmad: mp3 plugin'
  'libpulse: pulse plugin'
  'libsndfile: caf, fap, mat4, mat5, paf, pvf, sd2, sndfile, w64 and xi plugins'
  'libvorbis: vorbis plugin'
  'opencore-amr: amr_nb and amr_wb plugins'
  'opusfile: opus plugin'
  'twolame: mp3 plugin'
  'wavpack: wavpack plugin'
)
provides=('libsox.so')
source=("git+https://codeberg.org/sox_ng/sox_ng.git#tag=sox_ng-${pkgver}")
sha256sums=('1af4af4aecc6505a877887e9e53f88b59379cb45066fea6f3649372f95f7c01a')

prepare() {
  cd sox_ng
  autoreconf -vfi
}

build() {
  local configure_flags=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --enable-replace
    --with-distro="Arch Linux"
    --with-ffmpeg
  )

  cd sox_ng
  ./configure "${configure_flags[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make all README
}

check() (
  cd sox_ng/src

  export DESTDIR="${PWD}/tmp"
  mkdir -p "${DESTDIR}"

  export LD_LIBRARY_PATH="${DESTDIR}/usr/lib${LD_LIBRARY_PATH:+:$LD_LIBRARY_PATH}"

  make sox_sample_test
  make install
  make installcheck
)

package() {
  cd sox_ng
  make DESTDIR="${pkgdir}" install
  install -vDm644 AUTHORS ChangeLog README \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
