# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Contributor: philacs

pkgname=libvisio
pkgver=0.1.7
pkgrel=10
pkgdesc='Library providing ability to interpret and import visio diagrams'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://wiki.documentfoundation.org/DLP/Libraries/libvisio"
license=('LGPL')
depends=('libxml2' 'icu' 'librevenge'
         'gcc-libs' 'glibc')
makedepends=('boost' 'gperf' 'doxygen' 'cppunit')
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz
        a24c7ff.diff)
sha256sums=('8faf8df870cb27b09a787a1959d6c646faa44d0d8ab151883df408b7166bea4c'
            'ea4d93d328a46a93dd64a822663933bce818db3de8106f28856dc78033fd1a19')

prepare() {
  cd $pkgname-$pkgver
  # fix tests with libxml2 2.12
  # https://gerrit.libreoffice.org/c/libvisio/+/160542
  patch -Np1 -i ../a24c7ff.diff
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --build=${CHOST}
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
