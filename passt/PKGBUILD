# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=passt
pkgver=2024_09_06.6b38f07
pkgrel=1
pkgdesc="Plug A Simple Socket Transport"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://passt.top/passt/about/"
license=(
  BSD-3-Clause
  GPL-2.0-or-later
)
depends=(glibc)
optdepends=(
  'sh: for demo script'
)
source=(https://passt.top/$pkgname/snapshot/$pkgname-$pkgver.tar.zst)
sha512sums=('aece933b8ea9cf63a1b3d2316da0ff943d52d6fe9be54da8a347b3495b17b5922de609c67bca2dc4e292c193b37fea2670097d64d1dfb92267c75265df3b8115')
b2sums=('f95760dba7fc53d4dafac9f6b2a89211fdc30c17e8691d1fa4f9500a15d1fe7957750c91aee39d0f40819fa04ddc58325cfce4bd373b908bc1d73e7b3be00b50')

build() {
  make VERSION="$pkgver" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
