# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: SÃ©bastien Luttringer

pkgname=ipset
pkgver=7.24
pkgrel=1
pkgdesc='Administration tool for IP sets'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://netfilter.org/projects/ipset/'
license=('GPL-2.0-only')
makedepends=('git')
depends=('glibc' 'libmnl')
backup=("etc/${pkgname}.conf")
validpgpkeys=('B4DAC1EB5D49B76C8D7EFE6ED6A8E7CF924196F6') # Jozsef Kadlecsik <kadlec@blackhole.kfki.hu>
source=("git+https://git.netfilter.org/ipset.git#tag=v${pkgver}?signed"
        "${pkgname}.service")
sha256sums=('6ac4541c9f38bd8b2028ec8e3586b9246339d3c4c1b4a981b3b918559f178e49'
            '5a0e326a80fd5ab2d9545faa1189b647cc866c9568ab6a9dd0833ba5863f39ce')

prepare() {
  cd "${pkgname}"

  ./autogen.sh
}

build() {
  cd "${pkgname}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-kmod=no
  make
}

package() {
  pushd "${pkgname}"
  make DESTDIR="${pkgdir}" install
  # install doc
  install -dm 0755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 0644 README UPGRADE ChangeLog "${pkgdir}/usr/share/doc/${pkgname}"
  # install pkgconfig file
  install -Dm644 lib/libipset.pc "${pkgdir}/usr/lib/pkgconfig/libipset.pc"
  popd
  # systemd
  install -Dm 0644 "${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  # default config file
  install -Dm 0644 /dev/null "${pkgdir}/etc/${pkgname}.conf"
  # bash completion
  install -Dm 0644 ipset/utils/ipset_bash_completion/ipset \
    "${pkgdir}/usr/share/bash-completion/completions/ipset"
}

# vim:set ts=2 sw=2 et:
