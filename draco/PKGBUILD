# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Otreblan <otreblain@gmail.com>

pkgname=draco
pkgver=1.5.7
pkgrel=1
pkgdesc="A library for compressing and decompressing 3D geometric meshes and point clouds"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/google/draco"
license=('Apache-2.0')
makedepends=('cmake' 'ninja')
depends=('gcc-libs' 'glibc')
options=('staticlibs')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('bf6b105b79223eab2b86795363dfe5e5356050006a96521477973aba8f036fe1')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    CXXFLAGS+=' -ffat-lto-objects'
    cmake \
        -Bbuild \
        -GNinja \
        -DCMAKE_UNITY_BUILD=ON \
        -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON

    ninja -C build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    DESTDIR="$pkgdir" ninja -C build install
}
