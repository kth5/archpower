# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: tobias <tobias@justdreams.de>
# Modified by: eliott <eliott@cactuswax.net>
# Original by Benjamin Andresen, klapmuetz [at] gmail [dot] com

pkgname=fcgi
pkgver=2.4.4
pkgrel=1
depends=('gcc-libs')
pkgdesc='FASTCgi (fcgi) is a language independent, high performant extension to CGI'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('custom')
options=('!makeflags')
url='https://github.com/FastCGI-Archives/fcgi2'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/FastCGI-Archives/fcgi2/archive/${pkgver}.tar.gz")
sha256sums=('c0e0d9cc7d1e456d7278c974e2826f593ef5ca555783eba81e7e9c1a07ae0ecc')

prepare() {
  cd "${srcdir}/fcgi2-${pkgver}"

  ./autogen.sh
}

build() {
  cd "${srcdir}/fcgi2-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/fcgi2-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
