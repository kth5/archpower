# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=s2n-tls
pkgver=1.7.0
pkgrel=1
pkgdesc='A C99 implementation of the TLS/SSL protocols that is designed to be simple, small, fast, and with security as a priority'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/aws/s2n-tls'
license=(Apache-2.0)
provides=(s2n) # upstream renamed the project from s2n to s2n-tls
conflicts=(s2n)
replaces=(s2n)
depends=(
  gcc-libs
  glibc
  openssl
)
makedepends=(cmake)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('8db841a201320702eca400acf73ab31a1359da48faa8c77fdd9be7d0ecb2b30276e8f9b881db8e32366310a20d9cf177d22324c4a4b58f3670cc7a2e1c5017d7')

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  case "${CARCH}" in
    powerpc*) return 0 ;;
  esac

  cd $pkgname-$pkgver
  ctest --test-dir build --output-on-failure \
    -E s2n_override_openssl_random_test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
}
