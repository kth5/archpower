# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: tobias <tobias@arhlinux.org>

pkgname=exiv2
pkgver=0.28.7
pkgrel=1
pkgdesc="Exif, Iptc and XMP metadata manipulation library and tools"
url="https://exiv2.org"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
depends=(brotli libbrotlidec.so
         curl
         expat libexpat.so
         gcc-libs # libgcc_s.so libstdc++.so
         gettext
         glibc # libc.so libm.so
         libcurl.so
         libinih libINIReader.so
         zlib libz.so)
makedepends=('git' 'cmake' 'gtest' 'ninja')
checkdepends=('python')
provides=('libexiv2.so')
source=(git+https://github.com/Exiv2/exiv2#tag=v$pkgver)
sha512sums=('9b95fe28c04c836e3c862945f48afad4b2b5df2d6dbe63f837f8e1b1270478d1e5b778d8962ba196d1372fb425863ba42052c123ea8e447b522c4cfe502672f2')
b2sums=('6fe939fb95aca6ee070faa28a152f38987ef6c34f13dab6efc3db81320286699c3490e04353b04b27214e2e734a7ebf929bed39da62e67971ee9480938cdee53')

build() {
  cd ${pkgname}
  cmake -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DEXIV2_BUILD_SAMPLES=ON \
    -DEXIV2_BUILD_UNIT_TESTS=ON \
    -DEXIV2_ENABLE_VIDEO=ON \
    -DEXIV2_ENABLE_NLS=ON \
    -DEXIV2_ENABLE_XMP=ON \
    -DEXIV2_ENABLE_CURL=ON \
    -DEXIV2_ENABLE_WEBREADY=ON \
    -DEXIV2_ENABLE_BMFF=ON
  ninja -C build
}

check() {
  case "${CARCH}" in
    powerpc) return 0 ;;
  esac

  cd ${pkgname}
  LD_LIBRARY_PATH="$PWD"/build/lib \
  ninja -C build test
}

package() {
  cd ${pkgname}
  # remove samples instal which are only needed for unit tests
  sed '/samples\/cmake_install.cmake/d' -i build/cmake_install.cmake
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
