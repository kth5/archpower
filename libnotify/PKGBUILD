# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libnotify
pkgname=(
  libnotify
  libnotify-docs
)
pkgver=0.8.4
pkgrel=1
pkgdesc="Library for sending desktop notifications"
url="https://gitlab.gnome.org/GNOME/libnotify"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
)
makedepends=(
  docbook-xsl
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  gtk3
  meson
  xmlto
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('77851478ea96ddd38a89120777768240f5f12dabdcb48c2ceb803cd1501a415b2a35154a4bad35cc81ae6466cd763bb92b38187e81cb945fb9113d9161f7b265')
validpgpkeys=(
  98FF7532A4639119B8FB4F15E520923A38618D6C # Logan Rathbone (GNOME) <larathbone@gnome.org>
  D4C501DA48EB797A081750939449C2F50996635F # Marco Trevisan (Trevi√±o) <mail@3v1n0.net>
)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libnotify() {
  provides=(libnotify.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libnotify-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
