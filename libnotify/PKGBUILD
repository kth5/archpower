# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libnotify
pkgname=(
  libnotify
  libnotify-docs
)
pkgver=0.8.6
pkgrel=1
pkgdesc="Library for sending desktop notifications"
url="https://gitlab.gnome.org/GNOME/libnotify"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=(LGPL-2.1-or-later)
depends=(
  gdk-pixbuf2
  glib2
  glibc
)
makedepends=(
  docbook-xsl
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  xmlto
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('254c112fe783d2e470989ace2535c0bbb3485f577cf1ed2439a2c0cb0b6dad61a6e64aa3f365d0c87975f5c4337571c511724c19971a689bc1b3f84008e2d827')
validpgpkeys=(
  98FF7532A4639119B8FB4F15E520923A38618D6C # Logan Rathbone (GNOME) <larathbone@gnome.org>
  D4C501DA48EB797A081750939449C2F50996635F # Marco Trevisan (Trevi√±o) <mail@3v1n0.net>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    # Tests need user interaction
    -D tests=false
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

package_libnotify() {
  provides=(libnotify.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libnotify-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
