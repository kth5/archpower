# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>
# Contributor: Denis Zawada <deno@rootnode.net>

pkgname=gupnp-av
pkgver=0.14.1
pkgrel=1
pkgdesc="Library to ease handling and implementation of UPnP A/V profiles"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64 powerpc64le powerpc riscv64)
license=(LGPL)
depends=(glib2 libxml2)
makedepends=(gobject-introspection gtk-doc vala meson git)
options=(debug)
_commit=86f148ebb920a8113190fd3e6cae636c9fdd3ca7  # tags/gupnp-av-0.14.1^0
source=("git+https://gitlab.gnome.org/GNOME/gupnp-av.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gupnp-av
  git describe --tags | sed 's/^gupnp-av-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gupnp-av
}

build() {
  arch-meson gupnp-av build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object}-2.0.so)
  provides+=(libgupnp-av-1.0.so)

  meson install -C build --destdir "$pkgdir"
}
