# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=nasm
pkgver=3.01
pkgrel=1
pkgdesc='80x86 assembler designed for portability and modularity'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://ww.nasm.us'
license=(BSD-2-Clause)
depends=(glibc zlib)
makedepends=(
  perl-font-ttf
  perl-sort-versions
  fontconfig
  ttf-roboto
  ttf-roboto-mono
  ghostscript
  xmlto
  asciidoc
  diffutils
)
source=(https://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha512sums=('ca9550ad3239cfaa9546a908e8de86e11a7c7dd9d6006f42d272d0b025b601cdb205e60b8b3929f31123ef450dd0f0403e1300328cadf0ffdfd459f443944482')
b2sums=('182d908e949e8212ec29bfaa4b747312f7b57dd686195c9b44701f0055ced4b40555624e27d1e1183b356095da76ba1ac8b2a5aa84b2e0d2277106de8ca915d1')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/${pkgname} \
    --htmldir=/usr/share/doc/${pkgname}/html

  make all doc
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 -C test golden test diff
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install_doc
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/nasm
}

# vim: ts=2 sw=2 et:
