# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgbase=liblo
pkgname=(liblo liblo-docs)
pkgver=0.32
pkgrel=1
epoch=1
pkgdesc="A lightweight OSC (Open Sound Control) implementation"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://liblo.sourceforge.net/"
license=(LGPL-2.1-or-later)
makedepends=(doxygen)
source=(https://github.com/radarsat1/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('cd591b5eb53b96b1970bb35d4f16cc94476fcc07d7b13291e05c5ff63ab47f9e2906a27f765646ab4ec708fbcc11a89a3b5d485687db969900ffd0721d63e5e4')
b2sums=('ea1c60d9094c094d53974aaa9a86e77dfc29a09a0ab4b36f035cdd2617fc47a2f5df5f5cc6e9bc4cf3bc086176bbb7459054f150b8dbbb4af2c8225576aff7f1')

prepare() {
  cd $pkgname-$pkgver
  # fixing bug in autotools setup:
  # https://github.com/radarsat1/liblo/issues/96
  ln -svf README.md README
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_liblo() {
  depends=(glibc)
  optdepends=(
    'liblo-docs: for documentation and examples'
  )
  provides=(liblo.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/doc/man/man3/*.3 -t "$pkgdir/usr/share/man/man3"
}

package_liblo-docs() {
  pkgdesc+=" - documentation"

  install -vDm 644 $pkgbase-$pkgver/examples/*.{cpp,c} -t "$pkgdir/usr/share/$pkgbase/examples/"
  install -vDm 644 $pkgbase-$pkgver/doc/html/* -t "$pkgdir/usr/share/doc/$pkgbase/html/"
  install -vDm 644 $pkgbase-$pkgver/doc/latex/* -t "$pkgdir/usr/share/doc/$pkgbase/latex/"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgbase/"
}
# vim:set ts=2 sw=2 et:
