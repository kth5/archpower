# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgbase=liblo
pkgname=(liblo liblo-docs)
pkgver=0.34
pkgrel=1
epoch=1
pkgdesc="A lightweight OSC (Open Sound Control) implementation"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="http://liblo.sourceforge.net/"
license=(LGPL-2.1-or-later)
makedepends=(doxygen)
source=(https://github.com/radarsat1/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('3757675f908f6bb7be3414c2708c4958fd1dd92f55d22f394902b51a27230524ff9dd6500f85229a53d1383b71e3bc09c74c011c1b6b988ebd777283c58b7227')
b2sums=('46916aefd303f90677535af116092fbbece768d6653e47a70519c364e38a5350d3729752fbccf4dfe0c11f211e4267884c59592c050f90ae2da84d575a52b3eb')

prepare() {
  cd $pkgname-$pkgver
  # fixing bug in autotools setup:
  # https://github.com/radarsat1/liblo/issues/96
  ln -svf README.md README
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_liblo() {
  depends=(glibc)
  optdepends=(
    'liblo-docs: for documentation and examples'
  )
  provides=(liblo.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/doc/man/man3/*.3 -t "$pkgdir/usr/share/man/man3"
}

package_liblo-docs() {
  pkgdesc+=" - documentation"

  install -vDm 644 $pkgbase-$pkgver/examples/*.{cpp,c} -t "$pkgdir/usr/share/$pkgbase/examples/"
  install -vDm 644 $pkgbase-$pkgver/doc/html/* -t "$pkgdir/usr/share/doc/$pkgbase/html/"
  install -vDm 644 $pkgbase-$pkgver/doc/latex/* -t "$pkgdir/usr/share/doc/$pkgbase/latex/"
  install -vDm 644 $pkgbase-$pkgver/{AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgbase/"
}
# vim:set ts=2 sw=2 et:
