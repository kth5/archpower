# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname="openbsd-netcat"
pkgver=1.225_1
pkgrel=1
pkgdesc="TCP/IP swiss army knife. OpenBSD variant."
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://salsa.debian.org/debian/netcat-openbsd"
license=('BSD')
depends=('libbsd')
makedepends=('gcc' 'make')
provides=('netcat')
conflicts=('gnu-netcat')
source=("https://salsa.debian.org/debian/netcat-openbsd/-/archive/debian/${pkgver/_/-}/netcat-openbsd-debian-${pkgver/_/-}.tar.gz")
sha512sums=('82cf79976434997556f17bca5dfc404f5ab75caf27f8a1ac88590833890be1394d3eba7cb684f28ba387f048cee10ab300683b49679e6d14b4cba7110245f1ff')
b2sums=('7d2198ec1103a57c891d597de2def0fab63d5f053bf8ab9d7ce494ffecb49fb51f34da7b082d43606929260de65a098bc538361fb81ede20f15c44016095f9d4')

build() {
  cd netcat-openbsd-debian-${pkgver/_/-}
  for i in `cat debian/patches/series`; do
	echo "** patch $i" 1>&2
	cat "debian/patches/$i"
  done | patch -p1
  make \
	CFLAGS="$CFLAGS -DDEBIAN_VERSION=\"\\\"${pkgrel}\\\"\" -I/usr/include/libbsd" \
	LDFLAGS="$LDFLAGS -lbsd"
}

package() {
  cd netcat-openbsd-debian-${pkgver/_/-}
  install -Dm0755 nc "$pkgdir"/usr/bin/nc
  ln -s nc "$pkgdir"/usr/bin/netcat
  install -Dm0644 nc.1 "$pkgdir"/usr/share/man/man1/nc.1
  install -dm0755 "$pkgdir"/usr/share/licenses/$pkgname/
  head -n28 netcat.c | tail -n+2 >"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
