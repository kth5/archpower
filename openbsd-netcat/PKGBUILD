# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname="openbsd-netcat"
pkgver=1.234_1
_ver="${pkgver/_/-}"
pkgrel=1
pkgdesc="TCP/IP swiss army knife. OpenBSD variant."
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://salsa.debian.org/debian/netcat-openbsd"
license=(
  'BSD-2-Clause'
  'BSD-3-Clause'
)
depends=(
  'glibc'
  'libbsd'
)
provides=('netcat')
conflicts=('gnu-netcat')
source=("https://salsa.debian.org/debian/netcat-openbsd/-/archive/debian/${pkgver/_/-}/netcat-openbsd-debian-${pkgver/_/-}.tar.gz")
sha256sums=('54586351a3eb8b11fb95752d7efb42aabd06e7e30e7946cb6369d341e258db2f')
b2sums=('b3ef5c1d480048de321f8adff9d2354dfe200ec78b33b774ced89b3130544570f800ab2f51bf6e6c7c45a18031bc4d31172a995110d49d93c8fc5e6adfba5e0f')
_dir=netcat-openbsd-debian-$_ver

prepare() {
  # extract dedicated licenses
  sed -n '40,59p' $_dir/debian/copyright > BSD-2-Clause.txt
  sed -n '62,83p' $_dir/debian/copyright > BSD-3-Clause.txt
  sha256sum -c - <<EOF
9087578f9861610e1a5cd2fc5962bb8209619fff01949e9e3475f82dd375a37c  BSD-2-Clause.txt
66af2c844063fb3a149fcf64ffa364b3388674f72a8454a38e0098942553b065  BSD-3-Clause.txt
EOF

  cd $_dir
  while read patch; do
    printf "Applying %s...\n" "$patch"
    patch -Np1 -i debian/patches/$patch
  done < debian/patches/series
}

build() {
  cd $_dir
  make \
  CFLAGS="$CFLAGS -DDEBIAN_VERSION=\"\\\"${pkgver/_/-}\\\"\" -I/usr/include/libbsd" \
  LDFLAGS="$LDFLAGS -lbsd"
}

package() {
  cd $_dir
  install -Dm0755 nc "$pkgdir"/usr/bin/nc
  ln -s nc "$pkgdir"/usr/bin/netcat
  ln -s nc "$pkgdir"/usr/bin/nc.openbsd
  install -Dm0644 nc.1 "$pkgdir"/usr/share/man/man1/nc.1
  install -vDm 644 ../*.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
