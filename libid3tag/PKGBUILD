# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Chistian Heusel <gromit@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=libid3tag
pkgver=0.16.3
pkgrel=2
pkgdesc="ID3 tag manipulation library"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://codeberg.org/tenacityteam/libid3tag"
license=("GPL-2.0-or-later")
depends=(glibc zlib)
makedepends=(cmake)
provides=(libid3tag.so)
source=(
  $pkgname-$pkgver.tar.gz::https://codeberg.org/tenacityteam/libid3tag/archive/$pkgver.tar.gz
)
sha512sums=('d49bc637899e4251ed66b5b56aa4c910dcdecd6b03ed197866d74175fc4eadff40f40f336606b23e2505b0e11834c4212a1314feeeaa2c0e9713051fdb56cb45')
b2sums=('58a4ce211aebef4b2f1f0e1a7214e2804e71d2cea805d3c275812978fb702148c5b435c289f9e83fd81fa1970bf83b7b8e772ed24843bcae7519fff455b50c15')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
