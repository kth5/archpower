# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=cbindgen
pkgver=0.26.0
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(MPL2)
depends=(gcc-libs)
makedepends=(
  cargo
  git
)
checkdepends=(
  cmake
  cython0
  python
)
_commit=703b53c06f9fe2dbc0193d67626558cfa84a0f62  # tags/0.26.0
source=("git+$url#commit=$_commit")
b2sums=('SKIP')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

pkgver() {
  cd cbindgen
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd cbindgen
  case "${CARCH}" in
    riscv64) CARCH=riscv64gc ;;
  esac

  cargo fetch --locked --target ${CARCH}-unknown-linux-gnu
}

build() {
  cd cbindgen
  cargo build --release --frozen --all-targets
}

check() {
  cd cbindgen
  # Tests need nightly features
  RUSTC_BOOTSTRAP=1 cargo test --release --frozen
}

package() {
  cd cbindgen
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 CHANGES \
    {README,docs,internals}.md
}

# vim:set sw=2 sts=-1 et
