# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=obconf-qt
pkgver=0.16.5
pkgrel=1
pkgdesc="Openbox configuration tool. Qt port of ObConf"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
# See discussions about the license at https://github.com/lxqt/obconf-qt/issues/210
license=("GPL-2.0-or-later")
depends=("hicolor-icon-theme" "openbox" "qt6-base")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('50f3ac70ad3e31239eedfc498fac0bbc14371c13f270a52b2a90295655fa17024d2f081f56e1b9e5ca4f172835348a4813691f19ff41ab81648a7a74a2f19398'
            'SKIP')
validpgpkeys=(
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # https://github.com/tsujan.gpg
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
