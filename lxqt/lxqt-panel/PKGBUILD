# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=lxqt-panel
pkgver=2.0.0
pkgrel=1
pkgdesc="The LXQt desktop panel"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
license=("LGPL2.1")
depends=(
	"libdbusmenu-lxqt" "libxcomposite" "lxqt-menu-data" "libxtst"
	"lxqt-globalkeys" "solid" "libxcb" "libQt6Xdg.so" "layer-shell-qt"
)
optdepends=(
	"libpulse: Volume control plugin"
	"alsa-lib: Volume control plugin"
	"libstatgrab: CPU monitor and Network monitor plugins"
	"libsysstat: System Statistics plugin"
	"lm_sensors: Sensors plugin"
)
makedepends=(
	"lxqt-build-tools" "liblxqt" "libpulse" "libstatgrab" "libsysstat"
	"lm_sensors" "libxdamage" "alsa-lib"
)
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
	"2053.patch"
)
sha256sums=('ac8b3e01edc40c841874475739442e1287c2855d5521739119d1ec22b03e4207'
            'SKIP'
            'f9ef9ab0a0572e8ad2123f2434fdeb355b7a6e55086a5d0070ee34a84113355c')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 -i ../2053.patch
}

build() {
	cmake -B build -S "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make -C build
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
