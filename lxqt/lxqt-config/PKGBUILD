# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-config
pkgver=1.4.0
pkgrel=2
pkgdesc="LXQt system configuration."
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
# Upstream licenses are messy and need clarifications... https://github.com/lxqt/lxqt-config/issues/966
license=("LGPL2.1")
depends=("glibc" "gcc-libs"
         "qt5-base" "qt5-svg" "qt5-x11extras" "libkscreen5"
         "libQt5Xdg.so" "liblxqt" "lxqt-themes" "lxqt-menu-data"
         "libx11" "libxcb" "libxcursor" "libxfixes" "libxi" "xf86-input-libinput"
         "libudev.so" "zlib")
makedepends=("lxqt-build-tools")
optdepends=(
	'xsettingsd: help GTK applications apply themes on the fly'
)
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=('e60d5c3f00588fb373b48fa0c65f7a164500738467359472409a29b9db11c84b'
            'SKIP')
validpgpkeys=(
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # https://github.com/tsujan.gpg
)

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
