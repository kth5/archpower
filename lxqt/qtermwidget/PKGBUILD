# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=qtermwidget
pkgver=2.0.0
pkgrel=1
pkgdesc="A terminal widget for Qt, used by QTerminal"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://github.com/lxqt/$pkgname"
# Yep, it's messy when you're talking about licenses
# https://github.com/lxqt/qtermwidget/blob/2.0.0/README.md?plain=1#L13-L42
license=('LGPL-2.0-or-later' 'BSD-3-Clause')
depends=("qt6-base")
makedepends=("lxqt-build-tools"
            "python-pyqt6" "pyqt-builder" "sip" "python-installer")
optdepends=(
  'python: PyQt bindings'
)
source=(
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=('33ff5c84b93f7a46e5255ae6a25e073093e0f80cc7dacf0233e01b643663f700'
            'SKIP')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

build() {
  mkdir -p build
  cd build

  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/$pkgname-$pkgver"/pyqt
  CXXFLAGS="-I$PWD/../lib -I$PWD/../../build/lib" LDFLAGS="-L$PWD/../../build" sip-wheel --verbose --qmake /usr/bin/qmake6
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname-$pkgver/README.md" -t "$pkgdir"/usr/share/licenses/$pkgname

  cd "$srcdir/$pkgname-$pkgver"/pyqt
  python -m installer --destdir="$pkgdir" *.whl
}
