# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=libfm-qt
pkgver=2.2.0
pkgrel=4
pkgdesc="Core library of PCManFM-Qt (Qt binding for libfm)"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/lxqt/$pkgname"
# https://github.com/lxqt/libfm-qt/blob/2.0.1/README.md?plain=1#L8-L10
license=("LGPL-2.1-or-later")
depends=("menu-cache" "libexif" "qt6-base" "lxqt-menu-data")
makedepends=("lxqt-build-tools")
optdepends=(
  "org.freedesktop.secrets: allow to remember mount passwords"
)
provides=("libfm-qt6.so")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
         qt-6.10.patch
)
sha512sums=('9b5c8ba0cfd71861396cb8ba2a6d3cb13850f631b949dd0885abe9d829d703c02da3a72564819f74db5d28fb430d8a8599cfee717fc757533f6a37f0d8de74ad'
            'SKIP'
            'd9ae041fc56006edffe055e64c0169efcf777a925bfa3ebf3d04aa9f33d6f558c32c449a44f773cae6b0da6a293d11416dcf30c5bb0b9fbe261389a261dd2e4e')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)
# Work-around https://gitlab.kitware.com/cmake/cmake/-/issues/17122
# Relevant CMake codes: https://github.com/lxqt/libfm-qt/blob/1.2.1/src/CMakeLists.txt#L179
options=('!emptydirs')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../qt-6.10.patch
}

build() {
	cmake -B build -S "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=None
	make -C build
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
