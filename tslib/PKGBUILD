# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=tslib
pkgver=1.24
pkgrel=1
pkgdesc="Touchscreen Access Library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/kergoth/tslib"
license=(LGPL-2.1-only)
depends=(glibc)
makedepends=(git)
source=(git+https://github.com/kergoth/tslib#tag=$pkgver?signed)
sha256sums=('28f8be6333243dae15ef097ac0a16509003794aef038577b800f2fd2a0782387')
validpgpkeys=(F2082B880F9E423934686E3F500398DF5AB387D3) # Martin Kepplinger <martink@posteo.de>

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
