# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libassuan
pkgver=2.5.7
pkgrel=1
pkgdesc='IPC library used by some GnuPG related software'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.gnupg.org/related_software/libassuan/"
license=(
  FSFULLR
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  glibc
  libgpg-error
  sh
)
provides=(libassuan.so)
source=(https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('ca33bd0325bbebccb63b6a84cc0aa5c85b25c6275a68df83aeb3f3729b2cd38220198a941c3479bd461f16b7ddb6b558c0664697ca3153c7fb430544303d773f'
            'SKIP')
b2sums=('67cb01b7ed9be13a52a37db439666bb8eb709cc63204fe250ff57854459aa8b36655479e887b285fe9ad799c49e66f176f971ab3a871e114cfb217a1f2968d16'
        'SKIP')
validpgpkeys=(
  6DAA6E64A76D2840571B4902528897B826403ADA  # "Werner Koch (dist signing 2020)"
  AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD  # Niibe Yutaka (GnuPG Release Key)
)

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,README,ChangeLog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
