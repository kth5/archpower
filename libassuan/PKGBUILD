# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libassuan
pkgver=3.0.1
pkgrel=1.1
pkgdesc='IPC library used by some GnuPG related software'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.gnupg.org/related_software/libassuan/"
license=(
  FSFULLR
  GPL-2.0-or-later
  LGPL-2.1-or-later
)
depends=(
  glibc
  libgpg-error
  sh
)
provides=(libassuan.so)
source=(https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('6914a02c20053bae0fc4c29c5c40655f1cec711983d57fa85e46df34e90b10e33d31256dd50ae7c7faa8d8d750a529bf9072da0cda3bdd77ebfedbc0e26e5e16'
            'SKIP')
b2sums=('5d6a7e4e9ce1b196bc7d126786716d95896de6145b8d753e2289bd36cbc96efa216360ac127fadb7be2d577e7fa203c8c8695d4aa11afcca6ed5397dd40209cb'
        'SKIP')
validpgpkeys=(
  6DAA6E64A76D2840571B4902528897B826403ADA  # "Werner Koch (dist signing 2020)"
  AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD  # Niibe Yutaka (GnuPG Release Key)
)

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,README,ChangeLog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
