# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.6.0
pkgrel=3
pkgdesc="A spatial database extender for PostgreSQL"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://postgis.net/"
license=(GPL-2.0-or-later)
depends=(
  bash
  gcc-libs
  gdal
  geos
  glibc
  json-c
  libxml2
  pcre2
  perl
  postgresql
  postgresql-libs
  proj
  protobuf-c
)
makedepends=(
  clang
  llvm
)
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8caffef4b457ed70d5328bf4e5a21f9306b06c271662e03e1a65d30090e5f25f')
b2sums=('77c41c21e7cf97c116f292b6ff4b886f9d94c63d72a7b320947d02742514634c5cec04f30df7d28072c98b90ca09cb5648abc2b7f804d84343f0c1898c4cfbbf')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  # WiiU Espresso mcpu doesn't work with clang
  if [[ "${CFLAGS}" == *"mcpu=espresso"* ]]; then
    export CFLAGS="${CFLAGS/-mcpu=espresso/}"
    export CXXFLAGS="${CXXFLAGS/-mcpu=espresso/}"
  fi

  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
