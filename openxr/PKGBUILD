# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=openxr
pkgver=1.1.54
pkgrel=1
pkgdesc='An open standard for virtual reality and augmented reality platforms and devices'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.khronos.org/openxr/'
depends=('jsoncpp' 'libx11' 'vulkan-icd-loader' 'libglvnd' 'libxrandr' 'libxxf86vm')
makedepends=('cmake' 'vulkan-headers' 'python' 'ninja' 'wayland' 'mesa')
license=('Apache-2.0')
source=("https://github.com/KhronosGroup/OpenXR-SDK-Source/releases/download/release-${pkgver}/OpenXR-SDK-Source-release-${pkgver}.tar.gz"{,.asc}
        openxr-ppc32.patch)
validpgpkeys=(45207B2B1E53E1F2755FF63CC5A2D593A61DBC9D)
sha512sums=('be99658ff3d27bd8674dceb8b5d50bfbbfaf2f3667a8c38d6afb0cdb2701881c945a4c423ce68e4591cc468a9a499803ffb05a9a610f52a1c47fca97d73e13d8'
            'SKIP'
            '59a875d1bfe0c84e63981ceaf71a9db8ecd05b537d70674b19668cdec0575d09d382940e611a75d1a0de4a649bd342e2d62d88a3209fd321e6fec17553698389')

prepare() {
  cd OpenXR-SDK-Source-release-${pkgver}

  patch -Np1 -i ${srcdir}/openxr-ppc32.patch
}

build() {
  cd OpenXR-SDK-Source-release-${pkgver}

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=ON \
    -DPRESENTATION_BACKEND=xlib \
    -DDYNAMIC_LOADER=ON
  ninja -C build
}

package() {
  cd OpenXR-SDK-Source-release-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
}
