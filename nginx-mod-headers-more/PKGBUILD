# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-headers-more
pkgver=0.38
pkgrel=1

_modname="${pkgname#nginx-mod-}"

pkgdesc="Nginx module to set and clear input and output headers"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
makedepends=('nginx' 'nginx-src')
url="https://github.com/openresty/headers-more-nginx-module"
license=('BSD-2-Clause')

source=(
  https://github.com/openresty/$_modname-nginx-module/archive/v$pkgver/$_modname-$pkgver.tar.gz
)
sha256sums=('febf7271c0c3de69adbd02c1e98ee43e91a60eeb6b27abfb77b5b206fda5215a')

prepare() {
  mkdir -p build
  cd build
  ln -sf /usr/src/nginx/auto
  ln -sf /usr/src/nginx/src
}

build() {
  cd build
  /usr/src/nginx/configure \
    --with-ld-opt="$LDFLAGS" \
    --with-compat \
    --add-dynamic-module=../$_modname-nginx-module-$pkgver
  make modules
}

package() {
  if [[ "$BUILDTOOL" == devtools ]]; then
    local nginx_dep="nginx=$(nginx -v 2>&1 | sed 's|.*/||')"
    depends+=($nginx_dep)
  else
    depends+=(nginx)
  fi

  cd build/objs
  for mod in *.so; do
    install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done

  install -Dm644 "$srcdir"/$_modname-nginx-module-$pkgver/README.markdown \
                 "$pkgdir"/usr/share/licenses/$pkgname/README.markdown
}
