# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer:

pkgname=verdict
pkgver=1.4.2
pkgrel=1
pkgdesc='Compute quality functions of 2 and 3-dimensional regions'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://github.com/sandialabs/verdict'
license=(BSD)
makedepends=(cmake)
depends=(glibc)
source=(https://github.com/sandialabs/verdict/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('225c8c5318f4b02e7215cefa61b5dc3f99e05147ad3fefe6ee5a3ee5b828964b')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DVERDICT_ENABLE_TESTING=OFF \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package () {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
