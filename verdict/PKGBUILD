# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=verdict
pkgver=1.4.4
pkgrel=1
pkgdesc='Compute quality functions of 2 and 3-dimensional regions'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/sandialabs/verdict'
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(git cmake)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('45213ecf7f73cfcd343dac74a46114ea4b2770d925c648318f30cf76b55d3be6635a92f28ce415827565af55268059db4e9fca47b88f1cb3302bc3dbf528f813')
b2sums=('7f9592e12c1cc7f1a85729f7c7ccad4a01eb59b0b7173ecfcbc19a44f9db0c586677ba7472be5fadcb197600f94718aade0c172336890d85d3f45d6e8228455e')

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -W no-dev
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D BUILD_SHARED_LIBS=ON
    -D VERDICT_ENABLE_TESTING=OFF
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

package () {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
