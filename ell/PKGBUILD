# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=ell
pkgver=0.79
pkgrel=1
pkgdesc="Embedded Linux library"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://git.kernel.org/pub/scm/libs/ell/ell.git/"
license=('LGPL-2.1-or-later')
depends=('gcc-libs' 'glibc')
checkdepends=('dbus' 'procps-ng')
# https://mirrors.edge.kernel.org/pub/linux/libs/ell/sha256sums.asc
source=(https://mirrors.edge.kernel.org/pub/linux/libs/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('28dabdb1966fd2e47363c44e041de507e9420411b846947efa01b4cb0cda0d6b'
            'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659') # "Marcel Holtmann <marcel@holtmann.org>"

build() {
  cd "$pkgname-$pkgver"
 ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
