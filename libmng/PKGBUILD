# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libmng
pkgver=2.0.3
pkgrel=4
pkgdesc="A collection of routines used to create and manipulate MNG format graphics files"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://www.libmng.com/'
license=(LicenseRef-libmng)
depends=(glibc
         lcms2
         libjpeg-turbo
         zlib)
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('4a462fdd48d4bc82c1d7a21106c8a18b62f8cc0042454323058e6da0dbb57dd3'
            'SKIP')
validpgpkeys=('8048643BA2C840F4F92A195FF54984BFA16C640F')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --build=${CHOST} \
    --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
