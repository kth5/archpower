# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=librevenge
pkgver=0.0.4
pkgrel=4.1
pkgdesc="library for REVerses ENGineered formats filters"
arch=(x86_64 powerpc64le powerpc riscv64)
url="https://sf.net/p/libwpd/librevenge/"
license=('MPL')
depends=('boost-libs')
makedepends=('boost' 'doxygen' 'cppunit')
source=(https://sourceforge.net/projects/libwpd/files/librevenge/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz)
sha1sums=('533dd83a668d3efc0095ad82718e34662395635d')

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --disable-werror --build=${CHOST}
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd $pkgname-$pkgver
	make check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir/" install
}
