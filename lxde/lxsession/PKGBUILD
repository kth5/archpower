# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgbase=lxsession
pkgname=(lxsession lxsession-gtk3)
pkgver=0.5.6
pkgrel=1
epoch=1
pkgdesc='Lightweight X11 session manager'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://lxde.org/"
license=('GPL2')
depends=('gtk2' 'gtk3' 'polkit' 'which')
makedepends=('git' 'intltool' 'docbook-xsl' 'vala')
conflicts=('lxpolkit')
source=(git+https://github.com/lxde/lxsession.git#tag=${pkgver})
sha256sums=('552d9331d30c0349b28699ec51de201b23586b209ffb5ac6e32372117dc44557')

prepare() {
  cd $pkgbase
  sh autogen.sh
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib --enable-gtk3
  make
}

package_lxsession() {
  groups=('lxde')
  depends=('gtk2' 'polkit')
  replaces=('lxpolkit')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxsession-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3' 'polkit')
  conflicts+=('lxsession')

  cd gtk3
  make DESTDIR="$pkgdir" install

  # Ignore package by AppStream to avoid duplicated IDs
  echo 'X-AppStream-Ignore=true' >> "$pkgdir/usr/share/applications/lxsession-default-apps.desktop"
  echo 'X-AppStream-Ignore=true' >> "$pkgdir/usr/share/applications/lxsession-edit.desktop"
}
