# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxsession
pkgver=0.5.6
pkgrel=2
epoch=1
pkgdesc='Lightweight X11 session manager'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/lxde/lxsession'
license=(GPL-2.0-or-later)
depends=(
  bash
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libx11
  polkit
)
makedepends=(
  git
  intltool
  vala
)
conflicts=(
  lxpolkit
  lxsession-gtk3
)
replaces=(
  lxpolkit
  lxsession-gtk3
)
groups=(lxde)
source=("git+https://github.com/lxde/lxsession.git#tag=$pkgver")
b2sums=(3242d2c28baf4767a028b737912bb422a5a7bdc391c8fdf85ad6c62959e135a194ee364a87f5c3381a4aaaab47eb98819d4b59bf531c352697d82bd4b4e566fa)

prepare() {
  cd $pkgname

  # https://github.com/lxde/lxsession/pull/43
  sed -i 's/which /command -v /' lxlock/lxlock

  # https://github.com/lxde/lxsession/pull/44
  sed -i 's/^NotShowIn=.*/OnlyShowIn=LXDE;/' data/lxpolkit.desktop.in.in

  mkdir m4
  autoreconf -fi
}

build() {
  cd $pkgname
   ./configure \
     --prefix=/usr \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --libexecdir=/usr/lib \
     --enable-gtk3
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
