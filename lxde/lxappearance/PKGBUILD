# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=lxappearance
pkgname=(lxappearance lxappearance-gtk3)
pkgver=0.6.4
pkgrel=1
pkgdesc='Feature-rich GTK+ theme switcher of the LXDE Desktop'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
url='https://wiki.lxde.org/en/LXAppearance'
depends=('gtk2' 'gtk3')
makedepends=('git' 'intltool' 'dbus-glib')
source=(git+https://github.com/lxde/lxappearance.git#tag=${pkgver})
sha256sums=('94e0a5b3e362c3b0323df3c6a672f5546eca6acd896dbe9708e16042df10fc24')

prepare() {
  cd $pkgbase
  autoreconf -fiv
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr --enable-dbus
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --enable-dbus --enable-gtk3
  make
}

package_lxappearance() {
  groups=('lxde')
  depends=('gtk2')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxappearance-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3')
  conflicts=('lxappearance')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
