# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=lxappearance
pkgver=0.6.4
pkgrel=2
pkgdesc='Feature-rich GTK theme switcher of the LXDE Desktop'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/lxde/lxappearance'
license=(GPL-2.0-or-later)
depends=(
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libx11
)
makedepends=(
  git
  intltool
)
groups=(lxde)
conflicts=(lxappearance-gtk3)
replaces=(lxappearance-gtk3)
source=(
  "git+https://github.com/lxde/lxappearance.git#tag=$pkgver"
  lxappearance-theme-name.patch
  lxappearance-builtin-themes.patch
  lxappearance-transient-dialog.patch
)
b2sums=(
  6fa7d9f2193c2a2dbfa1e91f75541f4a2facc6f052aacf9d9ad7d703b3f638c7bc13d4d24d945241b714c21898ff12f661ac078eb752ed17611bc03a1bb4bef7
  161ded535844ccf9c116c4197c174416c338fe2837c984b89bba52f1c7813778d5ccf7df0889ec62355199fa2fe667bef5fddbdebb40cb11c62da3ec17432791
  940ea3c526872b9b123746ebe2faa43339ffe60f9d5e0a81883c4abb2fb06d3c7536d9bf58a85cea3378ec20e0b82249d665333991a6dd823e2c0955ac4c3d40
  49da625e9c18b42bb5d0e36b4bae8250ac657765ae529d0a761cf4243b4fdc5671015999bbd0ed028722c6fc1566ecbcc4f43210bcb27dc577d581eee899e317
)

prepare() {
  cd $pkgname

  # https://github.com/lxde/lxappearance/pull/15
  git apply -3 ../lxappearance-theme-name.patch

  # https://github.com/lxde/lxappearance/pull/16
  git apply -3 ../lxappearance-builtin-themes.patch

  # https://github.com/lxde/lxappearance/pull/17
  git apply -3 ../lxappearance-transient-dialog.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-dbus \
    --enable-gtk3
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
