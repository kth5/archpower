# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=menu-cache
pkgver=1.1.1
pkgrel=2
pkgdesc='Caching mechanism for freedesktop.org compliant menus'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/lxde/menu-cache'
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  libfm-extra
)
makedepends=(
  git
  gtk-doc
)
source=("git+https://github.com/lxde/menu-cache.git#tag=$pkgver")
b2sums=(bf9b2f53b4bd519b25536f4167c764a39ddd851de203c062b7bb2bd2cb5a01f8df6e99987dd856107e0e4aca2ce82eef4892c8a893cf87dfb7c08e1d5bba049a)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
