# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=menu-cache
pkgver=1.1.1
pkgrel=1
pkgdesc='Caching mechanism for freedesktop.org compliant menus'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
url='https://lxde.org/'
depends=('libfm-extra')
makedepends=('gtk-doc')
source=(https://github.com/lxde/menu-cache/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e8af90467df271c3c8700c840ca470ca2915699c6f213c502a87d74608748f08')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --enable-gtk-doc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
