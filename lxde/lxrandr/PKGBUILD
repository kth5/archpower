# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=lxrandr
pkgname=(lxrandr lxrandr-gtk3)
pkgver=0.3.3
pkgrel=1
pkgdesc="Monitor configuration tool (part of LXDE)"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
url="https://lxde.org/"
depends=('gtk2' 'gtk3' 'xorg-xrandr')
makedepends=('git' 'intltool')
source=("git+https://github.com/lxde/lxrandr.git#tag=${pkgver}")
sha256sums=('86647b70c29fcb4aa6cded36529aced6f0cf4f2bb2d89c9b19e2b61948b1220f')

prepare() {
  cd $pkgbase
  autoreconf -fiv
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package_lxrandr() {
  groups=('lxde')
  depends=('gtk2' 'xorg-xrandr')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxrandr-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3' 'xorg-xrandr')
  conflicts=('lxrandr')

  cd gtk3
  make DESTDIR="$pkgdir" install

  # Ignore package by AppStream to avoid duplicated IDs
  echo 'X-AppStream-Ignore=true' >> "$pkgdir/usr/share/applications/$pkgbase.desktop"
}
