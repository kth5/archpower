# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxmenu-data
pkgver=0.1.6
pkgrel=2
pkgdesc='Freedesktop.org desktop menus for LXDE'
arch=(any)
url='https://github.com/lxde/lxmenu-data'
license=(LGPL-2.1-or-later)
makedepends=(
  git
  intltool
)
source=("git+https://github.com/lxde/lxmenu-data.git#tag=$pkgver")
b2sums=(7612b6d8212ab33542176f14f6b94e0a33523543c7553db8fbd75b3b602bedbd93a6f02740f611806b39c13e6a4a9135527d9d97a6b93adcb21fdda9682b69ed)

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
