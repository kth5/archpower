# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=lxtask
pkgname=(lxtask lxtask-gtk3)
pkgver=0.1.12
pkgrel=1
pkgdesc='Task manager of the LXDE Desktop'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
url='https://lxde.org/'
depends=('gtk2' 'gtk3')
makedepends=('git' 'intltool')
source=(git+https://github.com/lxde/lxtask.git#tag=${pkgver})
sha256sums=('43f8cc6619407111b430ade797a03fcae73b47455feeb5c5388b122814d62b15')

prepare() {
  cd $pkgbase
  autoreconf -vif
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package_lxtask() {
  groups=('lxde')
  depends=('gtk2')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxtask-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3')
  conflicts=('lxtask')

  cd gtk3
  make DESTDIR="$pkgdir" install

  # Ignore package by AppStream to avoid duplicated IDs
  echo 'X-AppStream-Ignore=true' >> "$pkgdir/usr/share/applications/$pkgbase.desktop"
}
