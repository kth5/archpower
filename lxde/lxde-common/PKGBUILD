# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxde-common
pkgver=0.99.3
pkgrel=2
pkgdesc='Common files of the LXDE Desktop'
arch=(any)
url='https://github.com/lxde/lxde-common'
license=(GPL-2.0-or-later)
groups=(lxde)
depends=(bash)
makedepends=(
  git
  intltool
)
backup=(
  etc/xdg/lxpanel/LXDE/config
  etc/xdg/lxpanel/LXDE/panels/panel
  etc/xdg/lxsession/LXDE/autostart
  etc/xdg/lxsession/LXDE/desktop.conf
  etc/xdg/openbox/LXDE/menu.xml
  etc/xdg/openbox/LXDE/rc.xml
  etc/xdg/pcmanfm/LXDE/pcmanfm.conf
)
source=(
  "git+https://github.com/lxde/lxde-common.git#tag=$pkgver"
  dbus-update-environment.patch
  default-config.patch
)
b2sums=(
  2a242d11f931e57d0355716f2883bc79893de6412746bd929b211bd53aa07a88aa014f149e988ae9fc3187b0aedc47f6b597511ba663be7f33689e6c64ee2194
  8aceb3a1aba8ca686e6cf2650049c809485ca0c9591cd65e3a0d971f3cb6c87bcc78531f592e60eb73f9620046827c88fba1670556f54730ebd9bb69a6f2da6e
  9ff14cbdcfcc875b441abe8b9047bb1634a98b41ff46f30a5d776d02b29933902808e13d396d794d0e81611d3d8479260671d00fed48330dfbb4bb4012df2218
)

prepare() {
  cd $pkgname

  # Fix dbus activation
  patch -Np1 -i ../dbus-update-environment.patch

  # Set Adwaita as default GTK theme
  patch -Np1 -i ../default-config.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
