# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Filipp "Scorp" Andjelo <scorp@mailueberfall.de>

pkgbase=lxinput
pkgname=(lxinput lxinput-gtk3)
pkgver=0.3.6
pkgrel=1
pkgdesc="Small program to configure keyboard and mouse for LXDE"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://lxde.org/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'gtk3')
makedepends=('git' 'intltool')
source=(git+https://github.com/lxde/lxinput.git#tag=${pkgver})
sha256sums=('526fda829d408e476a08133a7cd0737704241c56e645f8988e49cc4ff39ea997')

prepare() {
  cd $pkgbase
  autoreconf -fiv
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase gtk2
  cd gtk2
  ./configure --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase gtk3
  cd gtk3
  ./configure --prefix=/usr --enable-gtk3
  make
}

package_lxinput() {
  groups=('lxde')
  depends=('gtk2')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxinput-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3')
  conflicts=('lxinput')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
