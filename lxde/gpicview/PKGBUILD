# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=gpicview
pkgver=0.3.1
pkgrel=3
pkgdesc='Lightweight image viewer'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/lxde/gpicview'
license=(GPL-2.0-or-later)
groups=(lxde)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libjpeg-turbo
  libx11
)
makedepends=(
  git
  intltool
)
replaces=(gpicview-gtk3)
source=(
  "git+https://github.com/lxde/gpicview.git#tag=$pkgver"
  gpicview.appdata.xml
  gpicview-viewing-mode.patch
  gpicview-state-event.patch
  gpicview-fix-memory-leak.patch
)
b2sums=(
  fd0334db05d48bd9184b0c182ad7d4b2d21952d6110e4b0696279a1a4fff7b66cb19f2f80003b9b247f2bcb8f102c16f46e1bd9011ead6ecba00d0dbcaefebb2
  11f27dc014c1749ff48c6f4e6d73855b4d40bd9e31418fa9b738e109d78a6ae2b34498a20c0830d35f480bbd919fd91fcfd17e5b470a5a0d6ff1328679957656
  1f040b8e79c37a6471d3a6380c8860b061a6d4c8641b9afb8ed7f7890a257c87fa738368524628ac81aced1c7cc377ea9b6f7acbfd111d8e2c2fb30d5635544e
  6023331be53f1bf15f577b9583f08ed257a2c35daf60a6728ee5019409edf0b3c6571100d19c2ba86d17cb0c879dc57c31e37d8179345ee44949daa767e7ed42
  1a038b0fdce81a809d2e8c0109190cf7b7e41701c1c8097488e7289b90f4627e44424f1036a1ff270b0845d3ec9412db7c9a2a01dc5a2a737ca0b4f30b1bd8d6
)

prepare() {
  cd $pkgname

  # https://github.com/lxde/gpicview/pull/14
  git apply -3 ../gpicview-viewing-mode.patch

  # https://github.com/lxde/gpicview/pull/15
  git apply -3 ../gpicview-state-event.patch

  # https://github.com/lxde/gpicview/pull/16
  git apply -3 ../gpicview-fix-memory-leak.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk3
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
