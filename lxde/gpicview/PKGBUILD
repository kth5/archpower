# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=gpicview
pkgver=0.3.1
pkgrel=1
pkgdesc='Lightweight image viewer'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.lxde.org/'
license=('GPL2')
groups=('lxde')
depends=('gtk3')
makedepends=('git' 'intltool')
replaces=('gpicview-gtk3')
source=("git+https://github.com/lxde/gpicview.git#tag=${pkgver}"
        'gpicview.appdata.xml')
sha256sums=('e56e90600bb2b0151269be015998c4e2cbc764b54640a6347ebb219c8e57f8d4'
            'dab79b2be536005044cc5edcc4f47e9f407a5fcf3126110a1ef2ab65c873dbf7')

prepare() {
  cd $pkgname

  # Apply only one main category
  sed -i '/^Categories=/ s/Utility;//' gpicview.desktop.in

  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk3 --build=${CHOST}
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
