# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=lxhotkey
pkgname=(lxhotkey lxhotkey-gtk3)
pkgver=0.1.2
pkgrel=1
pkgdesc='Keyboard shortcuts configurator (part of LXDE)'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
url='https://lxde.org/'
depends=('gtk2' 'gtk3' 'libfm')
makedepends=('git' 'intltool')
source=(git+https://github.com/lxde/lxhotkey.git#tag=${pkgver})
sha256sums=('0b5a0ed9174c72c1e305bcf770f4d9e64191c336064c3682ff6b8a63ea67ece6')

prepare() {
  cd $pkgbase
  autoreconf -fiv
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr --with-gtk=2
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --with-gtk=3
  make
}

package_lxhotkey() {
  groups=('lxde')
  depends=('gtk2' 'libfm')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxhotkey-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3' 'libfm' 'libunistring')
  conflicts=('lxhotkey')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
