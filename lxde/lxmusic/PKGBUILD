# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Marcus Schulderinsky <mmaacceess at gmail dot com>

pkgname=lxmusic
pkgver=0.4.8
pkgrel=1
pkgdesc='Lightweight music player (XMMS2 client)'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://lxde.org/'
license=('GPL2')
groups=('lxde')
depends=('gtk3' 'libnotify' 'xmms2')
makedepends=('git' 'intltool')
replaces=('lxmusic-gtk3')
source=(git+https://github.com/lxde/lxmusic.git#tag=${pkgver}
        'lxmusic.appdata.xml')
sha256sums=('7a8d8cf3a9bf7e26b774f1106c1bab297fef052bd23c44208e5a40c561f4df9e'
            '0093885fe76bda60a1b319970bd8d148903a3adb674690d3d9cf9262e053b05b')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk3
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
