# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=lxlauncher
pkgname=(lxlauncher lxlauncher-gtk3)
pkgver=0.2.8
pkgrel=1
pkgdesc='Open source clone of the Asus launcher for EeePC'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('GPL2')
url='https://lxde.org/'
depends=('gtk2' 'gtk3' 'startup-notification' 'lxmenu-data' 'menu-cache')
makedepends=('git' 'intltool')
backup=('etc/xdg/lxlauncher/gtk.css'
        'etc/xdg/lxlauncher/gtkrc'
        'etc/xdg/lxlauncher/settings.conf')
source=(git+https://github.com/lxde/lxlauncher.git#tag=${pkgver}
        fix-invisible-icons.patch)
sha256sums=('3d8b998cc318bdf77c64df6f52e7f4cfb909de30ca2b3170dbb5ac1ffd450e37'
            '34fd1774d962965795eac8002d77a17078a967e2812ebae3e71d35533e1b3799')

prepare() {
  cd $pkgbase

  # Fix invisible icons with GTK+ 3
  patch -Np1 -i ../fix-invisible-icons.patch

  autoreconf -fiv
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package_lxlauncher() {
  groups=('lxde')
  depends=('gtk2' 'startup-notification' 'lxmenu-data' 'menu-cache')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxlauncher-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3' 'startup-notification' 'lxmenu-data' 'menu-cache')
  conflicts=('lxlauncher')

  cd gtk3
  make DESTDIR="$pkgdir" install
}

