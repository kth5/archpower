# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: kiefer <jorgelmadrid@gmail.com>

pkgname=lxdm
pkgver=0.5.3
pkgrel=9
pkgdesc='Lightweight X11 Display Manager'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://sourceforge.net/projects/lxdm/'
license=(GPL-3.0-or-later)
depends=(
  bash
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libx11
  libxcb
  pam
  pango
  xorg-server
)
makedepends=(
  intltool
  iso-codes
)
optdepends=('iso-codes: show language names in language chooser')
conflicts=(lxdm-gtk3)
replaces=(lxdm-gtk3)
groups=(lxde)
backup=(
  etc/lxdm/LoginReady
  etc/lxdm/lxdm.conf
  etc/lxdm/PostLogin
  etc/lxdm/PostLogout
  etc/lxdm/PreLogin
  etc/lxdm/PreReboot
  etc/lxdm/PreShutdown
  etc/lxdm/Xsession
  etc/pam.d/lxdm
)
source=(
  "https://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.xz"
  lxdm-git-fixes.patch
  lxdm-fallback-interface.patch
  lxdm-set-path.patch
  lxdm-default-config.patch
  lxdm.pam
  Xsession
)
b2sums=(
  9d27feb60452af49127972ce0ecdc25122f5f86961f65512fd6c185c5d6ca03e637fab703fa2df156dba8a3d0ef4e7cf9f55e20762ba49bb14dc4ee8a82b1fa3
  86a40cb3761a8bc623768e93bd62291244ab924d51d282ff882eeb11332f5a8d74b652dfea8ce75bb9e58668e051fcaaff47f14ac665c50bfbe9b718a2ab13d9
  7c1025c61daaae4610790d16f4aa440765b02ac3ba7dcb8f97f7f8504ef915b7810720d3d91a7c68eb16e6bc3e5c3f17c767b760e66d43784a3d00324b91e547
  506f86be87465f797423ae99fd7d4165968235672bb089c2ed414a0857331a20730695bbc74209d3d0c777c32dc1cb13a450a00011757a0b8f1a8cecf1e3099f
  ce4591755af6025229681f5f42e7e79ac93c75c394ae0f2da27a1a846ee2ae4621f40f5287563d1b40bfcd084904764bf1c8e6e5cb5c57b445d7c0197bcf6504
  11f4c568c6b766d3baf65ea91be2ec0641381a6c195ef460a9a0e282591c1211603d0a3db630e901079fba28819a4efab45bb8b722947dfac37659daafe14aaf
  0276563db053a305f2d7be37f042514a9d343e271769ce25a572b9cd34756d259410f79e7d5ad32d0ab87fcf976a8140c2d46e3259d82666b6074e2bea775bac
)

prepare(){
  cd $pkgname-$pkgver

  # Apply fixes from git
  patch -Np1 -i ../lxdm-git-fixes.patch

  # Add missing fallback interface for GTK3
  patch -Np1 -i ../lxdm-fallback-interface.patch

  # Stretch background image for GTK3
  sed -i 's/background-repeat: round;/background-size: 100% 100%;/' data/themes/Industrial/gtk.css

  # Don't overwrite PATH if already defined
  patch -Np1 -i ../lxdm-set-path.patch

  # Adjust Arch-specific settings
  patch -Np1 -i ../lxdm-default-config.patch

  # Use our custom PAM and Xsession files
  cp ../lxdm.pam pam/lxdm
  cp ../Xsession data/Xsession

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/lxdm \
    --enable-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/etc/lxdm/lxdm.conf"

  # Setup system user and group
  install -dm755 "$pkgdir"/usr/lib/{sysusers,tmpfiles}.d
  echo 'u! lxdm - "Lightweight X11 Display Manager" /var/lib/lxdm' > "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /var/lib/lxdm 0700 lxdm lxdm - -' > "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
