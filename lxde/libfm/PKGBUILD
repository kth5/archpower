# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgbase=libfm
pkgname=(
  libfm
  libfm-extra
  libfm-gtk3
)
pkgver=1.4.0
pkgrel=2
pkgdesc='Library for file management'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://github.com/lxde/libfm'
license=('LGPL-2.1-or-later OR GPL-2.0-or-later')
depends=(
  at-spi2-core
  cairo
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libexif
  menu-cache
  pango
)
makedepends=(
  git
  glib2-devel
  gtk-doc
  intltool
  vala
)
source=("git+https://github.com/lxde/libfm.git#tag=$pkgver")
b2sums=(199c769ea44f8a8ded33a53b358e1fb3462d5545d05919957eb345311ef5e1f2f3aba097b8a04e2d04ba800eccfeb917b6b066b8c0de1860e4fa02b99b7e84da)

prepare() {
  cd $pkgbase
  autoreconf -fi
}

build() {
  cd $pkgbase
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk-doc \
    --with-gnu-ld \
    --with-gtk=3
  make
}

package_libfm() {
  depends=(
    glib2
    glibc
    libexif
    "libfm-extra=$pkgver"
    menu-cache
  )
  
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # Fix for FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm

  # Split libfm-extra
  [[ -d "$srcdir/libfm-extra" ]] && rm -r "$srcdir/libfm-extra/"
  mkdir "$srcdir"/libfm-extra
  mv "$pkgdir"/usr/include/libfm/fm-{extra,version,xml-file}.h \
     "$pkgdir"/usr/lib/libfm-extra.* \
     "$pkgdir"/usr/lib/pkgconfig/libfm-extra.pc \
     "$srcdir/libfm-extra/"

  # Split libfm-gtk3
  [[ -d "$srcdir/libfm-gtk3" ]] && rm -r "$srcdir/libfm-gtk3/"
  mkdir "$srcdir"/libfm-gtk3
  mv "$pkgdir/usr/bin" \
     "$pkgdir"/usr/lib/libfm-gtk3.* \
     "$pkgdir"/usr/lib/libfm/modules/gtk-* \
     "$pkgdir/usr/lib/pkgconfig/libfm-gtk3.pc" \
     "$pkgdir/usr/share/applications" \
     "$pkgdir/usr/share/man" \
     "$srcdir/libfm-gtk3/"
}

package_libfm-extra() {
  pkgdesc="Extra library for file management" 
  depends=(
    glib2
    glibc
  )

  cd "$srcdir"/libfm-extra
  mkdir -p "$pkgdir"/usr/{include/libfm,lib/pkgconfig}
  mv *.h "$pkgdir/usr/include/libfm"
  mv libfm-extra.so* "$pkgdir/usr/lib"
  mv libfm-extra.pc "$pkgdir/usr/lib/pkgconfig"
}

package_libfm-gtk3() {
  pkgdesc='GTK library for file management'
  depends=(
    at-spi2-core
    cairo
    gdk-pixbuf2
    glib2
    glibc
    gtk3
    "libfm=$pkgver"
    menu-cache
    pango
  )
  conflicts=(
    libfm-gtk2
    lxshortcut
  )

  cd "$srcdir"/libfm-gtk3
  mkdir -p "$pkgdir"/usr/{lib/{libfm/modules,pkgconfig},share}
  mv bin "$pkgdir/usr"
  mv libfm-gtk3.so* "$pkgdir"/usr/lib
  mv gtk-* "$pkgdir"/usr/lib/libfm/modules
  mv libfm-gtk3.pc "$pkgdir/usr/lib/pkgconfig"
  mv applications "$pkgdir/usr/share"
  mv man "$pkgdir/usr/share"
}
