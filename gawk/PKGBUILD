# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gawk
pkgver=5.3.1
pkgrel=2
pkgdesc="GNU version of awk"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso riscv64)
url="https://www.gnu.org/software/gawk/"
license=('GPL-3.0-or-later')
depends=('sh' 'glibc' 'mpfr')
provides=('awk')
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
  45d522673508273d3587515b7c333dbc438b9bf6.patch
)
validpgpkeys=('D1967C63788713177D861ED7DF597815937EC0D2') # Arnold Robbins
b2sums=('14644d71781bed740017a0e87be8f33219d8db50e614b098caa6d71431b61431e175d66f2535927067ee2596daec507aa195ede65694827e3f08b94756b2b5fd'
        'SKIP'
        '52772fb2136a9377333a1e8be76a5f0bb7c3f7299b13809fcb129a2d0c5234dd7e16ef3e8a0e6d2ed4c4f78a40326d175b44eaffbf24c5729041405c839bbb85')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../45d522673508273d3587515b7c333dbc438b9bf6.patch
  autoreconf -fiv
}
build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc --without-libsigsegv
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
