# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gawk
pkgver=5.4.0
pkgrel=1
pkgdesc="GNU version of awk"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://www.gnu.org/software/gawk/"
license=('GPL-3.0-or-later')
depends=('sh' 'glibc' 'mpfr')
provides=('awk')
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
)
validpgpkeys=('D1967C63788713177D861ED7DF597815937EC0D2') # Arnold Robbins
b2sums=('8a02100409abb9a3653c056acd8a62bfdbbe8b55de45ed554afc6072c19f183ef3ef7628a9c644f88f06e260434ee2c50de08cb303a8e381803742afe837a769'
        'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc --without-libsigsegv
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
