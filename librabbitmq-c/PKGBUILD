# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: travisghansen <travisghansen@yahoo.com>
# Contributor: aleiphoenix <aleiphoenix@gmail.com>

pkgname=librabbitmq-c
pkgver=0.14.0
pkgrel=1
pkgdesc='RabbitMQ(amqp) library written in C-language'
url='https://github.com/alanxz/rabbitmq-c'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=('MIT')
depends=('popt' 'openssl')
makedepends=('cmake' 'docbook-xsl' 'doxygen' 'xmlto' 'ninja')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('18cb429bcfa457e359128bf458c8b9f60b1c929a8ca3a8206f40d6390d7d4c6f4c5140eb7e9ab7b401d035fc48324cbe963d058100ab65ef3faba59e7f95607e')
b2sums=('cb800f1ad6e502e089595ce177179e6f05557151229a406eb0cc84956f3f957331e3714d9d83b421925b97d82f5c4d3b3636813ee2bf85f7e2ef273cd6cf3758')

build() {
  cd ${pkgname:3}-$pkgver
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TOOLS=ON \
    -DBUILD_TOOLS_DOCS=ON \
    -DBUILD_TESTS=OFF \
    -DENABLE_SSL_SUPPORT=ON \
    -DBUILD_API_DOCS=ON \
    -Wno-dev
  ninja -C build -v
}

package() {
  cd ${pkgname:3}-$pkgver
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
