# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Remi Gacogne <rgacogne@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=hiredis
pkgver=1.3.0
pkgrel=1
pkgdesc='Minimalistic C client library for Redis'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/redis/hiredis/"
license=('BSD-3-Clause')
depends=('glibc')
checkdepends=('valkey')
source=(https://github.com/redis/$pkgname/archive/v$pkgver/hiredis-$pkgver.tar.gz)
sha256sums=('25cee4500f359cf5cad3b51ed62059aadfc0939b05150c1f19c7e2829123631c')

build() {
  cd $pkgname-$pkgver
  make PREFIX=/usr
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
