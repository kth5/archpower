# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Hipp <thomashipp at gmail dot com>

pkgname=dqlite
pkgver=1.18.3
pkgrel=1
pkgdesc="Distributed SQLite"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url="https://github.com/CanonicalLtd/dqlite"
license=('LGPL-3.0-only')
depends=('glibc' 'libuv' 'sqlite' 'lz4')
makedepends=('tcl' 'readline' 'zlib')
options=(strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/canonical/${pkgname}/archive/v${pkgver}-fixed.tar.gz")
sha256sums=('ec10c975dffc189e02d514577bc3ee76ba9a55c7996f737f6d2510b81592ca25')

build() {
	cd "$pkgname-$pkgver-fixed"
	autoreconf -i 
	#PKG_CONFIG_PATH="/usr/lib/sqlite-replication/pkgconfig" ./configure --prefix=/usr
	./configure --prefix=/usr --enable-build-raft
	make
}

check() {
	cd "$pkgname-$pkgver-fixed"
	make check || true
}

package() {
	cd "$pkgname-$pkgver-fixed"
	make DESTDIR="$pkgdir/" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
