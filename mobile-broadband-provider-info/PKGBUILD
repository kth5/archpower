# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=mobile-broadband-provider-info
pkgver=20250613
pkgrel=1
pkgdesc="APN configuration presets for mobile broadband connections"
url="https://gitlab.gnome.org/GNOME/mobile-broadband-provider-info"
arch=(any)
license=(CC-PDDC)
makedepends=(
  git
  libxslt
  meson
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('9fe8a825b2c19731903cbfa42528f232576da11a44347a1b352dd355de8353c841b4596e2f996061a967c805fed1d038caf7ab88363503267ffe20080332e1ed')
validpgpkeys=(
  0DB3932762F78E592F6522AFBB5A2C77584122D3 # Guido GÃ¼nther <agx@sigxcpu.org>
)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
