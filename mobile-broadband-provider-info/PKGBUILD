# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=mobile-broadband-provider-info
pkgver=20240407
pkgrel=1
pkgdesc="APN configuration presets for mobile broadband connections"
url="https://gitlab.gnome.org/GNOME/mobile-broadband-provider-info"
arch=(any)
license=(CC-PDDC)
makedepends=(
  git
  libxslt
  meson
)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('2c90b4caa47d9a053d466a77eb390cbb70097f4bb09b9f6d8a5e21de7a8bbb4e78770137aa4e9eca1852dc9832bddf0fb5d1713baefaa8dddaa6e3075bc757fd')
validpgpkeys=(
  0DB3932762F78E592F6522AFBB5A2C77584122D3 # Guido GÃ¼nther <agx@sigxcpu.org>
)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
