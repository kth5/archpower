# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=bats-support
pkgver=0.3.0
pkgrel=5
pkgdesc='Supporting library for Bats test helpers'
url='https://github.com/bats-core/bats-support'
arch=(any)
license=("custom:CC0")
depends=(
  bash
  bats
)
checkdepends=(
  coreutils
)
replaces=(bash-bats-support)
source=(${url}/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('417a8054462a44a6f46e375526cc1c920bae54ea76ddec3ff46603fe68bdb5c58f51bfb1c7e066347a3b71ff4d43986fd5157eb55151e0c4b5d189d81282ea14')
b2sums=('2cd488f420e2e1975f744aa15fb84e7e34b2226b06dcc6dc2959404e267d0d08b5c218b34da8e14b49ffb5c430545a2153c889904ccd08c74a7861495bca9c3d')

check() {
  cd "${pkgname}-${pkgver}"
  bats --jobs "$(nproc)" --tap test
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 644 *.bash -t "${pkgdir}/usr/lib/bats/${pkgname}"
  install -Dm 644 src/*.bash -t "${pkgdir}/usr/lib/bats/${pkgname}/src"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
