# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
pkgname=libnbd
pkgver=1.20.3
pkgrel=2
pkgdesc="NBD client library in userspace"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/nbdkit/libnbd"
license=('LGPL-2.1-or-later')
depends=('glibc' 'gnutls' 'libxml2')
makedepends=('perl' 'ocamlbuild' 'ocaml-findlib' 'rust' 'fuse3' 'python' 'go')
optdepends=(
  'fuse3: For nbdfuse support'
  'ocaml: For OCaml bindings'
  'python>=3: For Python bindings'
)
source=(
  "https://download.libguestfs.org/libnbd/${pkgver%.*}-stable/libnbd-${pkgver}.tar.gz"{,.sig}
  libnbd-mr11.patch::https://gitlab.com/nbdkit/libnbd/-/merge_requests/11.patch
)
sha256sums=('1b1d3ee680c496d7ce79875a2b136b8c2dcc62bc5dada3ee61cb4729d2879c77'
            'SKIP'
            '73f27a4d07b7c3fb53a7b4be70e3bd0accfbc50f5adcabcc08eec894b6fe11d9')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv

  patch -Np1 -i "$srcdir"/libnbd-mr11.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  export CFLAGS+=" -ffat-lto-objects"

  case "${CARCH}" in
    powerpc|powerpc64)
      _configure_flags=(--disable-golang)
    ;;
  esac

  ./configure --prefix=/usr --sbindir=/usr/bin ${_configure_flags[@]}
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
