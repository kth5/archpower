# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
pkgname=libnbd
pkgver=1.20.0
pkgrel=2.1
pkgdesc="NBD client library in userspace"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/nbdkit/libnbd"
license=('LGPL-2.1-or-later')
depends=('glibc' 'gnutls' 'libxml2')
makedepends=('perl' 'ocamlbuild' 'ocaml-findlib' 'rust' 'fuse3' 'python' 'go')
optdepends=(
  'fuse3: For nbdfuse support'
  'ocaml: For OCaml bindings'
  'python>=3: For Python bindings'
)
source=(
  "https://download.libguestfs.org/libnbd/${pkgver%.*}-stable/libnbd-${pkgver}.tar.gz"{,.sig}
)
sha256sums=('21680ad26b1cccfeb47257c2eae8bfeaea7dfc11fdea723cce02f1a8e769353a'
            'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv
}

build() {
  cd "${pkgname}-${pkgver}"
  export CFLAGS+=" -ffat-lto-objects"

  case "${CARCH}" in
    powerpc|powerpc64)
      _configure_flags=(--disable-golang)
    ;;
  esac

  ./configure --prefix=/usr --sbindir=/usr/bin ${_configure_flags[@]}
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
