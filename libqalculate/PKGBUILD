# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libqalculate
pkgver=5.8.1
pkgrel=2
pkgdesc='Multi-purpose desktop calculator'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://qalculate.github.io/'
license=(GPL-2.0-only)
depends=(curl
         icu
         gcc-libs
         glibc
         gmp
         libxml2
         mpfr
         readline)
makedepends=(doxygen
             git
             intltool)
optdepends=('gnuplot: for plotting support')
source=(git+https://github.com/Qalculate/libqalculate#tag=v$pkgver)
sha256sums=('dbac0bac5f71b2580ecc344230c22b5bdeb568753bab5122ced9d181004a731f')

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
