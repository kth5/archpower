# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libqalculate
pkgver=5.2.0.1
pkgrel=1
pkgdesc='Multi-purpose desktop calculator'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://qalculate.github.io/'
license=(GPL-2.0-only)
depends=(curl
         icu
         gcc-libs
         glibc
         gmp
         libxml2
         mpfr
         readline)
makedepends=(doxygen
             git
             intltool)
optdepends=('gnuplot: for plotting support')
source=(git+https://github.com/Qalculate/libqalculate#tag=v$pkgver)
sha256sums=('13a3dddc9f8cd4b954d06fe6c3cba884eabe83c1771c0e3f6cd97764ed4b2f99')

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
