# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libqalculate
pkgver=5.0.0
pkgrel=2
pkgdesc='Multi-purpose desktop calculator'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://qalculate.github.io/'
license=(GPL-2.0-only)
depends=(curl
         icu
         gcc-libs
         glibc
         gmp
         libxml2
         mpfr
         readline)
makedepends=(doxygen
             intltool)
optdepends=('gnuplot: for plotting support')
source=(https://github.com/Qalculate/libqalculate/releases/download/v$pkgver/$pkgname-${pkgver}b.tar.gz)
sha256sums=('9b48688f9c57bc12730af83e59d19719e45f0a4dd13c5d42d7210d0dc83bb5ce')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
