# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander 'dma147' Mieland <dma147@linux-stats.org>
# Contributor: Simon Morgan <simon@16hz.net>

pkgname=chmlib
pkgver=0.40
pkgrel=8
pkgdesc="Library for dealing with Microsoft ITSS/CHM format files"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="http://www.jedrea.com/chmlib/"
license=('LGPL')
depends=('glibc')
source=("http://www.jedrea.com/chmlib/chmlib-0.40.tar.bz2"
        chmlib-0.39-stdtypes.patch
        chmlib-0.40-clang16.patch
        chmlib-0.40-headers.patch)
sha256sums=('3449d64b0cf71578b2c7e3ddc048d4af3661f44a83941ea074a7813f3a59ffa3'
            '7dbae5e2fb8932972f2e09a44bcaaf0e53f1f0b274d8b540a5ce2d7975a14c7f'
            'db90cbf3e2a00aa89b93e80d49cc42889653738a023c9267e3266763f260d650'
            'cd887bbfd3b06b47e6d4510f580f03e45cc3ef06bdf4e45ce13b5365fe9743b5')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/chmlib-0.39-stdtypes.patch
  patch -Np1 -i ${srcdir}/chmlib-0.40-clang16.patch
  patch -Np1 -i ${srcdir}/chmlib-0.40-headers.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  CFLAGS+=" -Wno-implicit-function-declaration -Wno-incompatible-pointer-types"
  ./configure --prefix=/usr \
              --enable-examples=yes \
              --build=${CHOST}
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
