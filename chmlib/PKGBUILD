# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander 'dma147' Mieland <dma147@linux-stats.org>
# Contributor: Simon Morgan <simon@16hz.net>

pkgname=chmlib
pkgver=0.40
pkgrel=10
pkgdesc='Library for dealing with Microsoft ITSS/CHM format files'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://www.jedrea.com/chmlib/'
license=(LGPL-2.1-or-later)
depends=(glibc)
source=(
  "$pkgname-$pkgver.tar.bz2::https://www.jedrea.com/chmlib/chmlib-0.40.tar.bz2"
  fix-integer-types.patch
)
sha512sums=('3a3e151cf455b19e9e7c163a1d1e5ac52e8196f0b98f77932c72af58f3517824b9244667a18279ae754ccea784f6d6ab0c47465e74e3768ce98bb7dca0ca16e3'
            'fe4b4c0b9dc6a4158c01079d317881a6b67d9ac77dd799b9be9fbce491ecae1ee9c0581b1dbed719a7a3321f4d1a53cb3dca84f45664c3a40c09ae3aaff1f385')
b2sums=('69216120f7efcc7bfaa30faa46d9f2e2490923a6b42a766fe39838bda2b3b033ea39cb0c05eeab85a66b5a69f9297a6a412c375a2d790a463c7f6338610432fc'
        'dd8f8fa2600835fa9e34dce4eb78711768bde66911bd851f7d39c1267116443ef058b18c0c3e2944876d81037bf8d3135c175eefc4fb40cc9538c4e1155c45a3')

prepare() {
  cd "$pkgname-$pkgver"

  # https://github.com/jedwing/CHMLib/commit/11f50f16714a584c482b6f7b469fb7edd611f869
  patch -p1 -N -i $srcdir/fix-integer-types.patch

  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"

  CFLAGS+=" -Wno-implicit-function-declaration"

  ./configure \
    --prefix=/usr \
    --enable-examples=yes \
    --disable-static

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
