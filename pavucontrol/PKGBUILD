# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pavucontrol
pkgver=6.0
pkgrel=1
epoch=1
pkgdesc="PulseAudio Volume Control"
url="https://freedesktop.org/software/pulseaudio/pavucontrol/"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtkmm-4.0
  json-glib
  libcanberra
  libpulse
  libsigc++-3.0
)
makedepends=(
  git
  lynx
  meson
)
checkdepends=(tidy)
optdepends=("pulseaudio: Audio backend")
source=("git+https://gitlab.freedesktop.org/pulseaudio/pavucontrol.git?signed#tag=v$pkgver")
b2sums=('641cf49861ee03c24570a666f7831581184a42cc78fd9f85570dd07b2136dd5ae40ae2283c0448286fa1164f39f28b45c8b017869ac17fc39aa17c409b4c9098')
validpgpkeys=(
  52DFA7B8BAC74687C8A88EF48165E3D1987E2132 # Arun Raghavan <arun@asymptotic.io>
)

prepare() {
  cd pavucontrol
}

build() {
  arch-meson pavucontrol build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
