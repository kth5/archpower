# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase='protobuf'
pkgname=('protobuf' 'python-protobuf' 'ruby-google-protobuf')
pkgver=31.1
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://developers.google.com/protocol-buffers/'
license=('BSD-3-Clause')
depends=(
  'gcc-libs'
  'glibc'
  'zlib'
  'abseil-cpp'
)
makedepends=(
  'cmake'
  'git'
  'gtest'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  'ruby'
  'ruby-ffi-compiler'
  'ruby-rake'
  'ruby-rake-compiler'
)
checkdepends=(
  python-pytest
  python-numpy
)
source=(
  git+https://github.com/protocolbuffers/protobuf.git#tag=v$pkgver
  protobuf-fix-build-type-none.patch
  # https://github.com/protocolbuffers/protobuf/issues/11935
  $pkgbase-25.3-ruby-disable-LTO.patch
  "$pkgbase-$pkgver-python.tar.gz::https://files.pythonhosted.org/packages/source/${pkgbase::1}/${pkgbase//-/_}/${pkgbase//-/_}-6.31.1.tar.gz"

)
sha512sums=('f87cfe2f3c1830df496460b1c3a2ebd2b4d21698121ff72bb602ac83566068d41ee736c5728549921097e2c623ce1aac734b2d850b2f9964f824d6189fa954e1'
            '18bc71031bbcbc3810a9985fa670465040f06a6c104ab8079b56bdfc499bb6cec40805a0cefd455031142490a576dc60aa8000523877ac0353b93558e9beabbd'
            '1ebdea4e533ee0f71baf1b3fe2623ca723b36a08c6b97475ea5996b10aeb6873cf94d9120596ddd1216bd2f6feb991f8c33078e8104008a5078ace5be5431efd'
            '595c125a116020344d5706adf444e1a3259eae5bce3ca61b92c1ab4cb63c167f97210c71e5e24b8c49c6489777ca6de95abdd398125ba3925313ad5ebe09cc18')

options=(!lto)

_gemname=google-protobuf

prepare() {
  cd "$pkgname"
  patch -p1 < ../protobuf-fix-build-type-none.patch # Fix cmake config compatibility mode
  patch -p1 < ../$pkgbase-25.3-ruby-disable-LTO.patch
}

build() {
  case "${CARCH}" in
    powerpc) CXXFLAGS+=' -latomic' ;;
  esac

  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects"
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
    -D protobuf_BUILD_SHARED_LIBS=ON
    -D protobuf_USE_EXTERNAL_GTEST=ON
    -D protobuf_ABSL_PROVIDER=package
    -S "$pkgbase"
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose

  cd "$srcdir/$pkgbase/ruby"
  local _gemdir="$(gem env gemdir)"
  local _gemver=4.$pkgver

  PROTOC="$srcdir"/build/protoc \
  rake
  rake gem

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "pkg/${_gemname}-${_gemver}.gem"

  install -D lib/google/protobuf_c.so \
    "tmp_install${_gemdir}/gems/${_gemname}-${_gemver}"/lib/google/$(ruby -e 'require "rbconfig"; puts RbConfig::CONFIG["RUBY_API_VERSION"]')/protobuf_c.so

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${_gemver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${_gemver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  pushd "${srcdir}/$pkgbase-6.31.1"
  python -m build --wheel --no-isolation
  popd
}

check() {
  ctest --test-dir build
}

package_protobuf() {
  replaces=('protobuf-cpp')
  provides=('libprotoc.so' 'libprotobuf.so' 'libprotobuf-lite.so')

  DESTDIR="$pkgdir" cmake --install build

  cd $pkgbase
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 editors/protobuf-mode.el \
    -t "$pkgdir/usr/share/emacs/site-lisp/"
  install -vDm 644 editors/proto.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax"
}

package_python-protobuf() {
  pkgdesc='Python 3 bindings for Google Protocol Buffers'
  depends=(
    "protobuf=$pkgver"
    'python'
  )

  python -m installer --destdir="$pkgdir" "$pkgbase-6.31.1"/dist/*.whl
  install -vDm 644 "${pkgbase}"-6.31.1/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_ruby-google-protobuf() {
  pkgdesc='Ruby bindings for Google Protocol Buffers'
  depends=(
    'ruby'
    'ruby-bigdecimal'
  )

  cp -a $pkgbase/ruby/tmp_install/* "$pkgdir"

  install -vDm 644 $pkgbase/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -Dm 0644 $pkgbase/ruby/*.md -t "$pkgdir/usr/share/doc/${pkgname}"
}
