# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Anatol Pomozov
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.7.2
pkgrel=3
pkgdesc='Device Tree Compiler'
url='https://www.devicetree.org/'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
license=(GPL-2.0-or-later)
depends=(
  bash
  glibc
  libyaml
)
makedepends=(
  git
  meson
  python-setuptools-scm
  swig
  valgrind
)
optdepends=('python: Python bindings')
source=("git+git://git.kernel.org/pub/scm/utils/dtc/dtc.git#tag=v$pkgver")
sha256sums=('3e28d3c2d3e069331d8e4799892933ab667ffec71164fdefd0b390d25a69e0b8')

prepare() {
  cd $pkgname
  git config user.name builduser
  git config user.email builduser@archlinux

  # pylibfdt/libfdt.i: fix backwards compatibility of return values
  git cherry-pick 9a969f3b70b07bbf1c9df44a38d7f8d1d3a6e2a5
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
