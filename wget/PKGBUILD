# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=wget
pkgver=1.25.0
pkgrel=2
pkgdesc='Network utility to retrieve files from the web'
url='https://www.gnu.org/software/wget/wget.html'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64 espresso)
license=('GPL-3.0-or-later')
depends=('glibc' 'zlib' 'gnutls' 'libidn2' 'libidn2.so' 'util-linux-libs' 'libuuid.so'
         'libpsl' 'libpsl.so' 'pcre2' 'nettle' 'libnettle.so')
checkdepends=('perl-http-daemon' 'perl-io-socket-ssl' 'python')
makedepends=('autoconf-archive' 'git' 'gperf' 'wget' 'perl')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
source=("git+https://git.savannah.gnu.org/git/wget.git?signed#tag=v${pkgver}"
        "git+https://git.savannah.gnu.org/git/gnulib.git")
sha256sums=('c63fd5c082a3a7ecf9309c2334df95e61abfc413cbefe81e5ed5d40a4301344e'
            'SKIP')
b2sums=('d4f747bd505a5b482013dfc20c307a34ce505833120ff2b77ba4d94b7476d1407e64eab862d6fd5490e96e5e9b8a7e3cf742404634a287996a7a702c8efd6304'
        'SKIP')
validpgpkeys=(
  'AC404C1C0BF735C63FF4D562263D6DF2E163E1EA' # Giuseppe Scrivano <gscrivano@gnu.org>
  '7845120B07CBD8D6ECE5FF2B2A1743EDA91A35B6' # Darshit Shah <darnir@gnu.org>
  '1CB27DBC98614B2D5841646D08302DB6A2670428' # Tim Rühsen <tim.ruehsen@gmx.de>
)

_bootstrap=0

prepare() {
  cd ${pkgname}

  # Setting the value of the above custom `$_bootstrap` variable to 1
  # allows for bootstrap scripts to not require `wget` itself by skipping the po files download
  # This is required to bootstrap the package or rebuild it in staging due to soname changes (see README.md for more details)
  # /!\ Building with `$_bootstrap=1` results in a package that does not include po files
  # so make sure to rebuild it with $_bootstrap=0 when the bootstrapping is done (see `checkpkg` output)
  if (( _bootstrap )); then
    # Skip downloading po files in bootstrap scripts
    # to avoid wget (make-)depending on itself
    export SKIP_PO="1"
    # The $SKIP_PO environment variable is only honored by
    # the gnulib submodule if using its "sh" implementation
    export GNULIB_TOOL_IMPL="sh"
  fi

  git submodule init
  git config submodule.gnulib.url "${srcdir}/gnulib"
  git -c protocol.file.allow=always submodule update

  sh bootstrap
  autoreconf -fiv

  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-rpath \
    --enable-nls \
    --with-ssl=gnutls
  make
}

check() {
  cd ${pkgname}
  make check < /dev/null
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
