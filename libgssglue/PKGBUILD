# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libgssglue
pkgver=0.9
pkgrel=1
pkgdesc="Library exporting a gssapi interface to call gssapi routines in other libraries"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/gsasl/libgssglue/"
license=(BSD-3-Clause)
depends=(glibc)
provides=(libgssglue.so)
backup=(etc/gssapi_mech.conf)
source=($url/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.gz)
sha512sums=('fd2632152bab19825759f6acb9eee3170e28add447ce81bea3be93b6983fc9f0415a1bd727fbfef70dc656bd9f82bfb475356fa9fcf8577a674e1537be7e1dfd')
b2sums=('736bd47e7d0c7e90ddf943e7c7a81efb23f7ba08f8c8c2362c281953fceafa04c483865154e6c45ffd0ac9638bca09e4dc00fc34c223ecb074479134e8546831')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  touch ChangeLog
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgname-$pkgver/doc/gssapi_mech.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/{AUTHORS,README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
}
