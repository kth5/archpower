# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libgssglue
pkgver=0.8
pkgrel=1
pkgdesc="Library exporting a gssapi interface to call gssapi routines in other libraries"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://gitlab.com/gsasl/libgssglue/"
license=(BSD-3-Clause)
depends=(glibc)
provides=(libgssglue.so)
backup=(etc/gssapi_mech.conf)
source=($url/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.gz)
sha512sums=('bcab214da41789cfb9a4a42d6fa381aba209423273b64b00cca8b6e0c8c92ced3bff095544bcc67bb4bdd36bbbfaf49190ccb6d58652c1c70b2f5c64502a7bbc')
b2sums=('d6cab8babb57d49df7b1edab204011c9c9e8f97061ee7db2817a7bbc828898b24b64868f8dda19fef6e495f7b29fd0b69df804962fdaca03ef6997253e01e436')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  touch ChangeLog
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgname-$pkgver/doc/gssapi_mech.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/{AUTHORS,README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
}
