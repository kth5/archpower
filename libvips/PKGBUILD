# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libvips
pkgver=8.18.0
pkgrel=1
pkgdesc="A fast image processing library with low memory needs"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('LGPL-2.1-or-later')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'libexif' 'libarchive' 'libimagequant' 'librsvg' 'libwebp' 'openexr'
         'highway' 'pango' 'libcgif' 'cairo' 'lcms2' 'openjpeg2')
makedepends=('gobject-introspection' 'libheif' 'libjxl' 'imagemagick' 'openslide'
             'poppler-glib' 'meson' 'gi-docgen' 'glib2-devel')
optdepends=('libheif: for heif module'
            'imagemagick: for magick module'
            'openslide: for openslide module'
            'poppler-glib: for poppler module'
            'python: for vipsprofile'
            'libjxl: for jxl module')
checkdepends=('python-pytest' 'python-pyvips')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.xz")
sha512sums=('5f1a9202a018a000c7552d2107989041ba98a624bf931d1199e18bbe47e97b096448e7cdfc8d512c36406ba350e098029b382b0b7ce829c7ffef2228127a3354')

build() {
  meson build vips-$pkgver \
    --prefix=/usr \
    -Ddocs=true
  meson compile -C build
}

check() {
  return 0
  export LD_LIBRARY_PATH="$srcdir/build/libvips"
  meson test -C build
  pytest -k "not test_text" -sv --log-cli-level=WARNING vips-$pkgver/test/test-suite
}

package() {
  meson install -C build --destdir="$pkgdir"
}
