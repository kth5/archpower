# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=zint
pkgname=(zint zint-qt)
pkgver=2.15.0
pkgrel=2
pkgdesc='Barcode encoding library supporting over 50 symbologies'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://zint.org.uk/'
license=('GPL3')
makedepends=('cmake' 'ninja' 'qt6-svg' 'qt6-tools')
source=("https://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver-src.tar.gz"
        'zint-qt.appdata.xml')
sha256sums=('bce37d9b86e6127cac63c8b6267ac421116d4ac086519d726eb724f5462d98c7'
            '268d1be50b94150f1bdad3d2831d10d68dae0708ea796fc413638d22fcbed2e6')

build() {
  cmake -S $pkgbase-$pkgver-src -B build -G Ninja -DCMAKE_INSTALL_PREFIX=/usr -DZINT_QT6=ON
  cmake --build build
}

package_zint() {
  depends=('libpng')

  DESTDIR="$pkgdir" cmake --install build

  rm "$pkgdir/usr/bin/zint-qt" \
     "$pkgdir/usr/include/qzint.h" \
     "$pkgdir/usr/lib/libQZint"*
}

package_zint-qt() {
  pkgdesc='Zint Barcode Studio GUI'
  depends=('hicolor-icon-theme' 'qt6-svg' 'qt6-tools' 'zint')

  DESTDIR="$pkgdir" cmake --install build/backend_qt
  DESTDIR="$pkgdir" cmake --install build/frontend_qt

  install -Dm644 $pkgbase-$pkgver-src/zint-qt.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/zint-qt.png"
  install -Dm644 $pkgbase-$pkgver-src/zint-qt.desktop "$pkgdir/usr/share/applications/zint-qt.desktop"
  install -Dm644 zint-qt.appdata.xml "$pkgdir/usr/share/metainfo/zint-qt.appdata.xml"
}
