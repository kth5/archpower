# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=npth
pkgver=1.7
pkgrel=1
pkgdesc='The new GNU portable threads library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.gnupg.org/software/npth/index.html"
license=(LGPL-2.1-or-later)
depends=(glibc sh)
provides=(libnpth.so)
source=(https://gnupg.org/ftp/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('8893023bcf953a3f7c490cf8116bed323a9d4af2bea35277e919f437916fda8b8e68ded28ec1777717cf9187c504892556159cbdb8bc02ed4a8b30b3d897d244'
            'SKIP')
b2sums=('39bc5857fb61a4264bef6ff23c28755d2a3b3ceef93034aefd5cf1c2ad905adf696c6753ebd5cea5e0a875df4ecb97db03e023a50a27f51ec0641bbc36b54089'
        'SKIP')
validpgpkeys=(
  'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch (dist sig)
  '46CC730865BB5C78EBABADCF04376F3EE0856959'  # David Shaw (GnuPG Release Signing Key)
  '031EC2536E580D8EA286A9F22071B08A33BD3F06'  # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>
  'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9'  # Werner Koch (Release Signing Key)
  '6DAA6E64A76D2840571B4902528897B826403ADA'  # Werner Koch (dist signing 2020)
)

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{README,NEWS,ChangeLog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
