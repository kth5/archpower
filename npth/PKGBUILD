# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=npth
pkgver=1.8
pkgrel=1
pkgdesc='The new GNU portable threads library'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.gnupg.org/software/npth/index.html"
license=(LGPL-2.1-or-later)
depends=(glibc sh)
provides=(libnpth.so)
source=(https://gnupg.org/ftp/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('34fdeea3d8a7a594d8fdbcc6d5d389b5c8e282e8e84c1491b1e51960c0fa007df6a1d62543f0107f0772f3215557d4b25c2a9c7067cb0ae2f8de7b4d63d09fb4'
            'SKIP')
b2sums=('554620f6a45f57bb958d245f7464abffd565b592941f0c404ce384c854be57ee35754a78dcf6afac751ee87e1df230bf3d2b78fb37cb1ef3e5ea534ddfd4ef2a'
        'SKIP')
validpgpkeys=(
  'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'  # Werner Koch (dist sig)
  '46CC730865BB5C78EBABADCF04376F3EE0856959'  # David Shaw (GnuPG Release Signing Key)
  '031EC2536E580D8EA286A9F22071B08A33BD3F06'  # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>
  'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9'  # Werner Koch (Release Signing Key)
  '6DAA6E64A76D2840571B4902528897B826403ADA'  # Werner Koch (dist signing 2020)
  AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD  # Niibe Yutaka (GnuPG Release Key)
)

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{README,NEWS,ChangeLog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
