# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Thomas Mader <thezema@gmail.com>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=astyle
pkgver=3.6.2
pkgrel=1
pkgdesc='A free, fast and small automatic formatter for C, C++, C#, and Java source code.'
arch=(x86_64 powerpc64le powerpc)
url='http://astyle.sourceforge.net/'
license=('LGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'jdk8-openjdk')
optdepends=('java-environment-openjdk')
source=(
  "$pkgname::git+https://gitlab.com/saalen/astyle.git#tag=$pkgver"
  'simplify-makefile.patch'
)
sha512sums=('04a16a2adb12a578d053c36b169ecf6f3a6746d1ffe7d528196099d18e505b9e1062e188c5c79cf796e77f6fb8738b81a9527a92212dc452ea1b204cce4787ce'
            '4a533c6b073a55206ea2c8351d6631ba6e056d59823c8988eae63a47a762e920ad852442b57ed1860a648199c34fbc08c31bb553f0dc3f1ed145c2c702ad0290')
b2sums=('7b108093500eeffd8da80660f3d2ab99095868e3a84c43c6d05a00ce7431b3706f8d7892d8f154cad16a603c0a641a3b81a0390e4609b59856e48dd599faf73a'
        '155dca3520e1669713efbf42431e4ba2c9c05006dcd2a14f58252d9e6913aab799af7b64c9040201e99ff8d12f953918124c83845fcf64ec025fc131e7acdf1f')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/simplify-makefile.patch"
}

build() {
  cd "$pkgname/AStyle/build/gcc"

  JAVA_HOME=/usr/lib/jvm/java-8-openjdk make shared release java
}

package() {
  cd "$pkgname/AStyle/build/gcc"

  DESTDIR="$pkgdir" make install
}
