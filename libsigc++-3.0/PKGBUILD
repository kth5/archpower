# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=libsigc++-3.0
pkgname=(
  libsigc++-3.0
  libsigc++-3.0-docs
)
pkgver=3.8.0
pkgrel=1
pkgdesc="Callback Framework for C++"
url="https://libsigcplusplus.github.io/libsigcplusplus/"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://github.com/libsigcplusplus/libsigcplusplus#tag=$pkgver")
b2sums=('5f04ac2a0353d1671a2cdc47370373ec32e9e492fdd459bff28d2df96209e96acf096a2d7790c6e8117c3b483d07243468cb3ed22a622a2139cd3c94c3d6deee')

prepare() {
  cd libsigcplusplus
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson libsigcplusplus build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libsigc++-3.0() {
  provides=(
    libsigc-3.0.so
  )

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_libsigc++-3.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
