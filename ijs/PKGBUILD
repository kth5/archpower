# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=ijs
pkgver=0.35
pkgrel=6
pkgdesc="a library which implements a protocol for transmission of raster page images"
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url="https://www.openprinting.org/download/ijs/"
license=('AGPL-3.0-or-later')
depends=('glibc' 'sh')
makedepends=('docbook-utils' 'ghostscript')
source=("https://www.openprinting.org/download/ijs/download/ijs-$pkgver.tar.bz2"
        # this one misses ijs-config and its manpage but adds AGPL v3 COPYING
        #ijs-$pkgver.tar.gz::https://github.com/ArtifexSoftware/ijs/archive/refs/tags/$pkgver.tar.gz
)
sha256sums=('11a5f5084488c480f3ff5a24d64d7147bb64272bf60a0ba51330a56c5b50cab9')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --disable-static \
    --enable-shared \
    --mandir=/usr/share/man
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # install doc
  install -Dm644 ijs_spec.pdf "${pkgdir}"/usr/share/doc/$pkgname/ijs_spec.pdf
}
