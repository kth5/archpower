# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>

pkgname=fastfetch
pkgver=2.59.0
pkgrel=1
pkgdesc="A feature-rich and performance oriented neofetch like system information tool"
url="https://github.com/fastfetch-cli/fastfetch"
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
license=('MIT')
depends=('glibc' 'yyjson')
makedepends=('chafa' 'cmake' 'dbus' 'dconf' 'ddcutil' 'directx-headers' 'imagemagick' 'libglvnd' 'libpulse' 'libxcb'
             'libxrandr' 'ocl-icd' 'opencl-headers' 'sqlite' 'vulkan-headers' 'vulkan-icd-loader' 'wayland' 'xfconf' 'zlib')
optdepends=('chafa: Image output as ascii art'
            'dbus: Bluetooth, Player & Media detection'
            'dconf: Needed for values that are only stored in DConf + Fallback for GSettings'
            'ddcutil: Brightness detection of external displays'
            'directx-headers: GPU detection in WSL'
            'glib2: Output for values that are only stored in GSettings'
            'hwdata: GPU output'
            'imagemagick: Image output using sixel or kitty graphics protocol'
            'libdrm: Displays detection'
            'libelf: st term font detection and fast path of systemd version detection'
            'libglvnd: OpenGL module'
            'libpulse: Sound detection'
            'libxrandr: Multi monitor support'
            'ocl-icd: OpenCL module'
            'python: Needed for zsh and fish completions'
            'sqlite: Needed for Sqlite integration and Soar packages count'
            'vulkan-icd-loader: Vulkan module & fallback for GPU output'
            'zlib: Faster image output when using kitty graphics protocol')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('978e2524d0dc1ff9fd8c89fb24ae5b131af18ff108da82c6d99823712557e499')

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
		-DCMAKE_BUILD_TYPE='RelWithDebInfo' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-DBUILD_FLASHFETCH='OFF' \
		-DBUILD_TESTS='ON' \
		-DENABLE_SQLITE3='ON' \
		-DENABLE_RPM='OFF' \
		-DENABLE_IMAGEMAGICK6='OFF' \
		-DENABLE_SYSTEM_YYJSON='ON' \
		-DPACKAGES_DISABLE_APK='ON' \
		-DPACKAGES_DISABLE_DPKG='ON' \
		-DPACKAGES_DISABLE_EMERGE='ON' \
		-DPACKAGES_DISABLE_EOPKG='ON' \
		-DPACKAGES_DISABLE_GUIX='ON' \
		-DPACKAGES_DISABLE_LINGLONG='ON' \
		-DPACKAGES_DISABLE_LPKG='ON' \
		-DPACKAGES_DISABLE_LPKGBUILD='ON' \
		-DPACKAGES_DISABLE_OPKG='ON' \
		-DPACKAGES_DISABLE_PACSTALL='ON' \
		-DPACKAGES_DISABLE_PALUDIS='ON' \
		-DPACKAGES_DISABLE_PKG='ON' \
		-DPACKAGES_DISABLE_PKGTOOL='ON' \
		-DPACKAGES_DISABLE_RPM='ON' \
		-DPACKAGES_DISABLE_SORCERY='ON' \
		-DPACKAGES_DISABLE_XBPS='ON' \
		-Wno-dev
	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
