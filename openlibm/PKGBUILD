# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>

pkgname=openlibm
pkgver=0.8.2
pkgrel=1
pkgdesc='Standalone implementation of C mathematical functions'
arch=(x86_64 powerpc64le powerpc64 powerpc riscv64)
url='https://openlibm.org/'
license=(BSD)
depends=(glibc)
source=(https://github.com/JuliaLang/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('7244f9aa468584744e260cef740d57d10eab6e9c05f62084f8f2ba457f4b4b1d')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|/usr/local|/usr|' -i Make.inc
}

build() {
  cd $pkgname-$pkgver
  make prefix=/usr
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
