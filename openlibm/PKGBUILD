# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>

pkgname=openlibm
pkgver=0.8.7
pkgrel=1
pkgdesc='Standalone implementation of C mathematical functions'
arch=(x86_64 powerpc64le powerpc64 powerpc espresso)
url='https://openlibm.org/'
license=(MIT)
depends=(glibc)
makedepends=(git)
source=(git+https://github.com/JuliaLang/$pkgname#tag=v$pkgver)
sha256sums=('30c5bfde490f0411b6368c017323b33f7d2a12573e99d7a511b9985c584ccca0')

prepare() {
  cd $pkgname
  sed -e 's|/usr/local|/usr|' -i Make.inc
}

build() {
  cd $pkgname
  make prefix=/usr
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
