# POWER Maintainer: Alexander Baldeck <alex.bldck@gmail.com>
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gtk-sharp-3
_pkgname=GtkSharp
pkgver=3.22.2
pkgrel=2
pkgdesc='C# bindings for GTK 3'
arch=(any)
url='https://github.com/GLibSharp/GtkSharp'
license=('LGPL-2.0-only AND GPL-2.0-only')
depends=(
  at-spi2-core
  bash
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  mono
  pango
)
makedepends=(
  git
  meson
)
optdepends=('perl: gapi-parser command')
source=("git+https://github.com/GLibSharp/$_pkgname.git#tag=$pkgver")
b2sums=(713d60ce8f6b779b7bc9cb5c905c54e8bc6c195dd1437de88b93b1919d7254bf98016d5764c513b8f8643c99e9cc7ac719d93fea5865ce0ed56777ea769131ad)

prepare() {
  cd $_pkgname

  # Fix reference to gio_api_includes
  git cherry-pick -n a1ffef907e06303bbd2787ced5c82a8bf6a7eef1

  # Add cairo-sharp.dll.config
  git cherry-pick -n 8cce7f0d19e6378f652aa75dcaac348a869f6ece

  # This commit causes random crashes in Gnome Subtitles - https://gitlab.gnome.org/GNOME/gnome-subtitles/-/issues/212
  git revert -n 2bb4d5c2440b917ef03ff2b38aca4afa9d93d065
}

build() {
  arch-meson $_pkgname build \
    -D b_pie=false \
    -D install=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
